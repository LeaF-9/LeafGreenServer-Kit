import:
	org.bukkit.event.player.PlayerFishEvent
	org.bukkit.event.player.PlayerFishEvent$State

on PlayerFishEvent:
	set {_player} to event.getPlayer()
	name of {_player}'s tool contains "Grappling Hook"
	stop if checkCT({_player}, "Grappling Hook") is false

	set {_state} to event.getState()
	if any:
		{_state} is State.REEL_IN
		{_state} is State.IN_GROUND
	then:
		set damage value of {_player}'s tool to 0
		set {_hook} to event.getHook()

		if block below {_hook} is not air:
			if "%type of block below {_hook}%" does not contain "water" or "lava":
				jump({_player}, {_hook}, {_vector})

		else if block at {_hook} is not air:
			jump({_player}, {_hook}, {_vector})

		else if block below {_hook} is air:
			if block below block below {_hook} is not air or water or lava:
				jump({_player}, {_hook}, {_vector})

local function jump(player: player, hook: object, vector: vector):
	playSound("entity.zombie.infect", 1, 2, {_player})
	setCT({_player}, "Grappling Hook", 7)

	set {_head} to head of {_player}
	set {_foot} to location of {_player}
	set {_hooked} to location of {_hook}
	set {_from} to {_head}
	if (y-pos of {_hooked}) < (y-pos of {_head}):
		set {_from} to {_foot}
	set {_to} to {_hooked}

	set {_vector} to setVector({_from}, {_to}, 0)
	set {_pow} to 0.165
	set {_vec} to {_vector} * vector({_pow}, {_pow}, {_pow})

	loop 5 times:
		set velocity of {_player} to {_vec}
		wait a tick

on rightclick holding a fishing rod:
	cancel event if checkCT(player, "Grappling Hook") is false

on damage:
	damage was caused by projectile
	name of attacker's tool contains "Grappling Hook"
	cancel event