on join:
	set {-BlockEnable::%player%} to false

on break:
	if {-BlockEnable::%player%} is true:
		stop
	if event-block is fire:
		stop
	cancel event

on place:
	if {-BlockEnable::%player%} is true:
		stop
	if player's tool is flint and steel:
		stop
	cancel the event



import:
	org.bukkit.event.Event$Result

on rightclick:
	if {-BlockEnable::%player%} is true:
		stop

	if "%world of player%" is "Kit-SkillCheck":
		if clicked block is note block:
			if block above clicked block is not passable:
				stop

	set {_tool} to tool of player
	set {_type} to kitItemTypeChecker({_tool})

	set {_always} to {_tool}.getItemMeta().getFood().canAlwaysEat()

	set {_item} to raw name of {_tool}
	replace "minecraft:" with "" in {_item}
	set {_allowitems::shield} to true
	set {_allowitems::fishing_rod} to true
	set {_allowitems::splash_potion} to true
	set {_allowitems::lingering_potion} to true

	set {_eventString} to "%event%"
	if {_eventString} is "PlayerInteractEntityEvent":
		stop if {_always} is true
		stop if {_type} is "Weapon"
		stop if {_allowitems::%{_item}%} is true

		cancel event
		stop

	set {_action} to event.getAction().toString()

	if {_action} is "RIGHT_CLICK_AIR":
		set {_always} to {_tool}.getItemMeta().getFood().canAlwaysEat()
		stop if {_always} is true
		stop if {_type} is "Weapon"
		stop if {_allowitems::%{_item}%} is true
		

	if {_action} is "RIGHT_CLICK_BLOCK":
		set {_block} to event.getClickedBlock()
		set {_blockString} to "%block data of {_block}%"
		replace "minecraft:" with "" in {_blockString}
		set {_strings::*} to split {_blockString} at "["
		set {_blockString} to {_strings::1}
		if {-BlockEditDeny.allow-click::%{_blockString}%} is true:
			stop
		
		set {_deny} to false
		set {_deny} to true if {_always} is true
		set {_deny} to true if {_type} is "Weapon"
		set {_deny} to true if {_allowitems::%{_item}%} is true

		if {_deny} is true:
			event.setUseInteractedBlock(Result.DENY)
			stop

	cancel event

on bed enter:
	if {-BlockEnable::%player%} is true:
		stop
	cancel event

on drop:
	if {-BlockEnable::%player%} is true:
		stop
	cancel event

on craft:
	if {-BlockEnable::%player%} is true:
		stop
	cancel event

on damage of armor stand:
	if {-BlockEnable::%attacker%} is true:
		stop
	cancel event

on rightclick on armor stand:
	if {-BlockEnable::%player%} is true:
		stop
	cancel event

on damage of item frame:
	if {-BlockEnable::%attacker%} is true:
		stop
	cancel event

on rightclick on item frame:
	if {-BlockEnable::%player%} is true:
		stop
	cancel event

on damage of painting:
	if {-BlockEnable::%attacker%} is true:
		stop
	cancel event