function spawnParticle(particle: particle, loc: location, players: players = all players):
	draw 1 of {_particle} at {_loc} with force for {_players::*}

function spawnParticle_Block(particle: object, block: block data, loc: location, players: players = all players):
	set {_particle} to "%{_particle}%" parsed as particle
	draw 1 of {_particle} using {_block} at {_loc} with force for {_players::*}

function spawnParticle_Color(particle: particle, R: number, G: number, B: number, loc: location, players: players = all players):
	set {_R} to (1 / 255) * {_R}
	set {_G} to (1 / 255) * {_G}
	set {_B} to (1 / 255) * {_B}
	draw 0 of {_particle} at {_loc} with offset vector({_R}, {_G}, {_B}) with force for {_players::*}

function spawnParticle_Dust(particle: particle, R: number, G: number, B: number, size: number, loc: location, players: players = all players):
	set {_dustOption} to dustOption(rgb({_R}, {_G}, {_B}), {_size})
	draw 1 of {_particle} using {_dustOption} at {_loc} with force for {_players::*}

function spawnParticle_DustTransition(particle: particle, fR: number, fG: number, fB: number, tR: number, tG: number, tB: number,size: number, loc: location, players: players = all players):
	set {_dustTransition} to dustTransition(rgb({_fR}, {_fG}, {_fB}), rgb({_tR}, {_tG}, {_tB}), {_size})
	draw 1 of {_particle} using {_dustTransition} at {_loc} with force for {_players::*}

function spawnParticle_Item(particle: particle, item: item type, loc: location, players: players = all players):
	draw 1 of {_particle} using {_item} at {_loc} with force for {_players::*}

function spawnParticle_Note(particle: particle, color: number, loc: location, players: players = all players):
	draw 0 of {_particle} at {_loc} with extra {_color} with force for {_players::*}

function spawnParticle_Size(particle: particle, size: number, loc: location, players: players = all players):
	draw 1 of {_particle} at {_loc} with extra {_size} with force for {_players::*}

function spawnParticle_Vector(particle: particle, vector: vector, loc: location, players: players = all players):
	draw 1 of {_particle} at {_loc} with offset {_vector} with force for {_players::*}

function spawnParticle_Vibration(particle: particle, toloc: location, speed: time span, loc: location, players: players = all players):
	set {_vibration} to vibration({_loc}, {_toloc}, {_speed})
	draw 1 of {_particle} using {_vibration} at {_loc} with force for {_players::*}