# Knightのスキル Ver1.0.0

# Weapons
on damage of player:
	metadata value "gametype" of attacker is "Kit"
	damage was caused by attack
	{Class::%attacker%} is "Knight"
	set {_tool} to attacker's tool
	set {_item.kit} to kitItemKitChecker({_tool})
	{_item.kit} is "Knight"
	set {_item.type} to kitItemTypeChecker({_tool})
	{_item.type} is "Weapon.main"
	set {_item.num} to kitItemNumberChecker({_tool})
	{_item.num} is 3
	attacker is sneaking
	playSound("entity.iron_golem.damage", 1, 1, victim)
	kitStun(victim, 0.5 seconds)
	loop 10 times:
		set {_el} to aroundEffect(victim)
		drawDot count 1, particle "blockcrack", material dirt, center {_el}, visibleRange 30

function kitWeaponKnight(player: player):
	if name of {_player}'s tool is "§3Cutlass":
		set {_tool} to {_player}'s tool
		set {_player}'s tool to air
		set {_player}'s offhand tool to {_tool}
	else if name of {_player}'s offhand tool is "§3Cutlass":
		if {_player}'s tool is air:
			set {_tool} to {_player}'s offhand tool
			set {_player}'s offhand tool to air
			set {_player}'s tool to {_tool}

on leftclick:
	if name of player's offhand tool is "§3Cutlass":
		checkCT(player, "§3Cutlass") is true
		set {Stats.%player%::Cutlass} to true
		set {_tool} to player's offhand tool
		detectCT(player, {_tool})
		set {_loc} to player's location
		set {_locpitch} to pitch of {_loc}
		set pitch of {_loc} to 0
		set {_loc} to location 0.5 meters to the left of {_loc}
		subtract 0.5 from y-coordinate of {_loc}
		set pitch of {_loc} to {_locpitch}
		set player's offhand tool to air
		if {_locpitch} < 0:
			set {_p} to 360 + {_locpitch}
		else:
			set {_p} to {_locpitch}
		set {_nbt} to nbt compound from "{Invisible:1b,Invulnerable:1b,NoBasePlate:1b,NoGravity:1b,ShowArms:1b,ArmorItems:[{},{},{},{}],HandItems:[{id:""golden_sword"",Count:1b},{}],Pose:{RightArm:[350f,%{_p}%f,270f]}}"
		spawn an armor stand at player with nbt {_nbt}
		set metadata value "cutlass" of spawned entity to "%player%"
		playSound("entity.arrow.shoot", 1, 1, player)
		set {_f} to 0
		loop 20 times:
			add 0.5 to {_f}
			set {_l} to location {_f} meters in front of {_loc}
			loop all entities:
				type of loop-entity is an armor stand
				metadata value "cutlass" of loop-entity is "%player%"
				teleport loop-entity to {_l}
			loop all entities in radius 2 around {_l}:
				{_hit.%id of loop-entity%} is not set
				kitCheckTeam(loop-entity, player, false) is true
				set {_hit.%id of loop-entity%} to true
				make player damage loop-entity by 6
				playSound("entity.player.attack.sweep", 1, 1, loop-entity)
				loop 5 times:
					set {_el} to aroundEffect(loop-entity)
					drawDot count 3, particle "blockcrack", material redstone block, center {_el}, visibleRange 30
			wait a tick
		loop all entities:
			type of loop-entity is an armor stand
			metadata value "cutlass" of loop-entity is "%player%"
			delete loop-entity
		if {Stats.%player%::Cutlass} is true:
			set player's offhand tool to {_tool}
		delete {Stats.%player%::Cutlass}

function kitTimerKnight(player: player):
	while {Class::%{_player}%} is "Knight":
		if {_player} has glowing:
			loop all items in {_player}'s inventory:
				name of loop-item is "§eExcalibur"
				if loop-item is not enchanted:
					excaliburEffect({_player}, true)
				enchant loop-item with sharpness 1
		else:
			loop all items in {_player}'s inventory:
				name of loop-item is "§eExcalibur"
				if loop-item is enchanted:
					excaliburEffect({_player}, false)
				disenchant loop-item
		wait a second

function excaliburEffect(player: player, bool: boolean):
	if {_bool} is true:
		playSound("block.beacon.activate", 1, 2, {_player})
		set {_baserad} to -72
		set {_y} to -0.2
		loop 10 times:
			add 0.2 to {_y}
			add 72 to {_baserad}
			set {_baseloc} to location of {_player}
			delete {_rad}
			loop 2 times:
				add {_baserad} to {_rad}
				add 180 to {_rad}
				set {_el} to {_baseloc}
				add 0.5 * sin({_rad}) to x-coordinate of {_el}
				add 0.5 * cos({_rad}) to z-coordinate of {_el}
				add {_y} to y-coordinate of {_el}
				drawDot count 1, particle "endrod", center {_el}, visibleRange 30
			wait a tick
	else:
		playSound("block.beacon.deactivate", 1, 2, {_player})
		set {_y} to 2.2
		loop 10 times:
			set {_baseloc} to location of {_player}
			subtract 0.2 from {_y}
			delete {_rad}
			loop 8 times:
				add 45 to {_rad}
				set {_el} to {_baseloc}
				add 0.5 * sin({_rad}) to x-coordinate of {_el}
				add 0.5 * cos({_rad}) to z-coordinate of {_el}
				add {_y} to y-coordinate of {_el}
				drawDot count 1, particle "mobspell", RGB 0, 0, 205, center {_el}, visibleRange 30
			wait a tick

# Skills
function kitSkillKnight(player: player) :: boolean:
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	if checkCT({_player}, name of {_player}'s tool) is true:
		if {_item.num} is 1:
			kitSkill_Knight_1({_player})
		else if {_item.num} is 2:
			kitSkill_Knight_2({_player})
		else if {_item.num} is 3:
			kitSkill_Knight_3({_player})
		else if {_item.num} is 4:
			kitSkill_Knight_4({_player})
		else if {_item.num} is 5:
			kitSkill_Knight_5({_player})
		else if {_item.num} is 6:
			kitSkill_Knight_6({_player})
		else if {_item.num} is 7:
			kitSkill_Knight_7({_player})
		else if {_item.num} is 8:
			kitSkill_Knight_8({_player})
	return true

function kitSkill_Knight_1(player: player):
	toolCT({_player}, "Knight", 1)
	playSound("entity.player.attack.sweep", 1, 1, {_player})
	kitMsg({_player}, "§310秒間§c汎ゆる遠距離攻撃を跳ね返します!")
	spawn an armor stand at location of {_player} with nbt compound from "{Invisible:1b,Invulnerable:1b,NoBasePlate:1b,NoGravity:1b,ShowArms:1b,Pose:{RightArm:[250f,0f,90f]}}"
	set metadata value "reject-projectile" of spawned entity to "%{_player}%"
	set spawned entity's tool to diamond sword
	loop 200 times:
		set {_tool} to {_player}'s tool
		set {_item.kit} to kitItemKitChecker({_tool})
		{_item.kit} is "Knight"
		set {_item.type} to kitItemTypeChecker({_tool})
		{_item.type} is "Skill"
		set {_item.num} to kitItemNumberChecker({_tool})
		if {_item.num} is 1:
			delete {_ok}
			loop all entities:
				type of loop-entity is armor stand
				metadata value "reject-projectile" of loop-entity is "%{_player}%"
				set {_ok} to true
				teleport loop-entity to location of {_player}
			if {_ok} is not set:
				spawn an armor stand at location of {_player} with nbt compound from "{Invisible:1b,Invulnerable:1b,NoBasePlate:1b,NoGravity:1b,ShowArms:1b,Pose:{RightArm:[250f,0f,90f]}}"
				set metadata value "reject-projectile" of spawned entity to "%{_player}%"
				set spawned entity's tool to diamond sword
			set {_floc} to location 1 meter in front of head of {_player}
			delete {_pok}
			loop all projectiles in radius 5 around {_player}:
				shooter of loop-projectile is not {_player}
				if type of shooter of loop-projectile is player:
					kitCheckTeam(shooter of loop-projectile, {_player}, false) is true
					set {_pok} to true
				else:
					set {_pok} to true
				set shooter of loop-projectile to {_player}
				set metadata value "reject-projectile" of loop-projectile to "true"
				playSound("block.anvil.land", 1, 1.5, {_player})
				drawDot count 1, particle "explosionlarge", center {_player}, visibleRange 50
				teleport loop-projectile to {_floc}
				delete velocity of loop-projectile
				set {_v} to setVector(location of head of {_player}, location 10 meters in front of head of {_player})
				set velocity of loop-projectile to {_v}
#				push loop-projectile forward at speed 5
		else:
			loop all entities:
				type of loop-entity is armor stand
				metadata value "reject-projectile" of loop-entity is "%{_player}%"
				delete loop-entity
		wait a tick
	kitMsg({_player}, "§5Reject Projectile§1の§9構え状態§1が終了しました")
	loop all entities:
		type of loop-entity is armor stand
		metadata value "reject-projectile" of loop-entity is "%{_player}%"
		delete loop-entity

on damage:
	damage was caused by projectile
	metadata value "reject-projectile" of projectile is "true"
	set damage to 3

function kitSkill_Knight_2(player: player):
	set {_tool} to {_player}'s tool
	toolCT({_player}, "Knight", 2)
	playSound("block.beacon.activate", 1, 1, {_player})
	set {_baseloc} to location of head of {_player}
	loop 30 times:
		{_tg} is not set
		add 1 to {_front}
		set {_loc} to location {_front} meters in front of {_baseloc}
		loop all players in radius 2 around {_loc}:
			kitCheckTeam(loop-player, {_player}, false) is true
			{_tg} is not set
			set {_tg} to loop-player
		loop 3 times:
			set {_el} to {_loc}
			add a random number between -0.5 and 0.5 to x-coordinate of {_el}
			add a random number between -0.5 and 0.5 to y-coordinate of {_el}
			add a random number between -0.5 and 0.5 to z-coordinate of {_el}
			drawDot count 1, particle "endrod", center {_el}, visibleRange 30
		wait a tick
	if {_tg} is set:
		set {_jewel} to saveJewels({_tg})
		set {_gem} to saveGems({_tg})
		set {_wool} to saveWools({_tg})
		set {_spwool} to saveSpecialWool({_tg})
		if {Class::%{_tg}%} is "Thief":
			set {team::%{_tg}%} to {TrueTeam::%{_tg}%}
		execute console command "/kjc %{_tg}%"
		execute console command "/class Knight %{_tg}% false"
		giveEmJItems({_tg}, {_jewel}, {_gem})
		giveStWItems({_tg}, {_wool}, {_spwool})
		message "§6あなたは §b%{_player}% §6によって §cKnight §6に転職しました!" to {_tg}
		kitMsg({_player}, "§a%{_tg}% §7のクラスを §cKnight §7に変更しました...")
		playSound("block.chest.open", 1, 1, {_tg})
		detectCT({_tg}, {_tool})
		set {_el} to location 1 meter above {_tg}
		loop 30 times:
			draw 1 endrod particle at {_el} with direction randomVector({_el}) and speed 0.3, keepFor 1 tick

function kitSkill_Knight_3(player: player):
	toolCT({_player}, "Knight", 3)
	kitMsg({_player}, "§73体の剣士を呼び出しました!")
	playSound("entity.skeleton.hurt", 1, 1, {_player})
	loop all blocks in radius 3 around {_player}:
		y-coordinate of loop-block is y-coordinate of block at {_player}
		add location of loop-block to {_ls::*}
	loop 3 times:
		set {_l} to a random location out of {_ls::*}
		remove {_l} from {_ls::*}
		set {_nbt} to nbt compound from "{CustomNameVisible:1,LeftHanded:0,CanPickUpLoot:1b,Health:20,Attributes:[{Name:""generic.maxHealth"",Base:20}],HandItems:[{id:""minecraft:stone_sword"",Count:1},{}],ArmorItems:[{},{},{id:""minecraft:iron_chestplate"",Count:1},{id:""minecraft:player_head"",tag:{SkullOwner:%{_player}%},Count:1}],Team:""kit-%{TrueTeam::%{_player}%}%""}"
		spawn a skeleton at {_l} with nbt {_nbt}
		set spawned skeleton's display name to "%{_player}%"
		set metadata value "soldsk" of spawned skeleton to "%{_player}%"
		set metadata value "gametype" of spawned skeleton to "Kit"
		drawDot count 10, particle "cloud", center location of block above {_l}, visibleRange 30, keepFor 20 ticks
	wait 10 seconds
	loop all skeletons:
		metadata value "soldsk" of loop-skeleton is "%{_player}%"
		drawDot count 10, particle "cloud", center location of block above loop-skeleton, visibleRange 30, keepFor 20 ticks
		delete loop-skeleton

function kitSkill_Knight_4(player: player):
	if {_player} is riding a horse:
		send action bar "§c現在騎馬中です" to {_player}
	else:
		set {_skillitem} to {_player}'s tool
		toolCT({_player}, "Knight", 4)
		set {_dis} to display name of {_skillitem}
		set {CTStop.%{_player}%::%{_dis}%} to true
		kitMsg({_player}, "§e馬を呼び出しました!")
		spawn a horse at {_player} with nbt compound from "{SaddleItem:{id:saddle,Count:1},Attributes:[{Name:""generic.movement_speed"",Base:0.25f},{Name:""horse.jump_strength"",Base:0.7f}],Age:0}"
		set spawned horse's display name to "%{_player}%"
		tame the spawned horse to {_player}
		make {_player} ride last spawned horse
		set metadata value "gametype" of spawned horse to "Kit"
		set {_horse} to true
		while {_horse} is true:
			if vehicle of {_player} is horse:
				set {_horse} to true
			else:
				set {_horse} to false
			wait a tick
		delete {CTStop.%{_player}%::%{_dis}%} 

function kitSkill_Knight_5(player: player):
	toolCT({_player}, "Knight", 5)
	set {Stats.%{_player}%::JusticePower} to true
	kitMsg({_player}, "§9与えるダメージと受けるダメージを§63§9にします...")
	playSound("entity.illusioner.prepare_mirror", 1, 1.5, {_player})
	loop 140 times:
		set {_loc} to location of {_player}
		add 0.02 to {_dy}
		if {_dy} >= 4:
			set {_dy} to 0
		if {_dy} > 2:
			set {_y} to 2 - ({_dy} - 2)
		else:
			set {_y} to {_dy}
		add {_y} to y-coordinate of {_loc}
		add 9 to {_rad}
		add 0.5 * sin({_rad}) to x-coordinate of {_loc}
		add 0.5 * cos({_rad}) to z-coordinate of {_loc}
		drawDot count 1, particle "mobspell", RGB 255, 165, 0, center {_loc}, visibleRange 30
		wait a tick
	delete {Stats.%{_player}%::JusticePower}
	kitMsg({_player}, "§6Justice Power§cの効果が切れました")

on damage:
	if {Stats.%attacker%::JusticePower} is true:
		cancel event
		if {Stats.%victim%::JusticeTimer} is not set:
			set {Stats.%victim%::JusticeTimer} to true
			kitDamage(attacker, victim, 3, true)
			playSound("entity.iron_golem.hurt", 1, 2, victim)
			loop all blocks in radius 2 around victim:
				chance of 20%:
					set {_loc} to location of loop-block
					add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
					add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
					add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
					drawDot count 3, particle "mobspell", RGB 255, 200, 0, center {_loc}, visibleRange 30, keepFor 10 ticks
			wait 10 ticks
			delete {Stats.%victim%::JusticeTimer}
	else if type of victim is player:
		if {Stats.%victim%::JusticePower} is true:
			if damage cause is not void:
				cancel event
				if {Stats.%victim%::JusticeTimer} is not set:
					set {Stats.%victim%::JusticeTimer} to true
					kitDamage(attacker, victim, 3, true)
					playSound("entity.iron_golem.hurt", 1, 2, victim)
					loop all blocks in radius 2 around victim:
						chance of 20%:
							set {_loc} to location of loop-block
							add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
							add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
							add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
							drawDot count 3, particle "mobspell", RGB 255, 200, 0, center {_loc}, visibleRange 30, keepFor 10 ticks
					wait 10 ticks
					delete {Stats.%victim%::JusticeTimer}

on damage of horse, undead horse or skeleton horse:
	metadata value "gametype" of victim is "Kit"
	set {_d} to victim's display name
	if {team::%{_d}%} is {team::%attacker%}:
		cancel event
	else:
		set damage to 100000

On vehicle Exit:
	vehicle of player is a horse
	kill vehicle of player

on death of horse, undead horse or skeleton horse:
	clear drops

function kitSkill_Knight_6(player: player):
	toolCT({_player}, "Knight", 6)
	kitMsg({_player}, "<##ff7f50>仲間達に<##dc143c>勇気<##ff7f50>を分け与えよう!")
	playSound("ui.toast.challenge_complete", 1.2, 1.5, {_player})
	kitHeal({_player}, 2)
	loop {CT.%{_player}%::*}:
		if loop-index is not "§cHero's Courage":
			subtract 10 from {CT.%{_player}%::%loop-index%}
	loop all blocks in radius 2 around {_player}:
		set {_loc} to location of loop-block
		add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
		add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
		add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
		drawDot count 5, particle "mobspell", RGB 255, 255, 100, center {_loc}, visibleRange 30, keepFor 5 ticks
	loop all players in radius 5 around {_player}:
		loop-player is not {_player}
		{team::%loop-player%} is {team::%{_player}%}
		{Stats.%loop-player%::NoSupport} is not set
		message "<##ffd700>CT短縮<##daa520>と<##ffd700>回復<##daa520>という勇気をもらった!" to loop-player
		kitHeal(loop-player, 2)
		loop {CT.%loop-player%::*}:
			if loop-index is not "§cHero's Courage":
				subtract 10 from {CT.%loop-player%::%loop-index%}
		loop all blocks in radius 2 around loop-player:
			set {_loc} to location of loop-block
			add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
			add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
			add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
			drawDot count 5, particle "mobspell", RGB 255, 255, 100, center {_loc}, visibleRange 30, keepFor 5 ticks
	set {_ploc} to location of block at {_player}
	set {_bn} to 1
	loop 5 times:
		loop all blocks in radius {_bn} around {_player}:
			loop-block is air
			block below loop-block is not air
			set {_bloc} to location of loop-block
			set {_bnn} to {_bn} - 1
			if distance between {_bloc} and {_ploc} >= {_bnn}:
				add -0.3 to y-coordinate of {_bloc}
				drawDot count 5, particle "mobspell", RGB 255, 100, 100, center {_bloc}, visibleRange 30
		add 1 to {_bn}
		wait a tick

function kitSkill_Knight_7(player: player):
	toolCT({_player}, "Knight", 7)
	set {Stats.%{_player}%::ReligiousShelter} to true
	kitMsg({_player}, "<##0000cd>3秒間,<##00bfff>体力以上のダメージを0にします!")
	strike lightning effect at {_player}
	playSound("entity.vex.ambient", 1, 1, {_player})
	loop 60 times:
		set {_baseloc} to location of {_player}
		add 0.2 to {_dy}
		if {_dy} >= 4:
			set {_dy} to 0
		if {_dy} > 2:
			set {_y} to 2 - ({_dy} - 2)
		else:
			set {_y} to {_dy}
		add {_y} to y-coordinate of {_baseloc}
		set {_rad} to 0
		loop 10 times:
			set {_el} to {_baseloc}
			add 36 to {_rad}
			add 0.5 * sin({_rad}) to x-coordinate of {_el}
			add 0.5 * cos({_rad}) to z-coordinate of {_el}
			drawDot count 5, particle "redstone", RGB 0, 255, 255, center {_el}, visibleRange 30
		wait a tick
	delete {Stats.%{_player}%::ReligiousShelter}
	kitMsg({_player}, "§bReligious Shelter§cの効果が切れました")

on damage of player:
	if {Stats.%victim%::ReligiousShelter} is true:
		set {_hp} to health of victim
		set {_dm} to damage
		if {_dm} > {_hp}:
			set damage to 0
			playSound("entity.phantom.bite", 1, 1, victim)
			loop all blocks in radius 2 around victim:
				chance of 10%:
					set {_loc} to location of loop-block
					add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
					add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
					add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
					drawDot count 1, particle "endrod", center {_loc}, visibleRange 30

function kitSkill_Knight_8(player: player):
	toolCT({_player}, "Knight", 8)
	kitMsg({_player}, "<##ffff00>名誉<##ff8c00>が光をもたらした!")
	playSound("item.totem.use", 1, 1.5, {_player})
	apply glowing 1 to {_player} for 10 seconds
	loop all players in radius 10 around {_player}:
		loop-player is not {_player}
		{Class::%loop-player%} is set
		apply glowing 1 to loop-player for 10 seconds
		message "<##ffff00>大きな光<##ffd700>で長い発光が付与された!" to loop-player
	set {_num} to 0
	set {_ploc} to location of block at {_player}
	loop 10 times:
		add 1 to {_num}
		loop all blocks in radius {_num} around {_player}:
			loop-block is air
			set {_bloc} to location of loop-block
			set {_bnn} to {_num} - 1
			if distance between {_bloc} and {_ploc} >= {_bnn}:
				chance of 20%:
					add a random number between -0.2 and 0.2 to x-coordinate of {_bloc}
					add a random number between -0.2 and 0.2 to y-coordinate of {_bloc}
					add a random number between -0.2 and 0.2 to z-coordinate of {_bloc}
					drawDot count 5, particle "mobspell", RGB 255, 255, 0, center {_bloc}, visibleRange 30
			chance of 1%:
				drawDot count 1, particle "endrod", center {_bloc}, visibleRange 30
		wait a tick

# Ultimate
function kitUltKnight(player: player):
	message "§c自身と周辺10m以内の味方に戦闘バフを付与します!" to {_player}
	message "§c体力3以下の味方へのバフは強力になります!" to {_player}
	loop 200 times:
		loop all players in radius 10 around {_player}:
			{TrueTeam::%{_player}%} is {TrueTeam::%loop-player%}
			{Stats.%loop-player%::NoSupport} is not set
			drawDot count 3, particle "mobspell", RGB 255, 125, 0, center loop-player, visibleRange 30
			if loop-player's health > 3:
				apply strength 1 to loop-player for a tick
				apply resistance 1 to loop-player for a tick
			else:
				apply strength 2 to loop-player for a tick
				apply resistance 2 to loop-player for a tick
		wait a tick