options:
	prefix: §9[§bFFA§9]§r

command /ffa-stage <text> <text> <text="none">:
	description: FFAのステージ設定コマンド
	usage: /ffa-stage <argument> <stage name> <options>
	permission: sk.admin
	trigger:
		load yml "plugins/skript-yaml/FFA Stages/All Stages.yml" as "ffa.allstage"
		set {_stages::*} to yml list "Stages" from "ffa.allstage"
		unload yml "ffa.allstage"
		if arg 1 is "create":
			if {_stages::*} does not contain "%arg 2%":
				add arg 2 to {_stages::*}
				load yml "plugins/skript-yaml/FFA Stages/All Stages.yml" as "ffa.allstage"
				set yml list "Stages" from "ffa.allstage" to {_stages::*}
				save yml "ffa.allstage"
				load yml "plugins/skript-yaml/FFA Stages/%arg 2%.yml" as "ffa.stage"
				set yml value "name" from "ffa.stage" to arg 2
				set yml value "enable" from "ffa.stage" to false
				save yml "ffa.stage"
				message formatted "{@prefix} §3FFA Map§7: §6§l%arg 2% §bを追加しました" to player
			else:
				message formatted "{@prefix} §c既にそのマップは存在しています" to player
		else if arg 1 is "enable":
			if {_stages::*} contains "%arg 2%":
				load yml "plugins/skript-yaml/FFA Stages/%arg 2%.yml" as "ffa.stage"
				set yml value "enable" from "ffa.stage" to true
				save yml "ffa.stage"
				message formatted "{@prefix} §3FFA Map§7: §6§l%arg 2% §bを§a有効化§bしました" to player
			else:
				message formatted "{@prefix} §cそのマップは存在しません" to player
		else if arg 1 is "disable":
			if {_stages::*} contains "%arg 2%":
				load yml "plugins/skript-yaml/FFA Stages/%arg 2%.yml" as "ffa.stage"
				set yml value "enable" from "ffa.stage" to false
				save yml "ffa.stage"
				message formatted "{@prefix} §3FFA Map§7: §6§l%arg 2% §bを§c無効化§bしました" to player
			else:
				message formatted "{@prefix} §cそのマップは存在しません" to player
		else if arg 1 is "author":
			if {_stages::*} contains "%arg 2%":
				if arg 3 is not "none":
					set {_player} to arg 3 parsed as offline player
					set {_uuid} to UUID of {_player}
					load yml "plugins/skript-yaml/FFA Stages/%arg 2%.yml" as "ffa.stage"
					set {_authors::*} to yml list "authors" from "ffa.stage"
					add {_uuid} to {_authors::*}
					set yml list "authors" from "ffa.stage" to {_authors::*}
					save yml "ffa.stage"
					message formatted "{@prefix} §6%arg 2% §bの製作者に§a§l%{_player}%§bを追加しました" to player
				else:
					message formatted "{@prefix} §c製作者名を入力してください" to player
			else:
				message formatted "{@prefix} §cそのマップは存在しません" to player
		else if arg 1 is "respawn":
			if {_stages::*} contains "%arg 2%":
				set {_loc} to location of player
				load yml "plugins/skript-yaml/FFA Stages/%arg 2%.yml" as "ffa.stage"
				set {_num} to 1
				loop yaml node keys "spawnpoints" from "ffa.stage":
					add 1 to {_num}
				set yml value "spawnpoints.%{_num}%.x" from "ffa.stage" to x-coordinate of {_loc}
				set yml value "spawnpoints.%{_num}%.y" from "ffa.stage" to y-coordinate of {_loc}
				set yml value "spawnpoints.%{_num}%.z" from "ffa.stage" to z-coordinate of {_loc}
				set yml value "spawnpoints.%{_num}%.yaw" from "ffa.stage" to yaw of {_loc}
				set yml value "spawnpoints.%{_num}%.pitch" from "ffa.stage" to pitch of {_loc}
				set yml value "spawnpoints.%{_num}%.world" from "ffa.stage" to ffaWorldNC(arg 2)
				message formatted "{@prefix} §6%arg 3%§bの§e§lSpawnpoint§bを追加しました" to player
			else:
				message formatted "{@prefix} §cそのマップは存在しません" to player
		else if arg 1 is "set":
			if {_stages::*} contains "%arg 2%":
				loadFFAStage(arg 2)
			else:
				message formatted "{@prefix} §cそのマップは存在しません" to player
		else:
			message formatted "{@prefix} §cargument 1が不適切です" to player
			message formatted "{@prefix} §earg 1§7: §acreate§2/§aenable§2/§adisable§2/§aauthor§2/§arespawn§2/§aset" to player

function ffaWorldNC(world: text) :: text:
	replace every " " with "" in {_world}
	set {_rw} to "FFA-%{_world}%"
	return {_rw}