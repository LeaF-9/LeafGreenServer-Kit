options:
	prefix: §e[§6§lVOTE§e]§r

function kitVoteSetting():
	# Listingの指定
	if {Kit-StageListing} is true:
		set {_stages::*} to {Kit-StageListUP::*}
	else:
		load yml "plugins/skript-yaml/Kit Stages/All Stages.yml" as "KitStages"
		set {_stages::*} to yml list "Stages" from "KitStages"
		unload yml "KitStages"
	# enable: trueチェック
	loop {_stages::*}:
		load yml "plugins/skript-yaml/Kit Stages/%loop-value%.yml" as "KitStage"
		if yml value "enable" from "KitStage" is not true:
			remove loop-value from {_stages::*}
			unload yml "KitStage"
			continue
		# Ruleの指定
		if {Kit-StageRandomize.rule} is set:
			if yml value "rule" from "KitStage" is not {Kit-StageRandomize.rule}:
				remove loop-value from {_stages::*}
				unload yml "KitStage"
				continue
		if {Kit-StageRandomize.team} is set:
			if yml value "team" from "KitStage" is not {Kit-StageRandomize.team}:
				remove loop-value from {_stages::*}
				unload yml "KitStage"
				continue
		unload yml "KitStage"
	# VoteStage設定
	loop 5 times:
		set {_stage} to a random element out of {_stages::*}
		remove {_stage} from {_stages::*}
		set {Kit-VoteStage::%loop-number%} to {_stage}

on inventory click:
	type of event-inventory is chest inventory
	name of event-inventory contains "§6§lMap Vote"
	cancel event
	set {_item} to clicked item
	set {_slot} to clicked slot
	set {_bool} to true
	if {_item} is enchanted:
		set {_bool} to false
	set {_name} to name of {_item}
	if {_item} is not set:
		stop
	replace "§6§l" with "" in {_name}
	loop {Kit-VoteStage::*}:
		if loop-value is {_name}:
			set {_num} to loop-index
	if {_num} is not set:
		message formatted "{@prefix} §c§l既に投票は終了しています" to player
		stop
	set {Kit-Vote.%player%::%{_num}%} to {_bool}
	if {_bool} is true:
		message formatted "{@prefix} §6§l%{_name}% §eに投票しました" to player
		enchant {_item} with unbreaking 1
	else:
		message formatted "{@prefix} §6§l%{_name}% §9への投票を取り消しました" to player
		disenchant {_item}
	set slot {_slot} of event-inventory to {_item}

function kitOpenVoting(player: player):
	open chest with 1 row named "§6§lMap Vote§r §b|| §a遊びたいマップを選んでね" to {_player}
	wait a tick
	set {_slot} to 2
	loop 5 times:
		if {Kit-VoteStage::%loop-number%} is not set:
			continue
		set {_name} to {Kit-VoteStage::%loop-number%}
		load yml "plugins/skript-yaml/Kit Stages/%{_name}%.yml" as "VoteInv"
		set {_team} to yml value "team" from "VoteInv"
		set {_rule.pre} to yml value "rule" from "VoteInv"
		set {_rule} to getCorrectRuleName({_rule.pre})
		unload yml "VoteInv"
		set {_slotitem} to diamond sword named "§6§l%{_name}%" with lore "§bチーム数§7: §6§l%{_team}%" and "§bゲームルール§7: §6§l%{_rule}%"
		hide additional tooltip of {_slotitem}
		set slot {_slot} of current inventory of {_player} to {_slotitem}
		add 1 to {_slot}