import:
	org.bukkit.RegionAccessor
	org.bukkit.Bukkit
	org.bukkit.entity.EntityType
	org.bukkit.attribute.Attribute

function kitSpawnMob(summoner: player, mob: entity type, loc: location, showDisplay: boolean = true) :: entity:
	set {_world} to world of {_loc}

	set {_mobString} to "%{_mob}%" in upper case
	replace " " with "_" in {_mobString}
	replace "UNDEAD" with "ZOMBIE" in {_mobString}  # zombie horseの妥協案
	replace "ADULT_" with "" in {_mobString}  # skeletonの妥協案

	set {_mobtype} to EntityType.valueOf({_mobString})

	set {_mob} to {_world}.spawnEntity({_loc}, {_mobtype})

	set display name of {_mob} to "%{_summoner}%"
	set metadata value "gametype" of {_mob} to "Kit"
	set metadata value "summon-player" of {_mob} to {_summoner}
	set {_uuid} to {_mob}.getUniqueId()
	set metadata value "uuid" of {_mob} to {_uuid}

	{_mob}.setCustomNameVisible(false) if {_showDisplay} is false

	set {_scoreboard} to Bukkit.getScoreboardManager().getMainScoreboard()
	set {_playerteam} to getTeam({_summoner})
	set {_team} to {_scoreboard}.getTeam("kit-%{_playerteam}%")
	{_team}.addEntity({_mob})
	return {_mob}

function kitSpawnMob_NBT(summoner: player, mob: entity type, loc: location, tag: text) :: entity:
	set {_nbt} to nbt compound from {_tag}
	spawn 1 of {_mob} at {_loc} with nbt {_nbt}
	set {_mob} to spawned entity
	set display name of {_mob} to "%{_summoner}%"
	set metadata value "gametype" of {_mob} to "Kit"
	set metadata value "summon-player" of {_mob} to {_summoner}
	set {_uuid} to {_mob}.getUniqueId()
	set metadata value "uuid" of {_mob} to {_uuid}
	return {_mob}

function setAttribute(entity: entity, attribute: text, value: number):
	set {_atext} to {_attribute} in upper case
	replace " " with "_" in {_atext}

	set {_attributeName} to Attribute.valueOf({_atext})
	{_entity}.getAttribute({_attributeName}).setBaseValue({_value})

function setEntityArmor(entity: entity, slot: text, item: item):
	set {_equipment} to {_entity}.getEquipment()

	if {_slot} is "helmet":
		{_equipment}.setHelmet({_item})
	else if {_slot} is "chestplate":
		{_equipment}.setChestplate({_item})
	else if {_slot} is "leggings":
		{_equipment}.setLeggings({_item})
	else if {_slot} is "boots":
		{_equipment}.setBoots({_item})
	else if {_slot} is "mainhand":
		{_equipment}.setItemInMainHand({_item})
	else if {_slot} is "offhand":
		{_equipment}.setItemInOffHand({_item})