function kitDeathEvent_DTC(attacker: player, victim: player, supporters: players):
	{TrueTeam::%{_victim}%} is not "purple"
	{TrueTeam::%{_attacker}%} is not "purple"
	# Death情報の更新
	add 1 to {Kit-Death.personal::%{_victim}%}
	kitScorePersonal({_victim}, 1, "§4%{Kit-Kill.personal::%{_victim}%}% §7/ §1%{Kit-Death.personal::%{_victim}%}%")
	set {Kit-KD.personal::%{_victim}%} to {Kit-Kill.personal::%{_victim}%} / {Kit-Death.personal::%{_victim}%}
	# Kill情報の更新
	add 1 to {Kit-Kill.personal::%{_attacker}%}
	kitScorePersonal({_attacker}, 1, "§4%{Kit-Kill.personal::%{_attacker}%}% §7/ §1%{Kit-Death.personal::%{_attacker}%}%")
	set {Kit-KD.personal::%{_attacker}%} to {Kit-Kill.personal::%{_attacker}%} / {Kit-Death.personal::%{_attacker}%}
	# 防衛時の処理 Leaf獲得のデータ処理の際に利用
	#
	# loop all blocks in radius 5 around location of victim:
	# 	{Kit-GameStageCoreLocation::*} contains location of loop-block
	# 	loop {Kit-GameStageCoreLocation::*}:
	# 		loop-value-2 is location of loop-block
	# 		set {_corename} to loop-index
	# 	if {Kit-GameStageCore_%{TrueTeam::%attacker%}%::*} contains {_corename}:
	# 		set {_true} to true
	# if {_true} is true:
	# 	execute console command "/givecoin %{LastKiller::%victim%}% 10 防衛"
	#
	# ##########################################################################

on damage of player:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "DTC"
	if {Join::%victim%} is "Kit":
		# Banditの即死処理
		if {TrueTeam::%victim%} is "purple":
			set damage to 100
			stop
		# Core周辺のTeamBuff適用
		delete {_ok}
		loop {Kit-GameStageCoreLocation::*}:
			distance between victim and loop-value <= 10
			set {_ok} to true
		if {_ok} is true:
			set {_teambuff} to 1 / {Kit-TeamBuff::%{TrueTeam::%victim%}%}
			setDamageModification_MUL(victim, {_teambuff})
	if {Join::%attacker%} is "Kit":
		# Core周辺のTeamBuff適用
		delete {_ok}
		loop {Kit-GameStageCoreLocation::*}:
			distance between attacker and loop-value <= 10
			set {_ok} to true
		if {_ok} is true:
			set {_teambuff} to {Kit-TeamBuff::%{TrueTeam::%attacker%}%}
			setDamageModification_MUL(victim, {_teambuff})

on break:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "DTC"
	# Banditの破壊阻止
	if {TrueTeam::%player%} is "purple":
		send action bar "§5§lBandit §c§lはコアの破壊ができません" to player
		stop
	# Core破壊処理
	if {Kit-GameStageCoreLocation::*} contains location of event-block:  # 破壊したブロックがCoreであるかどうかの確認
		# 破壊したCoreのNameを取得
		loop {Kit-GameStageCoreLocation::*}:
			loop-value is location of event-block
			set {_corename} to loop-index
		# 破壊処理
		set {_teamlist::*} to kitTeamList()
		loop {_teamlist::*}:
			set {_teamname} to loop-value
			set {_teamcolor} to kitTeamColor({_teamname})
			set {_slotbase} to kitSlotChecker_DTC({_teamname})
			if {Kit-GameStageCore_%{_teamname}%::*} contains {_corename}:
				if {TrueTeam::%player%} is not {_teamname}:
					subtract {Kit-TeamBuff::%{TrueTeam::%player%}%} from {Kit-GameStageCoreHP::%{_corename}%}
					set {_stop} to false
					set {_num} to 0
					loop {Kit-GameStageCore_%{_teamname}%::*}:
						if {_stop} is false:
							add 1 to {_num}
						if loop-value-2 is {_corename}:
							set {_stop} to true
					set {_slot} to {_slotbase} - {_num}
					kitScoreGlobal({_slot}, "§7- §6%{_corename}%§7: §b%ceil({Kit-GameStageCoreHP::%{_corename}%})%")
					add 1 to {Kit-Break.team::%{TrueTeam::%player%}%}
					add 1 to {Kit-Break.personal::%player%}
					set {_ok} to true
					send action bar "%{_teamcolor}%§l%{_teamname}%§8の§6コア: §l%{_corename}%§8を破壊中... §cHP§7: §b%{Kit-GameStageCoreHP::%{_corename}%}%" to player
					loop all players:
						{Join::%loop-player%} is "Kit"
						{TrueTeam::%loop-player%} is {_teamname}
						send title "§c§lコア: %{_corename}%が削られています!" with subtitle "§6コアHP§7: §b%{Kit-GameStageCoreHP::%{_corename}%}%" to loop-player for 1 seconds
					play sound "block.anvil.land" with volume 1 with pitch 1 at {Kit-GameStageCoreLocation::%{_corename}%}
					if {Kit-GameStageCoreHP::%{_corename}%} <= 0:
						loop all players:
							{Join::%loop-player%} is "Kit"
							{TrueTeam::%loop-player%} is {_teamname}
							send title "§4§lコア: %{_corename}%が破壊された!" to loop-player
						make a fake explosion at {Kit-GameStageCoreLocation::%{_corename}%}
						set block at {Kit-GameStageCoreLocation::%{_corename}%} to bedrock
						subtract 1 from {Kit-GameStage::corenumber-%{_teamname}%}
						if {Kit-GameStage::corenumber-%{_teamname}%} is less than 1:
							gameMsg("Kit", "%{_teamcolor}%§l%{_teamname}% Teamのコアが全て破壊された!")
							loop all players:
								{Join::%loop-player%} is "Kit"
								if {TrueTeam::%loop-player%} is {_teamname}:
									set {team::%loop-player%} to "purple"
									set {TrueTeam::%loop-player%} to "purple"
									execute console command "/team join bandit %loop-player%"
									execute console command "/cexplode %loop-player%"
							subtract 1 from {Kit-TeamLeft.Core}
				else:
					send action bar "§c§l自陣地のコアは破壊できません" to player
		# ゲーム終了チェック
		if {Kit-TeamLeft.Core} <= 1:
			loop {Kit-GameStageCoreHP::*}:
				loop-value is more than 0
				set {_corename} to loop-index
				if {Kit-GameStageCore_red::*} contains {_corename}:
					set {_teamcheck} to "red"
				if {Kit-GameStageCore_blue::*} contains {_corename}:
					set {_teamcheck} to "blue"
				if {Kit-GameStageCore_yellow::*} contains {_corename}:
					set {_teamcheck} to "yellow"
				if {Kit-GameStageCore_green::*} contains {_corename}:
					set {_teamcheck} to "green"
				{Kit-WinTeam::*} does not contain {_teamcheck}
				add {_teamcheck} to {Kit-WinTeam::*}
			gameMsg("Kit", "§c§l試合終了!")
			stopTimer("Kit-DTC")

function kitTimer_DTC():
	set {_num} to 0
	while {Kit-tochu} is true:
		add 1 to {_num}
		if {_num} >= 10:
			set {_num} to 0
			loop {Kit-GameStageCoreLocation::*}:
				set {_corename} to loop-index
				loop all players in radius 10 around loop-value:
					{Kit-GameStageCore_%{TrueTeam::%loop-player%}%::*} contains {_corename}
					apply regeneration 1 to loop-player for 10 seconds
		wait a second

function kitSlotChecker_DTC(team: text) :: number:
	if {_team} is "red":
		set {_slot} to 15
	else if {_team} is "blue":
		if {Kit-GameStage::team} is 2:
			set {_slot} to 9
		else:
			set {_slot} to 12
	else if {_team} is "yellow":
		set {_slot} to 9
	else if {_team} is "green":
		set {_slot} to 6
	return {_slot}