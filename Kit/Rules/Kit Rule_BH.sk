# 用意すべきyaml項目:
# - Kit-GameStageBalloons::*

function kitDeathEvent_BH(attacker: player, victim: player, supporters: players):
	# Death情報の更新
	add 1 to {Kit-Death.personal::%{_victim}%}
	kitScorePersonal({_victim}, 1, "§4%{Kit-Kill.personal::%{_victim}%}% §7/ §1%{Kit-Death.personal::%{_victim}%}%")
	set {Kit-KD.personal::%{_victim}%} to {Kit-Kill.personal::%{_victim}%} / {Kit-Death.personal::%{_victim}%}
	# Kill情報の更新
	add 1 to {Kit-Kill.personal::%{_attacker}%}
	kitScorePersonal({_attacker}, 1, "§4%{Kit-Kill.personal::%{_attacker}%}% §7/ §1%{Kit-Death.personal::%{_attacker}%}%")
	set {Kit-KD.personal::%{_attacker}%} to {Kit-Kill.personal::%{_attacker}%} / {Kit-Death.personal::%{_attacker}%}
	# ---victimがBalloon Carrierであった場合の処理---
	kitDropBalloons_BH({_victim})
	# ---------------------------------------------

function kitSpawnBalloon_BH(loc: location, color: text = "white"):  # 指定位置に設置状態のBalloonを召喚する関数
	set {_uuid} to a random uuid
	# ---地面に留めるためのエンティティ召喚---
	set {_groundloc} to {_loc}
	set y-coordinate of {_groundloc} to floor(y-coordinate of {_groundloc})
	set {_nbt} to nbt compound from "{Invisible:1b,Invulnerable:1b,PersistenceRequired:1b,NoBasePlate:1b,NoGravity:1b,Marker:1b}"
	spawn an armor stand at {_groundloc} with nbt {_nbt}
	set metadata value "gametype" of spawned armor stand to "Kit"
	set metadata value "balloon-type" of spawned armor stand to "base"
	set metadata value "balloon-uuid" of spawned armor stand to {_uuid}
	set {_entity.base} to spawned armor stand
	# -------------------------------------
	# ---マーカーエンティティ召喚---
	set {_markerloc} to {_loc}
	subtract 0.25 from y-coordinate of {_markerloc}
	loop 3 times:
		{_true} is not set
		if block at location 1 meters above {_markerloc} is not passable:
			set {_true} to true
		else:
			set {_markerloc} to location 1 meters above {_markerloc}
	set {_nbt} to nbt compound from "{NoGravity:1b,Size:0.5,Invulnerable:1,PersistenceRequired:1,NoAI:1,Silent:1,Glowing:1,DropItem:0,ActiveEffects:[{Id:14,Amplifier:0,Duration:12000,ShowParticles:0b}],Team:""kit-%{_color}%""}"
	spawn a magma cube at {_markerloc} with nbt {_nbt}
	set metadata value "gametype" of spawned magma cube to "Kit"
	set metadata value "balloon-type" of spawned magma cube to "marker"
	set metadata value "balloon-uuid" of spawned magma cube to {_uuid}
	set {_entity.marker} to spawned magma cube
	# ----------------------------
	# ---ベースとマーカーをつなぐ処理---
	leash {_entity.marker} to {_entity.base}
	# -------------------------------
	# ---風船エンティティ召喚---
	set {_balloonloc} to {_markerloc}
	subtract 0.25 from y-coordinate of {_balloonloc}
	set {_x} to x-coordinate of {_balloonloc}
	set {_y} to y-coordinate of {_balloonloc}
	set {_z} to z-coordinate of {_balloonloc}
	set {_world} to kitWorldNC({Kit-GameStage::name})
	set {_block} to "%{_color}%_wool"
	execute console command "/execute in %{_world}% run summon minecraft:armor_stand %{_x}% %{_y}% %{_z}% {NoGravity:1b,Marker:1b,Invisible:1,Invulnerable:1,NoBasePlate:1,Passengers:[{id:""falling_block"",BlockState:{Name:""%{_block}%""},NoGravity:1,Time:1,DropItem:0}]}"
	set {_dis} to 100
	loop all entities in radius 1 around {_balloonloc}:
		type of loop-entity is armor stand
		metadata value "gametype" of loop-entity is not set
		set {_d} to distance between {_balloonloc} and loop-entity
		if {_d} < {_dis}:
			set {_dis} to {_d}
			set {_entity} to loop-entity
	set metadata value "gametype" of {_entity} to "Kit"
	set metadata value "balloon-type" of {_entity} to "balloon-base"
	set metadata value "balloon-uuid" of {_entity} to {_uuid}
	set metadata value "balloon-color" of {_entity} to {_color}
	set metadata value "gametype" of passenger of {_entity} to "Kit"
	set metadata value "balloon-type" of passenger of {_entity} to "balloon"
	set metadata value "balloon-uuid" of passenger of {_entity} to {_uuid}
	set metadata value "balloon-color" of passenger of {_entity} to {_color}
	# ------------------------


function kitWoolSpawn():
	loop {Kit-GameStageWools::*}:
		if block at loop-value is air:
			set block at loop-value to white wool
			set {_loc} to loop-value
			add -0.25 to y-coordinate of {_loc}
			set {_spwool} to false
			chance of 10%:
				set block at loop-value to black wool
				set {_spwool} to true
				strike lightning effect at loop-value
				set {_ul} to true
			if {_spwool} is true:
				spawn a magma cube at {_loc} with nbt "{NoGravity:1b,Size:0.5,Invulnerable:1,PersistenceRequired:1,NoAI:1,Silent:1,Glowing:1,DropItem:0,ActiveEffects:[{Id:14,Amplifier:0,Duration:700,ShowParticles:0b}],Team:""Kit-SPWool""}"
				set metadata value "gametype" of spawned magma cube to "Kit"
				set metadata value "kit-wool" of spawned magma cube to true
			else:
				spawn a magma cube at {_loc} with nbt "{NoGravity:1b,Size:0.5,Invulnerable:1,PersistenceRequired:1,NoAI:1,Silent:1,Glowing:1,DropItem:0,ActiveEffects:[{Id:14,Amplifier:0,Duration:700,ShowParticles:0b}],Team:""Kit-Wool""}"
				set metadata value "gametype" of spawned magma cube to "Kit"
				set metadata value "kit-wool" of spawned magma cube to true
	if {_ul} is true:
		gameMsg("Kit", "§c§l---===[Special Wool spawned!]===---")
	else:
		gameMsg("Kit", "§b§l---===[Wool spawned!]===---")

on break of white wool:
	cancel event
	{Kit-GameStageWools::*} contains location of block
	if player has 3 sheep egg named "§7Wool":
		message "§c羊毛を所持しすぎているため取得できません!" to player
	else if player has llama egg named "§6§lSpecial Wool":
		message "§c特別な羊毛を所持しているため取得できません!" to player
	else:
		set block to air
		loop all magma cubes in radius 1.5 around location of block:
			metadata value "kit-wool" of loop-magma cube is true
			delete loop-magma cube
		give a sheep egg named "§7Wool" to player
		message "§6§l羊毛を入手しました!" to player

on break of black wool:
	cancel event
	{Kit-GameStageWools::*} contains location of block
	if player has sheep egg named "§7Wool":
		message "§c羊毛を所持しているため取得できません!" to player
	else if player has llama egg named "§6§lSpecial Wool":
		message "§c特別な羊毛を所持しているため取得できません!" to player
	else:
		set block to air
		loop all magma cubes in radius 1.5 around location of block:
			metadata value "kit-wool" of loop-magma cube is true
			delete loop-magma cube
		give a llama egg named "§6§lSpecial Wool" to player
		message "§c§l特別な羊毛を入手しました!" to player

on rightclick holding a sheep egg:
	cancel event
	if target entity is sheep:
		stop
	set {_tb} to target block
	set {_distance} to distance between player and {_tb}
	{_distance} is less than 5
	block below {_tb} is glowstone
	woolPlace(player, {TrueTeam::%player%}, {_tb}, 1)
	delete {Kit-AttackPoint::%player%}

on rightclick holding a llama egg:
	cancel event
	set {_tb} to target block
	set {_distance} to distance between player and {_tb}
	{_distance} is less than 5
	block below {_tb} is glowstone
	woolPlace(player, {TrueTeam::%player%}, {_tb}, 3)
	delete {Kit-AttackPoint::%player%}

function woolPlace(player: player, team: text, block: block, point: number):
	if type of {_block} is type of {Kit-GameStage::place-%{_team}%}:
		if {TrueTeam::%{_player}%} is {_team}:
			if {_point} is 1:
				spawnMob(sheep, {_team}, location of block above {_block})
				execute console command "/givecoin %{_player}% 5 Wool設置"
				remove a sheep egg named "§7Wool" from {_player}
			else:
				spawnMob(baby sheep, {_team}, location of block above {_block})
				execute console command "/givecoin %{_player}% 10 Wool設置"
				remove a llama egg named "§6§lSpecial Wool" from {_player}
			set {_color} to {_team} parsed as color
			set color of spawned sheep to {_color}
			set metadata value "gametype" of spawned sheep to "Kit"
			set knockback resistance attribute of spawned sheep to 1
			apply slowness 100 without particles to spawned sheep for 10000000000 seconds
			apply jump boost -2 without particles to spawned sheep for 10000000000 seconds
			apply fire resistance 1 without particles to spawned sheep for 10000000000 seconds
			add {_point} to {Kit-TeamWool::%{_team}%}
			if {_team} is "red":
				kitScoreGlobal(7, "§c赤§7: §6%{Kit-TeamWool::red}%")
			else if {_team} is "blue":
				kitScoreGlobal(6, "§9青§7: §6%{Kit-TeamWool::blue}%")
			else if {_team} is "yellow":
				kitScoreGlobal(5, "§e黄§7: §6%{Kit-TeamWool::yellow}%")
			else if {_team} is "green":
				kitScoreGlobal(4, "§a緑§7: §6%{Kit-TeamWool::green}%")
			add 1 to {Kit-WoolPlace::%{_player}%}
			set {_text} to {_team} in pascal case
			if {_point} is 1:
				gameMsg("Kit", "<%{_team}%>%{_text}% §7gained the Wool! §bTotal§7: §6§l%{Kit-TeamWool::%{_team}%}%")
			else:
				gameMsg("Kit", "<%{_team}%>%{_text}% §7gained the §6§lSpecial Wool§7! §bTotal§7: §6§l%{Kit-TeamWool::%{_team}%}%")
			if {Kit-TeamWool::%{_team}%} >= {Kit-GameStage::goal}:
				add {_team} to {Kit-WinTeam::*}
				gameMsg("Kit", "§c§l試合終了!")
				execute console command "/timer end Kit-StW"

on death of adult sheep:
	clear drops
	victim's display name is set
	world of victim is worldChecker("Kit", {Kit-GameStage::name})
	set {_dis} to victim's display name
	set {_attacker} to metadata value "lastattacker" of victim
	set {_attacker} to {_attacker} parsed as player
	give a sheep egg named "§7Wool" to {_attacker}
	woolDeath({_dis}, 1)
	execute console command "/givecoin %{_attacker}% 3 Wool奪取"

on death of baby sheep:
	clear drops
	victim's display name is set
	if victim's display name is "§7Wool":
		drop a sheep egg named "§7Wool"
		stop
	world of victim is worldChecker("Kit", {Kit-GameStage::name})
	set {_dis} to victim's display name
	give a llama egg named "§6§lSpecial Wool" to attacker
	woolDeath({_dis}, 3)
	execute console command "/givecoin %attacker% 5 Wool奪取"

function woolDeath(team: text, point: number):
	subtract {_point} from {Kit-TeamWool::%{_team}%}
	if {_team} is "red":
		kitScoreGlobal(7, "§c赤§7: §6%{Kit-TeamWool::red}%")
	else if {_team} is "blue":
		kitScoreGlobal(6, "§9青§7: §6%{Kit-TeamWool::blue}%")
	else if {_team} is "yellow":
		kitScoreGlobal(5, "§e黄§7: §6%{Kit-TeamWool::yellow}%")
	else if {_team} is "green":
		kitScoreGlobal(4, "§a緑§7: §6%{Kit-TeamWool::green}%")
	set {_text} to {_team} in pascal case
	if {_point} is 1:
		gameMsg("Kit", "<%{_team}%>%{_text}% §8lost the Wool! §bTotal§7: §6§l%{Kit-TeamWool::%{_team}%}%")
	else:
		gameMsg("Kit", "<%{_team}%>%{_text}% §8lost the §6§lSpecial Wool§8! §bTotal§7: §6§l%{Kit-TeamWool::%{_team}%}%")

on heal:
	type of event-entity is sheep
	world of event-entity is worldChecker("Kit", {Kit-GameStage::name})
	cancel event

on damage of adult sheep:
	world of victim is worldChecker("Kit", {Kit-GameStage::name})
	if attacker is set:
		if damage cause is not attack:
			cancel event
		else:
			if victim's display name is "%{TrueTeam::%attacker%}%":
				cancel event
			else:
				if attacker has 3 sheep egg named "§7Wool":
					cancel event
				else if attacker has llama egg named "§6§lSpecial Wool":
					cancel event
				else:
					if distance between attacker and victim is less than 5:
						set damage to 0
						set metadata value "lastattacker" of victim to "%attacker%"
						if attacker has mining fatigue:
							set {_HP} to health of victim
							subtract 0.8 from {_HP}
							set health of victim to {_HP}
						else:
							set {_amount} to amount of sheep egg named "§7Wool" in attacker's inventory
							set {_base} to 4 * {Kit-TeamBuff::%{TrueTeam::%attacker%}%}
							set {_damage} to {_base} / ({_amount} + 1)
							set {_HP} to health of victim
							subtract {_damage} from {_HP}
							set health of victim to {_HP}
						set {Kit-AttackPoint::%attacker%} to location of attacker
					else:
						cancel event
	else:
		cancel event
		if damage was caused by void or suffocation:
			teleport victim to {Kit-GameStage::voidtp-%victim's display name%}

on damage of baby sheep:
	world of victim is worldChecker("Kit", {Kit-GameStage::name})
	if attacker is set:
		if victim's display name is "%{TrueTeam::%attacker%}%":
			cancel event
		else if victim's display name is "§7Wool":
			set damage to 10
		else:
			if distance between attacker and victim is less than 20:
				if attacker has sheep egg named "§7Wool":
					cancel event
				else if attacker has llama egg named "§6§lSpecial Wool":
					cancel event
				else:
					set damage to 10
					set {Kit-AttackPoint::%attacker%} to location of attacker
			else:
				cancel event
	else:
		cancel event
		if damage was caused by void or suffocation:
			teleport victim to {Kit-GameStage::voidtp-%victim's display name%}

on damage of player:
	damage is caused by void
	{Kit-AttackPoint::%victim%} is set
	cancel event
	teleport victim to {Kit-AttackPoint::%victim%}
	kill victim
	if {LastKiller::%victim%} is set:
		execute console command "/givecoin %{LastKiller::%victim%}% 5 防衛"

on death of player:
	{Join::%victim%} is "Kit"
	delete {Kit-AttackPoint::%victim%}
	if victim has sheep egg named "§7Wool":
		set {_amount} to amount of sheep egg named "§7Wool" in victim's inventory
		loop {_amount} times:
			drop a sheep egg named "§7Wool" at victim
			set metadata value "undelete" of last dropped item to "true"
		set {_coin} to 5
	else if victim has llama egg named "§6§lSpecial Wool":
		drop a llama egg named "§6§lSpecial Wool" at victim
		set metadata value "undelete" of last dropped item to "true"
		set {_coin} to 10
	if {LastKiller::%victim%} is set:
		execute console command "/givecoin %{LastKiller::%victim%}% %{_coin}% Wool強奪"

on pick up sheep egg:
	name of item is "§7Wool"
	if player has 3 sheep egg named "§7Wool":
		cancel event
		stop
	else if player has llama egg named "§6§lSpecial Wool":
		cancel event
		stop
	execute console command "/givecoin %player% 5 Wool拾得"

on pick up llama egg:
	name of item is "§6§lSpecial Wool"
	if player has sheep egg named "§7Wool":
		cancel event
		stop
	else if player has llama egg named "§6§lSpecial Wool":
		cancel event
		stop
	execute console command "/givecoin %player% 10 Wool拾得"

every a second:
	loop all players:
		set {_walkremove} to 0
		if loop-player has sheep egg named "§7Wool":
			playSound("block.note_block.pling", 1.8, 1.5, loop-player)
			drawDot count 1, particle "redstone", RGB 255, 255, 255, center location 2 meters above loop-player, visibleRange 32, keepFor 10 ticks
			set {_amount} to amount of sheep egg named "§7Wool" in loop-player's inventory
			set {_walkremove} to 0.05 * {_amount}
		else if loop-player has llama egg named "§6§lSpecial Wool":
			playSound("block.note_block.pling", 1.8, 0.75, loop-player)
			apply glowing 1 to loop-player for 1 second
			set {_walkremove} to 0.1
		set {_speed} to 0.2 - {_walkremove}
		if walk speed of loop-player is not {_speed}:
			set walk speed of loop-player to {_speed}
	loop all sheeps:
		loop-sheep's display name is set
		loop-sheep's display name is not "§7Wool"
		world of loop-sheep is worldChecker("Kit", {Kit-GameStage::name})
		set {_name} to loop-sheep's display name
		if distance between loop-sheep and {Kit-GameStage::voidtp-%{_name}%} is more than 10:
			teleport loop-sheep to {Kit-GameStage::voidtp-%{_name}%}

function kitTimer_BH():
	set {_timer.balloon} to 0
	while {Kit-tochu} is true:
		add 1 to {_timer.balloon}
		if {_timer.balloon} >= 30:
			set {_timer.balloon} to 0
			gameMsg("Kit", "§b§l---===[Balloons spawned!]===---")
			loop {Kit-GameStageBalloons::*}:
				kitSpawnBalloon_BH(loop-value)
		wait a second