function kitGameStart(rule: text):
	if {_rule} is "DTC":
		set {Kit-TeamLeft.Core} to {Kit-GameStage::team}
		loop {Kit-GameStageCoreLocation::*}:
			set {_loc} to loop-value
			set block at {_loc} to {Kit-GameStageCoreMaterial::%loop-index%}
			add -0.25 to y-coordinate of {_loc}
			if {Kit-GameStageCore_red::*} contains loop-index:
				spawn a magma cube at {_loc} with nbt compound from "{NoGravity:1b,Size:0.5,Invulnerable:1,PersistenceRequired:1,NoAI:1,Silent:1,Glowing:1,DropItem:0,ActiveEffects:[{Id:14,Amplifier:0,Duration:12000,ShowParticles:0b}],Team:""kit-red""}"
			else if {Kit-GameStageCore_blue::*} contains loop-index:
				spawn a magma cube at {_loc} with nbt compound from "{NoGravity:1b,Size:0.5,Invulnerable:1,PersistenceRequired:1,NoAI:1,Silent:1,Glowing:1,DropItem:0,ActiveEffects:[{Id:14,Amplifier:0,Duration:12000,ShowParticles:0b}],Team:""kit-blue""}"
			else if {Kit-GameStageCore_yellow::*} contains loop-index:
				spawn a magma cube at {_loc} with nbt compound from "{NoGravity:1b,Size:0.5,Invulnerable:1,PersistenceRequired:1,NoAI:1,Silent:1,Glowing:1,DropItem:0,ActiveEffects:[{Id:14,Amplifier:0,Duration:12000,ShowParticles:0b}],Team:""kit-yellow""}"
			else if {Kit-GameStageCore_green::*} contains loop-index:
				spawn a magma cube at {_loc} with nbt compound from "{NoGravity:1b,Size:0.5,Invulnerable:1,PersistenceRequired:1,NoAI:1,Silent:1,Glowing:1,DropItem:0,ActiveEffects:[{Id:14,Amplifier:0,Duration:12000,ShowParticles:0b}],Team:""kit-green""}"
			set metadata value "gametype" of spawned magma cube to "Kit"
			set metadata value "kit-corelocation" of spawned magma cube to loop-index
		kitTimer_DTC()
	else if {_rule} is "CTF":
		set {Kit-FlagPoint::red} to 0
		set {Kit-FlagPoint::blue} to 0
		set {Kit-FlagPoint::yellow} to 0
		set {Kit-FlagPoint::green} to 0
		set {_flagloc} to {Kit-GameStageFlags::1}
		kitFlagSpawn_CTF({_flagloc})
		kitTimer_CTF()
	else if {_rule} is "SH":
		set {Kit-SheepPoint::red} to 0
		set {Kit-SheepPoint::blue} to 0
		set {Kit-SheepPoint::yellow} to 0
		set {Kit-SheepPoint::green} to 0
		kitSpawnCheck_SH()
		kitTimer_SH()
	else if {_rule} is "CP":
		kitSetHologram_CP({Kit-GameStage::name})
		set {Kit-TeamPoint::red} to 0
		set {Kit-TeamPoint::blue} to 0
		set {Kit-TeamPoint::yellow} to 0
		set {Kit-TeamPoint::green} to 0
		kitTimer_CP()

command /gameend <text>:
	usage: /gameend <rule>
	permission: sk.admin
	trigger:
		kitGameEnd(arg 1)

function kitGameEnd(rule: text):
	loop all players:
		{Join::%loop-player%} is "Kit"
		set gamemode of loop-player to spectator
	wait 5 seconds
	# 全チームの結果発表
	set {_var} to kitRuleVariable({_rule})
	# 例外処理: DTC戦においてはCoreの合計体力を計算
	if {_rule} is "DTC":
		loop {Kit-GameStageCoreLocation::*}:
			set {_corename} to loop-index
			if {Kit-GameStageCore_red::*} contains {_corename}:
				add {Kit-GameStageCoreHP::%{_corename}%} to {Kit-TotalCore::red}
			if {Kit-GameStageCore_blue::*} contains {_corename}:
				add {Kit-GameStageCoreHP::%{_corename}%} to {Kit-TotalCore::blue}
			if {Kit-GameStageCore_yellow::*} contains {_corename}:
				add {Kit-GameStageCoreHP::%{_corename}%} to {Kit-TotalCore::yellow}
			if {Kit-GameStageCore_green::*} contains {_corename}:
				add {Kit-GameStageCoreHP::%{_corename}%} to {Kit-TotalCore::green}
	# ###########################################
	gameMsg("Kit", "§2----------=====§5Game Result§2=====----------")
	gameMsg("Kit", " ")
	gameMsg("Kit", "§cRed§7: §a%{%{_var}%::red}%")
	gameMsg("Kit", "§9Blue§7: §a%{%{_var}%::blue}%")
	if {Kit-GameStage::team} is 4:
		gameMsg("Kit", "§eYellow§7: §a%{%{_var}%::yellow}%")
		gameMsg("Kit", "§aGreen§7: §a%{%{_var}%::green}%")
	gameMsg("Kit", " ")
	# 勝利チームの確認
	if size of {Kit-WinTeam::*} is 0:
		set {_maxData} to max({%{_var}%::*})
		loop {%{_var}%::*}:
			loop-value = {_maxData}
			add loop-index to {Kit-WinTeam::*}
	# 勝利チームの表示
	loop {Kit-WinTeam::*}:
		set {_color} to kitTeamColor(loop-value)
		set {_text} to "%{_color}%§l%loop-value%§r"
		add {_text} to {_winteams::*}
	gameMsg("Kit", "§6§lWINNER: %{_winteams::*}%")
	gameMsg("Kit", " ")
	# 最も貢献したプレイヤーの確認
	# TDM戦
	if {_rule} is "TDM":
		set {_maxData} to max({Kit-Kill.personal::*})
		loop {Kit-Kill.personal::*}:
			loop-value = {_maxData}
			add loop-index to {_highplayer::*}
		gameMsg("Kit", "§6§lTop Killer§7: §e§l%{_highplayer::*}% §9%{_maxData}% Kills")
	# DTC戦
	else if {_rule} is "DTC":
		set {_maxData} to max({Kit-Break.personal::*})
		loop {Kit-Break.personal::*}:
			loop-value = {_maxData}
			add loop-index to {_highplayer::*}
		gameMsg("Kit", "§6§lTop Breaker§7: §e§l%{_highplayer::*}% §9%{_maxData}% Breaks")
	# CTF戦
	else if {_rule} is "CTF":
		set {_maxData} to max({Kit-Hold.personal::*})
		loop {Kit-Hold.personal::*}:
			loop-value = {_maxData}
			add loop-index to {_highplayer::*}
		gameMsg("Kit", "§6§lTop Holder§7: §e§l%{_highplayer::*}% §9%{_maxData}% Seconds")
	# SH戦
	else if {_rule} is "SH":
		set {_maxData} to max({Kit-Carry.personal::*})
		loop {Kit-Carry.personal::*}:
			loop-value = {_maxData}
			add loop-index to {_highplayer::*}
		gameMsg("Kit", "§6§lTop Carrier§7: §e§l%{_highplayer::*}% §9%{_maxData}% Sheeps")
	# CP戦
	else if {_rule} is "CP":
		log "実装予定"
	# 自分の戦績と勝利チームのタイトル表示
	gameMsg("Kit", " ")
	loop all players:
		message "§aあなたの戦績§7: §4Kill: %{Kit-Kill.personal::%loop-player%}% §1Death: %{Kit-Death.personal::%loop-player%}% §6K/D: %{Kit-KD.personal::%loop-player%}%" to loop-player
		send title "§6ゲーム終了!" with subtitle "%{_winteams::*}% §5の勝利です!" to loop-player for 10 seconds
		delete {damager.%loop-player%::*}
	# ゲーム終了後のMap修正
	if {_rule} is "DTC":
		loop {Kit-GameStageCoreLocation::*}:
			set {_block} to "%{Kit-GameStageCoreMaterial::%loop-index%}%" parsed as item
			set block at loop-value to {_block}
	if {_rule} is "CP":
		loop {Kit-GameStageAreas::*}:
			set {_blocks::*} to kitCompileBlocksInArea_CP(loop-index)
			loop {_blocks::*}:
				set block at loop-value-2 to kitSetColorBlock_CP(block at loop-value-2, "white")
	# ゲーム記録をymlに記述
	kitGameInsert()
	wait 10 seconds
	# ゲーム記録の削除
	delete {Kit-Kill.team::*}
	delete {Kit-Death.team::*}
	delete {Kit-Kill.personal::*}
	delete {Kit-Death.personal::*}
	delete {Kit-KD.personal::*}
	delete {Kit-Break.team::*}
	delete {Kit-Break.personal::*}
	delete {Kit-Hold.personal::*}
	delete {Kit-TeamLeft.Core}
	delete {Kit-FlagPoint::*}
	delete {Kit-FlagHolder}
	delete {Kit-SheepPoint::*}
	delete {Kit-Carry.personal::*}
	delete {Kit-AreaRelease::*}
	delete {Kit-AreaOccupy::*}
	delete {Kit-AreaSecure::*}
	delete {Kit-TeamPoint::*}
	delete {Kit-PRank::*}
	delete {Kit-WinTeam::*}
	delete {Kit-TeamBuff::*}
	execute console command "/kjclear"

function kitRuleVariable(rule: text) :: text:  # 結果発表の際に表示するチームデータの変数名を返す関数
	if {_rule} is "TDM":
		set {_var} to "Kit-Kill.team"
	else if {_rule} is "DTC":
		set {_var} to "Kit-TotalCore"
	else if {_rule} is "CTF":
		set {_var} to "Kit-FlagPoint"
	else if {_rule} is "SH":
		set {_var} to "Kit-SheepPoint"
	else if {_rule} is "CP":
		set {_var} to "Kit-TeamPoint"
	return {_var}