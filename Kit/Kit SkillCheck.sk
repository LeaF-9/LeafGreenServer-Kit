command /skilltest <player=%player%>:
	trigger:
		if {Join::%arg 1%} is "Kit":
			message "§cJoin中はテストワールドに向かうことはできません!" to arg 1
		else:
			execute console command "/testworld teleport %arg 1%"

command /testworld <text> <player=%player%>:
	permission: sk.admin
	trigger:
		if arg 1 is "teleport":
			teleport arg 2 to {Kit-TestWorld}
			set {Kit-Testing::%arg 2%} to true
		else if arg 1 is "back":
			delete {team::%arg 2%}
			delete {TrueTeam::%arg 2%}
			delete {ClassChose::%arg 2%}
			delete {Class::%arg 2%}
			delete {CLS::%arg 2%}
			delete {Kit-Testing::%arg 2%}
			delete {Kit-Lich::%arg 2%}
			delete {Kit-Necromance::%arg 2%}
			make arg 2 execute command "/lobby"
		else if arg 1 is "set":
			set {_loc} to location of player
			set {Kit-TestWorld} to {_loc}

command /kit-standspawn:
	permission: sk.admin
	trigger:
		loop all slimes:
			display name of loop-entity is "Test-Stand"
			set metadata value "gametype" of loop-slime to "Kit"
			teleport loop-slime to location(-60.5, 103, 0.5, world "Kit-SkillCheck", 270, 0)
			set {_exist} to true
		{_exist} is not set
		spawn a slime at location(-60.5, 103, 0.5, world "Kit-SkillCheck", 270, 0) with nbt compound from "{Size:1.5,NoAI:1b,Silent:1b,CustomName:'{""text"":""Test-Stand""}'}"
		set metadata value "gametype" of spawned slime to "Kit"

on damage of slime with priority MONITOR:
	metadata value "gametype" of victim is "Kit"
	display name of victim is "Test-Stand"
	cancel event

on rightclick on a sign:
	if line 1 of target block is "§a[§e§lKit§a]":
		if line 2 of target block is "§2Join Ally":
			execute console command "/st green %player%"
			send action bar "§eチームを §a§lAlly §eに設定しました!" to player
		else if line 2 of target block is "§4Join Enemy":
			execute console command "/st red %player%"
			send action bar "§eチームを §c§lEnemy §eに設定しました!" to player
		else if line 2 of target block is "§bReset CoolTime":
			execute console command "/ctreset %player%"
			send action bar "§b§lCool Time §eを §c§lリセット §eしました!" to player
		else if line 2 of target block is "§dHealing":
			heal player by 100 hearts
			send action bar "§c§l体力 §eを §d§l回復 §eしました!" to player
		else:
			stop
		playSound("entity.player.levelup", 1, 1.5, player, true)

on death of player:
	if "%world of victim%" is "Kit-SkillCheck":
		kitFakeDeleteData(victim, "Machine-Transport Lift")

		set {_loc} to location of victim
		if y-coordinate of {_loc} <= -10:
			set {Kit-Respawn::%victim%} to {Kit-TestWorld}
		else:
			set {Kit-Respawn::%victim%} to {_loc}
		set {_spawnTime} to getKitStats(victim, "SpawnTime")
		if {_spawnTime} is set:
			set {Kit-SpawnTime::%victim%} to {_spawnTime}
		set {_resurrection} to getKitStats(victim, "Resurrection")
		if {_resurrection} is true:
			set {_class} to {Class::%victim%}
			set {Kit-Resurrect::%victim%} to true
		wait a tick
		force victim to respawn
		wait a tick
		if {_class} is set:
			execute console command "/class %{_class}% %victim% false"

on teleport:
	{Kit-Testing::%player%} is true
	set {_world} to "%world of player%"
	{_world} is not {@world-skillcheck}

	message formatted "§6§lSkillTest§c§lが中断されました" to player
	delete {team::%player%}
	delete {TrueTeam::%player%}
	delete {ClassChose::%player%}
	delete {Class::%player%}
	delete {CLS::%player%}
	delete {Kit-Testing::%player%}
	delete {Kit-Lich::%player%}
	delete {Kit-Necromance::%player%}

on quit:
	if {Kit-Testing::%player%} is true:
		delete {Kit-Testing::%player%}
		delete {team::%player%}
		delete {TrueTeam::%player%}
		delete {ClassChose::%player%}
		delete {Class::%player%}
		delete {CLS::%player%}



options:
	world-skillcheck: "Kit-SkillCheck"
	name-select: "Kit-Select"
	name-reset: "Kit-Reset"
	name-back: "§cBack to Lobby"

on rightclick on villager:
	set {_entity} to clicked entity
	set {_world} to "%world of {_entity}%"
	stop if {_world} is not {@world-skillcheck}

	set {_name} to display name of {_entity}
	if {_name} is {@name-select}:
		make player execute command "/kit-classinv"

	else if {_name} is {@name-reset}:
		execute console command "/classreset %player%"
	
	else if {_name} is {@name-back}:
		execute console command "/testworld back %player%"