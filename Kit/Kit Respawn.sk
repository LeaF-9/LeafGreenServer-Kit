on death of player:
	{Kit-tochu} is true
	{Join::%victim%} is "Kit"
	set {_loc} to location of victim
	if y-coordinate of {_loc} <= -10:
		set {Kit-Respawn::%victim%} to {Kit-GameStage::spec}
	else:
		set {Kit-Respawn::%victim%} to {_loc}
	if {Stats.%victim%::SpawnTime} is set:
		set {Kit-SpawnTime::%victim%} to {Stats.%victim%::SpawnTime}
	if {Stats.%victim%::Resurrection} is true:
		set {_class} to {Class::%victim%}
		set {Kit-Resurrect::%victim%} to true
	wait a tick
	force victim to respawn
	wait a tick
	if {_class} is set:
		execute console command "/class %{_class}% %victim% false"

on respawn:
	if {Kit-Respawn::%player%} is set:
		set respawn location to {Kit-Respawn::%player%}
		if {Kit-NoCorpse::%player%} is not set:
			if {Kit-Resurrect::%player%} is not set:
				spawn armor stand at location of {Kit-Respawn::%player%} with nbt compound from "{Invulnerable:1b,PersistenceRequired:1b,NoBasePlate:1b,NoGravity:1b,Small:1b,Invisible:1b,Marker:1b}"
				equip spawned armor stand with player's head
				set metadata value "gametype" of spawned armor stand to "Kit"
				set metadata value "deadplayer" of spawned armor stand to "%player%"
				equip spawned armor stand with leather chestplate
				set {_team} to {TrueTeam::%player%}
				if {_team} is "red":
					dye spawned armor stand's chestplate (255, 0, 0)
				else if {_team} is "blue":
					dye spawned armor stand's chestplate (0, 0, 255)
				else if {_team} is "yellow":
					dye spawned armor stand's chestplate (255, 255, 0)
				else if {_team} is "green":
					dye spawned armor stand's chestplate (0, 255, 0)
				else:
					dye spawned armor stand's chestplate (255, 0, 255)
			else:
				delete {Kit-Respawn::%player%}
				delete {Kit-Resurrect::%player%}
				send action bar "§d§l復活しました!" to player
				stop
		else:
			delete {Kit-NoCorpse::%player%}
			if {Kit-Resurrect::%player%} is set:
				delete {Kit-Respawn::%player%}
				delete {Kit-Resurrect::%player%}
				send action bar "§d§l復活しました!" to player
				stop
		delete {Kit-Respawn::%player%}
		set {Kit-DeadPlayer::%player%} to true
		set player's gamemode to spectator
		if {Kit-SpawnTime::%player%} is set:
			set {_time} to {Kit-SpawnTime::%player%}
			delete {Kit-SpawnTime::%player%}
		else:
			set {_time} to 5
		set {_timer} to {_time} * 20
		set {_aptime} to "%{_time}% seconds" parsed as time span
		apply blindness 1 to player for {_aptime}
		loop {_timer} times:
			if {Kit-DeadPlayer::%player%} is true:
				send title "§c§l死んでしまった!" with subtitle "§3§l%{_time}% §b秒後に復活します..." to player for 1 ticks with fadein 0 second
				if player does not have blindness:
					apply blindness 1 to player for {_aptime}
				wait a tick
		loop all entities:
			type of loop-entity is armor stand
			metadata value "deadplayer" of loop-entity is "%player%"
			delete loop-entity
		set {_tp} to a random location out of {Kit-GameStageRespawns.%{TrueTeam::%player%}%::*}
		teleport player to {_tp}
		set player's gamemode to survival
		{Kit-tochu} is true
		openKitInv(player)
		execute console command "/cclass %player% Kit-GameInv"
		apply blindness 1 to player for 10000000000 seconds
		apply slowness 10 to player for 10000000000 seconds
		apply jump boost -10 to player for 10000000000 seconds
		apply resistance 10 to player for 10000000000 seconds
		set {Kit-RespawnKill::%player%} to true
		set {Kit-RespawnPoint::%player%} to {_tp}
		loop 10 times:
			wait a second
			if {Kit-RespawnKill::%player%} is true:
				set {_dis} to distance between player and {_tp}
				if {_dis} >= 10:
					delete {Kit-RespawnKill::%player%}
				else:
					heal player by 10 hearts
		delete {Kit-RespawnKill::%player%}
		delete {Kit-RespawnPoint::%player%}
		send action bar "§dリスポーンキル防止の無敵化が解除されました!" to player

on damage of player:
	{Kit-tochu} is true
	{Join::%victim%} is "Kit"
	{Join::%attacker%} is "Kit"
	if {Class::%victim%} is not set:
		cancel event
		send action bar "§4§lリスポーン直後の敵は攻撃できません!" to attacker
		stop
	if {Class::%attacker%} is not set:
		cancel event
		send action bar "§4§lクラスを選択してください!" to attacker
		stop
	if {Kit-RespawnKill::%victim%} is true:
		cancel event
		send action bar "§4§lリスポーン直後の敵は攻撃できません!" to attacker
	if {Kit-RespawnKill::%attacker%} is true:
		if distance between attacker and {Kit-RespawnPoint::%attacker%} <= 5:
			set damage to damage * 2
		else:
			set damage to damage * 1.5
		delete {Kit-RespawnKill::%attacker%}
		delete {Kit-RespawnPoint::%attacker%}
		send action bar "§dリスポーンキル防止の無敵化が解除されました!" to attacker