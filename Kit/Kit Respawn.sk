on death of player:
	{Kit-tochu} is true
	{Join::%victim%} is "Kit"
	set {_loc} to location of victim
	if y-coordinate of {_loc} <= -70:
		set {Kit-Respawn::%victim%} to {Kit-GameStage::spec}
	else:
		set {Kit-Respawn::%victim%} to {_loc}
	set {_spawnTime} to getKitStats(victim, "SpawnTime")
	if {_spawnTime} is set:
		set {Kit-SpawnTime::%victim%} to {_spawnTime}
	set {_resurrection} to getKitStats(victim, "Resurrection")
	if {_resurrection} is true:
		set {_class} to {Class::%victim%}
		set {Kit-Resurrect::%victim%} to true
	wait a tick
	force victim to respawn
	wait a tick
	if {_class} is set:
		execute console command "/class %{_class}% %victim% false"

on respawn:
	if {Kit-Respawn::%player%} is set:
		set respawn location to {Kit-Respawn::%player%}
		if {Kit-NoCorpse::%player%} is not set:
			if {Kit-Resurrect::%player%} is not set:
				spawnCorpse(player, {Kit-Respawn::%player%})
			else:
				delete {Kit-Respawn::%player%}
				delete {Kit-Resurrect::%player%}
				send action bar "§d§l復活しました!" to player
				stop
		else:
			delete {Kit-NoCorpse::%player%}
			if {Kit-Resurrect::%player%} is set:
				delete {Kit-Respawn::%player%}
				delete {Kit-Resurrect::%player%}
				send action bar "§d§l復活しました!" to player
				stop
		delete {Kit-Respawn::%player%}
		set {Kit-DeadPlayer::%player%} to true  # 多分bishop等が途中で復活させた時に使う変数
		set player's gamemode to spectator
		if {Kit-SpawnTime::%player%} is set:
			set {Kit-Spawning::%player%} to {Kit-SpawnTime::%player%}
			delete {Kit-SpawnTime::%player%}
		if {Kit-Spawning::%player%} is not set:
			set {Kit-Spawning::%player%} to 5
		set {Kit-Spawning::%player%} to {Kit-Spawning::%player%} * 20
		while {Kit-Spawning::%player%} is set:
			if {Kit-Spawning::%player%} <= 0:
				delete {Kit-Spawning::%player%}
			else if {Kit-DeadPlayer::%player%} is true:
				set {_time} to {Kit-Spawning::%player%} / 20
				set {_timetexts::*} to "%{_time}%" split at "."
				if size of {_timetexts::*} is 1:
					set {_timetext} to "%{_time}%.00"
				else if length of {_timetexts::2} is 1:
					set {_timetext} to "%{_time}%0"
				else:
					set {_timetext} to "%{_time}%"
				send title "§c§l死んでしまった!" with subtitle "§3§l%{_timetext}% §b秒後に復活します..." to player for 3 ticks with fadein 0 second and fadeout 0 second
				apply blindness 1 to player for 1.5 seconds replacing existing effect
				player.setAllowFlight(true) if player.getAllowFlight() is false
				subtract 1 from {Kit-Spawning::%player%}
				wait a tick
			else:
				delete {Kit-Spawning::%player%}
		reset title of player
		player.setAllowFlight(false)
		loop all entities:
			type of loop-entity is armor stand
			metadata value "deadplayer" of loop-entity is "%player%"
			delete loop-entity
		if {Kit-DeadPlayer::%player%} is true:
			delete {Kit-DeadPlayer::%player%}
			if "%world of player%" is "Kit-SkillCheck":
				teleport player to {Kit-TestWorld}
				set player's gamemode to survival
				stop
			set {_tp} to a random location out of {Kit-GameStageRespawns.%{TrueTeam::%player%}%::*}
			teleport player to {_tp}
			set player's gamemode to survival
			{Kit-tochu} is true
			openKitInv(player)
			execute console command "/skclass %player% GameInv"
			apply blindness 1 to player for 10000000000 seconds
			apply slowness 10 to player for 10000000000 seconds
			apply jump boost -10 to player for 10000000000 seconds
			apply resistance 10 to player for 10000000000 seconds
			set {Kit-RespawnKill::%player%} to true
			set {Kit-RespawnPoint::%player%} to {_tp}
			loop 10 times:
				wait a second
				if {Kit-RespawnKill::%player%} is true:
					set {_dis} to distance between player and {_tp}
					if {_dis} >= 10:
						delete {Kit-RespawnKill::%player%}
					else:
						heal player by 10 hearts
			delete {Kit-RespawnKill::%player%}
			delete {Kit-RespawnPoint::%player%}
			send action bar "§dリスポーンキル防止の無敵化が解除されました!" to player
		else:  # 途中で復活させた際の処理
			log "まだ処理を作ってないよ"

on damage of player:
	{Kit-tochu} is true
	{Join::%victim%} is "Kit"
	{Join::%attacker%} is "Kit"
	if {Class::%victim%} is not set:
		cancel event
		send action bar "§4§lリスポーン直後の敵は攻撃できません!" to attacker
		stop
	if {Class::%attacker%} is not set:
		cancel event
		send action bar "§4§lクラスを選択してください!" to attacker
		stop
	if {Kit-RespawnKill::%victim%} is true:
		cancel event
		send action bar "§4§lリスポーン直後の敵は攻撃できません!" to attacker
	if {Kit-RespawnKill::%attacker%} is true:
		if distance between attacker and {Kit-RespawnPoint::%attacker%} <= 5:
			set damage to damage * 2
		else:
			set damage to damage * 1.5
		delete {Kit-RespawnKill::%attacker%}
		delete {Kit-RespawnPoint::%attacker%}
		send action bar "§dリスポーンキル防止の無敵化が解除されました!" to attacker



function spawnCorpse(player: player, loc: location):
	spawn armor stand at {_loc}
	set {_corpse} to spawned armor stand

	set metadata value "gametype" of {_corpse} to "Kit"
	set metadata value "deadplayer" of {_corpse} to "%{_player}%"

	{_corpse}.setInvulnerable(true)
	{_corpse}.setPersistenceRequired(true)
	{_corpse}.setBasePlate(false)
	{_corpse}.setGravity(false)
	{_corpse}.setSmall(true)
	{_corpse}.setInvisible(true)
	{_corpse}.setMarker(true)

	set {_helmet} to skull of {_player}
	set {_helmet} to {_helmet}.asItemType().createItemStack()
	{_corpse}.setHelmet({_helmet})

	set {_chestplate} to leather chestplate
	set {_team} to getTeam({_player})
	set {_color} to getTeamRGB({_team}).asBukkitColor()
	{_chestplate}.setColor({_color})
	{_corpse}.setChestplate({_chestplate})