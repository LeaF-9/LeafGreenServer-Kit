function kitCheckTeam(player1: entity, player2: entity, ally: boolean, all: boolean = false) :: boolean:
	if metadata value "gametype" of {_player1} is not "Kit":
		return false
	if metadata value "gametype" of {_player2} is not "Kit":
		return false

	if checkTestStand({_player1}, {_player2}, {_ally}) is true:
		return true

	set {_name1} to display name of {_player1}
	if {_name1} is not set:
		return false
	set {_name2} to display name of {_player2}
	if {_name2} is not set:
		return false
	if {_all} is true:
		set {_change} to "TrueTeam"
	else:
		set {_change} to "team"
	loop all players:
		if name of loop-player is {_name1}:
			set {_p1} to loop-player
		if name of loop-player is {_name2}:
			set {_p2} to loop-player
	if {_p1} is not set:
		return false
	if {_p2} is not set:
		return false
	if gamemode of {_p1} is spectator:
		return false
	if gamemode of {_p2} is spectator:
		return false
	if {_ally} is true:
		if {%{_change}%::%{_p1}%} is {%{_change}%::%{_p2}%}:
			if {-Stats.%{_player1}%::NoSupport} is true:
				return false
			else if {-Stats.%{_player2}%::NoSupport} is true:
				return false
			else:
				return true
		else:
			return false
	else:
		if {%{_change}%::%{_p1}%} is not {%{_change}%::%{_p2}%}:
			return true
		else:
			return false

local function checkTestStand(entity1: entity, entity2: entity, ally: boolean) :: boolean:
	set {_bool} to false

	set {_checkteam} to "red"
	if {_ally} is true:
		set {_checkteam} to "green"

	add "Test-Stand" to {_tests::*}
	add "Tester-2" to {_tests::*}

	if {_tests::*} contains display name of {_entity1}:
		if {team::%{_entity2}%} is {_checkteam}:
			set {_bool} to true
	else if {_tests::*} contains display name of {_entity2}:
		if {team::%{_entity1}%} is {_checkteam}:
			set {_bool} to true
	
	return {_bool}




function getTeam(player: player, all: boolean = false) :: text:
	set {_change} to "team"
	if {_all} is true:
		set {_change} to "TrueTeam"
	set {_team} to {%{_change}%::%{_player}%}
	if {_team} is not set:
		set {_team} to "white"
	return {_team}

function kitTeamColor(team: text) :: text:
	if {_team} is "red":
		set {_color} to "<##FF6464>"
	else if {_team} is "blue":
		set {_color} to "<##6464FF>"
	else if {_team} is "yellow":
		set {_color} to "<##FFFF64>"
	else if {_team} is "green":
		set {_color} to "<##64FF64>"
	else if {_team} is "bandit":
		set {_color} to "<##9600C8>"
	else:
		set {_color} to "<##FFFFFF>"
	return {_color}

function kitTeamRGB(team: text) :: color:
	if {_team} is "red":
		set {_color} to rgb(255, 0, 0)
	else if {_team} is "blue":
		set {_color} to rgb(0, 0, 255)
	else if {_team} is "yellow":
		set {_color} to rgb(255, 255, 0)
	else if {_team} is "green":
		set {_color} to rgb(0, 255, 0)
	else if {_team} is "bandit":
		set {_color} to rgb(255, 0, 255)
	else:
		set {_color} to rgb(255, 255, 255)
	return {_color}

function kitTeamList() :: texts:
	add "red" to {_teamlist::*}
	add "blue" to {_teamlist::*}
	add "yellow" to {_teamlist::*}
	add "green" to {_teamlist::*}
	return {_teamlist::*}