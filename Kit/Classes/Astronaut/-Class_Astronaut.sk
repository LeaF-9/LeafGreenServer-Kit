options:
	prefix: §2[%{CLS::%player%}%]
	attackprefix: §2[%{CLS::%attacker%}%]
	victimprefix: §2[%{CLS::%victim%}%]

# Astronautのスキル Ver1.0.0
on rightclick holding a eye of ender:
	name of player's tool is "§5Self Gravity"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 40
		remove slowness from player
		remove jump boost from the player
		apply speed 2 to the player for 10 seconds
		message "{@prefix} §5あなたの重力を少し歪めました..."
		play raw sound "mob.ghast.death" at the player with pitch 2 volume 1
		wait 10 seconds
		if {CLS::%player%} is "AST":
			apply jump boost 5 to the player for 300000000000 seconds
			apply slowness 2 to the player for 300000000000 seconds
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a slimeball:
	name of player's tool is "§8Heavy Gravity"
	cancel the event
	if {CT.%player%::%name of player's tool%} is not set:
		set {_tp} to the target player
		if {_tp} is set:
			if {team::%{_tp}%} is not {team::%player%}:
				set {CT.%player%::%name of player's tool%} to 50
				if {_tp} has slowness:
					set {_s} to true
				if {_tp} has jump boost:
					set {_j} to true
				remove slowness from target player
				remove jump boost from target player
				apply slowness 5 to {_tp} for 5 seconds
				apply jump boost -5 to {_tp} for 5 seconds
				drawDot count 1, particle "slime", center {_tp}, visibleRange 30, keepFor 100 ticks
				play raw sound "mob.wither.death" at {_tp} with pitch 2 volume 1
				message "{@prefix} §5%{_tp}%§8に強い重力を掛けました..."
				send "§4§l!!!!!とても体が重い!!!!!" to {_tp}
				wait 5 seconds
				remove jump boost from {_tp}
				remove slowness from {_tp}
				if {_s} is set:
					apply slowness 1 to {_tp} for 10000000000000 seconds
				if {_j} is set:
					apply jump boost 1 to {_tp} for 10000000000000 seconds
			else:
				set action bar of player to "§c§l味方プレイヤーには発動できません!"
		else:
			set action bar of player to "§c§l目先にプレイヤーがいません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a fire charge:
	name of player's tool is "§4Stardust Burn"
	set {_lore} to lore of player's tool
	shoot a fireball at speed 10
	set metadata value "stardust" of shot fireball to "true"
	if {_lore} is set:
		remove a fire charge named "§4Stardust Burn" with lore "%{_lore}%" from the player
	else:
		remove a fire charge named "§4Stardust Burn" from the player

on damage:
	damage was caused by projectile
	projectile is fireball
	if metadata value "stardust" of projectile is "true":
#		if {Ult.%attacker%::Astronaut} is true:
#			set damage to 10
#		else:
		set damage to 3
	else if metadata value "meteoric" of projectile is "true":
		set damage to 3

on damage of player:
	{CLS::%victim%} is "AST"
	block 0.1 below victim is air
	set damage to damage * 1.25

on rightclick holding a 11-disc:
	name of player's tool is "§0Black Hole"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		if {Stats.%player%::BlackHole} is true:
			message "{@prefix} §c現在発動中です..."
		else:
			set {_bhv} to location of block 2 above target block
			set {_tl} to target block
			if distance between {_tl} and player is more than 30:
				set action bar of player to "§c§l距離が遠すぎます!"
				stop
			set {_to} to location of target block to vector
			message "{@prefix} §5Black Holeを設置します..."
			play raw sound "portal.portal" at {_tl} with pitch 5 volume 1
			drawSphere style 1, particle "redstone", RGB 0, 0, 0, center {_bhv}, id "%player%", radius 1, density 50, visibleRange 30
			set {Stats.%player%::BlackHole} to true
			loop 60 times:
				loop all players:
					{team::%loop-player%} is not {team::%player%}
					{ClassChose::%loop-player%} is true
					set {_dis} to the distance between loop-player and {_tl}
					if {_dis} is less than 5:
						set {_from} to location of loop-player to vector
						set {_vector} to vector {_to} - {_from}
						set {_vector} to vector {_vector} normalized * 0.3
						set velocity of loop-player to {_vector}
				wait a tick
			delete {Stats.%player%::BlackHole}
			stopEffect "%player%"
			set {CT.%player%::§0Black Hole} to 50
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a magma cream:
	name of player's tool is "§1Extreme Gravity"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 50
		message "{@prefix} §1周辺の重力を歪めました!"
		play raw sound "portal.travel" at player with pitch 5 volume 1
		set {_cb} to location of block at player
		drawSphere style 1, particle "redstone", RGB 255, 0, 255, center {_cb}, id "%player%", radius 10, density 100, visibleRange 30
		loop 140 times:
			delete {_eg::*}
			loop all players in radius 10 around location of {_cb}:
				{team::%loop-player%} is not {team::%player%}
				drawDot count 5, particle "witchspell", center loop-player, visibleRange 30, keepFor 1 ticks
				set {_eg::%loop-player%} to true
				if {_jb::%loop-player%} is not set:
					set {_jb::%loop-player%} to true
					apply jump boost -2 to loop-player for 10 seconds
					set walk speed of loop-player to 0.05
					play raw sound "mob.wither.death" at loop-player with pitch 1.5 volume 0.5
			loop all players:
				{_eg::%loop-player%} is not set
				{_jb::%loop-player%} is true
				remove jump boost from loop-player
				set walk speed of loop-player to 0.2
				delete {_jb::%loop-player%}
			wait a tick
		loop all players:
			{_eg::%loop-player%} is true
			{_jb::%loop-player%} is true
			remove jump boost from loop-player
			set walk speed of loop-player to 0.2
		stopEffect "%player%"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a nether star:
	name of player's tool is "§6Meteoric Swarm"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {_tl} to target block
		if distance between {_tl} and player is more than 100:
			set action bar of player to "§c§l距離が遠すぎます!"
			stop
		message "{@prefix} §6流星群を降らせます!"
		set {CT.%player%::%name of player's tool%} to 40
		set {_base} to location of block at player
		loop 10 times:
			if block above {_base} is air:
				set {_base} to location of block above {_base}
		loop all blocks in radius 5 around location of {_base}:
			loop-block is air
			add location of loop-block to {_basing::*}
		loop all blocks in radius 3 around location of {_tl}:
			add location of loop-block to {_target::*}
		loop 5 times:
			set {_bs} to a random location out of {_basing::*}
			remove {_bs} from {_basing::*}
			set {_from} to location of {_bs} to vector
			set {_tg} to a random location out of {_target::*}
			remove {_tg} from {_target::*}
			set {_to} to location of {_tg} to vector
			set {_vector} to vector {_to} - {_from}
			set {_vector} to vector {_vector} normalized * 3
			spawn a fireball at {_bs}
			set metadata value "meteoric" of spawned fireball to "true"
			set shooter of spawned fireball to player
			drawDot count 1, particle "mobspell", center spawned fireball, rainbowMode true, visibleRange 30, keepFor 3 seconds
			set velocity of spawned fireball to {_vector}
			play raw sound "mob.wither.shoot" at spawned fireball with pitch 0.5 volume 0.75
			wait 5 ticks
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on projectile hit:
	projectile is fireball
	metadata value "meteoric" of projectile is "true"
	set {_l} to location of projectile
	wait a tick
	loop all blocks in radius 3 around location of {_l}:
		loop-block is fire
		set loop-block to air

on sneak toggle:
	{CLS::%player%} is "AST"
	{CT.%player%::GravityDrop} is not set
	player is not sneaking
	set {CT.%player%::GravityDrop} to 10
	set {_loc} to location of block below player
	block at {_loc} is air
	set {_check} to false
	set {_damage} to 1
	while {_check} is not true:
		set {_nums} to location of block below {_loc}
		set {_loc} to location of block at {_nums}
		if y-coordinate of {_loc} is less than 0:
			set {_check} to true
		else if block at {_loc} is air:
			add 1 to {_damage}
		else:
			set {_check} to true
	teleport player to block above {_loc}
	play raw sound "fireworks.largeBlast" at {_loc} with pitch 0.5 volume 10
	loop all players in radius 3 around block above {_loc}:
		{team::%loop-player%} is not {team::%player%}
		make player damage loop-player by {_damage}