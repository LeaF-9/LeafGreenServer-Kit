# weapon-name: Auric Cane

options:
	max-distance: 5
	max-tick: 10
	explosion-power: 3
	gold-min: 0
	gold-max: 2

function kitEffect_AuricCane(player: player):
	nameItemCT({_player}, "Auric Cane")
	set {_baseloc} to head of {_player}

	set {_m} to 0
	set {_addm} to {@max-distance} / {@max-tick}
	loop {@max-tick} times:
		if {_hit}:
			continue

		add {_addm} to {_m}
		set {_bulletloc} to location {_m} meters in front of {_baseloc}
		# entity hit check
		loop all entities in radius 1.5 around {_bulletloc}:
			kitCheckTeam(loop-entity, {_player}, false, true) is true
			set {_hit} to true

		# block hit check
		if block at {_bulletloc} is not passable:
			set {_bulletloc} to location {_addm} meters behind {_bulletloc}
			set {_hit} to true

		wait a tick
	kitExplosion({_player}, {@explosion-power}, {_bulletloc})
	set {_goldnum} to a random integer between {@gold-min} and {@gold-max}
	kitDropGolds_Rich({_player}, {_bulletloc}, {_goldnum})