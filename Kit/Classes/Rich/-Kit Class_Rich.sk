options:
	prefix: §2[%{CLS::%player%}%]
	attackprefix: §2[%{CLS::%attacker%}%]
	victimprefix: §2[%{CLS::%victim%}%]

# Richのスキル Ver1.0.0
on damage:
	attacker is holding golden sword
	name of attacker's tool is "§eRich Sword"
	if {-Stats.%attacker%::GoldBoost} is true:
		chance of 30%:
			drop a gold ingot named "%attacker%" at the victim
			set {_drop} to true
	else:
		chance of 30%:
			drop a gold nugget named "%attacker%" at the victim
			set {_drop} to true
	{_drop} is true
	loop all dropped items in radius 1.5 around victim:
		metadata value "delete" of loop-dropped item is not set
		set metadata value "delete" of loop-dropped item to "%attacker%"

on death:
	if {CLS::%{LastKiller::%victim%}%} is "RCH":
		add 3 gold ingot named "§eDrop Golds" to {LastKiller::%victim%}

on pick up gold nugget:
	if {CLS::%player%} is "THI":
		message "{@prefix} §6おっと! 良いものを拾った!"
		heal player by 0.5 hearts
		wait a tick
		remove all gold nugget named "%name of item%" from the player
	else:
		if name of item is not player:
			loop all players:
				name of item is loop-player
				loop-player is alive
				heal loop-players by 0.5 hearts
			wait a tick
			remove all gold nugget named "%name of item%" from the player
		else:
			cancel event

on pick up gold ingot:
	if {CLS::%player%} is "THI":
		message "{@prefix} §6おっと! 良いものを拾った!"
		heal player by 1 hearts
		wait a tick
		remove all gold ingot named "%name of item%" from the player
	else:
		if name of item is not player:
			loop all players:
				name of item is loop-player
				loop-player is alive
				heal loop-players by 1 hearts
			wait a tick
			remove all gold ingot named "%name of item%" from the player
		else:
			cancel event

on rightclick holding a blaze rod:
	name of player's tool is "§6Gold Boost"
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 60
		play raw sound "random.orb" at the player with pitch 0.5 volume 1
		message "{@prefix} §e10秒間、ドロップするアイテムが§6金インゴット§eになります!"
		set {-Stats.%player%::GoldBoost} to true
		wait 10 seconds
		delete {-Stats.%player%::GoldBoost}
		message "{@prefix} §e効果が切れました"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a gold block:
	name of player's tool is "§6Rich Ground"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {_tb} to targeted block
		if {_tb} is set:
			if block above {_tb} is air:
				set {CT.%player%::%name of player's tool%} to 50
				set {_gb} to location of block above {_tb}
				set block at {_gb} to gold block
				message "{@prefix} §e目先ブロック周辺のプレイヤー位置に金をドロップします!"
				loop 5 times:
					play raw sound "random.orb" at {_gb} with pitch 0.5 volume 1
					loop all players in radius 20 around location of {_gb}:
						play raw sound "random.orb" at loop-player with pitch 2 volume 1
						set {_lp} to location of block 2 above loop-player
						if {-Stats.%player%::GoldBoost} is true:
							chance of 50%:
								drop a gold ingot named "%player%" at {_lp}
								drawDot count 1, particle "itemcrack", material gold ingot, center {_lp}, visibleRange 30, keepFor 10 ticks
						else:
							chance of 50%:
								drop a gold nugget named "%player%" at {_lp}
								drawDot count 1, particle "itemcrack", material gold nugget, center {_lp}, visibleRange 30, keepFor 10 ticks
						loop all dropped items in radius 1.5 around location of {_lp}:
							metadata value "delete" of loop-dropped item is not set
							set metadata value "delete" of loop-dropped item to "%player%"
					wait a second
				set block at {_gb} to air
			else:
				set action bar of player to "§c§lその位置には設置できません!"
		else:
			set action bar of player to "§c§l目先にブロックがありません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a gold nugget:
	name of player's tool is "§6Gold Shower"
	if {CT.%player%::%name of player's tool%} is not set:
		set {_tb} to targeted block
		if {_tb} is set:
			set {CT.%player%::%name of player's tool%} to 40
			set {_sb} to location of block 8 above {_tb}
			set {_y} to y-coordinate of {_sb}
			loop all blocks in radius 5 around {_sb}:
				y-coordinate of loop-block is {_y}
				add location of loop-block to {_gs::*}
				drawDot count 5, particle "redstone", XYZ 255, 255, 0, center location of loop-block, visibleRange 32, keepFor 1 second
			message "{@prefix} §e目先のブロック上空から金塊を降らせます!"
			loop 10 times:
				set {_p} to a random location out of {_gs::*}
				if {-Stats.%player%::GoldBoost} is true:
					drop a gold ingot named "%player%" at {_p}
					play raw sound "random.orb" at {_p} with pitch 0.5 volume 1
				else:
					drop a gold nugget named "%player%" at {_p}
					play raw sound "random.orb" at {_p} with pitch 3 volume 1
				loop all dropped items in radius 1.5 around location of {_p}:
					metadata value "delete" of loop-dropped item is not set
					set metadata value "delete" of loop-dropped item to "%player%"
				wait 2 ticks
		else:
			set action bar of player to "§c§l目先にブロックがありません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a blaze spawn egg:
	name of player's tool is "§6Gold Pinata"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 50
		play raw sound "random.orb" at the player with pitch 1.5 volume 1
		set {_p} to location of player
		add 1 to y-coordinate of {_p}
		shoot a falling gold block from location of {_p} at speed 1
		push shot entity upwards at speed 1.2
		set metadata value "goldpinata" of shot entity to "%player%"
		drawDot count 5, particle "redstone", XYZ 255, 255, 0, center shot entity, visibleRange 32, keepFor 0.5 second
		wait 0.5 seconds
		loop all falling gold blocks:
			metadata value "goldpinata" of loop-falling gold block is "%player%"
			set {_l} to location of loop-falling gold block
			delete loop-falling gold block
			make a fake explosion at {_l}
			set {_n} to a random integer between 10 and 15
			loop {_n} times:
				if {-Stats.%player%::GoldBoost} is true:
					drop a gold ingot named "%player%" at {_l}
				else:
					drop a gold nugget named "%player%" at {_l}
			loop all dropped items in radius 1 around location of {_l}:
				push loop-dropped item north at speed random number from -1 to 1
				push loop-dropped item east at speed random number from -1 to 1
				if metadata value "delete" of loop-dropped item is not set:
					set metadata value "delete" of loop-dropped item to "%player%"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a glowstone dust:
	name of player's tool is "§eWealthy Present"
	if {CT.%player%::%name of player's tool%} is not set:
		set {_tp} to targeted player
		if {_tp} is set:
			if {team::%{_tp}%} is {team::%player%}:
				if {CLS::%{_tp}%} is not "BSK":
					set {CT.%player%::%name of player's tool%} to 40
					message "{@prefix} §6%{_tp}% §eに金の加護を与えます!"
					message "§e%player% §6に金の加護を授けられました!" to {_tp}
					apply absorption 3 to {_tp} for 10 seconds
					drawDot count 3, particle "mobspell", XYZ 255, 255, 0, center {_tp}, visibleRange 32, keepFor 10 second
					loop 10 times:
						play raw sound "random.orb" at {_tp} with pitch 1.5 volume 1
						loop all blocks in radius 5 around {_tp}:
							y-coordinate of loop-block is y-coordinate of block at {_tp}
							loop-block is air
							add location of loop-block to {_gs::*}
						set {_n} to a random integer between 0 and 2
						loop {_n} times:
							set {_p} to a random location out of {_gs::*}
							remove {_p} from {_gs::*}
							if {-Stats.%player%::GoldBoost} is true:
								drop a gold ingot named "%player%" at {_p}
							else:
								drop a gold nugget named "%player%" at {_p}
							loop all dropped items in radius 1.5 around location of {_p}:
								metadata value "delete" of loop-dropped item is not set
								set metadata value "delete" of loop-dropped item to "%player%"
						delete {_gs::*}
						wait a second
				else:
					set action bar of player to "§c§lこのプレイヤーには付与したくありません"
			else:
				set action bar of player to "§c§l味方プレイヤーにのみ発動可能です"
		else:
			set action bar of player to "§c§l目先にプレイヤーがいません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a gold ingot:
	name of player's tool is "§eDrop Golds"
	cancel event
	if {-Stats.%player%::GoldBoost} is true:
		drop a gold ingot named "%player%" at player
		play raw sound "random.orb" at the player with pitch 0.5 volume 1
	else:
		drop a gold nugget named "%player%" at the player
		play raw sound "random.orb" at the player with pitch 3 volume 1
	loop all dropped items in radius 1.5 around player:
		metadata value "delete" of loop-dropped item is not set
		set metadata value "delete" of loop-dropped item to "%player%"
	remove a gold ingot named "§eDrop Golds" from player

on player eating of 322:
	{CLS::%player%} is "RCH"
	if {-Stats.%player%::GoldBoost} is true:
		drop 1 gold ingot named "%player%" at the player
	else:
		drop 1 gold nugget named "%player%" at the player
	loop all dropped items in radius 1.5 around player:
		metadata value "delete" of loop-dropped item is not set
		set metadata value "delete" of loop-dropped item to "%player%"
	wait a tick
	remove regeneration from player
	remove absorption from player
	apply absorption 1 to player for 10 seconds
	apply regeneration 1 to player for 5 seconds

every 3 seconds:
	loop all players:
		{CLS::%loop-player%} is "RCH"
		loop-player's health is less than 3
		if {-Stats.%loop-player%::GoldBoost} is true:
			drop 1 gold ingot named "%loop-player%" at loop-player's location
		else:
			drop 1 gold nugget named "%loop-player%" at loop-player's location
		loop all dropped items in radius 1.5 around loop-player:
			metadata value "delete" of loop-dropped item is not set
			set metadata value "delete" of loop-dropped item to "%loop-player%"