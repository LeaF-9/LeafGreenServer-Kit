# Enormous Hurricane

function kitUltKnocker(player: player):
	set {_loc} to location of {_player}
	loop 200 times:
		# 竜巻中心点の設定
		set {_loc} to location 0.15 meters in front of {_loc}
		if block at {_loc} is not passable:
			set {_loc} to location 0.1 meters above {_loc}
		else if block 0.1 meters below {_loc} is passable:
			set {_loc} to location 0.1 meters below {_loc}
		# 演出関数
		kitParticle_EnormousHurricane_Hurricane({_loc}, loop-number)
		# 吹き飛ばし効果
		loop all entities:
			kitCheckTeam({_player}, loop-entity, false) is true
			# 効果範囲チェック
			set {_checkloc} to location of loop-entity
			if y-pos of {_checkloc} < y-pos of {_loc} - 5:
				continue
			if y-pos of {_checkloc} > y-pos of {_loc} + 30:
				continue
			set y-pos of {_checkloc} to y-pos of {_loc}
			if distance between {_loc} and {_checkloc} > 15:
				continue
			# vector設定
			set {_v} to setVector({_loc}, {_checkloc})
			set {_vl} to setDirToVec(location of loop-entity, {_v})
			set {_vyaw} to yaw of {_vl} - 80
			set {_vpitch} to -30
			set {_pushvec} to vector from yaw {_vyaw} and pitch {_vpitch}
			set {_pushvec} to normalized {_pushvec} * vector(0.5, 0.5, 0.5)
			# 吹き飛ばし
			set velocity of loop-entity to {_pushvec}
			# ヒット演出
			kitParticle_EnormousHurricane_Hit(loop-entity, {_pushvec})
		wait a tick

# 竜巻演出
function kitParticle_EnormousHurricane_Hurricane(baseloc: location, ln: number):
	# 音
	chance of 10%:
		set {_pitch} to a random number between 0.5 and 2
		playSound("entity.wither.shoot", 1, {_pitch}, {_baseloc})
	# 見た目
	set {_baserad} to {_ln} * 15
	set {_m} to 5
	loop 60 times:
		add 0.17 to {_m}
		add 0.5 to {_y}
		loop 10 times:
			set {_rotrad} to {_baserad} + 36 * loop-number-2
			set {_rad} to {_rotrad} + 20 * loop-number-1
			set {_el} to {_baseloc}
			add {_m} * sin({_rad}) to x-pos of {_el}
			add {_m} * cos({_rad}) to z-pos of {_el}
			add {_y} to y-pos of {_el}
			set {_size} to a random number between 0.5 and 2.5
			chance of 10%:
				spawnParticle_DustTransition(rgb(187, 226, 241), rgb(160, 216, 239), {_size}, {_el})

# 風演出
function kitParticle_EnormousHurricane_Hit(entity: entity, vector: vector):
	# 音
	set {_pitch} to a random number between 1 and 2
	playSound("entity.phantom.flap", 0.75, {_pitch}, {_entity})
	# 見た目
	loop 1 times:
		set {_baseel} to location of {_entity}
		add a random number between -1 and 1 to x-pos of {_baseel}
		add a random number between -1 and 1 to z-pos of {_baseel}
		add a random number between 0 and 2 to y-pos of {_baseel}
		set {_baseel} to setDirToVec({_baseel}, {_vector})
		set {_m} to 0
		loop 10 times:
			add 0.1 to {_m}
			set {_el} to location {_m} meters in front of {_baseel}
			spawnParticle_Dust(rgb(235, 246, 247), 0.5, {_el})