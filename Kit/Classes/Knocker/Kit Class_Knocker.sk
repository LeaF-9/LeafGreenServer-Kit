# Knockerのスキル Ver1.0.0

# Weapons

function kitWeaponKnocker(player: player):
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	checkCT({_player}, name of {_tool}) is true
	if {_item.num} is 2:
		kitWeapon_Knocker_2({_player})

# Skills
function kitSkillKnocker(player: player) :: boolean:
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	if checkCT({_player}, name of {_tool}) is true:
		if {_item.num} is 1:
			kitSkill_Knocker_1({_player})
		else if {_item.num} is 2:
			kitSkill_Knocker_2({_player})
		else if {_item.num} is 3:
			kitSkill_Knocker_3({_player})
		else if {_item.num} is 4:
			kitSkill_Knocker_4({_player})
		else if {_item.num} is 5:
			kitSkill_Knocker_5({_player})
		else if {_item.num} is 6:
			kitSkill_Knocker_6({_player})
		else if {_item.num} is 7:
			kitSkill_Knocker_7({_player})
		else if {_item.num} is 8:
			kitSkill_Knocker_8({_player})
	return true

# Abilities
on death of player:
	{Class::%{LastKiller::%victim%}%} is "Knocker"
	give 8 arrows to {LastKiller::%victim%}

on damage of player:
	metadata value "gametype" of victim is "Kit"
	damage was caused by fall
	if {Stats.%victim%::FallTiming} is set:
		set damage to {Stats.%victim%::FallDamage}
		delete {Stats.%victim%::FallTiming}
		delete {Stats.%victim%::FallDamage}
	else if {Stats.%victim%::FallRemove} is set:
		set damage to damage * 0.1
		delete {Stats.%victim%::FallRemove}

on damage:
	metadata value "gametype" of attacker is "Kit"
	{Class::%attacker%} is "Knocker"
	damage was caused by projectile
	set damage to damage * 0.8

# Timer
function kitTimerKnocker(player: player):
	while {Class::%{_player}%} is "Knocker":
		set {_tool} to {_player}'s tool
		set {_item.type} to kitItemTypeChecker({_tool})
		set {_item.num} to kitItemNumberChecker({_tool})
		add 1 to {_timer}
		if {_timer} >= 20:
			set {_timer} to 0
		if {_timer} = 0:
			if {Stats.%{_player}%::SpatialDistorter} is set:
				if {Stats.%{_player}%::SpatialDistorter} < 60:
					add 1 to {Stats.%{_player}%::SpatialDistorter}
		if {_item.type} is "Weapon.main":
			if {_item.num} is 2:
				kitWeaponSub_Knocker_2({_player})
		else if {_item.type} is "Skill":
			if {_item.num} is 4:
				kitSkill_LinkageChain_ActionBar({_player})
		wait a tick