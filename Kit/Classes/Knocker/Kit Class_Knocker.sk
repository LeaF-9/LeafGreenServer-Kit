# Knockerのスキル Ver1.0.0

# Abilities
on death of player:
	{Class::%{LastKiller::%victim%}%} is "Knocker"
	give 8 arrows to {LastKiller::%victim%}

on damage of player with priority LOW:
	metadata value "gametype" of victim is "Kit"
	damage was caused by fall
	if kitStats_GET(victim, "FallTiming") is set:
		set {_FallDamage} to kitStats_GET(victim, "FallDamage")
		setDamageModification_SET(victim, {_FallDamage})
		kitStats_DELETE(victim, "FallTiming")
		kitStats_DELETE(victim, "FallDamage")
	else if kitStats_GET(victim, "FallRemove") is set:
		setDamageModification_MUL(victim, 0.1)
		kitStats_DELETE(victim, "FallRemove")

# Timer
function kitTimerKnocker(player: player):
	while {Class::%{_player}%} is "Knocker":
		set {_tool} to {_player}'s tool
		set {_item.type} to kitItemTypeChecker({_tool})
		set {_item.num} to kitItemNumberChecker({_tool})
		add 1 to {_timer}
		if {_timer} >= 20:
			set {_timer} to 0
		if {_timer} = 0:
			if kitStats_GET({_player}, "SpatialDistorter") is set:
				if kitStats_GET({_player}, "SpatialDistorter") < 60:
					set {_SpatialDistorter} to kitStats_GET({_player}, "SpatialDistorter")
					add 1 to {_SpatialDistorter}
					kitStats_SET({_player}, "SpatialDistorter", {_SpatialDistorter})
		if {_item.type} is "Weapon":
			if {_item.num} is 2:
				kitWeaponSub_Knocker_2({_player})
		else if {_item.type} is "Skill":
			if {_item.num} is 4:
				kitSkill_LinkageChain_ActionBar({_player})
		wait a tick