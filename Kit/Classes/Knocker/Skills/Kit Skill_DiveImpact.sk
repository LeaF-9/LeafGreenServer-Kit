# Dive Impact

function kitEffect_DiveImpact(player: player):
	nameCT({_player}, "Dive Impact")
	kitMsg({_player}, "§c地面に衝撃を与えて敵を吹き飛ばします!")
	setKitStats({_player}, "DiveImpact", true)
	set velocity of {_player} to vector(0, 1.65, 0)
	playSound("entity.wither.shoot", 1, 0.8, {_player})
	loop 5 times:
		spawnParticle_BlockData(falling_dust, light blue concrete powder, {_player})
		wait a tick
	loop 100 times:
		if {_t} is not set:
			if block below {_player} is not passable:
				set {_t} to true
				playSound("entity.wither.spawn", 1, 0.5, {_player})
				loop all blocks in radius 5 around {_player}:
					(y-pos of (loop-block)) is (y-pos of (block at ({_player})))
					loop-block is passable
					kitParticle_DiveImpact(location of loop-block)
			else:
				spawnParticle_BlockData(falling_dust, white wool, {_player})
				wait a tick
	loop all entities in radius 5 around {_player}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		block below loop-entity is not passable
		make a fake explosion at loop-entity
		kitDamage({_player}, loop-entity, 2, "落下の衝撃")
		set velocity of loop-entity to vector(0, 1.1, 0)
		kitEffectDescription(loop-entity, "§4❤2")

local function kitParticle_DiveImpact(loc: location):
	loop 5 times:
		spawnParticle_Block(purple wool, {_loc})
		wait a tick

on damage of player:
	metadata value "gametype" of victim is "Kit"
	damage was caused by fall
	getKitStats(victim, "DiveImpact") is true
	cancel event
	deleteKitStats(victim, "DiveImpact")