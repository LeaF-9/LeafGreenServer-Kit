# Sylphy Mischief

function kitEffect_SylphyMischief(player: player):
	nameCT({_player}, "Sylphy Mischief")
	kitMsg({_player}, "§2風の妖精 §aが§e§lイタズラ§aをしにやって来ました!")
	sendTimeBossbar_GREEN({_player}, "Sylphy Mischief", 200)
	playSound("item.trident.riptide_3", 1, 2, {_player})
	set {_baseloc} to location of head of {_player}
	loop 10 times:
		add {_baseloc} to {_sylphies::*}
	
	add rgb(152, 251, 152) to {_colors.sylphy::*}
	add rgb(172, 238, 186) to {_colors.sylphy::*}
	add rgb(118, 243, 156) to {_colors.sylphy::*}

	add rgb(127, 255, 212) to {_colors.aura::*}
	add rgb(101, 238, 215) to {_colors.aura::*}
	add rgb(161, 238, 221) to {_colors.aura::*}

	loop 200 times:
		loop {_sylphies::*}:
			set {_sl} to loop-value-2
			delete {_ok}
			loop 10 times:
				{_ok} is not set
				set {_dl} to {_sl}
				set {_dv} to randomVector()
				set yaw of {_dl} to yaw of {_dv}
				set pitch of {_dl} to pitch of {_dv}
				if block at location 1 meters in front of {_dl} is passable:
					set {_ok} to true
			if {_ok} is true:
				set {_m} to a random number between 0 and 1
				set {_sl} to location {_m} meters in front of {_dl}
				set {_sylphies::%loop-index%} to {_sl}
			
			set {_count} to a random integer between 0 and 2
			loop {_count} times:
				set {_el} to moveRandomLocation({_sl}, 0.5)
				set {_color} to a random element out of {_colors.sylphy::*}
				spawnParticle_Dust({_color}, 1, {_el})
			
			loop 20 times:
				set {_vec} to randomVector(3)
				set {_el} to {_sl} ~ {_vec}
				chance of 3%:
					set {_color} to a random element out of {_colors.aura::*}
					spawnParticle_Color(entity_effect, {_color}, {_el})

			loop all entities in radius 3 around {_sl}:
				kitCheckTeam(loop-entity, {_player}, false) is true
				LastKiller(loop-entity, {_player})
				set {_to} to location of loop-entity
				set {_vector} to setVector({_sl}, {_to}, 0.5)
				set velocity of loop-entity to {_vector}
				playSound("item.trident.throw", 0.5, 2, loop-entity)
		wait a tick
	kitMsg({_player}, "§2風の妖精 §bは去っていきました")