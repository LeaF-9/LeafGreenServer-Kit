# Separate Field

function kitEffect_SeparateField(player: player):
	nameCT({_player}, "Separate Field")
	kitMsg({_player}, "§b3秒間敵を近くに寄せ付けません!")
	sendTimeBossbar_GREEN({_player}, "Separate Field", 60)
	apply mining fatigue 2 to {_player} for 3 seconds
	playSound("block.portal.trigger", 0.5, 1, {_player})
	set {_now} to now
	loop 60 times:
		set {_from} to location of {_player}
		loop all entities in radius 10 around {_player}:
			kitCheckTeam(loop-entity, {_player}, false) is true
			LastKiller(loop-entity, {_player})
			if checkEntityType(loop-entity, player) is true:
				chance of 30%:
					playSound("entity.breeze.idle_air", 1, 2, loop-entity)
				if getKitStats(loop-entity, "FallRemove") is not set:
					setKitStats(loop-entity, "FallRemove", {_now})
			set {_to} to location of loop-entity
			set {_vector} to setVector({_from}, {_to}, 0.5)
			set velocity of loop-entity to {_vector}
		
		particleSphere({_from})
		wait a tick
	kitMsg({_player}, "§7§lSeparate Field §9の効果が切れました")
	wait 3 seconds
	loop all players:
		getKitStats(loop-player, "FallRemove") is {_now}
		deleteKitStats(loop-player, "FallRemove")



local function particleSphere(loc: location):
	add rgb(100, 255, 255) to {_colors::*}
	add rgb(95, 196, 243) to {_colors::*}
	add rgb(141, 243, 235) to {_colors::*}

	set {_count} to 100
	loop {_count} times:
		set {_vec} to randomVector(10)
		set {_el} to {_loc} ~ {_vec}
		chance of 50%:
			set {_color} to a random element out of {_colors::*}
			spawnParticle_Dust({_color}, 1.5, {_el})