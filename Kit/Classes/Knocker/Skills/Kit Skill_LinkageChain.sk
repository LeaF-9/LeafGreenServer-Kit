# Linkage Chain

function kitEffect_LinkageChain(player: player):
	nameCT({_player}, "Linkage Chain")
	kitMsg({_player}, "§8チェーンを発射します...")
	set {_baseloc} to location of head of {_player}
	set {_m} to 0
	loop 30 times:
		{_target} is not set
		add 1 to {_m}
		set {_frontloc} to location {_m} meters in front of {_baseloc}
		playSound("block.chain.place", 1, 1, {_frontloc})
		loop all players in radius 2 around {_frontloc}:
			{_target} is not set
			kitCheckTeam({_player}, loop-player, false) is true
			set {_target} to loop-player
			setKitStats(loop-player, "LinkageChain", true)
			setKitStats({_player}, "LinkageChain", true)
		kitKnockerChainParticles({_baseloc}, {_m})
		wait a tick
	if {_target} is set:
		kitMsg({_player}, "§6%{_target}% §7がチェーンに引っ掛かった!")
		message formatted "§8チェーンに引っ掛かってしまった..." to {_target}
		kitEffectDescription({_target}, "§8§l拘束")
		sendTimeBossbar_GREEN({_player}, "Linkage Chain", 100)
		sendTimeBossbar_RED({_target}, "§8拘束", 100)
		playSound("block.glass.break", 1, 0.5, {_target})
		set {_max} to {_m} + 5
		loop 100 times:
			getKitStats({_player}, "LinkageChain") is true
			getKitStats({_target}, "LinkageChain") is true
			kitKnockerConnectParticles({_player}, {_target})
			set {_dis} to distance between {_player} and {_target}
			if {_dis} < {_m}:
				LastKiller({_target}, {_player})
				playSound("block.chain.hit", 0.8, 1, {_player})
				playSound("block.chain.hit", 0.8, 1, {_target})
				set {_vector} to setVector(location of {_player}, location of {_target}, 0.5)
				set velocity of {_target} to {_vector}
			if {_dis} > {_max}:
				LastKiller({_target}, {_player})
				playSound("block.chain.hit", 0.8, 1, {_player})
				playSound("block.chain.hit", 0.8, 1, {_target})
				set {_vector} to setVector(location of {_target}, location of {_player}, 0.5)
				set velocity of {_target} to {_vector}
			wait a tick
		kitMsg({_player}, "§eチェーンが外れました")
		message formatted "§7チェーンが取り外された!" to {_target}

options:
	radiusm: 0.25

local function kitKnockerChainParticles(baseloc: location, meter: number):
	set {_m} to 0
	loop {_meter} times:
		add 1 to {_m}
		set {_horim} to {_m} - 0.75
		set {_vertm} to {_m} - 0.25
		set {_horiloc} to location {_horim} meters in front of {_baseloc}
		set {_horiCreateLoc} to {_baseloc}
		add 90 to pitch of {_horiCreateLoc}
		set {_horiBaseVec} to setVector(({_horiCreateLoc}), (location 10 meters in front of {_horiCreateLoc}))

		set {_vertloc} to location {_vertm} meters in front of {_baseloc}
		set {_vertBaseVec} to setVector(({_baseloc}), (location 10 meters to the left of {_baseloc}))

		set {_rotateVec} to setVector(({_baseloc}), (location 10 meters in front of {_baseloc}), ({@radiusm}))

		# horizontal(水平) chain作成
		set {_rad} to 0
		loop 10 times:
			set {_vec} to {_rotateVec}
			rotate {_vec} around {_horiBaseVec} by {_rad}
			set {_h.eb} to {_horiloc} ~ {_vec}
			chance of 10%:
				spawnParticle_Dust(rgb(119, 136, 153), 1, {_h.eb})
			add 36 to {_rad}

		# vertical(垂直) chain作成
		set {_rad} to 0
		loop 10 times:
			set {_vec} to {_rotateVec}
			rotate {_vec} around {_vertBaseVec} by {_rad}
			set {_v.eb} to {_vertloc} ~ {_vec}
			chance of 10%:
				spawnParticle_Dust(rgb(112, 128, 144), 1, {_v.eb})
			add 36 to {_rad}

local function kitKnockerConnectParticles(player1: player, player2: player):
	set {_dis} to distance between {_player1} and {_player2}
	set {_dis} to round({_dis})
	set {_vector} to setVector({_player1}, {_player2})
	set {_baseloc} to location of {_player1}
	add 1 to y-pos of {_baseloc}
	set yaw of {_baseloc} to yaw of {_vector}
	set pitch of {_baseloc} to pitch of {_vector}

	set {_m} to 0
	loop {_dis} times:
		add 1 to {_m}
		set {_horim} to {_m} - 0.75
		set {_vertm} to {_m} - 0.25
		set {_horiloc} to location {_horim} meters in front of {_baseloc}
		set {_horiCreateLoc} to {_baseloc}
		add 90 to pitch of {_horiCreateLoc}
		set {_horiBaseVec} to setVector(({_horiCreateLoc}), (location 10 meters in front of {_horiCreateLoc}))

		set {_vertloc} to location {_vertm} meters in front of {_baseloc}
		set {_vertBaseVec} to setVector(({_baseloc}), (location 10 meters to the left of {_baseloc}))

		set {_rotateVec} to setVector(({_baseloc}), (location 10 meters in front of {_baseloc}), ({@radiusm}))

		# horizontal(水平) chain作成
		set {_rad} to 0
		loop 10 times:
			set {_vec} to {_rotateVec}
			rotate {_vec} around {_horiBaseVec} by {_rad}
			set {_h.eb} to {_horiloc} ~ {_vec}
			chance of 10%:
				spawnParticle_Dust(rgb(119, 136, 153), 1, {_h.eb})
			add 36 to {_rad}

		# vertical(垂直) chain作成
		set {_rad} to 0
		loop 10 times:
			set {_vec} to {_rotateVec}
			rotate {_vec} around {_vertBaseVec} by {_rad}
			set {_v.eb} to {_vertloc} ~ {_vec}
			chance of 10%:
				spawnParticle_Dust(rgb(112, 128, 144), 1, {_v.eb})
			add 36 to {_rad}