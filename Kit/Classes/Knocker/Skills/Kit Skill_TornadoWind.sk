# Tornado Wind

function kitEffect_TornadoWind(player: player):
	nameCT({_player}, "Tornado Wind")
	kitMsg({_player}, "§9周辺のプレイヤー§bを吹き飛ばします!")
	playSound("entity.wither.shoot", 1, 1, {_player})
	set {_center} to location of {_player}
	set {_now} to now
	loop all entities in radius 10 around {_player}:
		kitCheckTeam({_player}, loop-entity, false) is true
		LastKiller(loop-entity, {_player})
		add location of loop-entity to {_dm::*}
		push loop-entity upwards at speed 2
		if checkEntityType(loop-entity, player) is true:
			setKitStats(loop-entity, "FallTiming", {_now})
			setKitStats(loop-entity, "FallDamage", 3)
	set {_y} to 0
	set {_rad} to 0
	loop 5 times:
		loop 40 times:
			add 10 to {_rad}
			add 0.1 to {_y}
			set {_vec} to vector(10, 0, 0)
			rotate {_vec} around y-axis by {_rad}
			set {_el} to {_center} ~ {_vec}
			add {_y} to y-pos of {_el}
			spawnParticle_Dust(rgb(199, 227, 255), 3, {_el})

			loop {_dm::*}:
				set {_el} to loop-value-3
				set {_vec} to vector(1, 0, 0)
				rotate {_vec} around y-axis by {_rad}
				set {_el} to {_el} ~ {_vec}
				add {_y} to y-pos of {_el}
				spawnParticle_Dust(rgb(199, 227, 255), 2, {_el})
		wait a tick
	wait 5 seconds
	loop all players:
		getKitStats(loop-player, "FallTiming") is {_now}
		deleteKitStats(loop-player, "FallTiming")
		deleteKitStats(loop-player, "FallDamage")