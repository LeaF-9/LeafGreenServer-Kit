# Tornado Wind

function kitEffect_TornadoWind(player: player):
	nameCT({_player}, "Tornado Wind")
	kitMsg({_player}, "§9周辺のプレイヤー§bを吹き飛ばします!")
	playSound("entity.wither.shoot", 1, 1, {_player})
	set {_center} to location of {_player}
	set {_now} to now
	loop all entities in radius 10 around {_player}:
		kitCheckTeam({_player}, loop-entity, false) is true
		LastKiller(loop-entity, {_player})
		add location of loop-entity to {_dm::*}
		push loop-entity upwards at speed 2
		if checkEntityType(loop-entity, player) is true:
			kitStats_SET(loop-entity, "FallTiming", {_now})
			kitStats_SET(loop-entity, "FallDamage", 3)
	set {_y} to 0
	set {_rad} to 0
	loop 5 times:
		loop 40 times:
			add 10 to {_rad}
			add 0.1 to {_y}
			set {_el} to {_center}
			add 10 * sin({_rad}) to x-coordinate of {_el}
			add 10 * cos({_rad}) to z-coordinate of {_el}
			add {_y} to y-coordinate of {_el}
			spawnParticle_Dust(rgb(199, 227, 255), 1, {_el})
			loop {_dm::*}:
				set {_el} to loop-value-3
				add 1 * sin({_rad}) to x-coordinate of {_el}
				add 1 * cos({_rad}) to z-coordinate of {_el}
				add {_y} to y-coordinate of {_el}
				spawnParticle_Dust(rgb(199, 227, 255), 1, {_el})
		wait a tick
	wait 5 seconds
	loop all players:
		kitStats_GET(loop-player, "FallTiming") is {_now}
		kitStats_DELETE(loop-player, "FallTiming")
		kitStats_DELETE(loop-player, "FallDamage")