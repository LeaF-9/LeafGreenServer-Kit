# Mischievous Fairies

options:
	metadata-nodamage: "Mischievous Fairies - no damage"



function kitUltPaladin(player: player):
	loop all players:
		kitCheckTeam(loop-player, {_player}, true, true) is true
		if loop-player is not {_player}:
			setSupporter(loop-player, {_player})
			message formatted "<##e95388>§l%{_player}% <##ea618e>§lより妖精の加護を受け取りました!" to loop-player
		add loop-player to {_palult::*}
		setKitStats(loop-player, "Ult_Paladin", 10)
		set {_uuid} to UUID of loop-player
		set {_id} to "Mischievous Fairies-%{_uuid}%"
		set {_timer.title} to "§d§l妖精の加護§r §4❤10"
		create bossbar titled {_timer.title} and id {_id} for loop-player with progress 100 with colors pink with style solid
	loop 200 times:
		loop {_palult::*}:
			set {_loop-player} to loop-value-2
			set {_uuid} to UUID of {_loop-player}

			if getKitStats({_loop-player}, "Ult_Paladin") is set:
				chance of 20%:
					playSound("entity.bee.sting", 0.6, 2, {_loop-player})
				set {_num} to 0
				loop 3 times:
					add 1 to {_num}
					set {_color::%{_num}%} to a random integer between 0 and 255
				set {_el} to aroundEffect({_loop-player})
				spawnParticle_Color(entity_effect, rgb({_color::1}, {_color::2}, {_color::3}), {_el})

				set {_id} to "Mischievous Fairies-%{_uuid}%"
				set {_UltPaladin} to getKitStats({_loop-player}, "Ult_Paladin")
				set {_timer.title} to "§d§l妖精の加護§r §4❤%{_UltPaladin}%"
				set bossbar {_id} title to {_timer.title}

				set {_timer.prog} to {_UltPaladin} * 10
				set bossbar {_id} value to {_timer.prog}
			else:
				set {_id} to "Mischievous Fairies-%{_uuid}%"
				remove bossbar {_id}
				remove {_loop-player} from {_palult::*}
		wait a tick
	loop {_palult::*}:
		set {_loop-player} to loop-value
		set {_uuid} to UUID of {_loop-player}
		deleteKitStats({_loop-player}, "Ult_Paladin")
		message formatted "<##915da3>妖精の加護は消滅しました" to {_loop-player}
		set {_id} to "Mischievous Fairies-%{_uuid}%"
		remove bossbar {_id}

on damage of player with priority HIGHEST:
	getKitStats(victim, "Ult_Paladin") is set
	cancel event
	playSound("entity.iron_golem.repair", 1, 2, victim)

	# 重複確認
	set {_metadata} to metadata value {@metadata-nodamage} of victim
	if {_metadata} is set:
		set {_delay} to 10 ticks
		set {_now} to now
		set {_dif} to difference between {_metadata} and {_now}
		if {_dif} < {_delay}:
			stop
		delete metadata value {@metadata-nodamage} of victim

	set {_now} to now
	set metadata value {@metadata-nodamage} of victim to {_now}

	if damage cause is fall:
		stop

	set {_rawdamage} to getDamageModification_ALL(victim, damage)
	set {_UltPaladin} to getKitStats(victim, "Ult_Paladin")
	set {_ulthp} to {_UltPaladin} - {_rawdamage}
	if {_ulthp} <= 0:
		set {_attacker} to getAttacker(victim)
		deleteKitStats(victim, "Ult_Paladin")
		message formatted "<##4d4398>加護が崩れた! <##dc6b9a>妖精の回復魔法が付与されます...!" to victim
		apply regeneration 1 to victim for 10 seconds
		if {_attacker} exists:
			apply poison 1 to {_attacker} for 10 seconds
			message formatted "<##8d93c8>妖精のいたずらが身体を蝕む..." to {_attacker}
		loop 5 times:
			set {_baseloc} to location of victim
			set {_rad} to 0
			add 0.4 to {_high}
			loop 10 times:
				add 36 to {_rad}
				set {_el} to {_baseloc}
				add 0.5 * sin({_rad}) to x-coordinate of {_el}
				add 0.5 * cos({_rad}) to z-coordinate of {_el}
				add {_high} to y-coordinate of {_el}
				set {_num} to 0
				loop 3 times:
					add 1 to {_num}
					set {_color::%{_num}%} to a random integer between 0 and 255
					spawnParticle_Color(entity_effect, rgb({_color::1}, {_color::2}, {_color::3}), {_el})
			wait a tick
	else:
		setKitStats(victim, "Ult_Paladin", {_ulthp})