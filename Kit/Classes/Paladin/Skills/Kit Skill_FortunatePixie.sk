# Fortunate Pixie
options:
	delay-tick: 10

on damage of player:
	getKitStats(victim, "FortunatePixie") is set
	set {_now} to now
	setKitStats(victim, "FortunatePixie-Time", {_now})
	set {_dmg} to getDamageModification_ALL(victim, damage)
	kitParticle_FortunatePixie_Damage(victim)
	loop {@delay-tick} times:
		wait a tick
	getKitStats(victim, "FortunatePixie-Time") is {_now}
	kitEffectDescription(victim, "§d§l回復")
	kitParticle_FortunatePixie_Heal(victim)
	kitEffect_ForTunatePixie_Healing(victim, {_dmg}, {_now})



function kitEffect_FortunatePixie(player: player):
	nameCT({_player}, "Fortunate Pixie")
	kitMsg({_player}, "§d幸運の妖精が飛び回ります!")
	# Particles
	playSound("entity.player.levelup", 1, 0.5, {_player})
	set {_baseloc} to head of {_player}
	set {_axisVector} to getVectorFromRollAxis({_baseloc})
	set {_rotateVector} to getVectorFromYawAxis({_baseloc}, 5)
	set {_smallRotateVector} to getVectorFromYawAxis({_baseloc}, 0.5)

	set {_baserad} to 0
	set {_rad} to 0
	set {_m} to 0
	set {_pnum} to 0  # 付与したプレイヤー数
	loop 20 times:
		add 0.5 to {_m}
		set {_baseel} to location {_m} meters in front of {_baseloc}
		delete {_pixieloc::*}

		set {_ln} to 3 - {_pnum}
		loop {_ln} times:
			add 120 to {_baserad}
			set {_vec} to {_rotateVector}
			rotate {_vec} around {_axisVector} by {_baserad}
			set {_pl} to {_baseel} ~ {_vec}
			add {_pl} to {_pixieloc::*}

			loop 3 times:
				add 120 to {_rad}
				set {_vec} to {_smallRotateVector}
				rotate {_vec} around {_axisVector} by {_rad}
				set {_el} to {_pl} ~ {_vec}
				spawnParticle_Dust(rgb(255, 192, 203), 1.5, {_el})

		loop all players in radius 5 around {_baseel}:
			{_pnum} < 3
			{_pixie::%loop-player%} is not set
			loop-player is not {_player}
			kitCheckTeam(loop-player, {_player}, true) is true
			set {_pixie::%loop-player%} to true
			add 1 to {_pnum}
			set {_indnum} to 4 - {_pnum}
			set {_fromloc} to {_pixieloc::%{_indnum}%}
			kitEffect_FortunatePixie_Apply({_player}, loop-player, {_fromloc})
		add 10 to {_baserad}
		add 20 to {_rad}
		wait a tick

	wait 7 seconds
	loop all players:
		getKitStats(loop-player, "FortunatePixie") is {_player}
		deleteKitStats(loop-player, "FortunatePixie")
		message formatted "§e幸運の妖精は消え去りました..." to loop-player

local function kitEffect_FortunatePixie_Apply(player: player, target: player, from: location):
	# Particles
	set {_rad} to 0
	loop 20 times:
		set {_to} to head of {_target}
		set {_dis} to distance between {_from} and {_to} / 20 * loop-number
		set {_axisVector} to setVector({_from}, {_to})
		set {_from} to setDirToVec({_from}, {_axisVector})
		set {_rotateVector} to getVectorFromYawAxis({_from}, 0.5)

		set {_baseel} to location {_dis} meters in front of {_from}
		playSound("block.amethyst_cluster.hit", 1, 1, {_baseel})

		loop 3 times:
			add 120 to {_rad}
			set {_vec} to {_rotateVector}
			rotate {_vec} around {_axisVector} by {_rad}
			set {_el} to {_baseel} ~ {_vec}
			spawnParticle_Dust(rgb(255, 153, 238), 1.5, {_el})
		add 20 to {_rad}
		wait a tick
	
	message formatted "§a幸運の妖精が身を護ってくれます!" to {_target}
	kitEffectDescription({_target}, "§d§l幸運の妖精")
	sendTimeBossbar_GREEN({_target}, "§d§l幸運の妖精", 140)
	setKitStats({_target}, "FortunatePixie", {_player})
	kitParticle_FortunatePixie_Apply({_player}, {_target})
	kitLuck({_target}, 3, true)

local function kitEffect_ForTunatePixie_Healing(player: player, damage: number, time: date):
	set {_healamount} to {_damage} / 20
	loop 20 times:
		if getKitStats({_player}, "FortunatePixie-Time") is not {_time}:
			stop
		kitHeal({_player}, {_healamount}, 1)
		wait a tick
	deleteKitStats({_player}, "FortunatePixie-Time")





local function kitParticle_FortunatePixie_Apply(player: player, target: player):
	playSound("block.amethyst_cluster.break", 1, 0.5, {_player})
	loop 140 times:
		getKitStats({_target}, "FortunatePixie") is {_player}
		loop 10 times:
			chance of 10%:
				set {_el} to aroundEffect({_target})
				kitParticle_FortunatePixie_Dust({_el})
		wait a tick

local function kitParticle_FortunatePixie_Dust(loc: location):
	set {_rn} to a random integer between 1 and 5
	if {_rn} is 1:
		spawnParticle_BlockData(falling_dust, pink wool, {_loc})
	else if {_rn} is 2:
		spawnParticle_BlockData(falling_dust, red wool, {_loc})
	else if {_rn} is 3:
		spawnParticle_DustTransition(rgb(235, 110, 160), rgb(229, 103, 205), 1.5, {_loc})
	else if {_rn} is 4:
		spawnParticle_DustTransition(rgb(216, 52, 115), rgb(222, 130, 167), 2, {_loc})
	else:
		spawnParticle_Dust(rgb(255, 20, 147), 0.5, {_loc})

local function kitParticle_FortunatePixie_Damage(player: player):
	playSound("entity.allay.hurt", 1, 1.2, {_player})
	set {_baseloc} to location 1 meter above {_player}
	loop 10 times:
		spawnParticle_Vector(glow, randomVector(), 5, {_baseloc})

local function kitParticle_FortunatePixie_Heal(player: player):
	playSound("entity.allay.item_given", 1, 1.5, {_player})
	set {_baserad} to 0
	set {_height} to 0
	loop 20 times:
		set {_baseloc} to location of {_player}
		set {_countnum} to 0
		loop 4 times:
			add 1 to {_countnum}
			set {_rad} to {_baserad} + 90 * ({_countnum} - 1)
			set {_vec} to vector(0.5, {_height}, 0)
			rotate {_vec} around y-axis by {_rad}
			set {_el} to {_baseloc} ~ {_vec}
			spawnParticle_Dust(rgb(255, 105, 180), 0.5, {_el})
		add 10 to {_baserad}
		add 0.1 to {_height}
		if mod(loop-number, 4) is 0:
			wait a tick