# Attract Ego

function kitEffect_AttractEgo(player: player):
	nameCT({_player}, "Attract Ego")
	kitMsg({_player}, "§8周辺の味方の気配を消します!")
	playSound("entity.wither.ambient", 1, 1, {_player})
	sendTimeBossbar_YELLOW({_player}, "Attract Ego", 60)
	apply glowing 1 to {_player} for 3 seconds
	apply resistance 1 to {_player} for 3 seconds
	loop all players in radius 5 around {_player}:
		loop-player is not {_player}
		kitCheckTeam({_player}, loop-player, true) is true
		setSupporter(loop-player, {_player})
		kitInvisible(loop-player, 60)
		message formatted "§9透明化を§b%{_player}%§9に付与してもらった!" to loop-player
		kitEffectDescription(loop-player, "§b§l透明化")
	set {_to} to vector of location of {_player}
	loop all players in radius 10 around {_player}:
		kitCheckTeam(loop-player, {_player}, false) is true
		set {_from} to vector of location of loop-player
		set {_vector} to {_to} - {_from}
		set {_vector} to normalized {_vector}
		set {_yaw} to yaw of {_vector}
		set {_pitch} to pitch of {_vector}
		set {_loc} to location of loop-player
		set yaw of {_loc} to {_yaw}
		set pitch of {_loc} to {_pitch}
		teleport loop-player to {_loc}
		kitEffectDescription(loop-player, "§7§l方向転換")