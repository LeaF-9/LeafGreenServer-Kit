# Fairy Protection

function kitSkill_Paladin_3(player: player):
	toolCT({_player}, "Paladin", 3)
	kitStats_SET({_player}, "FairyProtection", true)
	kitStats_SET({_player}, "Resurrection", true)
	kitMsg({_player}, "§d妖精の加護を受けました!")
	apply regeneration 2 to {_player} for 5 seconds
	sendTimeBossbar_GREEN({_player}, "Fairy Protection", 100)
	playSound("entity.firework_rocket.twinkle", 1, 1.5, {_player})
	loop 100 times:
		if kitStats_GET({_player}, "FairyProtection") is true:
			set {_effloc} to location of {_player}
			add 18 to {_rad}
			add 0.02 to {_y}
			add {_y} to y-coordinate of {_effloc}
			add 0.5 * sin({_rad}) to x-coordinate of {_effloc}
			add 0.5 * cos({_rad}) to z-coordinate of {_effloc}
			spawnParticle_Color(entity_effect, rgb(255, 20, 147), {_effloc})
			wait a tick
	kitStats_DELETE({_player}, "Resurrection")
	if kitStats_GET({_player}, "FairyProtection") is true:
		kitStats_DELETE({_player}, "FairyProtection")
		kitMsg({_player}, "§e妖精は去っていきました")

on damage of player with priority LOW:
	kitStats_GET(victim, "FairyProtection") is true
	set {_attacker} to getAttacker(victim)
	{_attacker} is set
	apply poison 2 to {_attacker} for 2 seconds replacing existing effect
	kitEffectDescription({_attacker}, "§2§l毒")
	sendTimeBossbar_RED({_attacker}, "§2毒", 40)

on death of player:
	kitStats_GET(victim, "FairyProtection") is true
	kitMsg(victim, "§a妖精が命を分け与えてくれた!")
	playSound("entity.player.levelup", 1, 0.5, victim)
	wait 3 ticks
	kitLuck(victim, -3)
	set victim's health to 5
	set victim's leggings to air
	set slot 1 of victim to air