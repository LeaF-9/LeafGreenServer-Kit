# Holy Lance

on damage with priority LOW:
	set {_attacker} to getAttacker(victim)
	{Class::%attacker%} is "Paladin"
	set {_tool} to {_attacker}'s tool
	kitItemAllChecker({_tool}, "Paladin", "Weapon", 3) is true
	if kitStats_GET({_attacker}, "HolyLance-Charged") is set:
		playSound("entity.zombie_villager.cure", 1, 2, victim)
		kitParticle_HolyLance_Damage(victim)
		setDamageModification_MUL(victim, 2)
		applyEffect(victim, glowing, 1, 10 seconds)
		ignite victim for 10 seconds
		kitEffectDescription(victim, "§cx2 §8& §e§l発光 §8& §4着火")
		sendTimeBossbar_RED(victim, "Holy Lance", 200)
		stop
	set {_victimtype} to "%type of victim%"
	set {_undeads::*} to kitWeapon_HolyLance_UndeadList()
	{_undeads::*} contains {_victimtype}
	kitCheckTeam({_attacker}, victim, false) is true
	playSound("block.beacon.activate", 1, 1.3, {_attacker})
	set {_damage} to 100
	if metadata value "necromancer-entity-abyss" of victim is true:
		set {_damage} to 10
	setDamageModification_FIX(victim, {_damage})
	set {_now} to now
	kitStats_SET({_attacker}, "HolyLance-Charged", {_now})

	set {_ln.1} to 0
	loop 100 times:
		kitStats_GET({_attacker}, "HolyLance-Charged") is {_now}
		add 1 to {_ln.1}
		chargeActionBar_TimeDisplay({_attacker}, 50 - floor({_ln.1} * 0.5), 50, 100 - ({_ln.1} - 1))

		kitParticle_HolyLance_Charged({_attacker})
		wait a tick
	kitStats_GET({_attacker}, "HolyLance-Charged") is {_now}
	kitStats_DELETE({_attacker}, "HolyLance-Charged")

local function kitParticle_HolyLance_Damage(player: player):
	set {_baseloc} to location 1 meter above {_player}
	loop 15 times:
		set {_v} to randomVector({_baseloc})
		spawnParticle_Vector(firework, {_v}, 0.3, {_baseloc})
		set {_v} to randomVector({_baseloc})
		spawnParticle_Vector(end_rod, {_v}, 0.2, {_baseloc})

local function kitParticle_HolyLance_Charged(player: player):
	set {_baseloc} to aroundEffect({_player})
	loop 10 times:
		set {_el} to location loop-number * 0.1 meters above {_baseloc}
		spawnParticle(end_rod, {_el})
		wait a tick

local function kitWeapon_HolyLance_UndeadList() :: texts:
	add "zombie" to {_list::*}
	add "skeleton" to {_list::*}
	add "zombified piglin" to {_list::*}
	add "zoglin" to {_list::*}
	add "wither skeleton" to {_list::*}
	add "drowned" to {_list::*}
	add "husk" to {_list::*}
	add "phantom" to {_list::*}
	add "skeleton horse" to {_list::*}
	add "stray" to {_list::*}
	add "undead horse" to {_list::*}
	add "zombie villager" to {_list::*}
	return {_list::*}