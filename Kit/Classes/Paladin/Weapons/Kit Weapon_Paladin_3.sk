# Holy Lance

on damage:
	{Class::%attacker%} is "Paladin"
	set {_tool} to attacker's toloc
	kitItemAllChecker({_tool}, "Paladin", "Weapon.main", 3) is true
	if {Stats.%attacker%::HolyLance-Charged} is set:
		playSound("entity.zombie_villager.cure", 1, 2, victim)
		kitParticle_HolyLance_Damage(victim)
		set damage to damage * 2
		applyEffect(victim, glowing, 1, 10 seconds)
		ignite victim for 10 seconds
		stop
	set {_victimtype} to "%type of victim%"
	set {_undeads::*} to kitWeapon_HolyLance_UndeadList()
	{_undeads::*} contains {_victimtype}
	playSound("block.beacon.activate", 1, 1.3, attacker)
	set damage to 10000
	set {_now} to now
	set {Stats.%attacker%::HolyLance-Charged} to {_now}
	loop 100 times:
		{Stats.%attacker%::HolyLance-Charged} is {_now}
		kitParticle_HolyLance_Charged(attacker)
		wait a tick
	{Stats.%attacker%::HolyLance-Charged} is {_now}
	delete {Stats.%attacker%::HolyLance-Charged}

function kitParticle_HolyLance_Damage(player: player):
	set {_baseloc} to location 1 meter above {_player}
	loop 15 times:
		set {_v} to randomVector({_baseloc})
		spawnParticle_Vector(firework, {_v}, 0.3, {_baseloc})
		set {_v} to randomVector({_baseloc})
		spawnParticle_Vector(end_rod, {_v}, 0.2, {_baseloc})

function kitParticle_HolyLance_Charged(player: player):
	set {_baseloc} to aroundEffect({_player})
	loop 10 times:
		set {_el} to location loop-number * 0.1 meters above {_baseloc}
		spawnParticle(end_rod, {_el})
		wait a tick

function kitWeapon_HolyLance_UndeadList():
	add "zombie" to {_list::*}
	add "skeleton" to {_list::*}
	add "zombified piglin" to {_list::*}
	add "zoglin" to {_list::*}
	add "wither skeleton" to {_list::*}
	add "drowned" to {_list::*}
	add "husk" to {_list::*}
	add "phantom" to {_list::*}
	add "skeleton horse" to {_list::*}
	add "stray" to {_list::*}
	add "undead horse" to {_list::*}
	add "zombie villager" to {_list::*}
	return {_list::*}