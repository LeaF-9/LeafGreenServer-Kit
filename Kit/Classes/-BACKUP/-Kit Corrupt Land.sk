# Weapon-Name: Corrupt Land

on sneak toggle:
	{Class::%player%} is "Ghost"
	player is not sneaking
	set {_tool} to player's tool
	if kitItemAllChecker({_tool}, "Ghost", "Weapon.main", 2) is false:
		stop
	set {_true} to true
	wait a tick
	while {_true} is true:
		set {_tool} to player's tool
		if kitItemAllChecker({_tool}, "Ghost", "Weapon.main", 2) is false:
			set {_true} to false
			continue
		else if player is not sneaking:
			set {_true} to false
			continue
		kitEffect_CorruptLand(player)
		wait a tick

function kitEffect_CorruptLand(player: player):
	kitParticle_CorruptLand_Area(location of {_player})
	loop all players in radius 10 around {_player}:
		kitCheckTeam(loop-player, {_player}, false) is true
		loop-player does not have hunger
		applyEffect(loop-player, hunger, 1, 1 seconds)
		set food level of loop-player to 1
		kitParticle_CorruptLand_Player(loop-player)

function kitParticle_CorruptLand_Area(loc: location):
	chance of 5%:
		playSound("entity.player.burp", 1, 1, {_loc})
	set {_rad} to 0
	loop 36 times:
		set {_el} to {_loc}
		add 10 * sin({_rad}) to x-pos of {_el}
		add 10 * cos({_rad}) to z-pos of {_el}
		spawnParticle_Color(entity_effect, 91, 99, 86, {_el})
		add 10 to {_rad}

function kitParticle_CorruptLand_Player(player: player):
	loop 3 times:
		spawnParticle_Item(rotten flesh, aroundEffect({_player}))