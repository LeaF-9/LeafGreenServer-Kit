# Guardian Staff

on damage of player:
	if {Stats.%victim%::GuardianStaff-Active} is true:
		set damage to damage * 5
		kitParticle_GuardianStaff_Critical(victim)
		stop
	{Stats.%victim%::GuardianStaff-Received} is set
	set damage to damage * 0.5
	kitParticle_GuardianStaff_Damage(victim)

function kitWeapon_Paladin_5(player: player):
	loop 5 times:
		if {_player} is not sneaking:
			if {Stats.%{_player}%::GuardianStaff-Active} is true:
				playSound("entity.armor_stand.break", 1, 0.5, {_player})
				loop all players:
					{Stats.%loop-player%::GuardianStaff-Received} is {_player}
					delete {Stats.%loop-player%::GuardianStaff-Received}
			delete {Stats.%{_player}%::GuardianStaff-Active}
			stop
		if {Stats.%{_player}%::GuardianStaff-Active} is not set:
			playSound("item.armor.equip_diamond", 1, 0.5, {_player})
		kitEffect_GuardianStaff({_player})
		wait a tick

function kitEffect_GuardianStaff(player: player):
	set {Stats.%{_player}%::GuardianStaff-Active} to true
	kitParticle_GuardianStaff_Active(location of {_player})
	loop all players in radius 5 around {_player}:
		loop-player is not {_player}
		kitCheckTeam({_player}, loop-player, true) is true
		add loop-player to {_allies::*}
		set {Stats.%loop-player%::GuardianStaff-Received} to {_player}
		kitParticle_GuardianStaff_Received(loop-player)
	loop all players:
		{_allies::*} does not contain loop-player
		{Stats.%loop-player%::GuardianStaff-Received} is {_player}
		delete {Stats.%loop-player%::GuardianStaff-Received}

function kitParticle_GuardianStaff_Active(baseloc: location):
	set {_rad} to 0
	loop 15 times:
		set {_el} to {_baseloc}
		add 5 * sin({_rad}) to x-pos of {_el}
		add 5 * cos({_rad}) to z-pos of {_el}
		set {_y} to 0
		loop 5 times:
			add {_y} to y-pos of {_el}
			spawnParticle_Dust(rgb(0, 163, 175), 2, {_el})
			add 0.5 to {_y}
		add 24 to {_rad}

function kitParticle_GuardianStaff_Received(target: player):
	set {_baseloc} to location 1 meter above {_target}
	set {_rad} to 0
	loop 36 times:
		set {_el} to {_baseloc}
		add 0.5 * sin({_rad}) to x-pos of {_el}
		add 0.5 * cos({_rad}) to z-pos of {_el}
		spawnParticle_Dust(rgb(193, 228, 233), 0.1, {_el})
		add 10 to {_rad}

function kitParticle_GuardianStaff_Critical(victim: player):
	playSound("block.respawn_anchor.deplete", 1, 0.8, {_victim})
	loop 30 times:
		set {_el} to aroundEffect({_victim})
		spawnParticle_Item(diamond, {_el})

function kitParticle_GuardianStaff_Damage(victim: player):
	playSound("block.respawn_anchor.charge", 1, 1.9, {_victim})
	set {_baseloc} to location of {_victim}
	set {_rad} to 0
	loop 15 times:
		set {_el} to {_baseloc}
		add 0.5 * sin({_rad}) to x-pos of {_el}
		add 0.5 * cos({_rad}) to z-pos of {_el}
		set {_y} to 0
		loop 5 times:
			add {_y} to y-pos of {_el}
			spawnParticle_DustTransition(rgb(0, 123, 187), rgb(234, 244, 252), 1.5, {_el})
			add 0.5 to {_y}
		add {_rad} to 24