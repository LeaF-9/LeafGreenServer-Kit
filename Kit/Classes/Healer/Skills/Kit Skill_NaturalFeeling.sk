# Natural Feeling

function kitEffect_NaturalFeeling(player: player):
	if getKitStats({_player}, "NaturalFeeling") is not set:
		setKitStats({_player}, "NaturalFeeling", 10)
	loop 5 times:
		getKitStats({_player}, "NaturalFeeling") > 0
		playSound("block.grass.step", 0.8, 2, {_player})
		loop all players in radius 10 around {_player}:
			getKitStats({_player}, "NaturalFeeling") > 0
			loop-player is not {_player}
			kitCheckAlive(loop-player) is true
			kitCheckTeam(loop-player, {_player}, true) is true
			health of loop-player is not max health of loop-player
			set {_NaturalFeeling} to getKitStats({_player}, "NaturalFeeling")
			subtract 0.05 from {_NaturalFeeling}
			setKitStats({_player}, "NaturalFeeling", {_NaturalFeeling})
			setSupporter(loop-player, {_player})
			kitHeal(loop-player, 0.05, 1)
			kitEffectDescription(loop-player, "§d§l回復")
			spawnParticle_Color(entity_effect, rgb(121, 192, 110), loop-player)
		set {_angle} to 0
		loop 36 times:
			set {_loc} to location of {_player}
			add 10 * sin({_angle}) to x-coordinate of {_loc}
			add 10 * cos({_angle}) to z-coordinate of {_loc}
			add 1 to y-coordinate of {_loc}
			spawnParticle_Dust(rgb(121, 192, 110), 1, {_loc})
			add 10 to {_angle}
		if getKitStats({_player}, "NaturalFeeling") <= 0:
			loop all items in {_player}'s inventory:
				name of loop-item is "§aNatural Feeling"
				delete loop-item
			playSound("block.grass.break", 1, 1, {_player})
		else:
			set {_NaturalFeeling} to getKitStats({_player}, "NaturalFeeling")
			set {_barcount} to round({_NaturalFeeling} * 4)
			set {_bar} to 0
			set {_bartext} to ""
			loop 40 times:
				add 1 to {_bar}
				if {_bar} <= {_barcount}:
					set {_bartext} to "%{_bartext}%§a|"
				else:
					set {_bartext} to "%{_bartext}%§8|"
			send action bar "§e§l[ %{_bartext}% §e§l]" to {_player}
		wait a tick