# Cure Orb

function kitSkill_Healer_2(player: player):
	if {Kit-Odo::%{_player}%} >= 10:
		toolCT({_player}, "Healer", 2)
		subtract 10 from {Kit-Odo::%{_player}%}
		shoot a snowball from {_player} at speed 2
		set item of shot entity to heart of the sea
		set metadata value "cureorb" of shot entity to "true"
		kitParticle_CureOrb_Projectile(shot entity)
		playSound("entity.arrow.shoot", 1, 2, {_player})
	else:
		kitMsg({_player}, "§cオドが足りません! 必要オドは §310 §cです!")

on projectile hit:
	projectile is a snowball
	metadata value "cureorb" of projectile is "true"
	set {_loc} to location of projectile
	playSound("block.end_portal_frame.fill", 1, 1.5, {_loc})
	loop all players in radius 5 around projectile:
		loop-player is not shooter
		kitCheckTeam(loop-player, shooter, true) is true
		setSupporter(loop-player, shooter)
		kitHeal(loop-player, 3)
		kitEffectDescription(loop-player, "§d❤3")
		kitMsg(shooter, "<##006eb0>%loop-player% <##00a1e9>を回復しました!")
		message "<##006eb0>%shooter% <##00a1e9>に回復してもらった!" to loop-player
	loop all blocks in radius 5 around projectile:
		loop-block is air
		block below loop-block is not air
		set {_l} to location of loop-block
		subtract 0.4 from y-coordinate of {_l}
		kitParticle_CureOrb_Aura({_l})

function kitParticle_CureOrb_Projectile(proj: entity):
	loop 50 times:
		spawnParticle_Color(entity_effect, rgb(0, 161, 233), {_proj})
		wait a tick

function kitParticle_CureOrb_Aura(loc: location):
	loop 20 times:
		spawnParticle_Color(entity_effect, rgb(0, 161, 233), {_loc})
		wait a tick