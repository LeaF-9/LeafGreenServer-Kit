# Field Hospital

function kitSkill_Healer_3(player: player):
	if {Stats.%{_player}%::SkillLocation} is set:
		if {Kit-Odo::%{_player}%} is more than 39:
			toolCT({_player}, "Healer", 3)
			subtract 40 from {Kit-Odo::%{_player}%}
			set {_now} to now
			set {Stats.%{_player}%::FieldHospital-Time} to {_now}
			kitFakeDeleteData({_player}, "Field Hospital")
			set {_loc1} to {Stats.%{_player}%::SkillLocation}
			kitFakeBlock({_player}, {_loc1}, "minecraft:oak_fence", "Field Hospital")
			set {_loc2} to {Stats.%{_player}%::SkillLocation}
			add 1 to y-coordinate of {_loc2}
			kitFakeBlock({_player}, {_loc2}, "minecraft:torch", "Field Hospital")
			kitMsg({_player}, "§d周辺の味方を回復する装置を設置しました...")
			send action bar "§d現在のオド: §c%{Kit-Odo::%{_player}%}%" to {_player}
			playSound("block.portal.travel", 0.5, 1.5, {_loc1})
			while {Stats.%{_player}%::FieldHospital-Time} is {_now}:
				wait a second
				delete {_exist}
				loop all entities:
					type of loop-entity is armor stand
					metadata value "kit-armor" of loop-entity is "Field Hospital"
					metadata value "Field Hospital" of loop-entity is "%{_player}%"
					set {_exist} to true
				if {_exist} is true:
					kitFakeReload({_player}, "Field Hospital")
					loop all players in radius 5 around {_loc1}:
						if {team::%loop-player%} is {team::%{_player}%}:
							loop-player is not {_player}
							{Stats.%loop-player%::NoSupport} is not set
							setSupporter(loop-player, {_player})
							apply regeneration 1 to loop-player for 1 second
							kitParticle_FieldHospital_Player(loop-player)
					set {_baseloc} to {_loc1}
					set {_rad} to 0
					loop 36 times:
						set {_el} to {_baseloc}
						add 10 to {_rad}
						add 5 * sin({_rad}) to x-coordinate of {_el}
						add 5 * cos({_rad}) to z-coordinate of {_el}
						kitParticle_FieldHospital_Field({_el})
				else:
					delete {Stats.%{_player}%::FieldHospital-Time}
		else:
			kitMsg({_player}, "§cオドが足りません! 必要オドは §340 §cです!")
	else:
		send action bar "§c§l地点を指定してください" to {_player}

function kitParticle_FieldHospital_Player(player: player):
	loop 20 times:
		spawnParticle_Color(entity_effect, 255, 102, 255, {_player})
		wait a tick

function kitParticle_FieldHospital_Field(loc: location):
	loop 20 times:
		spawnParticle_Color(entity_effect, 255, 255, 153, {_loc})
		wait a tick