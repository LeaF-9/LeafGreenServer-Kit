# Recovery Staff

function kitWeapon_Healer_2(player: player):
	toolItemCT({_player}, "Healer", 2)
	set {_baseloc} to location of head of {_player}
	set {_m} to 0
	set {_rad} to 0
	set {_add.pitch} to 0
	loop 60 times:
		add 0.5 to {_m}
		set {_loc} to location {_m} meters in front of {_baseloc}
		# --- 効果の処理 ---
		loop all entities in radius 2 around {_loc}:
			loop-entity is not {_player}
			kitCheckTeam(loop-entity, {_player}, true) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			applyEffect(loop-entity, regeneration, 1, 5 seconds)
			healEffect(loop-entity)
			if checkEntityType(loop-entity, player) is true:
				setSupporter(loop-entity, {_player})
			playSound("block.amethyst_block.chime", 1, 1.2, loop-entity)
		# ----------------
		# --- パーティクルの演出 ---
		spawnParticle_DustTransition(rgb(238, 130, 238), rgb(221, 160, 221), 1, {_loc})
		add 36 to {_rad}
		set {_eb} to {_loc}
		set {_m1} to {_eb}
		set {_p} to pitch of {_m1}
		if {_p} < 0:
			set {_p} to -1 * 90 - {_p}
			set {_y} to yaw of {_m1}
			add 180 to {_y}
			if {_y} >= 360:
				set {_y} to {_y} - 360
			set yaw of {_m1} to {_y}
		else:
			set {_p} to {_p} - 90
		set pitch of {_m1} to {_p}
		set {_test1} to 1.5 * cos({_rad})
		set {_t1} to location {_test1} meters in front of {_m1}
		set {_m2} to {_eb}
		set pitch of {_m2} to 0
		set {_test2} to 1.5 * sin({_rad})
		set {_t2} to location {_test2} meters to the right of {_m2}
		set x-coordinate of {_eb} to x-coordinate of {_t1} + x-coordinate of {_t2} - x-coordinate of {_eb}
		set y-coordinate of {_eb} to y-coordinate of {_t1} + y-coordinate of {_t2} - y-coordinate of {_eb}
		set z-coordinate of {_eb} to z-coordinate of {_t1} + z-coordinate of {_t2} - z-coordinate of {_eb}
		spawnParticle(wax_off, {_eb})
		# -----------------------
		if mod(loop-number, 4) is 0:
			# --- 音の演出 ---
			add 0.1 to {_add.pitch}
			playSound("block.note_block.chime", 1, 0.5 + {_add.pitch}, {_loc})
			# ---------------
			wait a tick