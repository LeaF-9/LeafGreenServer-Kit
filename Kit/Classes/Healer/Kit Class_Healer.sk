# Healerのスキル Ver1.0.0

# Weapons
function kitWeaponHealer(player: player):
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	checkCT({_player}, name of {_player}'s tool) is true
	if {_item.num} is 4:
		kitWeapon_Healer_4({_player})
	else if {_item.num} is 5:
		kitWeapon_Healer_5({_player})

# Skills
function kitSkillHealer(player: player) :: boolean:
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	if checkCT({_player}, name of {_player}'s tool) is true:
		if {_item.num} is 1:
			kitSkill_Healer_1({_player})
		else if {_item.num} is 2:
			kitSkill_Healer_2({_player})
		else if {_item.num} is 3:
			kitSkill_Healer_3({_player})
		else if {_item.num} is 4:
			kitSkill_Healer_4({_player})
		else if {_item.num} is 5:
			kitSkill_Healer_5({_player})
		else if {_item.num} is 6:
			kitSkill_Healer_6({_player})
		else if {_item.num} is 7:
			kitSkill_Healer_7({_player})
		else if {_item.num} is 8:
			kitSkill_Healer_8({_player})
	return true

# Abilities
on death of player:
	{Class::%attacker%} is "HEA"
	kitHeal(attacker, 3)
	add 10 to {Kit-Odo::%attacker%}

on damage of player:
	{Class::%victim%} is "HEA"
	if damage was caused by void or fall:
		stop
	else if damage >= 100:
		stop
	else:
		set damage to damage * 1.5

function healEffect(player: entity, count: number = 5):
	loop {_count} times:
		set {_loc} to location of {_player}
		add a random number between -1 and 1 to x-coordinate of {_loc}
		add a random number between 0 and 2 to y-coordinate of {_loc}
		add a random number between -1 and 1 to z-coordinate of {_loc}
		drawDot count 1, particle "heart", center {_loc}, visibleRange 30

command /odoset <number> <player=%player%>:
	permission: sk.admin
	trigger:
		set {Kit-Odo::%arg 2%} to arg 1
		message colored "§c%arg 2%§dのオドを§b%arg 1%§dに設定しました"

# Timer
function kitTimerHealer(player: player):
	set {_odotimer} to 0
	create bossbar titled "&d&lOdo&5: &c&l%{Kit-Odo::%{_player}%}%" and id "%{_player}%-Odo" for {_player} with progress {Kit-Odo::%{_player}%} with colors pink with style segmented 10
	while {Class::%{_player}%} is "Healer":
		# --- オド関連の処理 ---
		add 1 to {_odotimer}
		if {_odotimer} >= 200:
			set {_odotimer} to 0
			add 10 to {Kit-Odo::%{_player}%}
		set bossbar "%{_player}%-Odo" title to "&d&lOdo&5: &c&l%{Kit-Odo::%{_player}%}%"
		set bossbar "%{_player}%-Odo" value to {Kit-Odo::%{_player}%}
		# --------------------
		# --- 体力を可視化する処理 ---
		loop all players:
			kitCheckTeam({_player}, loop-player, true) is true
			loop-player is not {_player}
			set {_health} to health of loop-player
			set {_max} to max health of loop-player
			set {_per} to {_health} / {_max}
			set {_g} to 255 * {_per}
			set {_r} to 255 * (1 - {_per})
			set {_baseloc} to location of loop-player
			add 2 to y-coordinate of {_baseloc}
			set {_rad} to 0
			loop 5 times:
				add 72 to {_rad}
				set {_el} to {_baseloc}
				add 0.5 * sin({_rad}) to x-coordinate of {_el}
				add 0.5 * cos({_rad}) to z-coordinate of {_el}
				drawDot count 1, particle redstone, RGB {_r}, {_g}, 0, center {_el}, isClientside {_player}, visibleRange 300
		# --------------------------
		# --- Weapon装備時の処理 ---
		set {_tool} to {_player}'s tool
		if kitItemAllChecker({_tool}, "Healer", "Weapon.main", 3) is true:
			kitWeapon_Healer_3({_player})
		# ------------------------
		wait a tick
	remove bossbar "%{_player}%-Odo"