# Healerのスキル Ver1.0.0

# Abilities
on death of player:
	{Class::%attacker%} is "Healer"
	kitHeal(attacker, 3)
	add 10 to {Kit-Odo::%attacker%}

on damage of player with priority LOW:
	{Class::%victim%} is "HEA"
	if damage was caused by void or fall:
		stop
	else if damage >= 100:
		stop
	setDamageModification_MUL(victim, 1.5)

command /odoset <number> <player=%player%>:
	permission: sk.admin
	trigger:
		set {Kit-Odo::%arg 2%} to arg 1
		message colored "§c%arg 2%§dのオドを§b%arg 1%§dに設定しました"

# Timer
function kitTimerHealer(player: player):
	set {_odotimer} to 0
	create bossbar titled "&d&lOdo&5: &c&l%{Kit-Odo::%{_player}%}%" and id "%{_player}%-Odo" for {_player} with progress {Kit-Odo::%{_player}%} with colors pink with style segmented 10
	while {Class::%{_player}%} is "Healer":
		# --- オド関連の処理 ---
		add 1 to {_odotimer}
		if {_odotimer} >= 200:
			set {_odotimer} to 0
			add 10 to {Kit-Odo::%{_player}%}
			kitEffectDescription({_player}, "<##15c8ec>§lオド§d§l回復")
		set bossbar "%{_player}%-Odo" title to "&d&lOdo&5: &c&l%{Kit-Odo::%{_player}%}%"
		set bossbar "%{_player}%-Odo" value to {Kit-Odo::%{_player}%}
		# --------------------
		# --- 体力を可視化する処理 ---
		loop all players:
			kitCheckTeam({_player}, loop-player, true) is true
			loop-player is not {_player}
			set {_health} to health of loop-player
			set {_max} to max health of loop-player
			set {_per} to {_health} / {_max}
			set {_g} to 255 * {_per}
			set {_r} to 255 * (1 - {_per})
			set {_baseloc} to location of loop-player
			add 2 to y-pos of {_baseloc}
			set {_rad} to 0
			loop 5 times:
				add 72 to {_rad}
				set {_vec} to vector(0.5, 0, 0)
				rotate {_vec} around y-axis by {_rad}
				set {_el} to {_baseloc} ~ {_vec}
				spawnParticle_Dust(rgb({_r}, {_g}, 0), 1, {_el}, {_player})
		# --------------------------
		wait a tick
	remove bossbar "%{_player}%-Odo"