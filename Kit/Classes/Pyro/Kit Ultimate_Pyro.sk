# ultimate-name: Eternal Phoenix

options:
	effect-tick: 200
	eruption-interval: 10
	fire-hit-radius: 3
	fire-damage: 3
	fire-ignite-time: 100
	regeneration-tier: 5
	regeneration-time: 1 second



on damage with priority LOW:
	set {_attacker} to getAttacker(victim)
	getKitStats({_attacker}, "EternalPhoenix") is true
	set {_now} to now
	set {_hit} to metadata value "eternal phoenix - hit" of victim
	delete metadata value "eternal phoenix - hit" of victim
	if {_hit} is {_now}:
		stop

	set {_firetick} to victim.getFireTicks()
	if {_firetick} > 0:
		setDamageModification_MUL(victim, 2)
		kitEffectDescription(victim, "§4❤§c§lx2")
		kitParticle_EternalPhoenix_Damage(victim)



function kitUltPyro(player: player):
	kitEffect_EternalPhoenix({_player})



local function kitEffect_EternalPhoenix(player: player):
	setKitStats({_player}, "EternalPhoenix", true)
	kitParticle_EternalPhoenix_Wing({_player})
	allow fly for {_player}
	loop {@effect-tick} times:
		if mod(loop-number, {@eruption-interval}) is 0 or {@eruption-interval}:
			kitEffect_EternalPhoenix_Burning({_player})
		wait a tick
	deleteKitStats({_player}, "EternalPhoenix")
	disallow fly for {_player}

local function kitEffect_EternalPhoenix_Burning(player: player):
	kitSound_EternalPhoenix_Burning({_player})
	loop 10 times:
		kitEffect_EternalPhoenix_Fire({_player})

local function kitEffect_EternalPhoenix_Fire(player: player):
	set {_baseloc} to location 1.5 meters above {_player}
	shoot a falling soul fire from {_baseloc} at speed 1 upwards
	set {_fireentity} to shot entity
	set metadata value "eternal phoenix - fire" of {_fireentity} to "%{_player}%"
	push {_fireentity} north at speed random number from -0.5 to 0.5
	push {_fireentity} east at speed random number from -0.5 to 0.5

	set {_enable} to true
	loop 100 times:
		set {_checkloc} to location 0.1 meters below {_fireentity}
		if block at {_checkloc} is not passable:
			set {_enable} to false
			continue
		
		loop all entities in radius {@fire-hit-radius} around {_fireentity}:
			{_hit} is not set
			kitCheckTeam(loop-entity, {_player}, false, true) is true
			set {_hit} to true
			set {_now} to now
			set metadata value "eternal phoenix - hit" of loop-entity to {_now}
			kitDamage({_player}, loop-entity, {@fire-damage}, "永遠の青炎")
			ignite loop-entity for convertTickTime({@fire-ignite-time})
			set {_igniteSeconds} to {@fire-ignite-time} / 20
			kitEffectDescription(loop-entity, "§4❤{@fire-damage} §8& §4§l炎上")

		if {_hit} is true:
			applyEffect({_player}, regeneration, {@regeneration-tier}, {@regeneration-time}, true)
			set {_enable} to false
		
		if {_enable} is false:
			loop all entities:
				loop-entity is {_fireentity}
				delete loop-entity
			stop
		kitParticle_EternalPhoenix_Fire(location of {_fireentity})
		wait a tick



local function kitParticle_EternalPhoenix_Wing(player: player):
	loop 200 times:
		if getKitStats({_player}, "EternalPhoenix") is not true:
			stop

		set {_wing.1::*} to kitParticle_EternalPhoenix_Wing_Upper({_player})
		set {_wing.2::*} to kitParticle_EternalPhoenix_Wing_Middle({_player})
		set {_wing.3::*} to kitParticle_EternalPhoenix_Wing_Lower({_player})
		loop 3 times:
			set {_ln} to loop-number-2
			loop {_wing.%{_ln}%::*}:
				set {_loc} to loop-value-3
				spawnParticle(flame, {_loc})
				chance of 10%:
					spawnParticle_Vector(small_flame, randomVector(), 0.15, {_loc})
		wait a tick

local function kitParticle_EternalPhoenix_Wing_Upper(player: player) :: locations:
	# baseloc
	set {_baseloc} to location 1.25 meters above {_player}
	set pitch of {_baseloc} to 0
	set {_baseloc} to location 0.25 meters behind {_baseloc}
	# left-1
	set {_baselefts::1} to location 0.25 meters to the left of {_baseloc}
	# left-2
	set {_baselefts::2} to location 0.25 meters to the left of {_baselefts::1}
	set {_baselefts::2} to location 0.25 meters above {_baselefts::2}

	# left-1 - left-2
	set {_dis} to distance between {_baselefts::1} and {_baselefts::2}
	set {_v} to setVector({_baselefts::1}, {_baselefts::2})
	set yaw of {_baselefts::1} to yaw of {_v}
	set pitch of {_baselefts::1} to pitch of {_v}

	loop 5 times:
		set {_el} to location {_dis} / 5 * loop-number meters in front of {_baselefts::1}
		add {_el} to {_resultlocs::*}

	# right-1
	set {_baserights::1} to location 0.25 meters to the right of {_baseloc}
	# right-2
	set {_baserights::2} to location 0.25 meters to the right of {_baserights::1}
	set {_baserights::2} to location 0.25 meters above {_baserights::2}

	# right-1 - right-2
	set {_dis} to distance between {_baserights::1} and {_baserights::2}
	set {_v} to setVector({_baserights::1}, {_baserights::2})
	set yaw of {_baserights::1} to yaw of {_v}
	set pitch of {_baserights::1} to pitch of {_v}

	loop 5 times:
		set {_el} to location {_dis} / 5 * loop-number meters in front of {_baserights::1}
		add {_el} to {_resultlocs::*}

	return {_resultlocs::*}

local function kitParticle_EternalPhoenix_Wing_Middle(player: player) :: locations:
	# baseloc
	set {_baseloc} to location 1.2 meters above {_player}
	set pitch of {_baseloc} to 0
	set {_baseloc} to location 0.25 meters behind {_baseloc}
	# left-1
	set {_baselefts::1} to location 0.3 meters to the left of {_baseloc}
	# left-2
	set {_baselefts::2} to location 0.5 meters to the left of {_baselefts::1}

	# left-1 - left-2
	set {_dis} to distance between {_baselefts::1} and {_baselefts::2}
	set {_v} to setVector({_baselefts::1}, {_baselefts::2})
	set yaw of {_baselefts::1} to yaw of {_v}
	set pitch of {_baselefts::1} to pitch of {_v}

	loop 10 times:
		set {_el} to location {_dis} / 10 * loop-number meters in front of {_baselefts::1}
		add {_el} to {_resultlocs::*}

	# right-1
	set {_baserights::1} to location 0.3 meters to the right of {_baseloc}
	# right-2
	set {_baserights::2} to location 0.5 meters to the right of {_baserights::1}

	# right-1 - right-2
	set {_dis} to distance between {_baserights::1} and {_baserights::2}
	set {_v} to setVector({_baserights::1}, {_baserights::2})
	set yaw of {_baserights::1} to yaw of {_v}
	set pitch of {_baserights::1} to pitch of {_v}

	loop 10 times:
		set {_el} to location {_dis} / 10 * loop-number meters in front of {_baserights::1}
		add {_el} to {_resultlocs::*}

	return {_resultlocs::*}

local function kitParticle_EternalPhoenix_Wing_Lower(player: player) :: locations:
	# baseloc
	set {_baseloc} to location 1.1 meters above {_player}
	set pitch of {_baseloc} to 0
	set {_baseloc} to location 0.25 meters behind {_baseloc}
	# left-1
	set {_baselefts::1} to location 0.35 meters to the left of {_baseloc}
	# left-2
	set {_baselefts::2} to location 1 meters to the left of {_baselefts::1}
	set {_baselefts::2} to location 1 meters below {_baselefts::2}

	# left-1 - left-2
	set {_dis} to distance between {_baselefts::1} and {_baselefts::2}
	set {_v} to setVector({_baselefts::1}, {_baselefts::2})
	set yaw of {_baselefts::1} to yaw of {_v}
	set pitch of {_baselefts::1} to pitch of {_v}

	loop 15 times:
		set {_el} to location {_dis} / 15 * loop-number meters in front of {_baselefts::1}
		add {_el} to {_resultlocs::*}

	# right-1
	set {_baserights::1} to location 0.35 meters to the right of {_baseloc}
	# right-2
	set {_baserights::2} to location 1 meters to the right of {_baserights::1}
	set {_baserights::2} to location 1 meters below {_baserights::2}

	# right-1 - right-2
	set {_dis} to distance between {_baserights::1} and {_baserights::2}
	set {_v} to setVector({_baserights::1}, {_baserights::2})
	set yaw of {_baserights::1} to yaw of {_v}
	set pitch of {_baserights::1} to pitch of {_v}

	loop 15 times:
		set {_el} to location {_dis} / 15 * loop-number meters in front of {_baserights::1}
		add {_el} to {_resultlocs::*}

	return {_resultlocs::*}



local function kitParticle_EternalPhoenix_Fire(loc: location):
	spawnParticle(soul_fire_flame, {_loc})

local function kitParticle_EternalPhoenix_Damage(player: player):
	playSound("item.bucket.fill_lava", 1, 1.5, {_player})
	loop 10 times:
		spawnParticle(lava, aroundEffect({_player}))



local function kitSound_EternalPhoenix_Burning(player: player):
	playSound("entity.ghast.shoot", 1, 0.7, {_player})