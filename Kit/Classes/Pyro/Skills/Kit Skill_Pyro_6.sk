# skill-name: Witchery Ignis

options:
	ignite-damage: 1
	ignite-radius: 5
	ignite-tick: 100
	soul-radius: 30
	soul-tick: 200
	soul-damage: 2
	soul-ignite: 3 seconds
	soul-time: 100
	soul-speed: 0.2



on damage with priority LOW:
	damage cause is fire, burning, campfire, magma or lava
	metadata value "witchery ignis" of victim is set
	setDamageModification_MUL(victim, 2)
	kitEffectDescription(victim, "§c§lダメージ倍増§7: §cx2")
	kitParticle_WitcheryIgnis_Burn(victim)



function kitSkill_Pyro_6(player: player):
	toolCT({_player}, "Pyro", 6)
	kitEffect_WitcheryIgnis({_player})



local function kitEffect_WitcheryIgnis(player: player):
	kitParticle_WitcheryIgnis_Launch({_player})
	kitMsg({_player}, "§3§l炎呪の人魂§8を放出します...")
	set {_baseloc} to location of {_player}

	# ignite
	loop all entities in radius {@ignite-radius} around {_baseloc}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		kitDamage({_player}, loop-entity, {@ignite-damage})
		ignite loop-entity for convertTickTime({@ignite-tick})
		kitParticle_WitcheryIgnis_Ignite(loop-entity)
		set {_igniteSeconds} to {@ignite-tick} / 20
		kitEffectDescription(loop-entity, "§6§l炎上§7: §b%{_igniteSeconds}%秒")
	
	# soul fire
	loop all entities in radius {@soul-radius} around {_baseloc}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		kitEffect_WitcheryIgnis_Check({_player}, loop-entity) is true
		add loop-entity to {_targets::*}
		if checkEntityType(loop-entity, player) is true:
			message formatted "§3§l人魂 §5が迫っている..." to loop-entity
	
	set {_baseloc} to location 1 meter above {_baseloc}
	loop {@soul-tick} times:
		if size of {_targets::*} is 0:
			stop

		loop {_targets::*}:
			set {_loop-entity} to loop-value-2
			set {_loop-id} to {_loop-entity}.getEntityId()
			# check alive
			if checkEntityType({_loop-entity}, player) is true:
				if kitCheckAlive({_loop-entity}) is false:
					remove {_loop-entity} from {_targets::*}
					continue
			else:
				if {_loop-entity} is dead:
					remove {_loop-entity} from {_targets::*}
					continue
			
			if {_soulloc.%{_loop-id}%} is not set:
				set {_soulloc.%{_loop-id}%} to {_baseloc}
			set {_v} to setVector({_soulloc.%{_loop-id}%}, location 1 meters above {_loop-entity})
			set {_soulloc.%{_loop-id}%} to setDirToVec({_soulloc.%{_loop-id}%}, {_v})
			set {_soulloc.%{_loop-id}%} to location {@soul-speed} meters in front of {_soulloc.%{_loop-id}%}
			kitParticle_WitcheryIgnis_Bullet({_soulloc.%{_loop-id}%})
			set {_hitchecker} to distance between {_soulloc.%{_loop-id}%} and location 1 meters above {_loop-entity}
			if {_hitchecker} <= 0.5:
				kitEffect_WitcheryIgnis_Hit({_player}, {_loop-entity})
				remove {_loop-entity} from {_targets::*}
				continue

		wait a tick

local function kitEffect_WitcheryIgnis_Check(player: player, target: entity) :: boolean:
	set {_from} to location 1 meter above {_player}
	set {_to} to location 1 meter above {_target}
	set {_from} to setDirToVec({_from}, setVector({_from}, {_to}))
	set {_dis} to distance between {_from} and {_to}
	set {_loopnum} to round({_dis} * 5)

	set {_addm} to 0.2
	set {_m} to 0
	loop {_loopnum} times:
		add {_addm} to {_m}
		set {_checkblock} to location {_m} meters in front of {_from}

		if block at {_checkblock} is not passable:
			return false
	return true

local function kitEffect_WitcheryIgnis_Hit(player: player, target: entity):
	kitParticle_WitcheryIgnis_Hit({_target})
	if checkEntityType({_target}, player) is true:
		kitSound_WitcheryIgnis_HitCheck({_player})
		kitMsg({_player}, "§3§l%{_target}% §bにヒット!")
		set {_soulSeconds} to {@soul-time} / 20
		kitEffectDescription({_target}, "§3§l炎呪§7: §b%{_soulSeconds}%秒")
		sendTimeBossbar_RED({_target}, "炎呪", {@soul-time})
		message formatted "§3§l炎呪§cにより§4§l火炎ダメージ§cが§6§l倍増§cする..." to {_target}

	kitDamage({_player}, {_target}, {@soul-damage})
	ignite {_target} for {@soul-ignite}
	
	set {_now} to now
	set metadata value "witchery ignis" of {_target} to {_now}

	wait convertTickTime({@soul-time})

	if metadata value "witchery ignis" of {_target} is {_now}:
		delete metadata value "witchery ignis" of {_target}
		message formatted "§3§l炎呪 §dは消え去った!" to {_target}




local function kitParticle_WitcheryIgnis_Launch(player: player):
	playSound("entity.illusioner.cast_spell", 1, 0.85, {_player})
	set {_baseloc} to location of {_player}

	loop 50 times:
		set {_el} to {_baseloc}
		set yaw of {_el} to a random number between 0 and 360
		set pitch of {_el} to 0
		set {_m} to a random number between 0 and {@ignite-radius}
		set {_el} to location {_m} meters in front of {_el}
		set {_vector} to randomVector()
		if y of {_vector} < 0:
			set y of {_vector} to y of {_vector} * -1
		spawnParticle_Vector(soul_fire_flame, {_vector}, 0.15, {_el})

	set {_baselaunch} to location 0.5 meters below {_baseloc}
	set {_baserad} to 0
	loop 10 times:
		set {_rad} to {_baserad}
		loop 36 times:
			set {_el} to {_baselaunch}
			add 10 to {_rad}
			add {@ignite-radius} * sin({_rad}) to x-pos of {_el}
			add {@ignite-radius} * cos({_rad}) to z-pos of {_el}
			spawnParticle_Vector(trial_spawner_detection_ominous, vector(0, 1, 0), 0.3, {_el})

		add 1 to {_baserad}
		wait a tick

local function kitParticle_WitcheryIgnis_Ignite(entity: entity):
	playSound("block.soul_sand.break", 1, 1.25, {_entity})
	set {_baseloc} to location 1 meter above {_entity}
	loop 30 times:
		spawnParticle_Vector(enchanted_hit, randomVector(), 1, {_baseloc})
		set {_el} to aroundEffect({_entity})
		set {_d} to dice(3)
		if {_d} is 1:
			set {_color.f} to rgb(0, 164, 151)
		else if {_d} is 2:
			set {_color.f} to rgb(0, 163, 175)
		else:
			set {_color.f} to rgb(22, 94, 131)
		spawnParticle_DustTransition({_color.f}, rgb(28, 48, 92), 2, {_el})

local function kitParticle_WitcheryIgnis_Bullet(loc: location):
	set {_d} to dice(3)
	if {_d} is 1:
		set {_color.f} to rgb(0, 164, 151)
	else if {_d} is 2:
		set {_color.f} to rgb(0, 163, 175)
	else:
		set {_color.f} to rgb(22, 94, 131)
	spawnParticle_DustTransition({_color.f}, rgb(28, 48, 92), 2.5, {_loc})

	loop 3 times:
		set {_el} to {_loc}
		add a random number between -0.5 and 0.5 to x-pos of {_el}
		add a random number between -0.5 and 0.5 to y-pos of {_el}
		add a random number between -0.5 and 0.5 to z-pos of {_el}
		set {_d} to dice(2)
		if {_d} is 1:
			spawnParticle_BlockData(falling_dust, warped planks, {_el})
		else:
			spawnParticle(soul_fire_flame, {_el})

local function kitParticle_WitcheryIgnis_Hit(entity: entity):
	playSound("particle.soul_escape", 1, 0.85, {_entity})
	set {_baseloc} to location 1 meter above {_baseloc}
	loop 20 times:
		spawnParticle_Vector(soul_fire_flame, randomVector(), 0.2, {_baseloc})

	set {_y} to 2
	set {_baserad} to 0	
	loop 11 times:
		set {_loc} to location of {_entity}
		set {_rad} to {_baserad}
		loop 4 times:
			set {_el} to {_loc}
			add 360 / 4 to {_rad}
			add 0.5 * sin({_rad}) to x-pos of {_el}
			add 0.5 * cos({_rad}) to z-pos of {_el}
			add {_y} to y-pos of {_el}

			set {_d} to dice(3)
			if {_d} is 1:
				set {_color.f} to rgb(0, 164, 151)
			else if {_d} is 2:
				set {_color.f} to rgb(0, 163, 175)
			else:
				set {_color.f} to rgb(22, 94, 131)
			spawnParticle_DustTransition({_color.f}, rgb(28, 48, 92), 2, {_loc})

		add 20 to {_baserad}
		subtract 0.2 from {_y}
		wait a tick

local function kitParticle_WitcheryIgnis_Burn(entity: entity):
	playSound("entity.firework_rocket.twinkle_far", 1, 2, {_entity})
	set {_baseloc} to location 1 meter above {_entity}
	loop 20 times:
		set {_el} to aroundEffect({_entity})
		set {_vector} to setVector({_baseloc}, {_el})
		spawnParticle_Vector(trial_omen, {_vector}, 1, {_el})



local function kitSound_WitcheryIgnis_HitCheck(player: player):
	playSound("entity.experience_orb.pickup", 1, 0.57, {_player}, true)