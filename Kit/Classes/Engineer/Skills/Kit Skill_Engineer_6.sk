# Respawn Controller

function kitSkill_Engineer_6(player: player, loc: location):
	toolCT({_player}, "Engineer", 6)
	set {_now} to now
	set {Stats.%{_player}%::RespawnController-Time} to {_now}
	kitFakeDeleteData({_player}, "Machine-Respawn Controller")
	set {_loc.base} to {_loc}
	set {_loc.core} to {_loc}
	set {_loc.top} to {_loc}
	add 1 to y-coordinate of {_loc.core}
	add 2 to y-coordinate of {_loc.top}
	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.5f,0f,-0.5f]", "[1f,1f,1f]")
	kitFakeBlock({_player}, {_loc.base}, "minecraft:nether_brick_fence", {_transformation}, "Machine-Respawn Controller")
	kitFakeBlock({_player}, {_loc.core}, "minecraft:nether_quartz_ore", {_transformation}, "Machine-Respawn Controller")
	kitFakeBlock({_player}, {_loc.top}, "minecraft:nether_brick_fence", {_transformation}, "Machine-Respawn Controller")
	kitMsg({_player}, "<##7F1184>Respawn Controller§cを設置しました!")
	playSound("entity.evoker.prepare_attack", 1, 1.5, {_loc.base})
	while {Stats.%{_player}%::RespawnController-Time} is {_now}:
		wait 5 ticks
		delete {_tp::*}
		loop all players in radius 10 around {_loc.base}:
			kitCheckTeam(loop-player, {_player}, false) is true
			if {Stats.%loop-player%::RespawnController} is not set:
				set {Stats.%loop-player%::RespawnController} to {_player}
				set {_el} to location of loop-player
				add 1 to y-coordinate of {_el}
				loop 10 times:
					spawnParticle_Vector(witch, randomVector(), 0.8, {_el})
				add loop-player to {_pl::*}
			set {_tp::%loop-player%} to true
		loop {_pl::*}:
			{Stats.%loop-value%::RespawnController} is {_player}
			{_tp::%loop-value%} is not set
			delete {Stats.%loop-value%::RespawnController}
			delete {_pl::%loop-index%}
		set {_exist} to kitFakeCheck({_player}, "Machine-Respawn Controller")
		if {_exist} is false:
			delete {Stats.%{_player}%::RespawnController-Time}
			loop {_pl::*}:
				{Stats.%loop-value%::RespawnController} is {_player}
				delete {Stats.%loop-value%::RespawnController}

on death of player:
	{Stats.%victim%::RespawnController} is set
	set {_player} to {Stats.%victim%::RespawnController}
	if {Stats.%{_player}%::RespawnController-NoFuel} is not set:
		if checkFuel({_player}, 6) is false:
			stop
	set {Stats.%victim%::SpawnTime} to 15