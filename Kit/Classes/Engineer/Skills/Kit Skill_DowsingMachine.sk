# Dowsing Machine

function kitEffect_DowsingMachine(player: player):
	set {_loc} to getKitStats({_player}, "SkillLocation")
	if {_loc} is not set:
		nullEngineerPlacePosition({_player})
		stop

	nameCT({_player}, "Dowsing Machine")
	set {_now} to now
	setKitStats({_player}, "DowsingMachine-Time", {_now})
	kitFakeDeleteData({_player}, "Machine-Dowsing Machine")
	set {_loc.base} to {_loc}
	set {_loc.core} to {_loc}
	add 1 to y-coordinate of {_loc.core}

	set {_rotation} to getQuaternionf(0, 0, 0, 1)
	set {_translation} to getVector3f(-0.5, 0, -0.5)
	set {_scale} to getVector3f(1, 1, 1)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	set {_block} to getBlockData(sea lantern)
	set {_coredisply} to kitFakeBlock({_player}, {_loc.core}, {_block}, {_transformation}, "Machine-Dowsing Machine")
	
	set {_block} to getBlockData(cobblestone wall)
	kitFakeBlock({_player}, {_loc.base}, {_block}, {_transformation}, "Machine-Dowsing Machine")
	kitMsg({_player}, "<##00FFFF>Dowsing Machine§9を設置しました!")
	playSound("block.conduit.activate", 1, 2, {_loc.base})

	# coreを回転させる関数
	kitEffect_DowsingMachine_Core({_player}, {_coredisplay})
	while getKitStats({_player}, "DowsingMachine-Time") is {_now}:
		wait a second
		loop all players in radius 20 around {_loc.core}:
			kitCheckTeam({_player}, loop-player, false) is true
			loop-player does not have glowing
			if {_nofuel} is not set:
				if checkFuel({_player}, 5) is false:
					continue
			kitDamage({_player}, loop-player, 2, "索敵装置")
			apply glowing 1 to loop-player for 10 seconds
			kitEffectDescription(loop-player, "§4❤2 §8& §e§l発光")
			sendTimeBossbar_RED(loop-player, "§e発光", 200)
			playSound("block.conduit.attack.target", 1, 1, loop-player)
			set {_elbase} to {_loc.core}
			add 0.5 to y-coordinate of {_elbase}
			set {_vector} to setVector({_elbase}, location of head of loop-player)
			set yaw of {_elbase} to yaw of {_vector}
			set pitch of {_elbase} to pitch of {_vector}
			set {_dis} to distance between {_elbase} and loop-player * 0.1
			set {_ln} to 0
			loop 10 times:
				add 1 to {_ln}
				set {_el} to location {_dis} * {_ln} meters in front of {_elbase}
				kitParticle_DowsingMachine_Beam({_el})
		set {_exist} to kitFakeCheck({_player}, "Machine-Dowsing Machine")
		if {_exist} is false:
			deleteKitStats({_player}, "DowsingMachine-Time")

local function kitEffect_DowsingMachine_Core(player: player, display: entity):
	set {_now} to getKitStats({_player}, "DowsingMachine-Time")
	set {_yaw} to yaw of {_display}
	set {_rotate.yaw} to 40
	set {_add.yaw} to {_rotate.yaw} / 20

	set {_ln} to 0
	while getKitStats({_player}, "DowsingMachine-Time") is {_now}:
		delete {_exist}
		loop all block displays:
			loop-block display is {_display}
			set {_exist} to true
		if {_exist} is not set:
			stop

		set {_baseloc} to location of {_display}
		set {_tploc} to {_baseloc}
		add {_add.yaw} to {_yaw}
		set yaw of {_tploc} to {_yaw}
		teleport {_display} to {_tploc}

		wait a tick



local function kitParticle_DowsingMachine_Beam(loc: location):
	loop 20 times:
		spawnParticle(enchanted_hit, {_loc})
		wait a tick