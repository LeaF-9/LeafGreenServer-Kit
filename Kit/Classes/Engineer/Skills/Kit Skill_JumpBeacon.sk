# Skill-Name: Jump Beacon

function kitEffect_JumpBeacon(player: player):
	set {_loc} to kitStats_GET({_player}, "SkillLocation")
	if {_loc} is not set:
		nullEngineerPlacePosition({_player})
		stop

	nameCT({_player}, "Jump Beacon")
	set {_now} to now
	# 装置数の確認
	loop all entities:
		type of loop-entity is block display
		set {_metadata} to metadata value "kit-block" of loop-entity
		{_metadata} contains "Machine-Jump Beacon"
		metadata value {_metadata} of loop-entity is "%{_player}%"
		add 0.5 to {_allnum}
	if {_allnum} >= 3:
		set {_toptime} to 0
		loop {-Stats.%{_player}%::*}:
			loop-index contains "JumpBeacon-Time"
			set {_value} to loop-value
			set {_ditime} to "%difference between {_value} and {_now}%"
			set {_diftime} to timeNumber({_ditime})
			if {_diftime} > {_toptime}:
				set {_toptime} to {_diftime}
				set {_index} to loop-index
		replace "JumpBeacon-Time-" with "" in {_index}
		kitFakeDeleteData({_player}, "Machine-Jump Beacon-%{_index}%")
		kitStats_DELETE({_player}, "JumpBeacon-Time-%{_index}%")
	set {_JumpBeacon-Number} to kitStats_GET({_player}, "JumpBeacon-Number")
	add 1 to {_JumpBeacon-Number}
	set {_num} to {_JumpBeacon-Number}
	kitStats_SET({_player}, "JumpBeacon-Number", {_JumpBeacon-Number})

	# 機械設置処理
	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.25f,0f,-0.25f]", "[0.5f,0.5f,0.5f]")
	kitFakeBlock({_player}, {_loc}, "minecraft:spruce_fence", {_transformation}, "Machine-Jump Beacon-%{_num}%")
	set {_spawned} to spawned block display
	add {_spawned} to {_jumpbeacons::*}

	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.15f,0.5f,-0.15f]", "[0.3f,0.3f,0.3f]")
	kitFakeBlock({_player}, {_loc}, "minecraft:redstone_block", {_transformation}, "Machine-Jump Beacon-%{_num}%")
	set {_spawned} to spawned block display
	add {_spawned} to {_jumpbeacons::*}
	
	kitStats_SET({_player}, "JumpBeacon-Time-%{_num}%", {_now})
	kitMsg({_player}, "<##E8383D>Jump Beacon§6を設置しました!")
	loop all players:
		kitCheckTeam({_player}, loop-player, true) is true
		kitEffectDescription(loop-player, "§c§lJump Beacon§6§l設置")
	playSound("entity.blaze.hurt", 1, 2, {_loc})
	while kitStats_GET({_player}, "JumpBeacon-Time-%{_num}%") is {_now}:
		wait a tick
		loop all players:
			kitCheckTeam({_player}, loop-player, true) is true
			loop-player does not have a redstone item named "§cJump to Beacon" with lore "味方のビーコンを選択してTPします"
			give a redstone item named "§cJump to Beacon" with lore "味方のビーコンを選択してTPします" to loop-player
		set {_exist} to kitFakeCheck({_player}, "Machine-Jump Beacon-%{_num}%")
		if {_exist} is true:
			loop all players:
				kitCheckTeam(loop-player, {_player}, true) is true
				if tool of loop-player is a redstone item named "§cJump to Beacon" with lore "味方のビーコンを選択してTPします":
					loop all block displays:
						{_jumpbeacons::*} contains loop-block display
						packetGlowing(loop-player, loop-block display)
				else:
					loop all block displays:
						{_jumpbeacons::*} contains loop-block display
						packetUnglowing(loop-player, loop-block display)
		else:
			kitStats_DELETE({_player}, "JumpBeacon-Time-%{_num}%")
	if kitStats_GET({_player}, "JumpBeacon-Time-%{_num}%") is not set:
		loop all players:
			kitCheckTeam({_player}, loop-player, true) is true
			loop-player has a redstone item named "§cJump to Beacon" with lore "味方のビーコンを選択してTPします"
			remove a redstone item named "§cJump to Beacon" with lore "味方のビーコンを選択してTPします" from loop-player

on rightclick holding a redstone item:
	name of player's tool is "§cJump to Beacon"
	cancel event
	kitStats_GET(player, "JumpBeacon-Using") is not set
	kitEffect_JumpBeacon_Select(player)

function kitEffect_JumpBeacon_Select(player: player):
	set {_baseloc} to location of head of {_player}
	loop 100 times:
		{_ng} is not set
		{_target} is not set
		set {_loc} to location loop-number meters in front of {_baseloc}
		loop all entities in radius 3 around {_loc}:
			{_target} is not set
			type of loop-entity is block display
			set {_metadata} to metadata value "kit-block" of loop-entity
			{_metadata} contains "Machine-Jump Beacon"
			set {_holder} to metadata value {_metadata} of loop-entity parsed as offline player
			kitCheckTeam({_player}, {_holder}, true) is true
			set {_target} to loop-entity
	if {_target} is not set:
		send action bar "§c§l目先にBeaconがありません" to {_player}
		stop
	kitEffect_JumpBeacon_Jump({_player}, {_target})

function kitEffect_JumpBeacon_Jump(jumper: player, target: entity):
	kitStats_SET({_jumper}, "JumpBeacon-Using", true)
	set {_loc} to location of {_jumper}
	set {_time} to 3
	loop 60 times:
		delete {_alive}
		loop all entities:
			loop-entity is {_target}
			set {_alive} to true
		if {_alive} is not set:
			kitStats_DELETE({_jumper}, "JumpBeacon-Using")
			message formatted "§cビーコン消滅によりジャンプが中断されました" to {_jumper}
			stop
		teleport {_jumper} to {_loc}
		if {_time} is 3, 2 or 1:
			message formatted "§c%{_time}%..." to {_jumper}
			playSound("ui.button.click", 1, 2, {_loc})
			playSound("ui.button.click", 1, 2, {_target})
		subtract 0.05 from {_time}
		wait a tick
	kitStats_DELETE({_jumper}, "JumpBeacon-Using")
	set {_metadata} to metadata value "kit-block" of {_target}
	set {_goaler} to metadata value {_metadata} of {_target} parsed as offline player
	if checkFuel({_goaler}, 3) is false:
		message formatted "§4燃料不足によりジャンプが中断されました" to {_jumper}
		stop
	kitParticle_JumpBeacon_Jumped(location of {_target})
	teleport {_jumper} to location of {_target}
	message formatted "§aジャンプに成功しました!" to {_jumper}
	kitFakeDeleteData({_goaler}, {_metadata})
	set {_num} to {_metadata}
	replace "Machine-Jump Beacon-" with "" in {_metadata}
	kitStats_DELETE({_player}, "JumpBeacon-Time-%{_num}%")

function kitParticle_JumpBeacon_Jumped(baseloc: location):
	playSound("entity.wither.shoot", 1, 2, {_baseloc})
	set {_up} to -0.5
	loop 5 times:
		set {_el} to {_baseloc}
		add 0.5 to {_up}
		add {_up} to y-coordinate of {_el}
		set {_rad} to 0
		loop 10 times:
			add 36 to {_rad}
			add 0.5 * sin({_rad}) to x-coordinate of {_el}
			add 0.5 * cos({_rad}) to z-coordinate of {_el}
			spawnParticle_Color(entity_effect, rgb(255, 0, 255), {_el})
		wait a tick