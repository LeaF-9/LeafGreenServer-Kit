# Tracking System

function kitSkill_Engineer_4(player: player):
	set {_loc} to {-Stats.%{_player}%::SkillLocation}
	if {_loc} is not set:
		nullEngineerPlacePosition({_player})
		stop

	toolCT({_player}, "Engineer", 4)
	set {_now} to now
# 装置数の確認
	loop all block displays:
		metadata value "kit-block" of loop-block display contains "Machine-Tracking System"
		set {_metadata} to metadata value "kit-block" of loop-block display
		metadata value {_metadata} of loop-block display is "%{_player}%"
		add 0.2 to {_allnum}
	if {_allnum} >= 5:
		set {_toptime} to 0	
		loop {-Stats.%{_player}%::*}:
			loop-index contains "TrackingSystem-Time"
			set {_value} to loop-value
			set {_ditime} to "%difference between {_value} and {_now}%"
			set {_diftime} to timeNumber({_ditime})
			if {_diftime} > {_toptime}:
				set {_toptime} to {_diftime}
				set {_index} to loop-index
		replace "TrackingSystem-Time-" with "" in {_index}
		kitFakeDeleteData({_player}, "Machine-Tracking System-%{_index}%")
		delete {-Stats.%{_player}%::TrackingSystem-Time-%{_index}%}
	add 1 to {-Stats.%{_player}%::TrackingSystem-Number}
	set {_num} to {-Stats.%{_player}%::TrackingSystem-Number}
	set {_blockdisplay} to kitEffect_TrackingSystem_Machine({_player}, {_num}, {_loc})
	set {_baseloc} to {_loc}
	add 0.5 to y-coordinate of {_baseloc}
	set {-Stats.%{_player}%::TrackingSystem-Time-%{_num}%} to {_now}
	kitMsg({_player}, "<##0075C2>Tracking System§bを設置しました!")
	playSound("block.respawn_anchor.charge", 1, 2, {_loc})
	while {-Stats.%{_player}%::TrackingSystem-Time-%{_num}%} is {_now}:
		wait a tick
		delete {_true}
		loop all entities in radius 5 around {_loc}:
			kitCheckTeam({_player}, loop-entity, false) is true
			getInvisibleStatus(loop-entity) is false
			delete {_true}
			if {_nofuel} is true:
				set {_true} to true
			else:
				add 1 to {_fuel}
				if mod({_fuel}, 20) = 0:
					if checkFuel({_player}, 4) is true:
						set {_true} to true
				else:
					if {Kit-Fuel::%{_player}%} >= 1:
						set {_true} to true
			if {_true} is true:
				kitDamage({_player}, loop-entity, 0.3, "追尾装置")
				if loop-entity does not have slowness:
					apply slowness 3 to loop-entity for a tick
				kitEffectDescription(loop-entity, "§4❤0.3 §8& §8§l鈍足")
				playSound("block.respawn_anchor.ambient", 0.1, 2, {_loc})
				set {_toloc} to location of loop-entity
				add 1 to y-coordinate of {_toloc}
				set {_vector} to setVector({_baseloc}, {_toloc})
				set {_el} to {_baseloc}
				set yaw of {_el} to yaw of {_vector}
				set pitch of {_el} to pitch of {_vector}
				set {_dis} to distance between {_baseloc} and {_toloc}
				set {_dis} to {_dis} / 10
				loop 10 times:
					set {_met} to {_dis} * loop-number
					set {_efl} to location {_met} meters in front of {_el}
					spawnParticle_Dust(rgb(0, 191, 255), 1, {_efl})
		set {_exist} to kitFakeCheck({_player}, "Machine-Tracking System-%{_num}%")
		if {_exist} is true:
			if {_true} is true:
				set glowing of {_blockdisplay} to true
				set {_tploc} to location of {_blockdisplay}
				add 18 to yaw of {_tploc}
				teleport {_blockdisplay} to {_tploc}
			else:
				set glowing of {_blockdisplay} to false
		else:
			delete {-Stats.%{_player}%::TrackingSystem-Time-%{_num}%}

local function kitEffect_TrackingSystem_Machine(player: player, num: number, loc: location) :: entity:
	# foot
	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[0.2f,0f,0.2f]", "[0.1f,0.2f,0.1f]")
	kitFakeBlock({_player}, {_loc}, "minecraft:white_stained_glass", {_transformation}, "Machine-Tracking System-%{_num}%")
	
	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.3f,0f,0.2f]", "[0.1f,0.2f,0.1f]")
	kitFakeBlock({_player}, {_loc}, "minecraft:white_stained_glass", {_transformation}, "Machine-Tracking System-%{_num}%")

	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[0.2f,0f,-0.3f]", "[0.1f,0.2f,0.1f]")
	kitFakeBlock({_player}, {_loc}, "minecraft:white_stained_glass", {_transformation}, "Machine-Tracking System-%{_num}%")

	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.3f,0f,-0.3f]", "[0.1f,0.2f,0.1f]")
	kitFakeBlock({_player}, {_loc}, "minecraft:white_stained_glass", {_transformation}, "Machine-Tracking System-%{_num}%")

	# head
	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.25f,0.25f,-0.25f]", "[0.5f,0.5f,0.5f]")
	kitFakeBlock({_player}, {_loc}, "minecraft:beacon", {_transformation}, "Machine-Tracking System-%{_num}%")
	set {_return} to spawned block display

	return {_return}