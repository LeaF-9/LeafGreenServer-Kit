# Launch Pad

function kitEffect_LaunchPad(player: player):
	set {_loc} to getKitStats({_player}, "SkillLocation")
	if {_loc} is not set:
		nullEngineerPlacePosition({_player})
		stop

	nameCT({_player}, "Launch Pad")
	set {_now} to now
	setKitStats({_player}, "LaunchPad-Time", {_now})
	kitFakeDeleteData({_player}, "Machine-Launch Pad")
	if {team::%{_player}%} is "red":
		set {_block} to getBlockState(red stained glass)
	else if {team::%{_player}%} is "blue":
		set {_block} to getBlockState(blue stained glass)
	else if {team::%{_player}%} is "yellow":
		set {_block} to getBlockState(yellow stained glass)
	else if {team::%{_player}%} is "green":
		set {_block} to getBlockState(lime stained glass)
	else:
		set {_block} to getBlockState(purple stained glass)
	set {_plate} to getBlockState(heavy weighted pressure plate)

	set {_loc.glass} to {_loc}
	subtract 0.5 from y-coordinate of {_loc.glass}
	set {_loc.plate} to {_loc}

	set {_transformation::left_rotation} to getAxisAngle4f(0, 0, 0, 1)
	set {_transformation::right_rotation} to getAxisAngle4f(0, 0, 0, 1)
	set {_transformation::translation} to getVector3f(-0.5, 0, -0.5)
	set {_transformation::scale} to getVector3f(1, 1, 1)

	kitFakeBlock({_player}, {_loc.plate}, {_plate}, {_transformation::*}, "Machine-Launch Pad")
	kitFakeBlock({_player}, {_loc.glass}, {_block}, {_transformation::*}, "Machine-Launch Pad")
	kitMsg({_player}, "<##D70035>Launch Pad§6を設置しました!")
	playSound("entity.blaze.hurt", 1, 2, {_loc})
	while getKitStats({_player}, "LaunchPad-Time") is {_now}:
		wait a tick
		loop {_time::*}:
			if {_time::%loop-index%} is set:
				if {_time::%loop-index%} <= 0:
					delete {_time::%loop-index%}
				else:
					subtract 1 from {_time::%loop-index%}
		loop all players in radius 1 around {_loc.plate}:
			{_time::%loop-player%} is not set
			kitCheckTeam({_player}, loop-player, true) is true
			if {_nofuel} is not set:
				if checkFuel({_player}, 1) is false:
					continue
			setSupporter(loop-player, {_player})
			setKitStats(loop-player, "LaunchPad", true)
			push loop-player upwards at speed 1
			push loop-player forward at speed 2
			set {_time::%loop-player%} to 20
			playSound("entity.wither.shoot", 1, 2, loop-player)
			loop 10 times:
				set {_el} to aroundEffect(loop-player)
				spawnParticle(campfire_cosy_smoke, {_el})
		set {_exist} to kitFakeCheck({_player}, "Machine-Launch Pad")
		if {_exist} is false:
			deleteKitStats({_player}, "LaunchPad-Time")
			continue

on damage of player:
	getKitStats(victim, "LaunchPad") is true
	damage was caused by fall
	cancel event
	deleteKitStats(victim, "LaunchPad")