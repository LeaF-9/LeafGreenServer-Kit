# Tracking System

function kitEffect_TrackingSystem(player: player):
	set {_loc} to getKitStats({_player}, "SkillLocation")
	if {_loc} is not set:
		nullEngineerPlacePosition({_player})
		stop

	nameCT({_player}, "Tracking System")
	set {_now} to now
# 装置数の確認
	loop all block displays:
		metadata value "kit-block" of loop-block display contains "Machine-Tracking System"
		set {_metadata} to metadata value "kit-block" of loop-block display
		metadata value {_metadata} of loop-block display is "%{_player}%"
		add 0.2 to {_allnum}
	if {_allnum} >= 5:
		set {_toptime} to 0	
		loop {-Stats.%{_player}%::*}:
			loop-index contains "TrackingSystem-Time"
			set {_value} to loop-value
			set {_ditime} to "%difference between {_value} and {_now}%"
			set {_diftime} to timeNumber({_ditime})
			if {_diftime} > {_toptime}:
				set {_toptime} to {_diftime}
				set {_index} to loop-index
		replace "TrackingSystem-Time-" with "" in {_index}
		kitFakeDeleteData({_player}, "Machine-Tracking System-%{_index}%")
		deleteKitStats({_player}, "TrackingSystem-Time-%{_index}%")
	set {_TrackingSystem-Number} to getKitStats({_player}, "TrackingSystem-Number")
	add 1 to {_TrackingSystem-Number}
	set {_num} to {_TrackingSystem-Number}
	setKitStats({_player}, "TrackingSystem-Number", {_TrackingSystem-Number})
	set {_blockdisplay} to kitEffect_TrackingSystem_Machine({_player}, {_num}, {_loc})
	set {_baseloc} to {_loc}
	add 0.5 to y-coordinate of {_baseloc}
	setKitStats({_player}, "TrackingSystem-Time-%{_num}%", {_now})
	kitMsg({_player}, "<##0075C2>Tracking System§bを設置しました!")
	playSound("block.respawn_anchor.charge", 1, 2, {_loc})
	while getKitStats({_player}, "TrackingSystem-Time-%{_num}%") is {_now}:
		wait a tick
		delete {_true}
		loop all entities in radius 5 around {_loc}:
			kitCheckTeam({_player}, loop-entity, false) is true
			getInvisibleStatus(loop-entity) is false
			delete {_true}
			if {_nofuel} is true:
				set {_true} to true
			else:
				add 1 to {_fuel}
				if mod({_fuel}, 20) = 0:
					if checkFuel({_player}, 4) is true:
						set {_true} to true
				else:
					if {Kit-Fuel::%{_player}%} >= 1:
						set {_true} to true
			if {_true} is true:
				kitDamage({_player}, loop-entity, 0.3, "追尾装置")
				if loop-entity does not have slowness:
					apply slowness 3 to loop-entity for a tick
				kitEffectDescription(loop-entity, "§4❤0.3 §8& §8§l鈍足")
				playSound("block.respawn_anchor.ambient", 0.1, 2, {_loc})
				set {_toloc} to location of loop-entity
				add 1 to y-coordinate of {_toloc}
				set {_vector} to setVector({_baseloc}, {_toloc})
				set {_el} to {_baseloc}
				set yaw of {_el} to yaw of {_vector}
				set pitch of {_el} to pitch of {_vector}
				set {_dis} to distance between {_baseloc} and {_toloc}
				set {_dis} to {_dis} / 10
				loop 10 times:
					set {_met} to {_dis} * loop-number
					set {_efl} to location {_met} meters in front of {_el}
					spawnParticle_Dust(rgb(0, 191, 255), 1, {_efl})
		set {_exist} to kitFakeCheck({_player}, "Machine-Tracking System-%{_num}%")
		if {_exist} is true:
			if {_true} is true:
				set glowing of {_blockdisplay} to true
				set {_tploc} to location of {_blockdisplay}
				add 18 to yaw of {_tploc}
				teleport {_blockdisplay} to {_tploc}
			else:
				set glowing of {_blockdisplay} to false
		else:
			deleteKitStats({_player}, "TrackingSystem-Time-%{_num}%")

local function kitEffect_TrackingSystem_Machine(player: player, num: number, loc: location) :: entity:
	set {_rotation} to getQuaternionf(0, 0, 0, 1)
	set {_translation} to getVector3f(0.2, 0, 0.2)
	set {_scale} to getVector3f(0.1, 0.2, 0.1)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	set {_block} to getBlockData(white stained glass)
	# foot
	set {_translation} to getVector3f(0.2, 0, 0.2)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	kitFakeBlock({_player}, {_loc}, {_block}, {_transformation}, "Machine-Tracking System-%{_num}%")
	
	set {_translation} to getVector3f(-0.3, 0, 0.2)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	kitFakeBlock({_player}, {_loc}, {_block}, {_transformation}, "Machine-Tracking System-%{_num}%")

	set {_translation} to getVector3f(0.2, 0, -0.3)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	kitFakeBlock({_player}, {_loc}, {_block}, {_transformation}, "Machine-Tracking System-%{_num}%")

	set {_translation} to getVector3f(-0.3, 0, -0.3)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	kitFakeBlock({_player}, {_loc}, {_block}, {_transformation}, "Machine-Tracking System-%{_num}%")

	# head
	set {_translation} to getVector3f(-0.25, 0.25, -0.25)
	set {_scale} to getVector3f(0.5, 0.5, 0.5)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	set {_block} to getBlockData(beacon)
	set {_return} to kitFakeBlock({_player}, {_loc}, {_block}, {_transformation}, "Machine-Tracking System-%{_num}%")

	return {_return}