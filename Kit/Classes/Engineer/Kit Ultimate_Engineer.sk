# Transport Lift

function kitUltEngineer(player: player):
	set {_tploc} to location of {_player}
	if {Kit-tochu} is true:
		loop {Kit-GameStageRespawns.%{TrueTeam::%{_player}%}%::*}:
			add loop-value to {_fromloc::*}
	else if "%world of {_player}%" is "Kit-SkillCheck":
		add {Kit-TestWorld} to {_fromloc::*}
	else:
		stop
	set {_now} to now
	set {Ult.%{_player}%::Engineer} to {_now}
	set {_mecloc} to {_tploc}
	add 2 to y-coordinate of {_mecloc}
	set yaw of {_mecloc} to 0
	set pitch of {_mecloc} to 0

	set {_matrix} to getMatrix4f(1, 0, 0, -0.5, 0, -0.5, -0, 0.5, 0, 0, -1, 0.5, 0, 0, 0, 1)
	set {_leftrotation} to getQuaternionFromMatrix({_matrix})
	set {_rightrotation} to getQuaternionf(0, 0, 0, 1)
	set {_translation} to getVector3f(-0.5, 0.375, 0.5)
	set {_scale} to getVector3f(1, 0.5, 1)
	set {_transformation} to getTransformation({_leftrotation}, {_rightrotation}, {_translation}, {_scale})
	set {_block} to getBlockData(shrieker)
	set {_transporter} to kitFakeBlock({_player}, {_mecloc}, {_block}, {_transformation}, "Machine-Transport Lift")

	loop {_fromloc::*}:
		set {_armloc} to loop-value
		add 2 to y-coordinate of {_armloc}
		set pitch of {_armloc} to 0
		set {_armloc} to location 3 meters in front of {_armloc}
		spawn an armor stand at {_armloc} with nbt compound from "{Invulnerable:1b,PersistenceRequired:1b,NoBasePlate:1b,NoGravity:1b,Invisible:1b,Marker:1b}"
		set metadata value "gametype" of spawned armor stand to "Kit"
		set metadata value "Kit-EngineerUlt" of spawned armor stand to "%{_player}%"
		set display name of spawned armor stand to "§3§lスニーク§7で§6§lテレポート§7します!"
	set {_tpy} to 0.5
	set {_fromy} to 0
	while {Ult.%{_player}%::Engineer} is {_now}:
		wait a tick
#	CT確認
		loop {_ct::*}:
			set {_ct} to loop-value
			set {_rect} to {_ct} - 1
			if {_rect} <= 0:
				delete {_ct::%loop-index%}
			else:
				set {_ct::%loop-index%} to {_rect}
#	テレポート動作
		# 機械の発光除去
		loop {_machineGlowing::*}:
			set {_loop-player} to loop-value
			packetUnglowing({_loop-player}, {_transporter})
		delete {_machineGlowing::*}

		# リスポーンから機械へ
		loop {_fromloc::*}:
			set {_temploc} to loop-value
			loop all players in radius 3 around {_temploc}:
				kitCheckTeam({_player}, loop-player, true) is true
				# スニークしていない場合は通知のみ
				if loop-player is not sneaking:
					sendSubtitle(loop-player, "§3§lスニーク§7で§e機械§7に§b§lテレポート§7します", 0.1 seconds, 0 seconds, 0 seconds)
					if {_machineGlowing::*} does not contain loop-player:
						packetGlowing(loop-player, {_transporter})
						add loop-player to {_machineGlowing::*}
					continue

				{_ct::%loop-player%} is not set
				set {_ct::%loop-player%} to 20
				teleport loop-player to {_tploc}
				send action bar "§5§lテレポートしました!" to loop-player
				playSound("entity.enderman.teleport", 1, 0.5, {_tploc})
				set {_beloc} to {_tploc}
				add 1 to y-coordinate of {_beloc}
				loop 30 times:
					spawnParticle_Vector(witch, randomVector(), 1, {_beloc})
		# 機械からランダムなリスポーンへ
		loop all players in radius 0.5 around {_tploc}:
			kitCheckTeam({_player}, loop-player, true) is true
			# スニークしていない場合は通知のみ
			if loop-player is not sneaking:
				sendSubtitle(loop-player, "§3§lスニーク§7で§e復活地点§7に§b§lテレポート§7します", 0.1 seconds, 0 seconds, 0 seconds)
				continue

			{_ct::%loop-player%} is not set
			set {_ct::%loop-player%} to 20
			set {_fromtp} to a random location out of {_fromloc::*}
			teleport loop-player to {_fromtp}
			send action bar "§5§lテレポートしました!" to loop-player
			playSound("entity.enderman.teleport", 1, 0.5, {_fromtp})
			set {_beloc} to {_fromtp}
			add 1 to y-coordinate of {_beloc}
			loop 30 times:
				spawnParticle_Vector(witch, randomVector(), 1, {_beloc})
#	エフェクト
		delete {_tpeffs::*}
		delete {_fromeffs::*}
		set {_tpeffbase} to {_tploc}
		subtract 0.01 from {_tpy}
		if {_tpy} <= 0:
			set {_tpy} to 0.5
		loop 4 times:
			set {_ln} to loop-number - 1
			set {_temploc} to {_tpeffbase}
			add 0.5 * {_ln} + {_tpy} to y-coordinate of {_temploc}
			add {_temploc} to {_tpeffs::*}
		loop {_tpeffs::*}:
			set {_tpeff} to loop-value
			set {_rad} to 0
			loop 10 times:
				add 36 to {_rad}
				set {_eff} to {_tpeff}
				add sin({_rad}) * 0.5 to x-coordinate of {_eff}
				add cos({_rad}) * 0.5 to z-coordinate of {_eff}
				spawnParticle_Dust(rgb(34, 58, 112), 1, {_eff})
		loop {_fromloc::*}:
			set {_fromeffbase} to loop-value
			add 0.01 to {_fromy}
			if {_fromy} >= 0.5:
				set {_fromy} to 0
			loop 4 times:
				set {_ln} to loop-number - 1
				set {_temploc} to {_fromeffbase}
				add 0.5 * {_ln} + {_fromy} to y-coordinate of {_temploc}
				add {_temploc} to {_fromeffs::*}
			loop {_fromeffs::*}:
				set {_fromeff} to loop-value-2
				set {_rad} to 0
				loop 36 times:
					add 10 to {_rad}
					set {_eff} to {_fromeff}
					add sin({_rad}) * 3 to x-coordinate of {_eff}
					add cos({_rad}) * 3 to z-coordinate of {_eff}
					spawnParticle_Dust(rgb(34, 58, 112), 1, {_eff})
#	存在確認	
		set {_exist} to kitFakeCheck({_player}, "Machine-Transport Lift")
		if {_exist} is false:
			delete {Ult.%{_player}%::Engineer}
	loop all entities:
		type of loop-entity is armor stand
		metadata value "Kit-EngineerUlt" of loop-entity is "%{_player}%"
		delete loop-entity