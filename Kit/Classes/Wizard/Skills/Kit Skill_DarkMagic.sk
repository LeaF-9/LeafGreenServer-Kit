# Dark Magic

function kitEffect_DarkMagic(player: player):
	set {_mana} to kitManaCalc({_player}, 10)
	if {Kit-Mana::%{_player}%} < {_mana}:
		send action bar "§4§lマナが足りません!" to {_player}
		stop

	nameCT({_player}, "Dark Magic")
	shoot a snowball from {_player} at speed 1
	set metadata value "magic" of shot snowball to "dark"
	set item of shot snowball to black concrete
	kitParticle_Wizard_Magic(shot snowball, 0, 0, 0)
	subtract {_mana} from {Kit-Mana::%{_player}%}

function kitEffect_DarkMagic_Magic(player: player, loc: location):
	loop all entities in radius 5 around {_loc}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		if "%type of loop-entity%" is "player":
			LastKiller(loop-entity, {_player})
		apply blindness 1 to loop-entity for 5 seconds
		apply wither 2 to loop-entity for 5 seconds
		message formatted "§0暗闇魔法を受けた!" to loop-entity
		kitMsg({_player}, "§5%loop-entity% §dに§0暗闇魔法§dを付与した!")
		kitEffectDescription(loop-entity, "§5暗闇魔法")
		sendTimeBossbar_RED(loop-entity, "暗闇魔法", 100)
		kitParticle_DarkMagic_Hit(loop-entity)

local function kitParticle_DarkMagic_Hit(entity: entity):
	loop 5 times:
		add 0.4 to {_h}
		set {_rad} to 0
		loop 10 times:
			set {_el} to location of {_entity}
			add 36 to {_rad}
			set {_vec} to vector(0.5, {_h}, 0)
			rotate {_vec} around y-axis by {_rad}
			set {_el} to {_el} ~ {_vec}
			spawnParticle_Color(entity_effect, rgb(0, 0, 0), {_el})
		wait a tick