# Earth Magic

function kitSkill_Wizard_8(player: player):
	set {_mana} to kitManaCalc({_player}, 20)
	if {Kit-Mana::%{_player}%} < {_mana}:
		send action bar "§4§lマナが足りません!" to {_player}
		stop
	toolCT({_player}, "Wizard", 8)
	set {_baseloc} to location of head of {_player}
	set {_front} to 0
	loop 20 times:
		if {_ok} is not set:
			loop 5 times:
				{_ok} is not set
				add 1 to {_front}
				set {_loc} to location {_front} meters in front of {_baseloc}
				spawnParticle(witch, {_loc})
				if block at {_loc} is not passable:
					set {_ok} to true
			wait a tick
	{_ok} is true
	subtract {_mana} from {Kit-Mana::%{_player}%}
	kitMsg({_player}, "<##8b4513>地響き<##f0e68c>を召喚します...")
	kitFreeze({_player}, 1 seconds, false)
	set {_bn} to 0
	while {_blockok} is not set:
		add 0.1 to {_bn}
		set {_bl} to location {_bn} meters above {_loc}
		if block at {_bl} is passable:
			set {_blockok} to true
			set {_point} to {_bl}
	kitParticle_EarthMagic({_point})
	set {_el} to location of {_player}
	loop 30 times:
		spawnParticle_Vector(witch, randomVector(), 0.3, {_el})
	wait a second
	playSound("entity.evoker.prepare_attack", 1.5, 1.5, {_point})
	set {_ran} to 0
	loop 8 times:
		add 1 to {_ran}
		loop all blocks in radius {_ran} around location of {_point}:
			block below loop-block is not passable
			loop-block is passable
			distance between {_point} and loop-block is between {_ran} - 1 and {_ran}
			set {_material} to block data of block below loop-block
			spawnParticle_Block({_material}, location of loop-block)
		loop all players in radius {_ran} around location of {_point}:
			kitCheckTeam({_player}, loop-player, false) is true
			{_hit::%loop-player%} is not set
			set {_hit::%loop-player%} to true
			kitStun(loop-player, 8 seconds)
		playSound("block.grass.break", 0.5, 1, location of {_point})
		wait a tick

function kitParticle_EarthMagic(point: location):
	playSound("entity.evoker.prepare_summon", 1.5, 2, {_point})
	loop 20 times:
	#	弧を描く
		set {_n.k} to 0
		loop 60 times:
			set {_effbaseloc} to {_point}
			add 8 * sin({_n.k}) to x-coordinate of {_effbaseloc}
			add 8 * cos({_n.k}) to z-coordinate of {_effbaseloc}
			spawnParticle_Dust(rgb(255, 215, 0), 1, {_effbaseloc})
			add 6 to {_n.k}
	#	弦を描く
		if {_l.A} is not set:
			set {_l.A} to {_point}
			set yaw of {_l.A} to 120
			set pitch of {_l.A} to 0
			add 8 * sin(90) to x-coordinate of {_l.A}
			add 8 * cos(90) to z-coordinate of {_l.A}
		if {_l.B} is not set:
			set {_l.B} to {_point}
			set yaw of {_l.B} to 0
			set pitch of {_l.B} to 0
			add 8 * sin(210) to x-coordinate of {_l.B}
			add 8 * cos(210) to z-coordinate of {_l.B}
		if {_l.C} is not set:
			set {_l.C} to {_point}
			set yaw of {_l.C} to 240
			set pitch of {_l.C} to 0
			add 8 * sin(330) to x-coordinate of {_l.C}
			add 8 * cos(330) to z-coordinate of {_l.C}
		if {_l.D} is not set:
			set {_l.D} to {_point}
			set yaw of {_l.D} to 180
			set pitch of {_l.D} to 0
			add 8 * sin(30) to x-coordinate of {_l.D}
			add 8 * cos(30) to z-coordinate of {_l.D}
		if {_l.E} is not set:
			set {_l.E} to {_point}
			set yaw of {_l.E} to 60
			set pitch of {_l.E} to 0
			add 8 * sin(150) to x-coordinate of {_l.E}
			add 8 * cos(150) to z-coordinate of {_l.E}
		if {_l.F} is not set:
			set {_l.F} to {_point}
			set yaw of {_l.F} to 300
			set pitch of {_l.F} to 0
			add 8 * sin(270) to x-coordinate of {_l.F}
			add 8 * cos(270) to z-coordinate of {_l.F}
		loop 16 times:
			add sqrt(3) / 2 to {_n.g}
	#		A-B
			set {_loc.g} to location {_n.g} meters in front of {_l.A}
			spawnParticle_Dust(rgb(255, 215, 0), 1, {_loc.g})
	#		D-E
			set {_loc.g} to location {_n.g} meters in front of {_l.D}
			spawnParticle_Dust(rgb(255, 215, 0), 1, {_loc.g})
	#		B-C
			set {_loc.g} to location {_n.g} meters in front of {_l.B}
			spawnParticle_Dust(rgb(255, 215, 0), 1, {_loc.g})
	#		E-F
			set {_loc.g} to location {_n.g} meters in front of {_l.E}
			spawnParticle_Dust(rgb(255, 215, 0), 1, {_loc.g})
	#		C-A
			set {_loc.g} to location {_n.g} meters in front of {_l.C}
			spawnParticle_Dust(rgb(255, 215, 0), 1, {_loc.g})
	#		F-D
			set {_loc.g} to location {_n.g} meters in front of {_l.F}
			spawnParticle_Dust(rgb(255, 215, 0), 1, {_loc.g})
		wait a tick