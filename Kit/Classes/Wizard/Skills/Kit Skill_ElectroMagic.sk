# Electro Magic

function kitEffect_ElectroMagic(player: player):
	set {_mana} to kitManaCalc({_player}, 10)
	if {Kit-Mana::%{_player}%} < {_mana}:
		send action bar "§4§lマナが足りません!" to {_player}
		stop

	nameCT({_player}, "Electro Magic")
	shoot a snowball from {_player} at speed 1
	set metadata value "magic" of shot snowball to "electro"
	set item of shot snowball to yellow concrete
	kitParticle_Wizard_Magic(shot snowball, 100, 100, 0)
	subtract {_mana} from {Kit-Mana::%{_player}%}

function kitEffect_ElectroMagic_Magic(player: player, loc: location):
	loop all entities in radius 5 around {_loc}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		kitDamage({_player}, loop-entity, 2, "電撃魔法", true)
		strike lightning effect at loop-entity
		message formatted "§e電撃魔法を受けた!" to loop-entity
		kitMsg({_player}, "§5%loop-entity% §dに§e電撃魔法§dを付与した!")
		kitEffectDescription(loop-entity, "§4❤2 §8& §e麻痺")
		if "%type of loop-entity%" is "player":
			kitPalsy(loop-entity, 200)
		kitParticle_ElectroMagic_Hit(loop-entity)

local function kitParticle_ElectroMagic_Hit(entity: entity):
	loop 5 times:
		add 0.4 to {_h}
		set {_rad} to 0
		loop 10 times:
			set {_el} to location of {_entity}
			add 36 to {_rad}
			set {_vec} to vector(0.5, {_h}, 0)
			rotate {_vec} around y-axis by {_rad}
			set {_el} to {_el} ~ {_vec}
			spawnParticle_Color(entity_effect, rgb(100, 100, 0), {_el})
		wait a tick