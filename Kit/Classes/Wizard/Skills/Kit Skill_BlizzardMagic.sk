# Skill-Name: Blizzard Magic

function kitEffect_BlizzardMagic(player: player):
	set {_mana} to kitManaCalc({_player}, 20)
	if {Kit-Mana::%{_player}%} < {_mana}:
		send action bar "§4§lマナが足りません!" to {_player}
		stop
	nameCT({_player}, "Blizzard Magic")
	set {_baseloc} to location of head of {_player}
	set {_front} to 0
	loop 20 times:
		if {_ok} is not set:
			loop 5 times:
				{_ok} is not set
				add 1 to {_front}
				set {_loc} to location {_front} meters in front of {_baseloc}
				spawnParticle(witch, {_loc})
				if block at {_loc} is not passable:
					set {_ok} to true
			wait a tick
	{_ok} is true
	subtract {_mana} from {Kit-Mana::%{_player}%}
	kitMsg({_player}, "<##00ced1>ブリザード<##5f9ea0>を召喚します...")
	kitFreeze({_player}, 20, false)
	set {_bn} to 0
	while {_blockok} is not set:
		add 0.1 to {_bn}
		set {_bl} to location {_bn} meters above {_loc}
		if block at {_bl} is passable:
			set {_blockok} to true
			set {_point} to {_bl}
	kitParticle_BlizzardMagic({_point})
	set {_el} to location of {_player}
	loop 30 times:
		spawnParticle_Vector(witch, randomVector(), 0.3, {_el})
	wait a second
	playSound("entity.illusioner.prepare_mirror", 1.5, 0.8, {_point})
	shoot a falling blue ice block from {_point} at speed 0.5 upward
	set metadata value "blizzard" of shot entity to "%{_player}%"
	kitParticle_BlizzardMagic_Summon(shot entity)
	set {_ran} to 0
	loop 4 times:
		add 2 to {_ran}
		loop all blocks in radius {_ran} around {_point}:
			block below loop-block is not air
			loop-block is air
			distance between {_point} and loop-block is between {_ran} - 1 and {_ran}
			spawnParticle_Color(entity_effect, rgb(0, 206, 209), location of loop-block)
		wait a tick
	wait 6 ticks
	loop all blocks in radius 8 around {_point}:
		loop-block is air
		block below loop-block is not air
		add location of loop-block to {_rb::*}
	loop all falling blue ice blocks:
		metadata value "blizzard" of loop-falling blue ice is "%{_player}%"
		set {_fl} to location of loop-falling blue ice
		delete loop-falling blue ice
	loop 12 times:
		set {_pitch} to a random number between 0.5 and 2
		playSound("block.glass.break", 1, {_pitch}, {_point})
		loop 3 times:
			shoot a snowball from {_fl} at speed 0.1 upwards
			kitParticle_BlizzardMagic_Fragment(shot snowball)
			push shot snowball north at speed random number from -0.5 to 0.5
			push shot snowball east at speed random number from -0.5 to 0.5
		loop 10 times:
			set {_rp} to a random location out of {_rb::*}
			set {_rp} to location of block above {_rp}
			set {_rn} to a random integer between 1 and 5
			if {_rn} is 1:
				spawnParticle_Block(ice, {_rp})
			else if {_rn} is 2:
				spawnParticle_Block(packed ice, {_rp})
			else if {_rn} is 3:
				spawnParticle_Block(blue ice, {_rp})
			else if {_rn} is 4:
				spawnParticle_Block(snow block, {_rp})
		# --- Entity込のFreeze ---
		loop all entities in radius 8 around {_point}:
			kitCheckTeam(loop-entity, {_player}, false) is true
			kitDamage({_player}, loop-entity, 1, "吹雪魔法")
			if checkEntityType(loop-entity, player) is true:
				if getDebuffs(loop-entity, "Freeze") is false:
					add loop-entity to {_fp::*}
					set {_wk} to walk speed of loop-entity
					subtract 0.025 from {_wk}
					if {_wk} <= 0:
						set walk speed of loop-entity to 0.2
						kitFreeze(loop-entity, 40)
						kitEffectDescription(loop-entity, "§4❤1 §8& §b凍結")
						playSound("entity.player.levelup", 1, 2, loop-entity)
					else:
						set walk speed of loop-entity to {_wk}
						kitEffectDescription(loop-entity, "§4❤1 §8& §c鈍足")
		# ------------------------
		wait 5 ticks
	wait a second
	loop {_fp::*}:
		set walk speed of loop-value to 0.2

function kitParticle_BlizzardMagic(point: location):
	playSound("entity.evoker.prepare_summon", 1.5, 2, {_point})

	set {_circleRad} to 8

	loop 20 times:
		# 弧を描く
		set {_n.k} to 0
		loop 60 times:
			set {_vec} to vector({_circleRad}, 0, 0)
			rotate {_vec} around y-axis by {_n.k}
			set {_effbaseloc} to {_point} ~ {_vec}
			spawnParticle_Dust(rgb(0, 206, 209), 1, {_effbaseloc})
			add 6 to {_n.k}

		# 弦を描く
		set {_rad} to 0
		set {_torad} to 120
		set {_addrad} to 60
		set {_length} to (sqrt(3) * {_circleRad})
		set {_lineCount} to 16
		set {_addMeter} to ({_length} / {_lineCount})

		loop 6 times:
			# from point
			set {_vec} to vector({_circleRad}, 0, 0)
			rotate {_vec} around y-axis by {_rad}
			set {_loc.from} to {_point} ~ {_vec}

			# to point
			set {_vec} to vector({_circleRad}, 0, 0)
			rotate {_vec} around y-axis by ({_rad} + {_torad})
			set {_loc.to} to {_point} ~ {_vec}

			# draw line
			set {_lineMeter} to 0
			loop {_lineCount} times:
				add {_addMeter} to {_lineMeter}
				set {_vec.line} to setVector({_loc.from}, {_loc.to}, {_lineMeter})
				set {_el} to {_loc.from} ~ {_vec.line}
				spawnParticle_Dust(rgb(0, 206, 209), 1, {_el})

			add {_addrad} to {_rad}
		wait a tick

function kitParticle_BlizzardMagic_Summon(entity: entity):
	loop 10 times:
		spawnParticle(item_snowball, location of {_entity})
		wait a tick

function kitParticle_BlizzardMagic_Fragment(proj: entity):
	loop 20 times:
		spawnParticle_Dust(rgb(100, 255, 255), 1, location of {_proj})
		wait a tick