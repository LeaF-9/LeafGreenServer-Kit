# Drain Magic

function kitEffect_DrainMagic(player: player):
	set {_mana} to kitManaCalc({_player}, 10)
	if {Kit-Mana::%{_player}%} < {_mana}:
		send action bar "§4§lマナが足りません!" to {_player}
		stop

	nameCT({_player}, "Drain Magic")
	shoot a snowball from {_player} at speed 1
	set metadata value "magic" of shot snowball to "drain"
	set item of shot snowball to green concrete
	kitParticle_Wizard_Magic(shot snowball, 0, 100, 0)
	subtract {_mana} from {Kit-Mana::%{_player}%}

function kitEffect_DrainMagic_Magic(player: player, loc: location):
	loop all players in radius 5 around {_loc}:
		kitCheckTeam(loop-player, {_player}, false) is true
		LastKiller(loop-player, {_player})
		kitEffect_DrainMagic_Hit({_player}, loop-player)
		message formatted "§2吸収魔法を受けた!" to loop-player
		kitMsg({_player}, "§5%loop-player% §dに§2吸収魔法§dを付与した!")
		kitEffectDescription(loop-player, "§2吸収魔法")
		sendTimeBossbar_RED(loop-player, "§2吸収魔法", 80)
		kitParticle_DrainMagic_Hit(loop-player)

local function kitEffect_DrainMagic_Hit(player: player, target: player):
	loop 4 times:
		wait a second
		kitDamage({_player}, {_target}, 0.5, "吸収魔法", true)
		kitHeal({_player}, 0.5, 3)
		kitEffectDescription({_target}, "§4❤0.5")
		kitParticle_DrainMagic_Drain({_player}, {_target})



local function kitParticle_DrainMagic_Hit(player: player):
	loop 5 times:
		add 0.4 to {_h}
		set {_rad} to 0
		loop 10 times:
			set {_el} to location of {_player}
			add 36 to {_rad}
			set {_vec} to vector(0.5, {_h}, 0)
			rotate {_vec} around y-axis by {_rad}
			spawnParticle_Color(entity_effect, rgb(0, 100, 0), {_el})
		wait a tick

local function kitParticle_DrainMagic_Drain(player: player, target: player):
	playSound("entity.generic.drink", 1, 0.85, {_target})
	playSound("block.composter.empty", 1, 1.3, {_player}, true)
	set {_baseloc} to location 1 meter above {_target}
	set {_to} to location 1 meter above {_player}
	set {_baseloc} to setDirToVec({_baseloc}, setVector({_baseloc}, {_to}))

	set {_dis} to distance between {_baseloc} and {_to}
	set {_addm} to ({_dis} / 5)

	set {_addnum} to 0
	loop 5 times:
		loop 5 times:
			add 0.2 to {_addnum}
			set {_el} to location {_addm} * {_addnum} meters in front of {_baseloc}
			spawnParticle(composter, {_el})

			add a random number between -0.5 and 0.5 to x-pos of {_el}
			add a random number between -0.5 and 0.5 to y-pos of {_el}
			add a random number between -0.5 and 0.5 to z-pos of {_el}
			set {_d} to dice(3)
			if {_d} is 1:
				spawnParticle_DustTransition(rgb(152, 217, 142), rgb(214, 233, 202), 0.75, {_el})
			else if {_d} is 2:
				spawnParticle_DustTransition(rgb(147, 202, 118), rgb(214, 233, 202), 0.85, {_el})
			else:
				spawnParticle_BlockData(falling_dust, lime concrete, {_el})
		wait a tick