# Meteor Magic

function kitSkill_Wizard_6(player: player):
	set {_mana} to kitManaCalc({_player}, 30)
	if {Kit-Mana::%{_player}%} < {_mana}:
		send action bar "§4§lマナが足りません!" to {_player}
		stop
	toolCT({_player}, "Wizard", 6)
	set {_baseloc} to location of head of {_player}
	set {_front} to 0
	loop 20 times:
		if {_ok} is not set:
			loop 5 times:
				{_ok} is not set
				add 1 to {_front}
				set {_loc} to location {_front} meters in front of {_baseloc}
				spawnParticle(witch, {_loc})
				if block at {_loc} is not passable:
					set {_ok} to true
			wait a tick
	{_ok} is true
	subtract {_mana} from {Kit-Mana::%{_player}%}
	kitMsg({_player}, "<##dc143c>メテオ<##8b008b>を召喚します...")
	kitFreeze({_player}, 20, false)
	set {_bn} to 0
	while {_blockok} is not set:
		add 0.1 to {_bn}
		set {_bl} to location {_bn} meters above {_loc}
		if block at {_bl} is passable:
			set {_blockok} to true
			set {_point} to {_bl}
	kitParticle_MeteorMagic({_point})
	set {_el} to location of {_player}
	loop 30 times:
		spawnParticle_Vector(witch, randomVector(), 0.3, {_el})
	wait a second
	playSound("entity.illusioner.prepare_blindness", 1.5, 0.8, {_point})
	set {_up} to 0
	loop 30 times:
		{_ch} is not set
		add 1 to {_up}
		set {_bc} to block {_up} meters above {_point}
		if {_bc} is not air:
			set {_ch} to true
	shoot a falling magma block from {_bc} at speed 1 downward
	kitParticle_MeteorMagic_Meteor(shot entity)
	set {_ran} to 0
	loop 3 times:
		add 3 to {_ran}
		loop all blocks in radius {_ran} around {_point}:
			block below loop-block is not air
			loop-block is air
			distance between {_point} and loop-block is between {_ran} - 1 and {_ran}
			spawnParticle_Color(entity_effect, rgb(220, 20, 60), location of loop-block)
		wait 3 ticks
	wait 11 ticks
	playSound("entity.generic.explode", 1.5, 0.5, {_point})
	kitParticle_MeteorMagic_Fragment({_point})
	loop all entities in radius 10 around {_point}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		set {_dis} to distance between loop-entity and {_point}
		set {_dm} to (10 - {_dis})
		message "<##dc143c>メテオ<##c71585>がヒットした!" to loop-entity
		kitDamage({_player}, loop-entity, {_dm}, "隕石魔法")
		kitEffectDescription(loop-entity, "§4❤%{_dm}%")
		apply blindness 1 to loop-entity for 3 seconds
		apply glowing 1 to loop-entity for 3 seconds
	loop all blocks in radius 5 around {_point}:
		loop-block is air
		add loop-block to {_blocks::*}
	loop 5 times:
		set {_rl} to a random location out of {_blocks::*}
		remove {_rl} from {_blocks::*}
		spawnParticle(explosion, {_rl})
		wait a tick

function kitParticle_MeteorMagic(point: location):
	playSound("entity.evoker.prepare_summon", 1.5, 2, {_point})
	loop 20 times:
#		弧を描く
		set {_n.k} to 0
		loop 60 times:
			set {_effbaseloc} to {_point}
			add 10 * sin({_n.k}) to x-coordinate of {_effbaseloc}
			add 10 * cos({_n.k}) to z-coordinate of {_effbaseloc}
			spawnParticle_Dust(rgb(220, 20, 60), 1, {_effbaseloc})
			add 6 to {_n.k}
#		弦を描く
		if {_l.A} is not set:
			set {_l.A} to {_point}
			set yaw of {_l.A} to 120
			set pitch of {_l.A} to 0
			add 10 * sin(90) to x-coordinate of {_l.A}
			add 10 * cos(90) to z-coordinate of {_l.A}
		if {_l.B} is not set:
			set {_l.B} to {_point}
			set yaw of {_l.B} to 0
			set pitch of {_l.B} to 0
			add 10 * sin(210) to x-coordinate of {_l.B}
			add 10 * cos(210) to z-coordinate of {_l.B}
		if {_l.C} is not set:
			set {_l.C} to {_point}
			set yaw of {_l.C} to 240
			set pitch of {_l.C} to 0
			add 10 * sin(330) to x-coordinate of {_l.C}
			add 10 * cos(330) to z-coordinate of {_l.C}
		if {_l.D} is not set:
			set {_l.D} to {_point}
			set yaw of {_l.D} to 180
			set pitch of {_l.D} to 0
			add 10 * sin(30) to x-coordinate of {_l.D}
			add 10 * cos(30) to z-coordinate of {_l.D}
		if {_l.E} is not set:
			set {_l.E} to {_point}
			set yaw of {_l.E} to 60
			set pitch of {_l.E} to 0
			add 10 * sin(150) to x-coordinate of {_l.E}
			add 10 * cos(150) to z-coordinate of {_l.E}
		if {_l.F} is not set:
			set {_l.F} to {_point}
			set yaw of {_l.F} to 300
			set pitch of {_l.F} to 0
			add 10 * sin(270) to x-coordinate of {_l.F}
			add 10 * cos(270) to z-coordinate of {_l.F}
		set {_n.g} to 0
		loop 19 times:
			add sqrt(3) / 2 to {_n.g}
#				A-B
			set {_loc.g} to location {_n.g} meters in front of {_l.A}
			spawnParticle_Dust(rgb(220, 20, 60), 1, {_loc.g})
#				D-E
			set {_loc.g} to location {_n.g} meters in front of {_l.D}
			spawnParticle_Dust(rgb(220, 20, 60), 1, {_loc.g})
#				B-C
			set {_loc.g} to location {_n.g} meters in front of {_l.B}
			spawnParticle_Dust(rgb(220, 20, 60), 1, {_loc.g})
#				E-F
			set {_loc.g} to location {_n.g} meters in front of {_l.E}
			spawnParticle_Dust(rgb(220, 20, 60), 1, {_loc.g})
#				C-A
			set {_loc.g} to location {_n.g} meters in front of {_l.C}
			spawnParticle_Dust(rgb(220, 20, 60), 1, {_loc.g})
#				F-D
			set {_loc.g} to location {_n.g} meters in front of {_l.F}
			spawnParticle_Dust(rgb(220, 20, 60), 1, {_loc.g})
		wait a tick

function kitParticle_MeteorMagic_Meteor(entity: entity):
	loop 20 times:
		spawnParticle(lava, {_entity})
		wait a tick

function kitParticle_MeteorMagic_Fragment(loc: location):
	loop 5 times:
		shoot an egg from {_loc} at speed 0.5 upwards
		add shot entity to {_projs::*}
		push shot egg north at speed random number from -0.5 to 0.5
		push shot egg east at speed random number from -0.5 to 0.5
	loop 40 times:
		loop {_projs::*}:
			spawnParticle(lava, location of loop-value-2)
		wait a tick