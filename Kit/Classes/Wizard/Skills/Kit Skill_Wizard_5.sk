# Drain Magic

function kitSkill_Wizard_5(player: player):
	set {_mana} to kitManaCalc({_player}, 10)
	if {Kit-Mana::%{_player}%} >= {_mana}:
		toolCT({_player}, "Wizard", 5)
		shoot a snowball from {_player} at speed 1
		set metadata value "magic" of shot snowball to "drain"
		kitParticle_Wizard_Magic(shot snowball, 0, 100, 0)
		subtract {_mana} from {Kit-Mana::%{_player}%}
	else:
		send action bar "§4§lマナが足りません!" to {_player}

function kitEffect_DrainMagic(player: player, loc: location):
	loop all players in radius 5 around {_loc}:
		kitCheckTeam(loop-player, {_player}, false) is true
		LastKiller(loop-player, {_player})
		kitDrain(loop-player, 5 seconds)
		message formatted "§2吸収魔法を受けた!" to loop-player
		kitMsg({_player}, "§5%loop-player% §dに§2吸収魔法§dを付与した!")
		kitParticle_DrainMagic_Hit(loop-player)

function kitParticle_DrainMagic_Hit(player: player):
	loop 5 times:
		add 0.4 to {_h}
		set {_rad} to 0
		loop 10 times:
			set {_el} to location of {_player}
			add 36 to {_rad}
			add 0.5 * sin({_rad}) to x-coordinate of {_el}
			add 0.5 * cos({_rad}) to z-coordinate of {_el}
			add {_h} to y-coordinate of {_el}
			spawnParticle_Color(entity_effect, 0, 100, 0, {_el})
		wait a tick