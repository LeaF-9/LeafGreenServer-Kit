# Magic Missile

on sneak toggle:
	{Class::%player%} is "Wizard"
	player is not sneaking
	set {_tool} to player's tool
	if kitItemAllChecker({_tool}, "Wizard", "Weapon.main", 3) is false:
		stop
	kitEffect_MagicMissile(player)

# マジックミサイル召喚
function kitEffect_MagicMissile(player: player):
	set {_true} to true
	set {_num.charge} to {Stats.%{_player}%::MagicMissile} * 20
	wait a tick
	while {_true} is true:
		set {_tool} to tool of {_player}
		if {_player} is not sneaking:
			set {_true} to false
			continue
		if kitItemAllChecker({_tool}, "Wizard", "Weapon.main", 3) is false:
			set {_true} to false
			continue
		if {_num.charge} >= 60:
			set {_true} to false
			continue
		kitParticle_MagicMissile_Prepare({_player})
		add 1 to {_num.charge}
		set {_barmax} to {_num.charge}
		while {_barmax} > 20:
			subtract 20 from {_barmax}
		chargeActionBar({_player}, {_barmax} * 2, 40)
		if mod({_num.charge}, 20) is 0:
			kitEffect_MagicMissile_Summon({_player})
		wait a tick

function kitEffect_MagicMissile_Summon(player: player):
	kitParticle_MagicMissile_Summon({_player})
	add 1 to {Stats.%{_player}%::MagicMissile}
	set {_mmentity} to kitSpawnMob_NBT({_player}, armor stand, location of {_player}, "{CustomNameVisible:0b,Invulnerable:1b,PersistenceRequired:1b,NoBasePlate:1b,NoGravity:1b,Invisible:1b,Marker:1b,Small:1b,ArmorItems:[{id:""minecraft:air"",Count:1b},{id:""minecraft:air"",Count:1b},{id:""minecraft:air"",Count:1b},{id:""minecraft:player_head"",tag:{SkullOwner:{Id:[I;-110805139,1605126504,-1314409618,-754615504],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMWYxNzhhM2U3MDU4YTEzYWFjNTBkMTkyOWE1NGQ0ODRkYmRmZmMwMzg1MzI0YzE5YjE4ZGZlMzQ0MWI0Yzc5ZSJ9fX0=""}]}}},Count:1b}],HandItems:[{id:""minecraft:air"",Count:1b},{id:""minecraft:air"",Count:1b}],ArmorDropChances:[0.085F,0.085F,0.085F,0.0F]}")
	set metadata value "magicmissile" of {_mmentity} to {Stats.%{_player}%::MagicMissile}
	set metadata value "magicmissile-launched" of {_mmentity} to false
	kitEffect_MagicMissile_Timer({_player})

function kitEffect_MagicMissile_Timer(player: player):
	if {Stats.%{_player}%::MagicMissile-Timer} is set:
		stop
	set {Stats.%{_player}%::MagicMissile-Timer} to true
	while {Class::%{_player}%} is "Wizard":
		if {Stats.%{_player}%::MagicMissile-Timer} is not set:
			stop
		set {_playerloc} to location of {_player}
		add 1 to y-pos of {_playerloc}
		set pitch of {_playerloc} to 0
		set {_locs::*} to particleLocation_CircleV({_playerloc}, 60, 1)
		add 1 to {_timer}
		if {_timer} >= 61:
			set {_timer} to 1
		loop all entities:
			metadata value "magicmissile" of loop-entity is set
			if metadata value "magicmissile-blasted" of loop-entity is set:
				delete loop-entity
				continue
			metadata value "magicmissile-launched" of loop-entity is false
			set {_id} to metadata value "magicmissile" of loop-entity
			set {_locid} to 20 * ({_id} - 1) + {_timer}
			if {_locid} > 60:
				subtract 60 from {_locid}
			teleport loop-entity to {_locs::%{_locid}%}
		wait a tick
	loop all entities:
		metadata value "magicmissile" of loop-entity is set
		delete loop-entity

function kitWeapon_Wizard_3(player: player):
	if {Stats.%{_player}%::MagicMissile} is not set:
		set {Stats.%{_player}%::MagicMissile} to 0
	if {Stats.%{_player}%::MagicMissile} is 0:
		stop
	kitParticle_MagicMissile_Launch({_player})
	set {_id} to {Stats.%{_player}%::MagicMissile}
	subtract 1 from {Stats.%{_player}%::MagicMissile}
	loop all entities:
		metadata value "magicmissile" of loop-entity is {_id}
		metadata value "magicmissile-launched" of loop-entity is false
		set {_bullet} to loop-entity
		set metadata value "magicmissile-launched" of loop-entity to true
	set {_m} to 0
	set {_baseloc} to location 1 meter above {_player}
	set {_blast} to false
	loop 100 times:
		if {_blast} is true:
			continue
		add 0.5 to {_m}
		set {_tool} to tool of {_player}
		if kitItemAllChecker({_tool}, "Wizard", "Weapon.main", 3) is false:  # 持っているアイテムがスキルのものでは無い場合の挙動
			set {_blast} to true
			continue
		set {_locnow} to location 1 meter above {_player}
		set {_base} to {_baseloc}
		set yaw of {_base} to yaw of {_locnow}
		set pitch of {_base} to pitch of {_locnow}
		set {_block} to location 1 meter in front of {_base}
		teleport {_bullet} to {_block}
		set {_baseloc} to {_block}
		if block at {_block} is not passable:  # ブロックと衝突した際の挙動
			set {_blast} to true
			continue
		loop all players in radius 2.5 around {_block}:
			kitCheckTeam(loop-player, {_player}, false) is true
			{_hit} is not set
			set {_hit} to loop-player
		if {_hit} is set:
			set {_blast} to true
			continue
		kitParticle_MagicMissile_Trail({_block})
		wait a tick
	kitParticle_MagicMissile_Blast({_baseloc})
	set metadata value "magicmissile-blasted" of {_bullet} to true
	if {_hit} is set:
		kitParticle_MagicMissile_Hit({_player}, {_hit})
		kitDamage({_player}, {_hit}, 2)
		set {_mana} to 5
		add {_mana} to {Kit-Mana::%{_player}%}
		kitMsg({_player}, "§bマナ §9§l%{_mana}% §7ゲット!")

# チャージ中演出 小さな青い光がキラキラ
function kitParticle_MagicMissile_Prepare(player: player):
	playSound("entity.experience_orb.pickup", 0.5, 2, {_player})
	set {_baseloc} to location of {_player}
	loop 5 times:
		set {_el} to {_baseloc}
		add a random number between -0.5 and 0.5 to x-pos of {_el}
		add a random number between -0.5 and 0.5 to z-pos of {_el}
		add a random number between 0.5 and 1.5 to y-pos of {_el}
		spawnParticle_DustTransition(rgb(0, 161, 233), rgb(160, 216, 239), 0.4, {_el})

# 魔法弾召喚時 ぽわわわーん
function kitParticle_MagicMissile_Summon(player: player):
	playSound("block.conduit.attack.target", 1, 1.5, {_player})
	set {_baseloc} to location of {_player}
	set {_y} to 0
	loop 5 times:
		set {_el} to {_baseloc}
		add {_y} to y-pos of {_el}
		spawnParticle_Color(entity_effect, 0, 172, 240, {_el})
		add 0.5 to {_y}

# 発射時の演出 渦と共にちょっと爆発
function kitParticle_MagicMissile_Launch(player: player):
	playSound("block.conduit.activate", 1, 2, {_player})
	set {_baseloc} to location 1 meter above {_player}
	loop 20 times:
		spawnParticle_Vector(scrape, randomVector({_baseloc}), 0.75, {_baseloc})
	set {_tornados::*} to particleLocation_TornadoV({_baseloc}, 10, 1.5, 1.5, 3)
	loop 20 times:
		set {_el} to {_tornados::%loop-number%}
		spawnParticle(bubble_pop, {_el})
		wait a tick

# 魔法弾の周囲にVertical円
function kitParticle_MagicMissile_Trail(loc: location):
	set {_circles::*} to particleLocation_CircleV({_loc}, 10, 0.5)
	loop {_circles::*}:
		spawnParticle_Dust(rgb(37, 183, 192), 0.5, loop-value)

# 柔らかい破裂
function kitParticle_MagicMissile_Blast(loc: location):
	playSound("block.respawn_anchor.charge", 1, 1.5, {_loc})
	loop 30 times:
		spawnParticle_Vector(sculk_charge_pop, randomVector({_loc}), 0.5, {_loc})

# 明るい音 直線の青緑色の光
function kitParticle_MagicMissile_Hit(player: player, target: player):
	set {_loct} to location 1 meter above {_target}
	set {_baseloc} to location 1 meter above {_player}
	playSound("block.amethyst_block.chime", 1.5, 2, {_baseloc})
	set {_dis} to distance between {_loct} and {_baseloc}
	set {_maxm} to round({_dis})
	set {_v} to setVector({_baseloc}, {_loct})
	set {_baseloc} to setDirToVec({_baseloc}, {_v})
	set {_spirals::*} to particleLocation_SpiralV({_baseloc}, 10, 10, 0.35, {_maxm})
	loop {_spirals::*}:
		spawnParticle_DustTransition(rgb(0, 115, 168), rgb(250, 253, 255), 0.5, {_loop-value})