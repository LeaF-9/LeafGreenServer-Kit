# Fairy Wand

function kitEffect_FairyWand(player: player):
	nameItemCT({_player}, "Fairy Wand")
	set {_baseloc} to location of head of {_player}
	kitSound_FairyWand_Launch({_baseloc})
	loop 20 times:
		set {_el} to location 0.5 * loop-number meters in front of {_baseloc}
		kitParticle_FairyWand_Beam({_el}, loop-number)
		loop all players in radius 3 around {_el}:
			{_hit.%loop-player%} is not set
			kitCheckTeam(loop-player, {_player}, false) is true
			set {_pl} to location 1 meters above loop-player
			if distance between {_el} and {_pl} <= 1.5:
				set {_hit.%loop-player%} to true
				kitEffect_FairyWand_Hit({_player}, loop-player)
		wait a tick

local function kitEffect_FairyWand_Hit(player: player, target: player):
	kitParticle_FairyWand_Hit({_target})
	kitDamage({_player}, {_target}, 1, "妖精の杖")
	applyEffect({_target}, poison, 2, 3 seconds)
	set {_FairyWand} to kitStats_GET({_player}, "FairyWand")
	add 3 to {_FairyWand}
	kitStats_SET({_player}, "FairyWand", {_FairyWand})
	send action bar "§9次のマナ消費量が §b§l%{_FairyWand}% §9減少します!" to {_player}



local function kitParticle_FairyWand_Beam(loc: location, a: number):
	playSound("block.amethyst_block.chime", 0.75, 2, {_loc})
	set {_rad} to {_a} * 20
	loop 3 times:
		if loop-number is 1:
			set {_color} to rgb(234, 97, 142)
		else if loop-number is 2:
			set {_color} to rgb(250, 197, 89)
		else:
			set {_color} to rgb(60, 179, 122)
		set {_el} to {_loc}
		set {_m1} to {_el}
		set pitch of {_m1} to pitch of {_m1} - 90
		set {_test1} to 1 * cos({_rad})
		set {_t1} to location {_test1} meters in front of {_m1}
		set {_m2} to {_el}
		set pitch of {_m2} to 0
		set {_test2} to 1 * sin({_rad})
		set {_t2} to location {_test2} meters to the right of {_m2}
		set x-pos of {_el} to x-pos of {_t1} + x-pos of {_t2} - x-pos of {_el}
		set y-pos of {_el} to y-pos of {_t1} + y-pos of {_t2} - y-pos of {_el}
		set z-pos of {_el} to z-pos of {_t1} + z-pos of {_t2} - z-pos of {_el}
		spawnParticle_DustTransition({_color}, rgb(247, 246, 251), 2, {_el})
		add 120 to {_rad}

local function kitParticle_FairyWand_Hit(player: player):
	playSound("entity.allay.item_thrown", 1, 0.8, {_player})
	loop 15 times:
		set {_dice} to dice(3)
		if {_dice} is 1:
			set {_block} to pink wool
		else if {_dice} is 2:
			set {_block} to yellow wool
		else:
			set {_block} to lime wool
		set {_el} to aroundEffect({_player})
		spawnParticle_BlockData(falling_dust, {_block}, {_el})




local function kitSound_FairyWand_Launch(loc: location):
	playSound("block.end_portal_frame.fill", 1, 1.7, {_loc})