# Reflectorのスキル Ver1.0.0

# Skills
function kitSkillReflector(player: player) :: boolean:
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	if checkCT({_player}, name of {_tool}) is true:
		if {_item.num} is 1:
			kitSkill_Reflector_1({_player})
		else if {_item.num} is 2:
			kitSkill_Reflector_2({_player})
		else if {_item.num} is 3:
			kitSkill_Reflector_3({_player})
		else if {_item.num} is 4:
			kitSkill_Reflector_4({_player})
		else if {_item.num} is 5:
			kitSkill_Reflector_5({_player})
		else if {_item.num} is 6:
			kitSkill_Reflector_6({_player})
		else if {_item.num} is 7:
			kitSkill_Reflector_7({_player})
		else if {_item.num} is 8:
			kitSkill_Reflector_8({_player})
	return true

on rightclick holding a clay item:
	name of player's tool is "§8Damage Absorption"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		if {Stats.%player%::PerfectReflect} is not set:
			set {CT.%player%::%name of player's tool%} to 40
			set {Stats.%player%::DamageAbsorption} to true
			message "{@prefix} §8ダメージを溜め込んで爆発させます...!"
			apply mining fatigue 2 to player for 3 seconds
			play raw sound "mob.irongolem.hit" at player with pitch 0.5 volume 1
			wait 3 seconds
			delete {Stats.%player%::DamageAbsorption}
			if {Stats.%player%::DAPoint} > 5:
				set {Stats.%player%::DAPoint} to 5
			message "{@prefix} §c爆発しました!"
			play raw sound "mob.irongolem.death" at player with pitch 0.5 volume 1
			drawWarpRings style 0, particle "happyvillager", center player, id "%player%", scan true, height 10, radius 5, ringCount 20, ringDensity 50, visibleRange 32
			loop all players in radius 5 around player:
				{team::%loop-player%} is not {team::%player%}
				make player damage loop-player by {Stats.%player%::DAPoint}
				message "§c反撃のダメージを喰らった!" to loop-player
				make a fake explosion at loop-player
			delete {Stats.%player%::DAPoint}
			wait 5 ticks
			stopEffect "%player%"
		else:
			set action bar of player to "§c§l別Skill発動中は使えません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on damage:
	if {Stats.%attacker%::DamageAbsorption} is true:
		cancel event
		set action bar of attacker to "§c我慢中は攻撃できません!"
	if {Stats.%victim%::DamageAbsorption} is true:
		cancel event
		add 1 to {Stats.%victim%::DAPoint}
		drawDot count 1, particle "angryvillager", center location of block 2 above victim, visibleRange 30, keepFor 10 ticks
		play raw sound "mob.irongolem.hit" at victim with pitch 2 volume 1

on rightclick holding a iron ingot:
	name of player's tool is "§7Apply Reflection"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {_tg} to target player
		if {_tg} is set:
			if {team::%{_tg}%} is {team::%player%}:
				set {CT.%player%::%name of player's tool%} to 30
				set {Stats.%{_tg}%::ApplyReflection} to true
				message "{@prefix} §7%{_tg}% §aに反射能力を5秒間分け与えました!"
				message "§8%player% §fに反射能力を付与してもらいました!" to {_tg}
				play raw sound "random.orb" at {_tg} with pitch 0.8 volume 1
				drawComplexSpiral particle "redstone", RGB 150, 150, 150, center {_tg}, id "%{_tg}%", clockwise true, scan true, radius 0.5, density 50, height 5, effectMod 2, start 0, visibleRange 30
				wait 10 seconds
				stopEffect "%{_tg}%"
				delete {Stats.%{_tg}%::ApplyReflection}
				message "§7Reflection§eの効果が切れました" to {_tg}
			else:
				set action bar of player to "§c§l敵に付与することは出来ません!"
		else:
			set action bar of player to "§c§l対象のプレイヤーがいません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on damage of player:
	{Stats.%victim%::ApplyReflection} is true
	set {_dm} to damage / 4
	set {LastKiller::%attacker%} to victim
	add victim to {damager.%attacker%::*}
	damage attacker by {_dm}
	play raw sound "random.orb" at attacker with pitch 2 volume 1

on rightclick holding a firework star:
	name of player's tool is "§8Invert Bomb"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 40
		message "{@prefix} §3進行方向を逆にする爆弾を投げました..."
		shoot a snowball from player at speed 1
		set metadata value "invert" of shot snowball to "true"
		drawDot count 5, particle "witchspell", center shot snowball, visibleRange 30, keepFor 1 seconds
		play raw sound "random.bow" at player with pitch 0.5 volume 1
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on projectile hit:
	projectile is a snowball
	metadata value "invert" of projectile is "true"
	play raw sound "random.levelup" at projectile with pitch 0.5 volume 1
	drawSphere style 1, particle "redstone", RGB 255, 255, 255, center location of projectile, id "%shooter%", radius 5, density 100, visibleRange 30
	loop all players in radius 5 around projectile:
		{team::%loop-player%} is not {team::%shooter%}
		add loop-player to {_in::*}
		set walk speed of loop-player to 0.1
		set {_l} to location of loop-player
		set {_y} to yaw of {_l}
		add 180 to {_y}
		if {_y} > 360:
			set {_y} to {_y} - 360
		set yaw of {_l} to {_y}
		teleport loop-player to {_l}
		apply blindness 1 to loop-player for 2 seconds
		message "§eなぜだか足がうまく動かない!" to loop-player
		play raw sound "random.orb" at loop-player with pitch 0.5 volume 1
		drawDot count 5, particle "mobspell", RGB 255, 255, 255, center loop-player, visibleRange 30, keepFor 5 seconds
	wait a second
	stopEffect "%shooter%"
	wait 2 seconds
	loop {_in::*}:
		set walk speed of loop-value to 0.2
		message "§6足の動きがもとに戻った!" to loop-value

on rightclick holding a bone:
	name of player's tool is "§fMirror Ball"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 30
		message "{@prefix} §fミラーボールを発射します...!"
		set {_mhp} to max health of player
		set {_chp} to health of player
		set {_dmg} to ({_mhp} - {_chp})
		add 2 to {_dmg}
		shoot a falling glass from player at speed 1
		set metadata value "mirrorball" of shot falling glass to "%player%"
		drawDot count 5, particle "mobspell", RGB 255, 255, 255, center shot falling glass, visibleRange 30, keepFor 3 seconds
		play raw sound "random.bow" at player with pitch 1.5 volume 1
		loop 60 times:
			delete {_true}
			loop all falling glass:
				metadata value "mirrorball" of loop-falling glass is "%player%"
				set {_true} to true
				loop all players in radius 2 around loop-falling glass:
					{team::%loop-player%} is not {team::%player%}
					make player damage loop-player by {_dmg}
					play raw sound "dig.glass" at loop-player with pitch 1.5 volume 1
					delete loop-falling glass
			if {_true} is true:
				wait a tick
		loop all falling glass:
			metadata value "mirrorball" of loop-falling glass is "%player%"
			delete loop-falling glass
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"