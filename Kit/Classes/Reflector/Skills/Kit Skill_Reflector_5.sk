# skill-name: Speculum Shield

options:
	width: 10
	height: 5

function kitSkill_Reflector_5(player: player):
	toolCT({_player}, "Reflector", 5)
	kitEffect_SpeculumShield({_player})

local function kitEffect_SpeculumShield(player: player):
	kitMsg({_player}, "§f§l鏡面シールド§7を展開します!")
	sendTimeBossbar_GREEN({_player}, "Speculum Shield", 200)
	set {_baseloc} to location of head of {_player}
	set pitch of {_baseloc} to 0
	set {_loc.shield} to location 1.5 meter in front of {_baseloc}
	kitSound_SpeculumShield_Launch({_loc.shield})
	set {_now} to now
	# 見た目
	set {_translation.up} to ({@height} - 3) * 0.5

	set {_item} to "minecraft:light_gray_stained_glass_pane"
	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[0f,%{_translation.up}%f,0f]", "[{@width}f,{@height}f,1f]")
	set {_nbt} to nbt compound from "{transformation:%{_transformation}%,item:{id:""%{_item}%"",Count:1b}}"
	spawn an item display at {_loc.shield} with nbt {_nbt}
	set {_itemdisplay} to spawned item display
	set metadata value "gametype" of {_itemdisplay} to "Kit"
	set metadata value "Speculum Shield" of {_itemdisplay} to "%{_player}%"
	set metadata value "Speculum Shield - Time" of {_itemdisplay} to {_now}
	# 当たり判定 (左上ベース)
	set {_loc.hit} to {_loc.shield}
	add ({@height} - 2) to y-pos of {_loc.hit}
	set {_loc.hit} to location {@width} / 2 - 0.25 meters to the left of {_loc.hit}
	set {_h} to 0
	loop {@height} * 2 times:
		set {_loc.hit.height} to location {_h} meters below {_loc.hit}
		set {_w} to 0
		loop {@width} * 2 times:
			set {_loc.slime} to location {_w} meters to the right of {_loc.hit.height}
			kitParticle_SpeculumShield_Launch({_loc.slime})
			spawn a slime at {_loc.slime} with nbt compound from "{NoGravity:1b,Size:0.5,PersistenceRequired:1,NoAI:1,Silent:1,DropItem:0,Team:""noCollision"",active_effects:[{id:invisibility,duration:12000,amplifier:0,show_particles:0b}]}"
			set {_slime} to spawned slime
			set metadata value "gametype" of {_slime} to "Kit"
			set metadata value "shield entity" of {_slime} to true
			set metadata value "Speculum Shield" of {_slime} to "%{_player}%"
			set metadata value "Speculum Shield - Time" of {_slime} to {_now}
			set display name of {_slime} to "%{_player}%"
			add 0.5 to {_w}
		add 0.5 to {_h}
	wait 10 seconds
	kitMsg({_player}, "§f鏡面シールド§8は消滅した...")
	kitSound_SpeculumShield_Break({_loc.shield})
	loop all entities:
		metadata value "Speculum Shield" of loop-entity is "%{_player}%"
		metadata value "Speculum Shield - Time" of loop-entity is {_now}
		kitParticle_SpeculumShield_Break(location of loop-entity)
		delete loop-entity

on damage of slime:
	set {_name} to victim's display name parsed as player
	metadata value "Speculum Shield" of victim is set
	cancel event
	set {_attacker} to getAttacker(victim)
	if kitCheckTeam({_name}, {_attacker}, false) is false:
		stop
	if damage cause is not void or suffocation:
		set {_rawdamage} to getDamageModification_ALL(victim, damage)
		set {_dmg} to {_rawdamage} / 2
		kitDamage({_name}, {_attacker}, {_dmg}, "鏡面反射")
		kitEffectDescription({_attacker}, "§f§l反射")
		kitParticle_SpeculumShield_Damage(location of victim)
	else:
		cancel event



local function kitSound_SpeculumShield_Launch(loc: location):
	playSound("entity.player.levelup", 1, 0.65, {_loc})

local function kitSound_SpeculumShield_Break(loc: location):
	playSound("block.glass.break", 1, 0.6, {_loc})

local function kitParticle_SpeculumShield_Launch(loc: location):
	loop 3 times:
		spawnParticle_Vector(wax_off, randomVector(), 1, {_loc})

local function kitParticle_SpeculumShield_Break(loc: location):
	set {_ln} to a random integer between 1 and 3
	loop {_ln} times:
		spawnParticle_Block(light_gray_stained_glass, {_loc})

local function kitParticle_SpeculumShield_Damage(loc: location):
	playSound("block.amethyst_block.break", 1.2, 2, {_loc})
	loop 5 times:
		spawnParticle_Vector(scrape, randomVector(), 1.5, {_loc})