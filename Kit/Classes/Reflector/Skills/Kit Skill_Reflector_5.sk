# skill-name: Speculum Shield

options:
	width: 7
	height: 3

function kitSkill_Reflector_5(player: player):
	toolCT({_player}, "Reflector", 5)
	kitEffect_SpeculumShield({_player})

local function kitEffect_SpeculumShield(player: player):
	kitMsg({_player}, "§f§l鏡面シールド§7を展開します!")
	set {_baseloc} to location of head of {_player}
	set pitch of {_baseloc} to 0
	set {_loc.shield} to location 1.5 meter in front of {_baseloc}
	set {_now} to now
	# 見た目
	set {_item} to "minecraft:light_gray_stained_glass_pane"
	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[0f,0f,0f]", "[{@width}f,{@height}f,1f]")
	set {_nbt} to nbt compound from "{transformation:%{_transformation}%,item:{id:""%{_item}%"",Count:1b}}"
	spawn an item display at {_loc.shield} with nbt {_nbt}
	set {_itemdisplay} to spawned item display
	set metadata value "gametype" of {_itemdisplay} to "Kit"
	set metadata value "Speculum Shield" of {_itemdisplay} to "%{_player}%"
	set metadata value "Speculum Shield - Time" of {_itemdisplay} to {_now}
	# 当たり判定 (左上ベース)
	set {_loc.hit} to {_loc.shield}
	add 1 to y-pos of {_loc.hit}
	set {_loc.hit} to location {@width} / 2 - 0.25 meters to the left of {_loc.hit}
	set {_h} to 0
	loop {@height} * 2 times:
		set {_loc.hit.height} to location {_h} meters below {_loc.hit}
		set {_w} to 0
		loop {@width} * 2 times:
			set {_loc.slime} to location {_w} meters to the right of {_loc.hit.height}
			spawn a slime at {_loc.slime} with nbt compound from "{NoGravity:1b,Size:0.5,PersistenceRequired:1,NoAI:1,Silent:1,DropItem:0,Team:""noCollision"",ActiveEffects:[{Id:14,Amplifier:0,Duration:12000,ShowParticles:0b}]}"
			set {_slime} to spawned slime
			set metadata value "gametype" of {_slime} to "Kit"
			set metadata value "Speculum Shield" of {_slime} to "%{_player}%"
			set metadata value "Speculum Shield - Time" of {_slime} to {_now}
			set display name of {_slime} to "%{_player}%"
			add 0.5 to {_w}
		add 0.5 to {_h}
	wait 10 seconds
	kitMsg({_player}, "§f鏡面シールド§8は消滅した...")
	loop all entities:
		metadata value "Speculum Shield" of loop-entity is "%{_player}%"
		metadata value "Speculum Shield - Time" of loop-entity is {_now}
		delete loop-entity

on damage of slime:
	set {_name} to victim's display name parsed as player
	metadata value "Speculum Shield" of victim is set
	cancel event
	if kitCheckTeam({_name}, attacker, false) is false:
		stop
	if damage was caused by attack, projectile or potion:
		set {_dmg} to damage / 2
		kitDamage({_name}, attacker, {_dmg})
	else:
		cancel event