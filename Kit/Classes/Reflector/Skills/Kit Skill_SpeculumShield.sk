# skill-name: Speculum Shield

options:
	width: 10
	height: 5

function kitEffect_SpeculumShield(player: player):
	nameCT({_player}, "Speculum Shield")
	kitMsg({_player}, "§f§l鏡面シールド§7を展開します!")
	sendTimeBossbar_GREEN({_player}, "Speculum Shield", 200)
	set {_baseloc} to location of head of {_player}
	set pitch of {_baseloc} to 0
	set {_loc.shield} to location 1.5 meter in front of {_baseloc}
	kitSound_SpeculumShield_Launch({_loc.shield})
	set {_now} to now
	# 見た目
	set {_translation.up} to ({@height} - 3) * 0.5

	set {_item} to light gray stained glass pane
	set {_rotation} to getQuaternionf(0, 0, 0, 1)
	set {_translation} to getVector3f(0, {_translation.up}, 0)
	set {_scale} to getVector3f({@width}, {@height}, 1)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})

	spawn an item display at {_loc.shield}
	set {_itemdisplay} to spawned item display

	setBrightness({_itemdisplay})
	{_itemdisplay}.setTeleportDuration(1)
	{_itemdisplay}.setInterpolationDuration(1)
	{_itemdisplay}.setViewRange(1000)
	setDisplayItem({_itemdisplay}, {_item})
	{_itemdisplay}.setTransformation({_transformation})

	set metadata value "gametype" of {_itemdisplay} to "Kit"
	set metadata value "Speculum Shield" of {_itemdisplay} to "%{_player}%"
	set metadata value "Speculum Shield - Time" of {_itemdisplay} to {_now}
	# 当たり判定 (左上ベース)
	set {_loc.hit} to {_loc.shield}
	add ({@height} - 2) to y-pos of {_loc.hit}
	set {_loc.hit} to location {@width} / 2 - 0.25 meters to the left of {_loc.hit}
	set {_h} to 0
	loop {@height} * 2 times:
		set {_loc.hit.height} to location {_h} meters below {_loc.hit}
		set {_w} to 0
		loop {@width} * 2 times:
			set {_loc.slime} to location {_w} meters to the right of {_loc.hit.height}
			kitParticle_SpeculumShield_Launch({_loc.slime})
			set {_slime} to spawnShieldEntity({_player}, {_loc.slime})

			set metadata value "Speculum Shield" of {_slime} to "%{_player}%"
			set metadata value "Speculum Shield - Time" of {_slime} to {_now}
			add 0.5 to {_w}
		add 0.5 to {_h}
	wait 10 seconds
	kitMsg({_player}, "§f鏡面シールド§8は消滅した...")
	kitSound_SpeculumShield_Break({_loc.shield})
	loop all entities:
		metadata value "Speculum Shield" of loop-entity is "%{_player}%"
		metadata value "Speculum Shield - Time" of loop-entity is {_now}
		kitParticle_SpeculumShield_Break(location of loop-entity)
		delete loop-entity

on damage of slime:
	set {_name} to victim's display name parsed as player
	metadata value "Speculum Shield" of victim is set
	cancel event
	set {_attacker} to getAttacker(victim)
	if kitCheckTeam({_name}, {_attacker}, false) is false:
		stop
	if damage type is not void or suffocation:
		set {_rawdamage} to getDamageModification_ALL(victim, damage)
		set {_dmg} to {_rawdamage} / 2
		kitDamage({_name}, {_attacker}, {_dmg}, "鏡面反射")
		kitEffectDescription({_attacker}, "§f§l反射")
		kitParticle_SpeculumShield_Damage(location of victim)
	else:
		cancel event



local function kitSound_SpeculumShield_Launch(loc: location):
	playSound("entity.player.levelup", 1, 0.65, {_loc})

local function kitSound_SpeculumShield_Break(loc: location):
	playSound("block.glass.break", 1, 0.6, {_loc})

local function kitParticle_SpeculumShield_Launch(loc: location):
	loop 3 times:
		spawnParticle_Vector(wax_off, randomVector(), 1, {_loc})

local function kitParticle_SpeculumShield_Break(loc: location):
	set {_ln} to a random integer between 1 and 3
	loop {_ln} times:
		spawnParticle_Block(light_gray_stained_glass, {_loc})

local function kitParticle_SpeculumShield_Damage(loc: location):
	playSound("block.amethyst_block.break", 1.2, 2, {_loc})
	loop 5 times:
		spawnParticle_Vector(scrape, randomVector(), 1.5, {_loc})