# skill-name: Virtual Figuring

function kitSkill_Reflector_6(player: player):
	set {_SkillTarget} to kitStats_GET({_player}, "SkillTarget")
	if {_SkillTarget} is not set:
		send action bar "§c§l対象を指定してください" to {_player}
		stop
	toolCT({_player}, "Reflector", 6)
	set {_target} to {_SkillTarget}
	kitEffect_VirtualFiguring({_player}, {_target})

local function kitEffect_VirtualFiguring(player: player, target: player):
	kitParticle_VirtualFiguring_Figure({_player}, {_target})
	kitMsg({_player}, "§b%{_target}% §fの姿を映し描いた...")
	message formatted "§b§l%{_player}% §9によって姿を隠してもらった!" to {_target}
	kitEffectDescription({_target}, "§b§l透明化")
	sendTimeBossbar_GREEN({_player}, "Virtual Figuring", 200)
	# 味方を透明化させる効果
	setSupporter({_target}, {_player})
	kitInvisible({_target}, 100)
	# 自分の姿を対象のものとする効果
	execute console command "/disguiseplayer %{_player}% Player %{_target}%"
	wait 10 seconds
	kitParticle_VirtualFiguring_End({_player})
	execute console command "/undisplayer %{_player}%"



local function kitParticle_VirtualFiguring_Figure(player: player, target: player):
	playSound("block.large_amethyst_bud.break", 0.75, 0.9, {_player})
	playSound("block.large_amethyst_bud.break", 0.75, 0.9, {_target})
	# 自分に円
	set {_baseloc} to location 1 meter above {_player}
	set {_count} to 10
	set {_rad} to 0
	loop {_count} times:
		set {_el} to {_baseloc}
		set {_m1} to {_el}
		set pitch of {_m1} to -90
		set {_test1} to 1 * cos({_rad})
		set {_t1} to location {_test1} meters in front of {_m1}
		set {_m2} to {_el}
		set pitch of {_m2} to 0
		set {_test2} to 0.5 * sin({_rad})
		set {_t2} to location {_test2} meters to the right of {_m2}
		set x-pos of {_el} to x-pos of {_t1} + x-pos of {_t2} - x-pos of {_el}
		set y-pos of {_el} to y-pos of {_t1} + y-pos of {_t2} - y-pos of {_el}
		set z-pos of {_el} to z-pos of {_t1} + z-pos of {_t2} - z-pos of {_el}
		spawnParticle_DustTransition(rgb(255, 255, 255), rgb(221, 160, 221), 1.5, {_el})
		add 360 / {_count} to {_rad}
	# 対象に円
	set {_baseloc} to location 1 meter above {_target}
	set {_rad} to 0
	loop {_count} times:
		set {_el} to {_baseloc}
		set {_m1} to {_el}
		set pitch of {_m1} to -90
		set {_test1} to 1 * cos({_rad})
		set {_t1} to location {_test1} meters in front of {_m1}
		set {_m2} to {_el}
		set pitch of {_m2} to 0
		set {_test2} to 0.5 * sin({_rad})
		set {_t2} to location {_test2} meters to the right of {_m2}
		set x-pos of {_el} to x-pos of {_t1} + x-pos of {_t2} - x-pos of {_el}
		set y-pos of {_el} to y-pos of {_t1} + y-pos of {_t2} - y-pos of {_el}
		set z-pos of {_el} to z-pos of {_t1} + z-pos of {_t2} - z-pos of {_el}
		spawnParticle_DustTransition(rgb(255, 255, 255), rgb(221, 160, 221), 1.5, {_el})
		add 360 / {_count} to {_rad}
	# 線分
	set {_vector} to setVector(location of {_player}, location of {_target})
	set {_baseloc} to location 1 meter above {_player}
	set {_baseloc} to setDirToVec({_baseloc}, {_vector})
	set {_dis} to distance between {_player} and {_target}
	set {_addm} to {_dis} / 10
	loop 10 times:
		set {_el} to location {_addm} * loop-number meters in front of {_baseloc}
		spawnParticle(scrape, {_el})
	# 自分にキラキラ
	loop 15 times:
		spawnParticle_Vector(wax_off, randomVector(), 0.25, {_baseloc})

local function kitParticle_VirtualFiguring_End(player: player):
	# ふわって音
	playSound("entity.zombie.infect", 1, 1.15, {_player})
	# ふわって演出
	set {_baseloc} to location 1 meter above {_player}
	loop 10 times:
		spawnParticle_Vector(cloud, randomVector(), 0.35, {_baseloc})