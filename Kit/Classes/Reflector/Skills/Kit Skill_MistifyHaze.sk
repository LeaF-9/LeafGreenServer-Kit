# skill-name: Mistify Haze

options:
	effect-tick: 140



function kitEffect_MistifyHaze(player: player):
	nameCT({_player}, "Mistify Haze")
	kitMsg({_player}, "§d投擲物を消滅させる霧玉§5を発射します...")
	sendTimeBossbar_GREEN({_player}, "Mistify Haze", {@effect-tick})
	kitParticle_MistifyHaze_Launch({_player})
	set {_baseloc} to location of head of {_player}
	set {_m} to 0
	loop {@effect-tick} times:
		add 0.15 to {_m}
		set {_effloc} to location {_m} meters in front of {_baseloc}
		kitParticle_MistifyHaze_Bulb({_effloc})
		loop all projectiles in radius 10 around {_effloc}:
			set {_shooter} to shooter of loop-projectile
			kitCheckTeam({_shooter}, {_player}, false) is true
			set {_loc.proj} to location of loop-projectile
			delete loop-projectile
			kitParticle_MistifyHaze_Beam({_effloc}, {_loc.proj})
			if {_hits::%{_shooter}%} is not set:
				set {_hits::%{_shooter}%} to 20
				kitEffect_MistifyHaze_Hit({_player}, {_shooter})
				kitParticle_MistifyHaze_Beam({_loc.proj}, location 1 meter above {_shooter}, true)
		loop {_hits::*}:
			subtract 1 from {_hits::%loop-index%}
			if {_hits::%loop-index%} <= 0:
				delete {_hits::%loop-index%}
		wait a tick
	kitParticle_MistifyHaze_Vanish({_effloc})

local function kitEffect_MistifyHaze_Hit(player: player, target: player):
	send action bar "§d§l神秘の薄霧§eが§b投擲物§eを追跡してきた!" to {_target}
	kitEffectDescription({_target}, "§4❤2 §8& §e§l麻痺")
	kitDamage({_player}, {_target}, 2, "神秘の薄霧")
	kitPalsy({_target}, 100)



local function kitParticle_MistifyHaze_Launch(player: player):
	playSound("entity.illusioner.cast_spell", 1, 1.25, {_player})
	set {_baseloc} to location 1 meter in front of head of {_player}
	loop 15 times:
		set {_el} to {_baseloc}
		add a random number between -1.5 and 1.5 to x-pos of {_el}
		add a random number between -1.5 and 1.5 to y-pos of {_el}
		add a random number between -1.5 and 1.5 to z-pos of {_el}
		set {_rn} to a random integer between 1 and 3
		if {_rn} is 1:
			spawnParticle_BlockData(falling_dust, purple wool, {_el})
		else if {_rn} is 2:
			spawnParticle_DustTransition(rgb(103, 69, 152), rgb(73, 55, 89), 1.5, {_el})
		else:
			spawnParticle_Dust(rgb(165, 154, 202), 0.6, {_el})
	loop 10 times:
		set {_el} to {_baseloc}
		spawnParticle_Vector(witch, randomVector(), 0.6, {_el})

local function kitParticle_MistifyHaze_Bulb(loc: location):
	chance of 10%:
		playSound("block.amethyst_block.chime", 1, a random number between 1.5 and 2, {_loc})
	spawnParticle(witch, {_loc})
	set {_bulb::*} to particleLocation_Sphere({_loc}, 5, 1.5, 10)
	loop {_bulb::*}:
		set {_el} to loop-value
		set {_rn} to a random integer between 1 and 3
		if {_rn} is 1:
			spawnParticle_Dust(rgb(144, 121, 173), 2, {_el})
		else if {_rn} is 2:
			spawnParticle_Dust(rgb(101, 49, 142), 2, {_el})
		else:
			spawnParticle_Dust(rgb(116, 83, 153), 2, {_el})
	loop 3 times:
		set {_el} to {_loc}
		set yaw of {_el} to a random number between 0 and 360
		set pitch of {_el} to a random number between -90 and 90
		set {_m} to a random number between 0 and 10
		set {_el} to location {_m} meters in front of {_el}
		set {_rn} to a random integer between 1 and 2
		if {_rn} is 1:
			spawnParticle_BlockData(falling_dust, amethyst block, {_el})
		else:
			spawnParticle_BlockData(falling_dust, purple wool, {_el})

local function kitParticle_MistifyHaze_Beam(from: location, to: location, target: boolean = false):
	if {_target} is true:
		playSound("block.respawn_anchor.deplete", 1, 2, {_to})
	else:
		playSound("block.respawn_anchor.charge", 1, 1.85, {_to})
	# base - proj
	set {_beam.proj} to setDirToVec({_from}, setVector({_from}, {_to}))
	set {_distance} to distance between {_from} and {_to}
	set {_loopnum} to round({_distance} * 10)
	loop {_loopnum} times:
		set {_el} to location loop-number * 0.1 meters in front of {_beam.proj}
		spawnParticle(witch, {_el})
	# if target exists
	if {_target} is true:
		set {_locs::*} to particleLocation_SpiralV({_beam.proj}, 10, 2.5, 1, {_distance})
		loop {_locs::*}:
			set {_el} to loop-value
			set {_rn} to a random integer between 1 and 5
			if {_rn} is 1:
				spawnParticle_Dust(rgb(103, 69, 152), 1.5, {_el})
			else if {_rn} is 2:
				spawnParticle_Dust(rgb(112, 88, 163), 1.25, {_el})
			else if {_rn} is 3:
				spawnParticle_Dust(rgb(165, 154, 202), 1, {_el})
			else if {_rn} is 4:
				spawnParticle_DustTransition(rgb(116, 50, 92), rgb(70, 14, 68), 1.5, {_el})
			else:
				spawnParticle_DustTransition(rgb(85, 41, 91), rgb(70, 14, 68), 1.25, {_el})

local function kitParticle_MistifyHaze_Vanish(loc: location):
	playSound("entity.zombie.infect", 1, 1.35, {_loc})
	loop 10 times:
		set {_el} to {_loc}
		add a random number between -1 and 1 to x-pos of {_el}
		add a random number between -1 and 1 to y-pos of {_el}
		add a random number between -1 and 1 to z-pos of {_el}
		spawnParticle_BlockData(falling_dust, black wool, {_el})