# skill-name: Alienation Dome

function kitSkill_Reflector_8(player: player):
	toolCT({_player}, "Reflector", 8)
	kitEffect_AlienationDome({_player})



local function kitEffect_AlienationDome(player: player):
	kitMsg({_player}, "§7視覚を隔離するドームを展開します...")
	kitParticle_AlienationDome_Dome({_player})
	loop 200 times:
		if kitCheckAlive({_player}) is false:
			continue
		delete {_targets::*}
		delete {_others::*}
		delete {_allplayers::*}
		delete {_not.target::*}
		delete {_not.other::*}
		set {_baseloc} to location of {_player}
		loop all players:
			loop-player is not {_player}
			metadata value "gametype" of loop-player is "Kit"
			if distance between {_baseloc} and loop-player <= 10:
				add loop-player to {_targets::*}
				kitParticle_AlienationDome_Inner({_player}, loop-player)
			else:
				add loop-player to {_others::*}
			if {Kit-Invisible::%loop-player%} is not set:
				add loop-player to {_allplayers::*}
		# reveal list
		loop {_allplayers::*}:
			if {_targets::*} does not contain loop-value-2:
				add loop-value-2 to {_not.target::*}
			if {_others::*} does not contain loop-value-2:
				add loop-value-2 to {_not.other::*}
		# reveal
		reveal {_not.target::*} from {_others::*}
		reveal {_not.other::*} from {_targets::*}
		# hide
		hide {_others::*} from {_targets::*}
		hide {_targets::*} from {_others::*}
		wait a tick
	delete {_allplayers::*}
	loop all players:
		loop-player is not {_player}
		if {Kit-Invisible::%loop-player%} is not set:
			add loop-player to {_allplayers::*}
	reveal {_allplayers::*} from all players



local function kitParticle_AlienationDome_Dome(player: player):
	playSound("entity.illusioner.prepare_mirror", 1, 1.3, {_player})
	set {_baseloc} to location of {_player}
	set yaw of {_baseloc} to 0
	set pitch of {_baseloc} to 0
	loop 200 times:
		delete {_move}
		if kitCheckAlive({_player}) is false:
			stop
		set {_baseloc} to location of {_player}
		set yaw of {_baseloc} to 0
		set pitch of {_baseloc} to 0
		set {_efflocs::*} to particleLocation_Sphere({_baseloc}, 30, 10, 10)
		loop {_efflocs::*}:
			set {_el} to loop-value-2
			spawnParticle_Dust(rgb(0, 172, 151), 1, {_el})
		wait a tick
	playSound("entity.ender_eye.death", 1, 0.8, {_player})

local function kitParticle_AlienationDome_Inner(player: player, target: player):
	set {_efflocs::*} to particleLocation_Circle(location 1 meter above {_target}, 6, 0.5)
	loop {_efflocs::*}:
		set {_el} to loop-value
		spawnParticle_BlockData(falling_dust, dark prismarine, {_el}, {_player})