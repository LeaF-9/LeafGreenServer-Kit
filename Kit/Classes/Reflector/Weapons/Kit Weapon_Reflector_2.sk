# weapon-name: Mirror Blade

on damage of player:
	{Class::%victim%} is "Reflector"
	set {_attacker} to getAttacker
	{_attacker} is set
	{Stats.%victim%::MirrorBlade} is true
	delete {Stats.%victim%::MirrorBlade}
	set {_dmg} to damage
	cancel event
	kitMsg(victim, "§e§lパリィ§f成功!")
	kitEffectDescription(victim, "§f§lパリィ")
	kitEffectDescription({_attacker}, "§f§lパリィ")
	kitParticle_MirrorBlade_Parry(victim)
	kitDamage(victim, {_attacker}, {_dmg})

on sneak toggle:
	{Class::%player%} is "Reflector"
	player is not sneaking
	set {_tool} to player's tool
	kitItemAllChecker({_tool}, "Reflector", "Weapon.main", 2) is true
	checkItemCT(player, {_tool}) is true
	set item cooldown of {_tool} for player to 1 second
	kitEffect_MirrorBlade(player)



local function kitEffect_MirrorBlade(player: player):
	kitParticle_MirrorBlade_Launch({_player})
	sendTimeBossbar_GREEN({_player}, "Mirror Blade", 3)
	set {Stats.%{_player}%::MirrorBlade} to true
	wait 3 ticks
	delete {Stats.%{_player}%::MirrorBlade}



local function kitParticle_MirrorBlade_Launch(player: player):
	playSound("entity.zombie.infect", 0.5, 1.9, {_player})
	set {_baseloc} to location 1 meters above {_player}
	loop 10 times:
		spawnParticle_Vector(wax_off, randomVector(), 10, {_baseloc})

local function kitParticle_MirrorBlade_Parry(player: player):
	playSound("item.trident.throw", 1, 0.8, {_player})
	loop 20 times:
		spawnParticle_Vector(totem_of_undying, randomVector(), 1, location 1 meter above {_player})