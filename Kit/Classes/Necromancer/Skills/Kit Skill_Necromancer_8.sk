# Demise Smell

function kitSkill_Necromancer_8(player: player):
	set {_tool} to kitNecromanceItem({_player}, 8)
	if kitCheckNecromance({_player}, {_tool}) is true:
		toolCT({_player}, "Necromancer", 8)
		kitRemoveNecromance({_player}, {_tool})
		kitMsg({_player}, "§5§lAbyss§4を召喚します...")
		playSound("entity.wither.spawn", 1, 0.5, {_player})
		kitSpawn_Necromancer_Wraith({_player}, location of {_player})
		set {_spawnedentity} to spawned entity
		loop 60 times:
			spawnParticle_Color(entity_effect, 0, 0, 0, location of {_spawnedentity})
			wait a tick

function kitSpawn_Necromancer_Wraith(player: player, loc: location):
	set {_nbt} to nbt compound from "{PersistenceRequired:1b,Health:100,Attributes:[{Name:""generic.max_health"",Base:100},{Name:""generic.follow_range"",Base:50},{Name:""generic.knockback_resistance"",Base:1f}],Silent:1,HandItems:[{id:""minecraft:netherite_sword"",tag:{display:{Name:'{""text"":""Wraith Sword""}'},Enchantments:[{id:knockback,lvl:2}]},Count:1},{}],ArmorItems:[{},{id:""minecraft:leather_leggings"",tag:{display:{color:0}},Count:1},{id:""minecraft:leather_chestplate"",tag:{display:{color:0}},Count:1},{id:""minecraft:player_head"",Count:1b,tag:{SkullOwner:{Id:[I;-843851991,-552779082,-1798462201,-1255611656],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2EwYzg3ZmVjNzQxM2RjYjdkMWY2MWFmMjRjZjY0OWE1NTZlMDViMDIyNTAyMmMxZjYxZDgwODkzZjMzOTg3ZiJ9fX0=""}]}}},Count:1b}],ActiveEffects:[{Id:14,Amplifier:0,Duration:2147483647},{Id:28,Amplifier:0,Duration:2147483647}]}"
	set {_mob} to kitSpawnMob_NBT({_player}, wither skeleton, {_loc}, {_nbt})
	set metadata value "necromancer-entity" of {_mob} to {_player}
	set metadata value "necromancer-entity-abyss" of {_mob} to true
	kitWraithTimer({_player}, {_mob})

function kitWraithTimer(player: player, wraith: entity):
	set {_true} to true
	set {_num} to 0
	loop 30 times:
		delete {_ok}
		if {_wraith} is alive:
			set {_ok} to true
		if {_ok} is not set:
			continue
		add 1 to {_num}
		if mod({_num}, 3) is not 0:
			wait a second
			continue
		# --- ターゲット設定 ---
		delete {_target}
		set {_target} to target of {_wraith}
		if {_target} is not set:
			delete {_target::*}
			loop all players in radius 50 around {_wraith}:
				kitCheckTeam({_player}, loop-player, false) is true
				add loop-player to {_targets::*}
			set {_target} to a random player out of {_targets::*}
			set target of {_wraith} to {_target}
		# --------------------	
		if {_target} is not set:
			wait a second
			continue
		set {_dis} to distance between {_target} and {_wraith}
		# --- 10m効果 ---
		if {_dis} <= 10:
			set {_chance} to a random integer between 1 and 3
			# --- 突進攻撃 ---
			if {_chance} is 1:
				kitWraith_Charge({_player}, {_wraith})
			# ---------------
			# --- 暗闇攻撃 ---
			else if {_chance} is 2:
				kitWraith_Dark({_player}, {_wraith})
			# ---------------
			# --- 前方広範囲攻撃 ---
			else if {_ct::wave} is 3:
				kitWraith_Wave({_player}, {_wraith})
			# --------------------
		# --------------
		# --- 30m効果 ---
		else if {_dis} <= 30:
			set {_chance} to a random integer between 1 and 2
			apply slowness 10 to {_wraith} for a second
			# --- 落雷攻撃 ---
			if {_chance} is 1:
				kitWraith_Thunder({_player}, {_wraith})
			# ---------------
			# --- 召喚攻撃 ---
			else if {_chance} is 2:
				kitWraith_Summon({_player}, {_wraith})
			# ---------------
		# --------------
		# --- 50m効果 ---
		else if {_dis} <= 50:
			apply slowness 10 to {_wraith} for a second
			# --- 波動砲攻撃 ---
			kitWraith_Beam({_player}, {_wraith})
			# ----------------
		# --------------
		# --- 静止効果 ---
		else:
			apply slowness 10 to {_wraith} for a second
		# ---------------
		wait a second
	loop all entities:
		loop-entity is {_wraith}
		delete loop-entity

function kitWraith_Charge(player: player, wraith: entity):
	set {_target} to target of {_wraith}
	set {_tploc} to location of {_wraith}
	playSound("entity.warden.agitated", 1, 1.5, {_wraith})
	loop 20 times:
		teleport {_wraith} to {_tploc}
		kitWraith_Charge_Particle_1({_wraith})
		wait a tick
	set {_frontloc} to location 10 meters in front of {_tploc}
	set {_vector} to setVector({_tploc}, {_frontloc}, 10)
	set velocity of {_wraith} to {_vector}
	playSound("entity.warden.roar", 1, 1.2, {_wraith})
	loop 10 times:
		kitWraith_Charge_Particle_2({_wraith}, {_vector})
		loop all entities in radius 3 around {_wraith}:
			kitCheckTeam({_player}, loop-entity, false) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 4)
			playSound("entity.warden.heartbeat", 1, 2, loop-entity)
		wait a tick

function kitWraith_Charge_Particle_1(wraith: entity):
	set {_baseloc} to location 1 meters above {_wraith}
	set {_fromloc} to {_baseloc}
	add a random number between -3 and 3 to x-coordinate of {_fromloc}
	add a random number between -3 and 3 to y-coordinate of {_fromloc}
	add a random number between -3 and 3 to z-coordinate of {_fromloc}
	set {_dis} to distance between {_baseloc} and {_fromloc} / 5
	set {_v} to setVector({_fromloc}, {_baseloc})
	set yaw of {_fromloc} to yaw of {_v}
	set pitch of {_fromloc} to pitch of {_v}
	loop 5 times:
		set {_el} to location {_dis} * loop-number meters in front of {_fromloc}
		spawnParticle(end_rod, {_el})

function kitWraith_Charge_Particle_2(wraith: entity, vector: vector):
	set {_newVector} to {_vector} ** vector(-1, -1, -1)
	set {_newVector} to {_newVector} ++ vector(a random number between -1 and 1, a random number between -1 and 1, a random number between -1 and 1)
	spawnParticle_Vector(smoke, {_newVector}, 0.5, location of {_wraith})

function kitWraith_Dark(player: player, wraith: entity):
	set {_baseloc} to location of {_wraith}
	set {_baserad} to 0
	set {_m} to 0
	playSound("entity.warden.tendril_clicks", 1, 0.85, {_baseloc})
	loop 10 times:
		add 0.5 to {_m}
		set {_num.rad} to 0
		loop 4 times:
			add 1 to {_num.rad}
			set {_rad} to {_baserad} + (90 * {_num.rad})
			set {_el} to {_baseloc}
			add {_m} * sin({_rad}) to x-coordinate of {_el}
			add {_m} * cos({_rad}) to z-coordinate of {_el}
			spawnParticle_Dust(rgb(89, 66, 85), 1, {_el})
		add 15 to {_baserad}
		wait a tick
	# --- 暗闇効果付与 ---
	loop all entities in radius 5 around {_baseloc}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		applyEffect(loop-entity, blindness, 1, 4 seconds)
		setSupporter(loop-entity, {_player})
	# ------------------
	# --- 音の演出 ---
	playSound("block.sculk_catalyst.bloom", 1, 0.75, {_baseloc})
	# --------------
	# --- パーティクルの演出 ---
	set {_rad} to 0
	loop 10 times:
		set {_el} to {_baseloc}
		add 36 to {_rad}
		add 5 * sin({_rad}) to x-coordinate of {_el}
		add 5 * cos({_rad}) to z-coordinate of {_el}
		spawnParticle_DustTransition(rgb(0, 0, 0), rgb(89, 0, 74), 5, {_el})
	loop 15 times:
		set {_el} to {_baseloc}
		add a random number between -5 and 5 to x-coordinate of {_el}
		add a random number between -5 and 5 to z-coordinate of {_el}
		spawnParticle_DustTransition(rgb(0, 0, 0), rgb(89, 0, 74), 5, {_el})
	# -----------------------

function kitWraith_Wave(player: player, wraith: entity):
	set {_baseloc} to location of {_wraith}
	set pitch of {_baseloc} to 0
	set {_leftbase} to location 5 meters to the left of {_baseloc}
	set {_rightbase} to location 5 meters to the right of {_baseloc}
	playSound("block.sculk_sensor.clicking_stop", 1, 0.6, {_baseloc})
	loop 20 times:
		set {_m} to 0
		loop 50 times:
			add 0.2 to {_m}
			set {_el.left} to location {_m} meters in front of {_leftbase}
			set {_el.right} to location {_m} meters in front of {_rightbase}
			spawnParticle_Dust(rgb(13, 0, 21), 0.2, {_el.left})
			spawnParticle_Dust(rgb(13, 0, 21), 0.2, {_el.right})
		wait a tick
	playSound("block.sculk_shrieker.shriek", 1, 1, {_baseloc})
	set {_m} to 0
	loop 5 times:
		add 2 to {_m}
		set {_el.front} to location {_m} meters in front of {_leftbase}
		set {_el.front} to location 3 meters above {_el.front}
		set {_el.back} to location {_m} - 2 meters in front of {_rightbase}
		loop all entities within {_el.front} and {_el.base}:
			kitCheckTeam(loop-entity, {_player}, false) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 3)
			if type of loop-entity is a player:
				kitFear(loop-entity, 5 seconds)
		set {_sub} to 0
		loop 2 times:
			set {_baseel} to location {_m} - {_sub} meters in front of {_leftbase}
			subtract 90 from yaw of {_baseel}
			set {_subm} to 0
			loop 11 times:
				set {_el} to location {_subm} meters in front of {_baseel}
				spawnParticle_Vector(sculk_soul, vector(0, 1, 0), 1, {_el})
				add 1 to {_subm}
			add 1 to {_sub}
		wait a tick

function kitWraith_Thunder(player: player, wraith: entity):
	set {_baseloc} to location of {_wraith}
	loop all players in radius 30 around {_baseloc}:
		kitCheckTeam(loop-player, {_player}, false) is true
		playSound("block.conduit.activate", 1, 1.8, loop-player)
		loop 5 times:
			set {_loc} to location of loop-player
			add a random number between -5 and 5 to x-coordinate of {_loc}
			add a random number between -5 and 5 to z-coordinate of {_loc}
			add {_loc} to {_locs::*}
	set {_rad} to 0
	loop 10 times:
		add 36 to {_rad}
		loop {_locs::*}:
			set {_el} to {_locs::%loop-index%}
			add 1.5 * sin({_rad}) to x-coordinate of {_el}
			add 1.5 * cos({_rad}) to z-coordinate of {_el}
			add {_el} to {_els::*}
		loop {_els::*}:
			set {_el} to {_els::%loop-index%}
			spawnParticle_Dust(rgb(106, 93, 33), 0.5, {_el})
		wait a tick
	loop 10 times:
		loop {_els::*}:
			set {_el} to {_els::%loop-index%}
			spawnParticle_Dust(rgb(255, 223, 79), 0.5, {_el})
		wait a tick
	loop 2 times:
		loop {_locs::*}:
			set {_loc} to {_locs::%loop-index%}
			chance of 50%:
				subtract {_loc} from {_locs::*}
				playSound("entity.zombie_villager.cure", 0.8, 2, {_loc})
				loop all players in radius 1.6 around {_loc}:
					kitCheckTeam(loop-player, {_player}, false) is true
					kitDamage({_player}, loop-player, 3, true)
					kitPalsy(loop-player, 5 seconds)
				set {_m} to 0
				loop 30 times:
					add 1 to {_m}
					set {_l} to location {_m} meters above {_loc}
					spawnParticle(end_rod, {_l})
		wait 5 ticks
	loop {_locs::*}:
		set {_loc} to {_locs::%loop-index%}
		subtract {_loc} from {_locs::*}
		playSound("entity.zombie_villager.cure", 0.8, 2, {_loc})
		loop all players in radius 1.6 around {_loc}:
			kitCheckTeam(loop-player, {_player}, false) is true
			kitDamage({_player}, loop-player, 3, true)
			kitPalsy(loop-player, 5 seconds)
		set {_m} to 0
		loop 30 times:
			add 1 to {_m}
			set {_l} to location {_m} meters above {_loc}
			spawnParticle(end_rod, {_l})

function kitWraith_Summon(player: player, wraith: entity):
	set {_baseloc} to location of {_wraith}
	playSound("entity.wither.spawn", 1, 0.9, {_baseloc})
	loop 3 times:
		set {_loc} to {_baseloc}
		add a random number between -5 and 5 to x-coordinate of {_loc}
		add a random number between -5 and 5 to z-coordinate of {_loc}
		delete {_true}
		set {_am} to 0
		while {_true} is not set:
			set {_el} to location {_am} meters above {_loc}
			if block at {_el} is passable:
				set {_true} to true
			else if {_am} >= 10:
				set {_true} to false
			add 1 to {_am}
		if {_true} is true:
			set {_loc} to {_el}
			add {_loc} to {_locs::*}
	loop {_locs::*}:
		set {_nbt} to nbt compound from "{PersistenceRequired:1b,Health:100,Attributes:[{Name:""generic.max_health"",Base:100},{Name:""generic.follow_range"",Base:50},{Name:""generic.knockback_resistance"",Base:1f}],Silent:1,HandItems:[{},{}],ArmorItems:[{},{},{id:""minecraft:leather_chestplate"",tag:{display:{color:3237701}},Count:1},{id:""minecraft:player_head"",Count:1b,tag:{SkullOwner:{Id:[I;-1890619866,1909802641,-1301297704,2019997793],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZTdkM2ViZWQxYTIyYjQ1OWFlNmIxMGMwZTk1ZTRhMGExODA3NGVhNmQzNzViYzJlNzVhOWEyMTczMzMyNDJlIn19fQ==""}]}}}}],ActiveEffects:[{Id:14,Amplifier:2,Duration:2147483647},{Id:28,Amplifier:0,Duration:2147483647}]}"
		set {_mob} to kitSpawnMob_NBT({_player}, skeleton, {_l}, {_nbt})
		set metadata value "necromancer-entity" of {_mob} to {_player}
		set metadata value "necromancer-entity-wraith" of {_mob} to true

function kitWraith_Beam(player: player, wraith: entity):
	set {_target} to target of {_wraith}
	set {_fromloc} to location of head of {_wraith}
	set {_toloc} to location of head of {_target}
	set {_vector} to setVector({_fromloc}, {_toloc})
	set yaw of {_fromloc} to yaw of {_vector}
	set pitch of {_fromloc} to pitch of {_vector}
	kitWraith_Beam_Particle_1({_fromloc})
	wait a second
	set {_m} to 0
	loop {_distance} times:
		add 1 to {_m}
		set {_baseel} to location {_m} meters in front of {_baseloc}
		# --- 効果 ---
		loop all entities in radius 3 around {_baseel}:
			kitCheckTeam(loop-entity, {_player}, false) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 4, true)
		# -----------
		# --- 音の演出 ---
		playSound("entity.zombie.break_wooden_door", 0.75, 0.5, {_baseel})
		# ---------------
		# --- パーティクル演出 ---
		kitWraith_Beam_Particle_2({_baseel})
		# ---------------------

function kitWraith_Beam_Particle_1(baseloc: location):
	set {_distance} to 50
	set {_rad} to 0
	loop 10 times:
		set {_m} to 0
		loop {_distance} - 1 times:
			set {_baseel} to location {_m} + 0.5 meters in front of {_baseloc}
			set {_el} to {_baseel}
			set {_m1} to {_el}
			set pitch of {_m1} to pitch of {_m1} - 90
			set {_test1} to 1 * cos({_rad})
			set {_t1} to location {_test1} meters in front of {_m1}
			set {_m2} to {_el}
			set pitch of {_m2} to 0
			set {_test2} to 1 * sin({_rad})
			set {_t2} to location {_test2} meters to the right of {_m2}
			set x-coordinate of {_el} to x-coordinate of {_t1} + x-coordinate of {_t2} - x-coordinate of {_el}
			set y-coordinate of {_el} to y-coordinate of {_t1} + y-coordinate of {_t2} - y-coordinate of {_el}
			set z-coordinate of {_el} to z-coordinate of {_t1} + z-coordinate of {_t2} - z-coordinate of {_el}
			add {_el} to {_efflocs::*}
			playSound("block.nether_wood_button.click_on", 0.75, 0.5 + (0.15 * (loop-number-1 -1)), {_baseel})
			add 1 to {_m}
		loop {_efflocs::*}:
			set {_el} to loop-value-2
			spawnParticle_DustTransition(rgb(122, 65, 113), rgb(79, 69, 92), 1, {_el})
		add 36 to {_rad}
		wait a tick
	loop 10 times:
		set {_m} to 0
		loop {_distance} - 1 times:
			set {_baseel} to location {_m} + 0.5 meters in front of {_baseloc}
			playSound("block.nether_wood_button.click_on", 0.75, 0.5 + (0.15 * (loop-number-1 -1)), {_baseel})
		loop {_efflocs::*}:
			set {_el} to loop-value-2
			spawnParticle_DustTransition(rgb(122, 65, 113), rgb(79, 69, 92), 1, {_el})
		wait a tick

function kitWraith_Beam_Particle_2(baseloc: location):
	set {_rad} to 0
	loop 4 times:
		add 90 to {_rad}
		set {_el} to {_baseloc}
		set {_m1} to {_el}
		set pitch of {_m1} to pitch of {_m1} - 90
		set {_test1} to 0.25 * cos({_rad})
		set {_t1} to location {_test1} meters in front of {_m1}
		set {_m2} to {_el}
		set pitch of {_m2} to 0
		set {_test2} to 0.25 * sin({_rad})
		set {_t2} to location {_test2} meters to the right of {_m2}
		set x-coordinate of {_el} to x-coordinate of {_t1} + x-coordinate of {_t2} - x-coordinate of {_el}
		set y-coordinate of {_el} to y-coordinate of {_t1} + y-coordinate of {_t2} - y-coordinate of {_el}
		set z-coordinate of {_el} to z-coordinate of {_t1} + z-coordinate of {_t2} - z-coordinate of {_el}
		spawnParticle(witch, {_el})

on damage of skeleton:
	metadata value "necromancer-entity-wraith" of victim is true
	cancel event
	if kitCheckTeam(victim, attacker, false) is true:
		set {_baseloc} to location of victim
		playSound("entity.wither.ambient", 1, 0.8, {_baseloc})
		delete victim
		loop 5 times:
			set {_el} to {_baseloc}
			add a random number between -1 and 1 to x-coordinate of {_el}
			add a random number between -1 and 1 to z-coordinate of {_el}
			add a random number between 0 and 2 to y-coordinate of {_el}
			spawnParticle_DustTransition(rgb(0, 0, 0), rgb(70, 14, 68), 3, {_el})

on damage of player:
	metadata value "gametype" of victim is "Kit"
	metadata value "necromancer-entity-wraith" of attacker is set
	set damage to 3
	applyEffect(victim, darkness, 1, 3 seconds)
	set {_baseloc} to location of victim
	playSound("entity.wither.ambient", 1, 0.8, {_baseloc})
	delete victim
	loop 5 times:
		set {_el} to {_baseloc}
		add a random number between -1 and 1 to x-coordinate of {_el}
		add a random number between -1 and 1 to z-coordinate of {_el}
		add a random number between 0 and 2 to y-coordinate of {_el}
		spawnParticle_DustTransition(rgb(0, 0, 0), rgb(70, 14, 68), 3, {_el})

on damage of wither skeleton:
	metadata value "necromancer-entity-abyss" of victim is true
	if damage >= 10:
		set damage to 10
