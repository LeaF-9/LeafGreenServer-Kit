# Demise Smell

function kitSkill_Necromancer_8(player: player):
	set {_tool} to kitNecromanceItem({_player}, 8)
	if kitCheckNecromance({_player}, {_tool}) is true:
		toolCT({_player}, "Necromancer", 8)
		kitRemoveNecromance({_player}, {_tool})
		kitMsg({_player}, "§5§lAbyss§4を召喚します...")
		sendTimeBossbar_GREEN({_player}, "Demise Smell", 600)
		playSound("entity.wither.spawn", 1, 0.5, {_player})
		kitSpawn_Necromancer_Wraith({_player}, location of {_player})
		set {_spawnedentity} to spawned entity
		loop 60 times:
			spawnParticle_Color(entity_effect, 0, 0, 0, location of {_spawnedentity})
			wait a tick

local function kitSpawn_Necromancer_Wraith(player: player, loc: location):
	set {_nbt} to "{PersistenceRequired:1b,Health:100,Attributes:[{Name:""generic.max_health"",Base:100},{Name:""generic.follow_range"",Base:100},{Name:""generic.knockback_resistance"",Base:1f}],Silent:1,HandItems:[{id:""minecraft:netherite_sword"",components:{custom_name:'[{""text"":""Wraith Sword""}]',enchantments:{levels:{knockback:2}}},Count:1},{}],ArmorItems:[{},{id:""minecraft:leather_leggings"",components:{dyed_color:{rgb:0}},Count:1},{id:""minecraft:leather_chestplate"",components:{dyed_color:{rgb:0}},Count:1},{id:""minecraft:player_head"",Count:1b,components:{profile:{id:[I;-843851991,-552779082,-1798462201,-1255611656],properties:[{name:""textures"",value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2EwYzg3ZmVjNzQxM2RjYjdkMWY2MWFmMjRjZjY0OWE1NTZlMDViMDIyNTAyMmMxZjYxZDgwODkzZjMzOTg3ZiJ9fX0=""}]}},Count:1b}],active_effects:[{id:invisibility,duration:12000,amplifier:0,show_particles:0b},{id:slow_falling,duration:12000,amplifier:0,show_particles:0b}]}"
	set {_mob} to kitSpawnMob_NBT({_player}, wither skeleton, {_loc}, {_nbt})
	set metadata value "necromancer-entity" of {_mob} to {_player}
	set metadata value "necromancer-entity-abyss" of {_mob} to true
	kitWraithTimer({_player}, {_mob})

local function kitWraithTimer(player: player, wraith: entity):
	set {_true} to true
	set {_num} to -1
	loop 30 times:
		delete {_ok}
		if {_wraith} is alive:
			set {_ok} to true
		if {_ok} is not set:
			continue
		# --- ターゲット設定 ---
		delete {_target}
		set {_dis} to 1000000
		loop all players:
			kitCheckTeam({_player}, loop-player, false) is true
			if {_dis} > distance between {_player} and loop-player:
				set {_dis} to distance between {_player} and loop-player
				set {_target} to loop-player
		if {_target} is set:
			set target of {_wraith} to {_target}
		# --------------------	
		if {_target} is not set:
			wait a second
			continue
		# --- タイマー ---
		add 1 to {_num}
		if mod({_num}, 3) is not 0:
			wait a second
			continue
		# ----------------
		set {_dis} to distance between {_target} and {_wraith}
		# --- 10m効果 ---
		if {_dis} <= 10:
			set {_chance} to a random integer between 1 and 5
			# --- 突進攻撃 ---
			if {_chance} is 1:
				kitWraith_Charge({_player}, {_wraith})
			# ---------------
			# --- 暗闇攻撃 ---
			else if {_chance} is 2:
				kitWraith_Dark({_player}, {_wraith})
			# ---------------
			# --- 防御攻撃 ---
			else if {_chance} is 3:
				kitWraith_Barrier({_player}, {_wraith})
			# --------------------
			# --- 落雷攻撃 ---
			else if {_chance} is 4:
				kitWraith_Thunder({_player}, {_wraith})
			# ---------------
			# --- 召喚攻撃 ---
			else if {_chance} is 5:
				kitWraith_Summon({_player}, {_wraith})
			# ---------------
		# --------------
		# --- それ以上離れている場合はテレポート ---
		else:
			kitWraith_Teleport({_player}, {_wraith})
		# --------------
		wait a second
	loop all entities:
		if loop-entity is {_wraith}:
			delete loop-entity
			continue
		metadata value "necromancer-entity" of loop-entity is {_player}
		metadata value "necromancer-entity-wraith" of loop-entity is true
		delete loop-entity



# 突進攻撃
# ターゲットに向かいとんでもない突進を行う
local function kitWraith_Charge(player: player, wraith: entity):
	set {_target} to target of {_wraith}
	set {_tploc} to location of {_wraith}
	playSound("entity.warden.agitated", 1, 1.5, {_wraith})
	loop 20 times:
		teleport {_wraith} to {_tploc}
		kitWraith_Charge_Particle_1({_wraith})
		wait a tick
	set {_frontloc} to location 10 meters in front of {_tploc}
	set {_vector} to setVector({_tploc}, {_frontloc}, 3)
	set velocity of {_wraith} to {_vector}
	playSound("entity.warden.roar", 1, 1.2, {_wraith})
	loop 10 times:
		kitWraith_Charge_Particle_2({_wraith}, {_vector})
		loop all entities in radius 3 around {_wraith}:
			kitCheckTeam({_player}, loop-entity, false) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 4)
			playSound("entity.warden.heartbeat", 1, 2, loop-entity)
		wait a tick

local function kitWraith_Charge_Particle_1(wraith: entity):
	set {_baseloc} to location 1 meters above {_wraith}
	set {_fromloc} to {_baseloc}
	add a random number between -3 and 3 to x-coordinate of {_fromloc}
	add a random number between -3 and 3 to y-coordinate of {_fromloc}
	add a random number between -3 and 3 to z-coordinate of {_fromloc}
	set {_dis} to distance between {_baseloc} and {_fromloc} / 5
	set {_v} to setVector({_fromloc}, {_baseloc})
	set yaw of {_fromloc} to yaw of {_v}
	set pitch of {_fromloc} to pitch of {_v}
	loop 5 times:
		set {_el} to location {_dis} * loop-number meters in front of {_fromloc}
		spawnParticle(end_rod, {_el})

local function kitWraith_Charge_Particle_2(wraith: entity, vector: vector):
	set {_newVector} to {_vector} * vector(-1, -1, -1)
	set {_newVector} to {_newVector} + vector(a random number between -1 and 1, a random number between -1 and 1, a random number between -1 and 1)
	spawnParticle_Vector(smoke, {_newVector}, 0.5, location of {_wraith})



# 暗闇攻撃
# 周囲のプレイヤーに盲目を付与し、更に盲目が付与されているプレイヤーを自身の位置に吸引する
local function kitWraith_Dark(player: player, wraith: entity):
	set {_baseloc} to location of {_wraith}
	set {_baserad} to 0
	set {_m} to 0
	playSound("entity.warden.tendril_clicks", 1, 0.85, {_baseloc})
	loop 10 times:
		add 0.5 to {_m}
		set {_num.rad} to 0
		loop 4 times:
			add 1 to {_num.rad}
			set {_rad} to {_baserad} + (90 * {_num.rad})
			set {_el} to {_baseloc}
			add {_m} * sin({_rad}) to x-coordinate of {_el}
			add {_m} * cos({_rad}) to z-coordinate of {_el}
			spawnParticle_Dust(rgb(89, 66, 85), 1, {_el})
		add 15 to {_baserad}
		wait a tick
	# --- 暗闇効果付与 ---
	loop all entities in radius 10 around {_baseloc}:
		kitCheckTeam(loop-entity, {_player}, false) is true
		applyEffect(loop-entity, blindness, 1, 3 seconds)
		setSupporter(loop-entity, {_player})
	# ------------------
	kitWraith_Dark_Particle_Launch({_baseloc})
	wait 10 ticks
	set {_count} to 0
	loop 40 times:
		playSound("entity.warden.attack_impact", 1, 1.2, {_baseloc})
		set {_baseloc} to location of {_wraith}
		add 1 to {_count}
		kitWraith_Dark_Particle_Vortex({_baseloc}, {_count})
		if mod({_count}, 5) is not 0:
			wait a tick
			continue
		
		loop all entities in radius 10 around {_baseloc}:
			loop-entity has blindness
			kitCheckTeam(loop-entity, {_player}, false) is true
			set {_from} to location of loop-entity
			set {_vec} to setVector({_from}, {_baseloc}, 0.5)
			set velocity of loop-entity to {_vec}
			kitWraith_Dark_Particle_Hit(loop-entity, {_vec})
		wait a tick


local function kitWraith_Dark_Particle_Launch(baseloc: location):
	# --- 音の演出 ---
	playSound("block.sculk_catalyst.bloom", 1, 0.75, {_baseloc})
	# --------------
	# --- パーティクルの演出 ---
	set {_rad} to 0
	loop 10 times:
		set {_el} to {_baseloc}
		add 36 to {_rad}
		add 5 * sin({_rad}) to x-coordinate of {_el}
		add 5 * cos({_rad}) to z-coordinate of {_el}
		spawnParticle_DustTransition(rgb(0, 0, 0), rgb(89, 0, 74), 5, {_el})
	loop 15 times:
		set {_el} to {_baseloc}
		add a random number between -5 and 5 to x-coordinate of {_el}
		add a random number between -5 and 5 to z-coordinate of {_el}
		spawnParticle_DustTransition(rgb(0, 0, 0), rgb(89, 0, 74), 5, {_el})
	# -----------------------


local function kitWraith_Dark_Particle_Vortex(loc: location, num: number):
	set {_baserad} to {_num} * 10
	set {_baseloc} to location 1 meter above {_loc}

	set {_count} to 4
	set {_radius} to 10
	set {_loop} to 20
	set {_rad} to {_baserad}
	set {_addrad} to 360 / {_count}
	set {_el} to {_baseloc}
	set {_addm} to {_radius} / {_loop}
	set {_meter} to 0
	loop {_loop} times:
		add {_addm} to {_meter}
		# 円
		loop {_count} times:
			set {_el} to {_baseloc}
			add {_meter} * sin({_rad}) to x-pos of {_el}
			add {_meter} * cos({_rad}) to z-pos of {_el}
			spawnParticle_Dust(colorRandomizer(0, 0, 0, 50, 0, 50), 1.5, {_el})
			add {_addrad} to {_rad}

local function kitWraith_Dark_Particle_Hit(entity: entity, vector: vector):
	chance of 10%:
		playSound("entity.warden.nearby_close", 1, 2, {_entity})
	set {_baseloc} to setDirToVec(location 1 meter above {_entity}, {_vector})
	loop 3 times:
		set {_baseel} to {_baseloc}
		add a random number between -1 and 1 to x-pos of {_baseel}
		add a random number between -1 and 1 to y-pos of {_baseel}
		add a random number between -1 and 1 to z-pos of {_baseel}





# 防御攻撃
# ダメージを完全に無効化し、ダメージを与えた敵にダメージを与える
local function kitWraith_Barrier(player: player, wraith: entity):
	playSound("entity.shulker.shoot", 1, 0.6, {_wraith})
	set metadata value "abyss-barrier" of {_wraith} to true
	loop 60 times:
		if {_wraith} is dead:
			stop
		spawnParticle_Dust(rgb(73, 55, 89), 0.85, particleLocation_Sphere(location 1.5 meters above {_wraith}, 10, 1.5))
		wait a tick
	set metadata value "abyss-barrier" of {_wraith} to false

on damage of wither skeleton with priority LOW:
	metadata value "necromancer-entity-abyss" of victim is true
	if metadata value "abyss-barrier" of victim is true:
		cancel event
		applyEffect(attacker, wither, 3, 1 seconds)
		playSound("entity.shulker_bullet.hit", 1, 0.7, victim)
		loop 10 times:
			set {_v} to randomVector(1)
			spawnParticle_Vector(sculk_charge_pop, {_v}, 0.5, location 1.5 meters above victim)



# 落雷魔法
# 上空に飛び上がり、周囲の敵めがけて落雷魔法を落とす
local function kitWraith_Thunder(player: player, wraith: entity):
	# 滑空
	kitWraith_Thunder_Particle_Prepare({_wraith})
	set {_baseloc} to location of {_wraith}
	loop all entities in radius 10 around {_wraith}:
		kitCheckTeam(loop-entity, {_player}, false) is true

		loop a random integer between 1 and 3 times:
			set {_loc} to location of loop-entity
			add a random number between -5 and 5 to x-coordinate of {_loc}
			add a random number between -5 and 5 to z-coordinate of {_loc}
			kitWraith_Thunder_Particle_Warning({_loc})
			add {_loc} to {_locs::*}

	applyEffect({_wraith}, levitation, 5, 1 second, true, "none")
	# 落雷
	wait a second
	loop 2 times:
		loop {_locs::*}:
			chance of 30%:
				continue
			set {_baseloc} to loop-value-2
			remove {_baseloc} from {_locs::*}
			set {_addy} to 0
			loop 40 times:
				add 0.5 to {_addy}
				set {_el} to location {_addy} meters above {_baseloc}
				playSound("entity.zombie_villager.cure", 0.5, 2, {_el})
				set {_hits::*} to checkHitEntities({_el})
				loop {_hits::*}:
					set {_hitentity} to loop-value-4
					kitCheckTeam({_hitentity}, {_player}, false) is true
					kitDamage({_player}, {_hitenttiy}, 3, true)
					kitPalsy({_hitenttiy}, 5 seconds)
					applyEffect({_hitenttiy}, glowing, 1, 5 seconds)

				kitWraith_Thunder_Particle_Lightning({_el})
		wait 10 ticks

	loop {_locs::*}:
		set {_baseloc} to loop-value
		set {_addy} to 0
		loop 40 times:
			add 0.5 to {_addy}
			set {_el} to location {_addy} meters above {_baseloc}
			playSound("entity.zombie_villager.cure", 0.5, 1.5, {_el})
			set {_hits::*} to checkHitEntities({_el})
			loop {_hits::*}:
				set {_hitentity} to loop-value-3
				kitCheckTeam({_hitentity}, {_player}, false) is true
				kitDamage({_player}, {_hitenttiy}, 3, true)
				kitPalsy({_hitenttiy}, 5 seconds)
				applyEffect({_hitenttiy}, glowing, 1, 5 seconds)

			kitWraith_Thunder_Particle_Lightning({_el})
		


local function kitWraith_Thunder_Particle_Prepare(wraith: entity):
	playSound("entity.evoker.cast_spell", 1, 0.85, {_wraith})
	set {_a} to 0
	loop 20 times:
		add 20 to {_a}
		set {_baseloc} to location 1.25 meters above {_wraith}
		# 周囲に浮くふわふわパーティクル
		loop 5 times:
			set {_el} to {_baseloc}
			add a random number between -3 and 3 to x-pos of {_el}
			add a random number between -3 and 3 to y-pos of {_el}
			add a random number between -3 and 3 to z-pos of {_el}
			spawnParticle_Dust(colorRandomizer(255, 200, 255, 55, 0, 55), 2, {_el})
		# クルクルパーティクル
		spawnParticle(wax_off, particleLocation_Circle({_baseloc}, 2, 0.5, {_a}))
		wait a tick

local function kitWraith_Thunder_Particle_Warning(loc: location):
	loop 20 times:
		spawnParticle_Dust(rgb(100, 0, 0), 1, particleLocation_Circle({_loc}, 50, 1))
		wait a tick

local function kitWraith_Thunder_Particle_Lightning(loc: location):
	spawnParticle(end_rod, {_loc})
	loop a random integer between 0 and 3 times:
		spawnParticle_Vector(electric_spark, randomVector(), 0.5, {_loc})



# 召喚魔法
# 自身の周囲に従者を5体召喚する この従者は無敵な上、敵を攻撃すると5秒間の恐怖効果を付与する
local function kitWraith_Summon(player: player, wraith: entity):
	set {_baseloc} to location of {_wraith}
	applyEffect({_wraith}, slowness, 10, 1 second, true, "none")
	playSound("entity.guardian.attack", 1, 1.5, {_wraith})
	loop a random integer between 3 and 5 times:
		set {_loc} to {_baseloc}
		add a random number between -5 and 5 to x-coordinate of {_loc}
		add a random number between -5 and 5 to z-coordinate of {_loc}
		delete {_true}
		set {_am} to 0
		while {_true} is not set:
			set {_el} to location {_am} meters above {_loc}
			if block at {_el} is passable:
				set {_true} to true
			else if {_am} >= 10:
				set {_true} to false
			add 1 to {_am}
		if {_true} is true:
			set {_loc} to {_el}
			kitWraith_Summon_Particle_Prepare({_el})
			add {_el} to {_locs::*}
	
	wait a second
	playSound("entity.wither.spawn", 1, 0.9, {_baseloc})
	loop {_locs::*}:
		set {_l} to loop-value
		set {_nbt} to "{PersistenceRequired:1b,Health:100,Attributes:[{Name:""generic.max_health"",Base:100},{Name:""generic.follow_range"",Base:50},{Name:""generic.knockback_resistance"",Base:1f}],Silent:1,HandItems:[{},{}],ArmorItems:[{},{},{id:""minecraft:leather_chestplate"",components:{dyed_color:{rgb:3237701}},Count:1},{id:""minecraft:player_head"",Count:1b,components:{profile:{id:[I;-1890619866,1909802641,-1301297704,2019997793],properties:[{name:""textures"",value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZTdkM2ViZWQxYTIyYjQ1OWFlNmIxMGMwZTk1ZTRhMGExODA3NGVhNmQzNzViYzJlNzVhOWEyMTczMzMyNDJlIn19fQ==""}]}}}],active_effects:[{id:invisibility,amplifier:2,duration:2147483647},{id:slow_falling,amplifier:0,duration:2147483647}]}"
		set {_mob} to kitSpawnMob_NBT({_player}, skeleton, {_l}, {_nbt})
		set metadata value "necromancer-entity" of {_mob} to {_player}
		set metadata value "necromancer-entity-wraith" of {_mob} to true
		kitWraith_Summon_Particle_Appear({_l})

local function kitWraith_Summon_Particle_Prepare(loc: location):
	set {_a} to 0
	set {_y} to 0
	loop 20 times:
		set {_el} to {_loc}
		add {_y} to y-pos of {_el}
		spawnParticle(wax_off, particleLocation_Circle({_el}, 4, 0.5, {_a}))
		add 20 to {_a}
		add 0.1 to {_y}
		wait a tick

local function kitWraith_Summon_Particle_Appear(loc: location):
	add 1 to y-pos of {_loc}
	loop 30 times:
		spawnParticle_Vector(end_rod, randomVector(), 0.3, {_loc})



# 転移魔法
# プレイヤーとの距離が離れすぎている場合に使用される
local function kitWraith_Teleport(player: player, wraith: entity):
	set {_target} to target of {_wraith}
	set {_fromloc} to location 1 meter above {_wraith}
	set {_toloc} to location 1 meter above {_target}
	kitWraith_Teleport_Particle_Path({_fromloc}, {_toloc})
	apply slowness 10 without particles to {_wraith} for 1 second replacing existing effect
	teleport {_wraith} to location of {_target}
	kitWraith_Teleport_Particle_End({_toloc})

local function kitWraith_Teleport_Particle_Path(from: location, to: location):
	set {_dis} to distance between {_from} and {_to}
	set {_count} to round({_dis} * 5)
	set {_baseloc} to setDirToVec({_from}, setVector({_from}, {_to}))
	set {_m} to 0
	loop {_count} times:
		add 0.2 to {_m}
		set {_el} to location {_m} meters in front of {_baseloc}
		chance of 5%:
			kitWraith_Teleport_Particle_Line({_el})
		spawnParticle(witch, {_el})

local function kitWraith_Teleport_Particle_Line(loc: location):
	set {_baseloc} to {_loc}
	add a random number between -1 and 1 to x-pos of {_baseloc}
	add a random number between -1 and 1 to y-pos of {_baseloc}
	add a random number between -1 and 1 to z-pos of {_baseloc}
	set {_d} to dice(3)
	if {_d} is 1:
		set {_color} to rgb(77, 67, 152)
	else if {_d} is 2:
		set {_color} to rgb(70, 14, 68)
	else:
		set {_color} to rgb(67, 61, 60)
	set {_m} to 0
	loop 10 times:
		add 0.2 to {_m}
		set {_el} to location {_m} meters in front of {_baseloc}
		


local function kitWraith_Teleport_Particle_End(loc: location):
	playSound("entity.enderman.teleport", 1, 0.55, {_loc})
	loop 100 times:
		spawnParticle_Vector(end_rod, randomVector(), 0.3, {_loc})



local function kitWraith_Beam(player: player, wraith: entity):
	set {_target} to target of {_wraith}
	set {_fromloc} to location of head of {_wraith}
	set {_toloc} to location of head of {_target}
	set {_vector} to setVector({_fromloc}, {_toloc})
	set yaw of {_fromloc} to yaw of {_vector}
	set pitch of {_fromloc} to pitch of {_vector}
	kitWraith_Beam_Particle_1({_fromloc})
	wait a second
	set {_distance} to 50
	set {_m} to 0
	loop {_distance} times:
		add 1 to {_m}
		set {_baseel} to location {_m} meters in front of {_fromloc}
		# --- 効果 ---
		loop all entities in radius 3 around {_baseel}:
			kitCheckTeam(loop-entity, {_player}, false) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 4, true)
		# -----------
		# --- 音の演出 ---
		playSound("entity.zombie.break_wooden_door", 0.5, 0.5, {_baseel})
		# ---------------
		# --- パーティクル演出 ---
		kitWraith_Beam_Particle_2({_baseel})
		# ---------------------

local function kitWraith_Beam_Particle_1(baseloc: location):
	set {_distance} to 50
	set {_rad} to 0
	loop 10 times:  # 時間のループ
		set {_m} to 5
		loop {_distance} / 5 times:  # ビーム距離のループ
			set {_baseel} to location {_m} meters in front of {_baseloc}
			delete {_els::*}
			set {_els::*} to particleLocation_CircleV({_baseel}, 10, 1)
			set {_loopradnum} to 0
			loop {_rad} / 36 times:
				add 1 to {_loopradnum}
				set {_el} to {_els::%{_loopradnum}%}
				spawnParticle_DustTransition(rgb(122, 65, 113), rgb(79, 69, 92), 1, {_el})
			playSound("block.nether_wood_button.click_on", 0.5, 0.5 + (0.15 * (loop-number-1 + -1)), {_baseel})
			add 5 to {_m}
		add 36 to {_rad}
		wait a tick
	loop 10 times:
		set {_m} to 5
		loop {_distance} / 5 times:
			set {_baseel} to location {_m} meters in front of {_baseloc}
			delete {_els::*}
			set {_els::*} to particleLocation_CircleV({_baseel}, 10, 1)
			playSound("block.nether_wood_button.click_on", 0.5, 2, {_baseel})
			loop {_els::*}:
				set {_el} to loop-value-3
				spawnParticle_DustTransition(rgb(85, 41, 91), rgb(70, 14, 68), 1.5, {_el})
			add 5 to {_m}
		wait a tick

local function kitWraith_Beam_Particle_2(baseloc: location):
	set {_rad} to 0
	loop 4 times:
		add 90 to {_rad}
		set {_el} to {_baseloc}
		set {_m1} to {_el}
		set pitch of {_m1} to pitch of {_m1} - 90
		set {_test1} to 0.25 * cos({_rad})
		set {_t1} to location {_test1} meters in front of {_m1}
		set {_m2} to {_el}
		set pitch of {_m2} to 0
		set {_test2} to 0.25 * sin({_rad})
		set {_t2} to location {_test2} meters to the right of {_m2}
		set x-coordinate of {_el} to x-coordinate of {_t1} + x-coordinate of {_t2} - x-coordinate of {_el}
		set y-coordinate of {_el} to y-coordinate of {_t1} + y-coordinate of {_t2} - y-coordinate of {_el}
		set z-coordinate of {_el} to z-coordinate of {_t1} + z-coordinate of {_t2} - z-coordinate of {_el}
		spawnParticle(witch, {_el})

on damage of skeleton:
	metadata value "necromancer-entity-wraith" of victim is true
	cancel event
	if kitCheckTeam(victim, attacker, false) is true:
		set {_baseloc} to location of victim
		playSound("entity.wither.ambient", 1, 0.8, {_baseloc})
		delete victim
		loop 5 times:
			set {_el} to {_baseloc}
			add a random number between -1 and 1 to x-coordinate of {_el}
			add a random number between -1 and 1 to z-coordinate of {_el}
			add a random number between 0 and 2 to y-coordinate of {_el}
			spawnParticle_DustTransition(rgb(0, 0, 0), rgb(70, 14, 68), 3, {_el})

on damage of player:
	metadata value "gametype" of victim is "Kit"
	metadata value "necromancer-entity-wraith" of attacker is set
	set damage to 3
	applyEffect(victim, darkness, 1, 3 seconds)
	set {_baseloc} to location of victim
	playSound("entity.wither.ambient", 1, 0.8, {_baseloc})
	delete attacker
	loop 5 times:
		set {_el} to {_baseloc}
		add a random number between -1 and 1 to x-coordinate of {_el}
		add a random number between -1 and 1 to z-coordinate of {_el}
		add a random number between 0 and 2 to y-coordinate of {_el}
		spawnParticle_DustTransition(rgb(0, 0, 0), rgb(70, 14, 68), 3, {_el})

on damage of wither skeleton:
	metadata value "necromancer-entity-abyss" of victim is true
	if damage >= 10:
		setDamageModification_SET(victim, 10)