# Death Detector

function kitEffect_DeathDetector(player: player):
	set {_SkillLocation} to getKitStats({_player}, "SkillLocation")
	if {_SkillLocation} is not set:
		send action bar "§c§l地点が設定されていません!" to {_player}
		stop

	nameCT({_player}, "Death Detector")
	set {_now} to now
	setKitStats({_player}, "DeathDetector", true)
	setKitStats({_player}, "DeathDetector-Time", {_now})

	kitFakeDeleteData({_player}, "Death Detector")
	set {_baseloc} to {_SkillLocation}

	set {_rotation} to getQuaternionf(0, 0, 0, 1)
	set {_translation} to getVector3f(-0.25, 0, -0.25)
	set {_scale} to getVector3f(0.5, 0.75, 0.5)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	set {_block} to getBlockData(bone block)
	kitFakeBlock({_player}, {_baseloc}, {_block}, {_transformation}, "Death Detector")

	set {_loc1} to {_baseloc}
	add 0.75 to y-pos of {_loc1}
	set {_scale} to getVector3f(0.5, 0.5, 0.5)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	set {_block} to getBlockData(soul fire)
	kitFakeBlock({_player}, {_loc1}, {_block}, {_transformation}, "Death Detector")

	set {_loc2} to {_baseloc}
	add 1 to y-pos of {_loc2}
	set {_translation} to getVector3f(-0.5, 0, -0.5)
	set {_scale} to getVector3f(1, 1, 1)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	set {_block} to getBlockData(white stained glass)
	kitFakeBlock({_player}, {_loc2}, {_block}, {_transformation}, "Death Detector")

	set {_loc3} to {_baseloc}
	add 1.75 to y-pos of {_loc3}
	set {_translation} to getVector3f(0, 0, 0)
	set {_transformation} to getTransformation({_rotation}, {_rotation}, {_translation}, {_scale})
	set {_item} to skeleton skull
	kitFakeItem({_player}, {_loc3}, {_item}, {_transformation}, "Death Detector")

	kitMsg({_player}, "<##8b0000>死を感知して骨を召喚する装置を設置しました...")
	playSound("block.end_portal_frame.fill", 1, 0.5, {_baseloc})
	while getKitStats({_player}, "DeathDetector-Time") is {_now}:
		wait a second
		set {_exist} to kitFakeCheck({_player}, "Death Detector")
		if {_exist} is false:
			deleteKitStats({_player}, "DeathDetector-Time")
	if getKitStats({_player}, "DeathDetector-Time") is not set:
		deleteKitStats({_player}, "DeathDetector")

function kitEffect_DeathDetector_Summon(player: player):  # 死んだ際に発動する関数
	loop all entities:
		metadata value "kit-block" of loop-entity is "Death Detector"
		metadata value "Death Detector" of loop-entity is "%{_player}%"
		set {_loc} to location of loop-entity
	if {_loc} is set:
		playSound("entity.wither_skeleton.death", 1, 1, {_loc})
		loop 5 times:
			set {_effloc} to {_loc}
			add a random number between -1.5 and 1.5 to x-coordinate of {_effloc}
			add a random number between -1.5 and 1.5 to z-coordinate of {_effloc}
			add a random number between 0.5 and 2.5 to y-coordinate of {_effloc}
			spawnParticle(witch, {_effloc})
		loop all blocks in radius 3 around {_loc}:
			loop-block is air
			block below loop-block is not passable
			add location of loop-block to {_locs::*}
		set {_num} to a random integer between 0 and 2
		if {_num} is 0:
			stop
		kitEffectDescription({_player}, "§f呼骨")
		loop {_num} times:
			delete {_vengeful}
			set {_c} to kitPercent({_player}, 10)
			chance of {_c}%:
				set {_vengeful} to true
			set {_sumloc} to a random location out of {_locs::*}
			if {_vengeful} is true:
				kitSpawn_Necromancer_Vengeful({_player}, {_sumloc})
			else:
				set {_skeleton} to kitSpawn_Necromancer_Skeleton({_player}, {_sumloc})
				set max health of {_skeleton} to 1
		kitEffect_DeathDetector_Glowing({_player})

local function kitEffect_DeathDetector_Glowing(player: player):
	loop all entities:
		metadata value "kit-item" of loop-entity is "Death Detector"
		metadata value "Death Detector" of loop-entity is "%{_player}%"
		set {_glowEntity} to loop-entity
	
	{_glowEntity} is set
	set glowing of {_glowEntity} to true
	wait 3 seconds
	set glowing of {_glowEntity} to false