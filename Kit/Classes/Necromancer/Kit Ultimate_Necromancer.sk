# Immortal Soul

function kitUltNecromancer(player: player):
	message "§c自身のアンデッドを全てNecromanceに変換します!" to {_player}
	message "§c10体のReaperが召喚され,Reaperの攻撃時にNecromanceが増えます!" to {_player}
	set {Ult.%{_player}%::Necromancer} to true
	loop all entities:
		metadata value "necromancer-entity" of loop-entity is {_player}
		metadata value "necromancer-entity-undead" of loop-entity is set
		spawnParticle(witch, location of loop-entity)
		delete loop-entity
		kitAddNecromance({_player}, 1)
	loop all blocks in radius 10 around {_player}:
		loop-block is passable
		block below loop-block is not passable
		add location of loop-block to {_locs::*}
	loop 10 times:
		set {_loc} to a random location out of {_locs::*}
		remove {_loc} from {_locs::*}
		kitSpawn_Necromancer_Reaper({_player}, {_loc})
	wait 10 seconds
	delete {Ult.%{_player}%::Necromancer}
	loop all skeletons:
		metadata value "necromancer-entity" of loop-skeleton is {_player}
		metadata value "necromancer-entity-reaper" of loop-skeleton is true
		delete loop-skeleton

function kitSpawn_Necromancer_Reaper(player: player, loc: location):
	set {_nbt} to nbt compound from "{PersistenceRequired:1b,Health:100,Attributes:[{Name:""generic.max_health"",Base:100},{Name:""generic.knockback_resistance"",Base:1f},{Name:""generic.attack_damage"",Base:6}],Silent:1,HandItems:[{id:""minecraft:netherite_hoe"",Count:1},{}],ArmorItems:[{},{},{id:""minecraft:leather_chestplate"",tag:{display:{color:0}},Count:1},{id:""minecraft:player_head"",tag:{SkullOwner:{Id:[I;296808892,54742945,-1675401107,1649769798],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYWVhMDM0MDc2Y2ExMDk1OWRlOWQ1NDFjODE3Y2QyOGQyYThiMGFhZDdkOGE4YWZjMmI1NDI1M2Y2ZjBmZWQzIn19fQ==""}]}}},Count:1b}],ActiveEffects:[{Id:1,Amplifier:2,Duration:999999},{Id:14,Amplifier:0,Duration:2147483647},{Id:28,Amplifier:0,Duration:2147483647}]}"
	set {_mob} to kitSpawnMob_NBT({_player}, skeleton, {_loc}, {_nbt})
	set metadata value "necromancer-entity" of {_mob} to {_player}
	set metadata value "necromancer-entity-reaper" of {_mob} to true
	loop 200 times:
		{_mob} is alive
		spawnParticle(witch, location of {_mob})
		wait a tick

on damage of player:
	metadata value "necromancer-entity-reaper" of attacker is true
	set {_player} to metadata value "necromancer-entity" of attacker
	kitAddNecromance({_player}, 1)