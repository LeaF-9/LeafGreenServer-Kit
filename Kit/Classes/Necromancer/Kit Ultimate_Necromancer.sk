# Immortal Soul

import:
	org.bukkit.attribute.Attribute

function kitUltNecromancer(player: player):
	loop all entities:
		metadata value "necromancer-entity" of loop-entity is {_player}
		metadata value "necromancer-entity-undead" of loop-entity is set
		spawnParticle(witch, location of loop-entity)
		delete loop-entity
		kitAddNecromance({_player}, 1)
	loop all blocks in radius 10 around {_player}:
		loop-block is passable
		block below loop-block is not passable
		add location of loop-block to {_locs::*}
	loop 10 times:
		set {_loc} to a random location out of {_locs::*}
		remove {_loc} from {_locs::*}
		kitSpawn_Necromancer_Reaper({_player}, {_loc})
	wait 10 seconds
	loop all skeletons:
		metadata value "necromancer-entity" of loop-skeleton is {_player}
		metadata value "necromancer-entity-reaper" of loop-skeleton is true
		delete loop-skeleton

function kitSpawn_Necromancer_Reaper(player: player, loc: location):
	set {_mob} to kitSpawnMob({_player}, skeleton, {_loc})
	set metadata value "necromancer-entity" of {_mob} to {_player}
	set metadata value "necromancer-entity-reaper" of {_mob} to true

	{_mob}.setPersistent(true)
	{_mob}.setSilent(true)

	{_mob}.getAttribute(Attribute.MAX_HEALTH).setBaseValue(100)
	{_mob}.getAttribute(Attribute.KNOCKBACK_RESISTANCE).setBaseValue(1)
	{_mob}.getAttribute(Attribute.ATTACK_DAMAGE).setBaseValue(6)

	{_mob}.setHealth(100)

	load yml "plugins/skript-yaml/Kit Classes/Necromancer.yml" as "head.reaper"
	set {_head} to yml value "kititems.Reaper Head" from "head.reaper"
	unload yml "head.reaper"

	set {_chestplate} to leather chestplate
	set {_color} to rgb(0, 0, 0).asBukkitColor()
	set {_colormeta} to {_chestplate}.getItemMeta()
	{_colormeta}.setColor({_color})
	{_chestplate}.setItemMeta({_colormeta})

	set {_tool} to netherite hoe

	set {_equip} to {_mob}.getEquipment()
	{_equip}.setHelmet({_head})
	{_equip}.setChestplate({_chestplate})
	{_equip}.setLeggings(air)
	{_equip}.setBoots(air)
	{_equip}.setItemInMainHand({_tool})

	applyEffect({_mob}, speed, 1, 10 seconds, true, "none")
	applyEffect({_mob}, invisibility, 1, 10 seconds, true, "none")
	applyEffect({_mob}, slow falling, 1, 10 seconds, true, "none")

	loop 200 times:
		{_mob} is alive
		spawnParticle(witch, location of {_mob})
		wait a tick

on damage of player:
	set {_attacker} to getAttacker(victim)
	metadata value "necromancer-entity-reaper" of {_attacker} is true
	set {_player} to metadata value "necromancer-entity" of {_attacker}
	kitAddNecromance({_player}, 1)