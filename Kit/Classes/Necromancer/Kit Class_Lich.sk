on rightclick holding a wither skeleton egg:
	name of player's tool is "§0§lSummon Ghost"
	cancel event
	if checkCT(player, name of tool of player) is true:
		setCT(player, name of player's tool, 3, false)
		loop 5 times:
			kitSpawnGhost(player, location of player, false)
		kitMsg(player, "§5下僕を召喚した...")
		playSound("entity.wither.ambient", 1, 0.5, player)

on rightclick holding a phantom membrane:
	name of player's tool is "§0§lWraith Form"
	cancel event
	if checkCT(player, name of tool of player) is true:
		set {_tool} to player's tool
		if kitCheckNecromance(player, {_tool}) is true:
			detectCT(player, {_tool})
			kitRemoveNecromance(player, {_tool})
			set {Stats.%player%::Invincible} to true
			set {Stats.%player%::WraithForm} to true
			apply ambient speed 3 to player for 3 seconds replacing existing effect
			hide player from all players
			kitMsg(player, "§5§l別次元を渡り歩く...")
			playSound("block.end_portal_frame.fill", 1, 0.5, player)
			loop 10 times:
				set {_el} to aroundEffect(player)
				drawDot count 1, particle "campfirecozy", center {_el}, visibleRange 30
			loop 60 times:
				if {Stats.%player%::WraithForm} is true:
					wait a tick
			delete {Stats.%player%::WraithForm}
			reveal player from all players
			delete {Stats.%player%::Invincible}
			apply ambient speed 1 to player for 10000000000 seconds replacing existing effect
			kitMsg(player, "§4次元移動が終了した...")
			playSound("block.end_portal_frame.fill", 1, 2, player)
			set {_loc} to location of player
			add 1 to y-coordinate of {_loc}
			loop 50 times:
				draw 1 smoke particle at {_loc} with direction randomVector({_loc}) and speed 2, keepFor 1 tick

on sneak toggle:
	{Stats.%player%::WraithForm} is true
	player is not sneaking
	delete {Stats.%player%::WraithForm}

on rightclick holding a prismarine crystal:
	name of player's tool is "§0§lTime Freeze"
	cancel event
	if checkCT(player, name of tool of player) is true:
		set {_tool} to player's tool
		if kitCheckNecromance(player, {_tool}) is true:
			detectCT(player, {_tool})
			kitRemoveNecromance(player, {_tool})
			kitMsg(player, "§5§l時間が凍結する...")
			playSound("entity.ghast.scream", 1, 0.5, player)
			loop all players:
				{team::%loop-player%} is not {team::%player%}
				loop {Kit-Skill.%{Class::%loop-player%}%::*}:
					set {_dis} to display name of {Kit-Skill.%{Class::%loop-player%}%::%loop-index%}
					add 20 to {CT.%loop-player%::%{_dis}%}
				delete {_sub}
				if {Class::%loop-player%} is "Werewolf":
					set {_sub} to "Instinct"
				else if {Class::%loop-player%} is "Vampire":
					set {_sub} to "Bat"
				if {_sub} is set:
					loop {Kit-Skill.%{Class::%loop-player%}%_%{_sub}%::*}:
						set {_dis} to display name of {Kit-Skill.%{Class::%loop-player%}%_%{_sub}%::%loop-index%}
						add 20 to {CT.%loop-player%::%{_dis}%}
				message "§1時間が歪んでクールタイムが進んでしまった!" to loop-player
				delete {_sub}
			loop 200 times:
				loop all players in radius 5 around player:
					{team::%loop-player%} is not {team::%player%}
					loop-player does not have slowness
					apply slowness 3 to loop-player for 3 seconds replacing existing effect
				wait a tick

on rightclick holding a blaze powder:
	name of player's tool is "§0§lSpace Warp"
	cancel event
	if checkCT(player, name of tool of player) is true:
		set {_tool} to player's tool
		if kitCheckNecromance(player, {_tool}) is true:
			set {_tg} to target block
			if {_tg} is set:
				detectCT(player, {_tool})
				kitRemoveNecromance(player, {_tool})
				set {_base} to location of block above {_tg}
				kitMsg(player, "§5§l空間の歪みを生成した...")
				playSound("entity.zombie_villager.cure", 1, 0.5, {_base})
				set {_c} to drawCount(player)
				drawSphere style 1, particle "redstone", RGB 0, 0, 0, center {_base}, id "%player%-%{_c}%", radius 5, density 30, visibleRange 30
				loop 200 times:
					loop all players in radius 5 around {_base}:
						{team::%loop-player%} is not {team::%player%}
						loop-player's gamemode is survival
						make player damage loop-player by 2
						apply blindness 1 to loop-player for 3 seconds replacing existing effect
					wait a tick
				stopEffect "%player%-%{_c}%"
				kitMsg(player, "§4歪みは消滅した...")
			else:
				send action bar "§c§l発動地点を指定してください" to player

on rightclick holding an eye of ender:
	name of player's tool is "§0§lHell Calling"
	cancel event
	if checkCT(player, name of tool of player) is true:
		set {_tool} to player's tool
		if kitCheckNecromance(player, {_tool}) is true:
			detectCT(player, {_tool})
			kitRemoveNecromance(player, {_tool})
			loop all players in radius 8 around player:
				{team::%loop-player%} is not {team::%player%}
				loop-player's gamemode is survival
				kitFreeze(loop-player, 5 seconds, false)
				message "§0際限ない恐怖に襲われた!" to loop-player
			kitMsg(player, "§5§l地獄からの咆哮が聞こえる...")
			playSound("block.end_portal.spawn", 1, 0.5, player)
			set {_baseloc} to location of player
			add 0.5 to y-coordinate of {_baseloc}
			loop 8 times:
				add 1 to {_aro}
				set {_rad} to 0
				loop 36 times:
					set {_loc} to {_baseloc}
					add 10 to {_rad}
					add {_aro} * sin({_rad}) to x-coordinate of {_loc}
					add {_aro} * cos({_rad}) to z-coordinate of {_loc}
					drawDot count 1, particle "redstone", RGB 50, 0, 0, center {_loc}, visibleRange 32
				wait 3 ticks

on rightclick holding a nether star:
	name of player's tool is "§0§lCollapse World"
	cancel event
	if checkCT(player, name of tool of player) is true:
		set {_tool} to player's tool
		if kitCheckNecromance(player, {_tool}) is true:
			detectCT(player, {_tool})
			kitRemoveNecromance(player, {_tool})
			loop all entities in radius 10 around player:
				kitCheckTeam(loop-entity, player, false) is true
				message formatted "§0殲滅効果を受けてしまった!" to loop-entity
				kitDamage(player, loop-entity, 5, true)
				apply blindness 1 to loop-entity for 10 seconds
			kitMsg(player, "§5§l周辺のプレイヤーを崩壊へ導いた...")
			playSound("entity.ender_dragon.death", 1, 0.5, player)
			set {_baseloc} to location of player
			set {_hei} to 0
			loop 10 times:
				set {_rad} to 0
				add 0.3 to {_hei}
				loop 36 times:
					set {_loc} to {_baseloc}
					add 10 to {_rad}
					add 10 * sin({_rad}) to x-coordinate of {_loc}
					add 10 * cos({_rad}) to z-coordinate of {_loc}
					add {_hei} to y-coordinate of {_loc}
					drawDot count 1, particle "redstone", RGB 0, 0, 0, center {_loc}, visibleRange 32
				wait 2 ticks

on death of player:
	{Class::%{LastKiller::%victim%}%} is "Necromancer"
	{Stats.%{LastKiller::%victim%}%::Lich} is true
	kitSpawnGhost({LastKiller::%victim%}, location of victim, false)

on death of player:
	{Class::%victim%} is "Necromancer"
	{Stats.%victim%::Lich} is true
	loop 5 times:
		kitSpawnGhost(victim, location of victim, false)