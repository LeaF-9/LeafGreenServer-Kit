# Bunshin

function kitEffect_Bunshin(player: player):
	set {_skill} to name of {_player}'s tool
	setCT({_player}, {_skill}, 10)
	kitMsg({_player}, "§2忍法・分身の術!")
	sendTimeBossbar_GREEN({_player}, "Bunshin", 100)
	playSound("block.fire.extinguish", 1, 2, player)
	delete {_player}'s tool
	loop 3 times:
		set {_bunshin} to kitSpawnMob({_player}, zombie, location of {_player})

		{_bunshin}.setAdult()
		{_bunshin}.setSilent(true)
		setAttribute({_bunshin}, "movement speed", 0.6)

		set {_head} to player head
		set skull owner of {_head} to {_player}

		set {_chestplate} to leather chestplate
		set {_color} to rgb(0, 0, 0).asBukkitColor()

		set {_meta} to {_chestplate}.getItemMeta()
		{_meta}.setColor({_color})
		{_chestplate}.setItemMeta({_meta})

		setEntityArmor({_bunshin}, "helmet", {_head})
		setEntityArmor({_bunshin}, "chestplate", {_chestplate})
		setEntityArmor({_bunshin}, "leggings", air)
		setEntityArmor({_bunshin}, "boots", air)
		setEntityArmor({_bunshin}, "mainhand", air)
		setEntityArmor({_bunshin}, "offhand", air)

		applyEffect({_bunshin}, jump boost, 3, 12000, true, "none")

		set metadata value "bunshin" of {_bunshin} to "%{_player}%"
		add {_bunshin} to {_bunshins::*}
	kunaiNinjutsu({_player}, "Bunshin")
	wait 5 seconds
	loop {_bunshins::*}:
		loop-value.remove()
	send action bar "§7§l分身は解除された!" to {_player}