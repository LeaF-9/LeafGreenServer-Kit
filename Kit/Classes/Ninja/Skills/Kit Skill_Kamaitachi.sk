# Kamaitachi

function kitEffect_Kamaitachi(player: player):
	if getKitStats({_player}, "Kamaitachi") is set:
		send action bar "§c§l現在発動中です" to {_player}
		stop

	set {_skill} to name of {_player}'s tool
	setCT({_player}, {_skill}, 10)
	setKitStats({_player}, "Kamaitachi", true)
	kitMsg({_player}, "§f忍法・鎌鼬の術!")
	delete {_player}'s tool
	kunaiNinjutsu({_player}, "Kamaitachi")
	apply speed 3 to {_player} for 5 seconds replacing existing effect
	sendTimeBossbar_GREEN({_player}, "Kamaitachi", 100)
	loop 100 times:
		if getKitStats({_player}, "Kamaitachi") is set:
			loop all entities in radius 5 around {_player}:
				kitCheckTeam({_player}, loop-entity, false) is true
				kitDamage({_player}, loop-entity, 0.25, "鎌鼬の術")
				kitEffectDescription(loop-entity, "§4❤0.25")
				set {_loc} to location of loop-entity
				add 1 to y-coordinate of {_loc}
				spawnParticle_Vector(cloud, randomVector(), 0.5, {_loc})
			set {_pitch} to a random number between 0.5 and 2
			playSound("entity.player.player,sweep", 1, {_pitch}, {_player})
			loop all blocks in radius 5 around {_player}:
				loop-block is passable
				chance of 1%:
					spawnParticle(explosion, location of loop-block)
			wait a tick
	getKitStats({_player}, "Kamaitachi") is set
	apply speed 2 to {_player} for 10000000000 seconds
	deleteKitStats({_player}, "Kamaitachi")
	send action bar "§7§l鎌鼬は解除された!" to {_player}

on damage of player:
	set {_attacker} to getAttacker(victim)
	if getKitStats({_attacker}, "Kamaitachi") is true:
		cancel event