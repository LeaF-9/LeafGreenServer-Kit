# Kusarigama

function kitEffect_Kusarigama(player: player):
	set {_tool} to {_player}'s tool
	nameItemCT({_player}, "Kusarigama")
	setKitStats({_player}, "Kusarigama", true)
	set {_player}'s tool to chain
	set {_baseloc} to location of {_player}
	add 1 to y-pos of {_baseloc}
	set pitch of {_baseloc} to 0
	set {_item} to "minecraft:iron_hoe"
	set {_transformation} to "[1.0000f,0.0000f,0.0000f,-0.3750f,0.0000f,-0.0000f,-1.0000f,0.0000f,0.0000f,1.0000f,-0.0000f,0.4375f,0.0000f,0.0000f,0.0000f,1.0000f]"
	set {_nbt} to nbt compound from "{transformation:%{_transformation}%,item:{id:""%{_item}%"",Count:1b},teleport_duration:1}"
	spawn an item display at {_baseloc} with nbt {_nbt}
	set {_itemdisplay} to spawned item display
	playSound("entity.arrow.shoot", 1, 0.5, {_player})
	set {_yaw} to yaw of {_baseloc}
	loop 20 times:
		if {_blocked} is set:
			continue
		add 1 to {_dis.count}  # 何m進んだかどうかを示す変数
		add 0.5 to {_front}
		add -54 to {_yaw}
		if {_yaw} < 0:
			add 360 to {_yaw}
			playSound("block.chain.break", 1, 1, {_loc})
		set {_loc} to location {_front} meters in front of {_player}
		add 1 to y-pos of {_loc}
		set yaw of {_loc} to {_yaw}
		set pitch of {_loc} to 0
		teleport {_itemdisplay} to {_loc}
		set {_num} to 0
		set {_effront} to 0
		loop loop-number times:
			add 1 to {_num}
			add 0.5 to {_effront}
			set {_effloc} to location {_effront} meters in front of {_player}
			add 1 to y-pos of {_effloc}
			spawnParticle_Dust(rgb(123, 124, 125), 0.65, {_effloc})		
		loop all entities in radius 2 around {_loc}:
			kitCheckTeam(loop-entity, {_player}, false, true) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 2, "鎖鎌")
		if block at {_loc} is not passable:
			set {_blocked} to true
			playSound("block.anvil.land", 1, 1.75, {_loc})
		wait a tick
	set {_baseloc} to {_loc}
	loop {_dis.count} times:
		set {_met} to loop-number
		add -54 to {_yaw}
		if {_yaw} < 360:
			add 360 to {_yaw}
			playSound("block.chain.break", 1, 1, {_loc})
		set {_d} to distance between {_baseloc} and {_player}
		set {_dd} to {_d} / {_dis.count}
		set {_dc} to {_dd} * {_met}
		set {_playerloc} to location of {_player}
		add 1 to y-pos of {_playerloc}
		set {_vector} to setVector({_baseloc}, {_playerloc})
		set {_baseyaw} to yaw of {_vector}
		set {_basepitch} to pitch of {_vector}
		set {_base} to {_baseloc}
		set yaw of {_base} to {_baseyaw}
		set pitch of {_base} to {_basepitch}
		set {_armloc} to {_base}
		set {_loc} to location {_dc} meters in front of {_armloc}
		set yaw of {_loc} to {_yaw}
		set pitch of {_loc} to 0
		teleport {_itemdisplay} to {_loc}
		set {_dc} to round({_dc})
		set {_effront} to 0
		loop {_dis.count} times:
			add {_dd} to {_effront}
			if {_effront} > {_dc}:
				set {_effloc} to location {_effront} meters in front of {_base}
				spawnParticle_Dust(rgb(123, 124, 125), 0.65, {_effloc})
		loop all entities in radius 2 around {_loc}:
			kitCheckTeam(loop-entity, {_player}, false, true) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 2, "鎖鎌")
		wait a tick
	loop all entities:
		loop-entity is {_itemdisplay}
		delete loop-entity
	getKitStats({_player}, "Kusarigama") is true
	set slot 0 of {_player} to {_tool}