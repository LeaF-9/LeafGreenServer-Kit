# Acrobatic Aggression

function kitUltNinja(player: player):
	set {Ult.%{_player}%::Ninja} to true
	set {Stats.%{_player}%::UltNinja} to true
	set {_weapon} to kitPlayerWeaponItem({_player}, "Ninja")
	load yml "plugins/skript-yaml/Kit Classes/Ninja.yml" as "ultninja.%{_player}%"
	set {_UltKatana} to yml value "kititems.Katana [竜]" from "ultninja.%{_player}%"
	set slot 0 of {_player} to {_UltKatana}
	wait 10 seconds
	unload yml "ultninja.%{_player}%"
	delete {Ult.%{_player}%::Ninja}
	{Stats.%{_player}%::UltNinja} is true
	delete {Stats.%{_player}%::UltNinja}
	set slot 0 of {_player} to {_weapon}

on rightclick holding a netherite sword:
	{Stats.%player%::UltNinja} is true
	set {_UltKatana} to yml value "kititems.Katana [竜]" from "ultninja.%player%"
	player's tool is {_UltKatana}
	cancel event
	if checkCT(player, "Ninja's Ult") is true:
		setCT(player, "Ninja's Ult", 1)
		playSound("entity.arrow.shoot", 1, 2, player)
		set {_loc} to location of player
		set pitch of {_loc} to 0
		set {_fb} to location 100 meters in front of {_loc}
		kitParticle_AcrobaticAggresion_Trail(player)
		set {_from} to vector of {_fb}
		set {_to} to vector of player
		set {_vector} to {_from} - {_to}
		set {_vector} to normalized {_vector}
		set x of {_vector} to x of {_vector} * 3
		set y of {_vector} to y of {_vector} * 3
		set z of {_vector} to z of {_vector} * 3
		set velocity of player to {_vector}
		loop 10 times:
			loop all entities in radius 2 around player:
				kitCheckTeam(player, loop-entity, false) is true
				{_hit.%id of loop-entity%} is not set
				set {_hit.%id of loop-entity%} to true
				kitDamage(player, loop-entity, 3, "竜の斬撃")
				kitEffectDescription(loop-entity, "§4❤3")
				spawnParticle(explosion, location of loop-entity)
			wait a tick

function kitParticle_AcrobaticAggresion_Trail(player: player):
	loop 10 times:
		spawnParticle(cloud, location of {_player})
		wait a tick