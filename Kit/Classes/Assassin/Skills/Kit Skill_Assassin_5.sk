# Pursuit

function kitSkill_Assassin_5(player: player):
	toolCT({_player}, "Assassin", 5)
	kitMsg({_player}, "§5追撃を放ちます...")
	set {_baseloc} to location of {_player}
	loop 15 times:
		{_ng} is not set
		set {_tgloc} to location loop-number meters in front of {_baseloc}
		if block at {_tgloc} is not passable:
			set {_ng} to true
			set {_tgloc} to location loop-number - 1 meters in front of {_baseloc}
		else:
			# --- パーティクル演出 ---
			kitEffect_Pursuit_1({_tgloc})
			# -----------------------
			loop all players in radius 1.5 around {_tgloc}:
				kitCheckTeam(loop-player, {_player}, false) is true
				{_tgplayer} is not set
				set {_tgplayer} to loop-player
			if {_tgplayer} is set:
				set {_ng} to true
				set {_tgloc} to location of {_tgplayer}
				set yaw of {_tgloc} to yaw of {_baseloc}
				set pitch of {_tgloc} to pitch of {_baseloc}
				set {_tgloc} to location 1 meters behind {_tgloc}
	teleport {_player} to {_tgloc}
	playSound("entity.player.attack.sweep", 1, 0.6, {_tgloc})
	if {_tgplayer} is set:
		set {_damage} to 2
		if {Stats.%{_tgplayer}%::Pursuit} is set:
			add 2 * {Stats.%{_tgplayer}%::Pursuit} to {_damage}
		kitDamage({_player}, {_tgplayer}, {_damage}, "追斬撃", true)
		kitMsg({_player}, "§1%{_tgplayer}% §4にクリーンヒット!")
		message formatted "§4§l痛い! §5斬撃がヒットした..." to {_tgplayer}
		kitEffectDescription({_player}, "§4❤%{_damage}%")
		playSound("entity.zombie.break_wooden_door", 1, 2, {_tgplayer})
		# --- ヒット時のパーティクル演出 ---
		kitEffect_Pursuit_2({_player}, {_tgplayer})
		# --------------------------------

# 斬撃(残像) 演出
function kitEffect_Pursuit_1(loc: location):
	set {_baseel} to {_loc}
	set pitch of {_baseel} to 0
	set {_baseel} to location a random number between -0.5 and 0.5 to the left of {_baseel}
	set pitch of {_baseel} to pitch of {_loc}
	add a random number between 0 and 2 to y-coordinate of {_baseel}
	set {_m} to 0
	loop 7 times:
		set {_el} to location {_m} meters in front of {_baseel}
		spawnParticle_DustTransition(rgb(162, 32, 65), rgb(102, 20, 41), 0.5, {_el})
		add 0.5 to {_m}

# ヒット時演出
function kitEffect_Pursuit_2(player: player, target: player):
	set {_baseloc} to location of {_target}
	add 1 to y-coordinate of {_baseloc}
	set pitch of {_baseloc} to 0
	set yaw of {_baseloc} to yaw of {_player}
	set {_baseyaw} to yaw of {_baseloc} - 90
	set {_basepitch} to -135
	loop 2 times:  # yaw基準のloop
		set {_yaw} to {_baseyaw} + (180 * loop-number)
		set {_bl} to {_baseloc}
		set yaw of {_bl} to {_yaw}
		loop 2 times:  # pitch基準のloop
			set {_pitch} to {_basepitch} + (90 * loop-number-2)
			set pitch of {_bl} to {_pitch}
			set {_m} to 0
			loop 11 times:
				set {_baseel} to location {_m} meters in front of {_bl}
				set pitch of {_baseel} to pitch of {_baseel} - 180
				loop 3 times:
					add 90 to pitch of {_baseel}
					loop 5 times:
						set {_sidem} to 1.5 - (0.15 * (loop-number-3 + -1))
						set {_sidem} to {_sidem} / 5
						set {_el} to location {_sidem} * loop-number-5 meters in front of {_baseel}
						spawnParticle_DustTransition(rgb(162, 32, 65), rgb(102, 20, 41), 1.5, {_el})
				add 0.5 to {_m}

function kitSkillSub_Assassin_5(victim: player, asn: number):
	set {_now} to now
	set {Stats.%{_victim}%::Pursuit} to {_asn}
	set {Stats.%{_victim}%::PursuitTime} to {_now}
	wait 3 seconds
	if {Stats.%{_victim}%::PursuitTime} is {_now}:
		delete {Stats.%{_victim}%::Pursuit}
		delete {Stats.%{_victim}%::PursuitTime}