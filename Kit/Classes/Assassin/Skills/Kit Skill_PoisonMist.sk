# Poison Mist

function kitEffect_PoisonMist(player: player):
	nameCT({_player}, "Poison Mist")
	playSound("entity.arrow.shoot", 1, 0.5, {_player})
	shoot a thrown potion of mundane from {_player} at speed 1
	set metadata value "poisonmist" of shot thrown potion to "true"
	set {_shotentity} to shot thrown potion
	loop 20 times:
		spawnParticle_Color(entity_effect, rgb(0, 150, 0), {_shotentity})
		wait a tick

on projectile hit:
	projectile is thrown potion
	metadata value "poisonmist" of projectile is "true"
	sendTimeBossbar_GREEN(shooter, "Poison Mist", 100)
	set {_l} to location of projectile
	playSound("block.fungus.break", 1.5, 0.5, {_l})
	loop 100 times:
		delete {_jc}
		delete {_eg::*}
		set {_s} to 0
		set {_t} to 0
		set {_pl} to {_l}
		loop 19 time:
			loop 36 time:
				set {_loc} to {_pl}
				set {_vector} to {_loc}
				set {_x} to x-coordinate of {_loc}
				set {_y} to y-coordinate of {_loc}
				set {_z} to z-coordinate of {_loc}
				set x-coordinate of {_vector} to {_x} + ( 5 * sin({_s}) * cos({_t}) )
				set y-coordinate of {_vector} to {_y} + ( 5 * cos({_s}) )
				set z-coordinate of {_vector} to {_z} + ( 5 * sin({_s}) * sin({_t}) )
				chance of 10%:
					spawnParticle_Dust(rgb(0, 100, 0), 3, {_vector})
				add 10 to {_t}
			add 10 to {_s}
		loop all blocks in radius 5 around {_l}:
			loop-block is passable
			chance of 1%:
				spawnParticle_Color(entity_effect, rgb(0, 100, 0), location of loop-block)
		loop all entities in radius 5 around {_l}:
			if kitCheckTeam(loop-entity, shooter, false) is true:
				spawnParticle_Color(entity_effect, rgb(0, 150, 0), loop-entity)
				set {_eg::%uuid of loop-entity%} to true
				if {_jb::%uuid of loop-entity%} is not set:
					set {_jb::%uuid of loop-entity%} to true
					apply poison 3 to loop-entity for 10 seconds replacing existing effect
					kitEffectDescription(loop-entity, "§c§l毒")
			else if loop-entity is shooter:
				loop-entity does not have glowing
				set {_jc} to true
				if getInvisibleStatus(shooter) is true:
					kitInvisible(shooter, 3)
					set {_notInvTimer} to 20
				else:
					if {_notInvTimer} is not set:
						kitInvisible(shooter, 3)
					else:
						subtract 1 from {_notInvTimer}
						if {_notInvTimer} <= 0:
							delete {_notInvTimer}
		if {_jc} is not set:
			if getInvisibleRemains(shooter) <= 3:
				removeInvisible(shooter)
		loop all entities:
			metadata value "gametype" of loop-entity is "Kit"
			{_eg::%uuid of loop-entity%} is not set
			{_jb::%uuid of loop-entity%} is true
			remove poison from loop-entity
			delete {_jb::%uuid of loop-entity%}
		wait a tick

	loop all entities:
		{_eg::%uuid of loop-entity%} is true
		{_jb::%uuid of loop-entity%} is true
		remove poison from loop-entity

on consume of potion:
	set {_tool} to player's tool
	kitItemAllChecker({_tool}, "Assassin", "Skill", 6) is true
	cancel event