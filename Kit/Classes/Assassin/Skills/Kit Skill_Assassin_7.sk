# Psychopath

function kitSkill_Assassin_7(player: player):
	if {Stats.%{_player}%::Psychopath} is not set:
		toolCT({_player}, "Assassin", 7)
		playSound("particle.soul_escape", 1, 1, {_player})
		kitMsg({_player}, "<##990000>敵を欺く準備をします...")
		loop 60 times:
			if {_target} is not set:
				if mod(61 - loop-number, 20) is 0:
					loop 10 times:
						set {_loc} to aroundEffect({_player})
						spawnParticle(enchant, {_loc})
				loop all players in radius 50 around {_player}:
					{_target} is not set
					kitCheckTeam({_player}, loop-player, false) is true
					delete {_tg}
					set {_tg} to target player of loop-player
					if {_tg} is {_player}:
						set {_bl} to location of head of loop-player
						set {_dis} to round(distance between loop-player and {_player})
						set {_m} to 0
						delete {_blocked}
						loop {_dis} times:
							add 1 to {_m}
							set {_checkblock} to location {_m} meters in front of {_bl}
							if block at {_checkblock} is not passable:
								set {_blocked} to true
						if {_blocked} is not set:
							log "hey 2"
							set {_target} to loop-player
				wait a tick
		if {_target} is set:
			kitFear({_target}, 60)
			kitInvisible({_player}, 1 seconds)
			set {_tploc} to location of {_target}
			set pitch of {_tploc} to 0
			set {_tploc} to location 1 meters behind {_tploc}
			teleport {_player} to {_tploc}
			kitMsg({_player}, "<##a22041>§l%{_target}% <##1f3134>を恐怖に陥れた...")
			message formatted "§4サイコパスに目をつけられた!" to {_target}
			playSound("entity.vex.charge", 1, 0.5, {_target})
			loop 10 times:
				set {_el} to location 1 meters above {_target}
				set {_ev} to randomVector(0.1)
				spawnParticle_Vector(witch, {_ev}, 1, {_el})
		else:
			kitMsg({_player}, "<##800000>敵を欺くことは出来なかった...")
	else:
		send action bar "§c§l現在Skill発動中です!" to {_player}