# Psychopath

function kitSkill_Assassin_7(player: player):
	if {Stats.%{_player}%::Psychopath} is not set:
		toolCT({_player}, "Assassin", 7)
		playSound("particle.soul_escape", 1, 1, {_player})
		kitMsg({_player}, "<##990000>敵を欺く準備をします...")
		loop 60 times:
			if {_target} is not set:
				if mod(61 - loop-number, 20) is 0:
					loop 10 times:
						set {_loc} to aroundEffect({_player})
						spawnParticle(enchant, {_loc})
				loop all players in radius 50 around {_player}:
					{_target} is not set
					kitCheckTeam({_player}, loop-player, false) is true
					playerCanSee(loop-player, {_player}) is true
					set {_target} to loop-player
				wait a tick
		if {_target} is set:
			kitFear({_target}, 3 seconds)
			kitInvisible({_player}, 1 seconds)
			set {_tploc} to location of {_target}
			set pitch of {_tploc} to 0
			set {_tploc} to location 1 meters behind {_tploc}
			teleport {_player} to {_tploc}
			kitMsg({_player}, "<##a22041>§l%{_target}% <##1f3134>を恐怖に陥れた...")
			message formatted "§4サイコパスに目をつけられた!" to {_target}
			playSound("entity.vex.charge", 1, 0.5, {_target})
			loop 10 times:
				set {_el} to location 1 meters above {_target}
				set {_ev} to randomVector({_el}, 0.1)
				spawnParticle_Vector(witch, {_ev}, {_el})
		else:
			kitMsg({_player}, "<##800000>敵を欺くことは出来なかった...")
	else:
		send action bar "§c§l現在Skill発動中です!" to {_player}