# Assassinのスキル Ver1.0.0

# Abilities
on damage of player:
	damage was caused by fall
	{Class::%victim%} is "Assassin"
	cancel event

on damage with priority LOW:
	set {_attacker} to getAttacker(victim)
	{Class::%{_attacker}%} is "Assassin"
	{Stats.%{_attacker}%::UltAssassin} is not set
	set {_dis} to distance between {_attacker} and victim
	if {_dis} <= 1.5:
		kitMsg({_attacker}, "§4近距離効果でダメージが2増えます!")
		playSound("entity.arrow.hit_player", 1, 1.5, {_attacker}, true)
		send action bar "§c接近されてしまった!" to victim
		spawnParticle_Block(redstone block, victim)
		setDamageModification_ADD(victim, 2)
		add 1 to {_asn}
	if victim's facing is {_attacker}'s facing:
		kitMsg({_attacker}, "§4不意打ち効果でダメージが2倍になった!")
		playSound("entity.arrow.hit_player", 1, 0.75, {_attacker}, true)
		send action bar "§c不意を突かれた!" to victim
		spawnParticle_Block(redstone block, victim)
		setDamageModification_MUL(victim, 2)
		add 1 to {_asn}
	if {_asn} is set:
		checkEntityType(victim, player) is true
		kitSkillSub_Assassin_5(victim, {_asn})
		if {_asn} >= 2:
			kitWeapon_Assassin_2({_attacker}, victim) is true
			setDamageModification_FIX(victim, 100)