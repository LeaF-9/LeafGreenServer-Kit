# Weapon-Name: Vander

on damage:
	metadata value "gametype" of attacker is "Kit"
	{Class::%attacker%} is "Mercenary"
	damage was caused by attack
	attacker is sneaking
	set {_tool} to attacker's tool
	kitItemAllChecker({_tool}, "Mercenary", "Weapon.main", 2) is true
	{Kit-Blood::%victim%} is set
	set damage to damage + 2
	kitEffect_Vander(attacker, victim)

local function kitEffect_Vander(player: player, target: player):
	kitParticle_Vander_Edge({_player}, {_target})
	delete {Kit-Blood::%{_target}%}
	message formatted "§4傷口から吸血された..." to {_target}
	kitParticle_Vander_Heal({_player}, {_target})

local function kitParticle_Vander_Edge(attacker: player, victim: player):
	playSound("entity.fox.bite", 1, 1, {_victim})
	set {_atkloc} to location of {_attacker}
	set {_vcmloc} to location of {_victim}
	set {_dism} to distance between {_atkloc} and {_vcmloc}
	set {_v} to setVector({_atkloc}, {_vcmloc})
	set {_atkloc} to setDirToLoc({_atkloc}, {_v})
	set {_calcyaw} to -20 
	loop 11 times:
		set {_baseel} to {_atkloc}
		add {_calcyaw} to yaw of {_baseel}
		set {_el} to location {_dism} meters in front of {_baseel}
		spawnParticle_Dust(rgb(220, 20, 60), 3, {_el})
		add 4 to {_calcyaw}
		wait a tick

local function kitParticle_Vander_Heal(player: player, target: player):
	playSound("entity.player.burp", 1, 0.65, {_player})
	set {_dism} to round(distance between {_player} and {_target})
	set {_locs::*} particleLocation_TornadoV(location of {_player}, 10, 0.5, 0.1, {_dism})
	loop {_locs::*}:
		spawnParticle_Dust(rgb(199, 21, 133), 0.45, loop-value)