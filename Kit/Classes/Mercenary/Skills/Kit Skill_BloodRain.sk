# Blood Rain

function kitEffect_BloodRain(player: player):
	nameCT({_player}, "Blood Rain")
	kitMsg({_player}, "§5血の雨を降らせます...")
	set {_baseloc} to location of {_player}
	set {_sky} to location 10 meters above {_baseloc}
	set pitch of {_sky} to 0
	set {_meter} to 0
	loop 60 times:
		add 0.2 to {_meter}
		set {_skying} to location {_meter} meters in front of {_sky}
		loop all blocks in radius 5 around {_skying}:
			(y-pos of loop-block) is (y-pos of (location of (block at ({_skying}))))
			loop-block is passable
			loop 3 times:
				chance of 10%:
					set {_sel} to location of loop-block
					set {_add.x} to a random number between -0.5 and 0.5
					set {_add.y} to a random number between -0.5 and 0.5
					set {_add.z} to a random number between -0.5 and 0.5
					add {_add.x} to x-pos of {_sel}
					add {_add.y} to y-pos of {_sel} 
					add {_add.z} to z-pos of {_sel}
					spawnParticle_Dust(rgb(150, 0, 0), 2, {_sel})
			chance of 5%:
				shoot a snowball from loop-block at speed 1 downward
				set item of shot snowball to redstone block
				setShooter(shot snowball, {_player})
				set metadata value "kit-bloodrain" of shot snowball to "%{_player}%"
				set {_shot} to shot snowball
				kitParticle_BloodRain({_shot})
				playSound("entity.generic.splash", 0.5, 0.5, location of loop-block)
		wait a tick

function kitParticle_BloodRain(proj: entity):
	loop 20 times:
		chance of 10%:
			spawnParticle_Color(entity_effect, rgb(255, 0, 0), {_proj})
		wait a tick

on damage with priority LOW:
	metadata value "gametype" of victim is "Kit"
	damage was caused by projectile
	projectile is a snowball
	metadata value "kit-bloodrain" of projectile is set
	setDamageModification_SET(victim, 1)
	if type of victim is a player:
		kitBlood(victim)
		kitEffectDescription(victim, "§4出血 §8& §4❤1")
	loop 3 times:
		set {_el} to aroundEffect(victim)
		spawnParticle_Color(entity_effect, rgb(50, 0, 0), {_el})