# Ischemia Curse

function kitSkill_Mercenary_6(player: player):
	toolCT({_player}, "Mercenary", 6)
	kitMsg({_player}, "§4§l血痕魔法陣§cを展開します...")
	sendTimeBossbar_GREEN({_player}, "Ischemia Curse", 200)
	playSound("entity.phantom.ambient", 1, 0.8, {_player})
	set {_point} to location of {_player}
# 弧を描く
	set {_n.k} to 0
	loop 30 times:
		set {_effbaseloc} to {_point}
		add 6 * sin({_n.k}) to x-coordinate of {_effbaseloc}
		add 6 * cos({_n.k}) to z-coordinate of {_effbaseloc}
		kitParticle_IschemiaCurse_Magic({_effbaseloc})
		add 12 to {_n.k}
# 弦を描く
		if {_l.A} is not set:
			set {_l.A} to {_point}
			set yaw of {_l.A} to 120
			set pitch of {_l.A} to 0
			add 6 * sin(90) to x-coordinate of {_l.A}
			add 6 * cos(90) to z-coordinate of {_l.A}
		if {_l.B} is not set:
			set {_l.B} to {_point}
			set yaw of {_l.B} to 0
			set pitch of {_l.B} to 0
			add 6 * sin(210) to x-coordinate of {_l.B}
			add 6 * cos(210) to z-coordinate of {_l.B}
		if {_l.C} is not set:
			set {_l.C} to {_point}
			set yaw of {_l.C} to 240
			set pitch of {_l.C} to 0
			add 6 * sin(330) to x-coordinate of {_l.C}
			add 6 * cos(330) to z-coordinate of {_l.C}
	loop 12 times:
		add sqrt(3) / 2 to {_n.g}
#	A-B
		set {_loc.g} to location {_n.g} meters in front of {_l.A}
		kitParticle_IschemiaCurse_Magic({_loc.g})
#	B-C
		set {_loc.g} to location {_n.g} meters in front of {_l.B}
		kitParticle_IschemiaCurse_Magic({_loc.g})
#	C-A
		set {_loc.g} to location {_n.g} meters in front of {_l.C}
		kitParticle_IschemiaCurse_Magic({_loc.g})
	loop 200 times:
		delete {_eg::*}
		loop all players in radius 6 around {_point}:
			kitCheckTeam({_player}, loop-player, false) is true
			{Kit-Blood::%loop-player%} is set
			spawnParticle_Color(entity_effect, 100, 1, 37, location of loop-player)
			set {_eg::%loop-player%} to true
			if {_jb::%loop-player%} is not set:
				set {_jb::%loop-player%} to true
				set {Stats.%loop-player%::IschemiaCurse} to true
				playSound("entity.zombie.infect", 1, 0.5, {_player})
		loop all players:
			metadata value "gametype" of loop-entity is "Kit"
			{_eg::%loop-player%} is not set
			{_jb::%loop-player%} is true
			delete {_jb::%loop-player%}
			delete {Stats.%loop-player%::IschemiaCurse}
		wait a tick
	kitMsg({_player}, "§5血痕魔法陣は消滅しました...")
	loop all players:
		{_eg::%loop-player%} is true
		{_jb::%loop-player%} is true
		delete {Stats.%loop-player%::IschemiaCurse}

function kitParticle_IschemiaCurse_Magic(loc: location):
	loop 200 times:
		spawnParticle_Dust(rgb(100, 1, 37), 1, {_loc})
		wait a tick

on damage of player with priority LOW:
	metadata value "gametype" of victim is "Kit"
	{Stats.%victim%::IschemiaCurse} is true
	setDamageModification_MUL(victim, 2)
	send action bar "§5§l血痕魔法陣が傷を増幅する..." to victim
	kitEffectDescription(victim, "§4x2")
	playSound("item.axe.strip", 1, 0.9, victim)
	set {_el} to location of victim
	add 1 to y-coordinate of {_el}
	loop 10 times:
		spawnParticle_Vector(end_rod, randomVector(), 1, {_el})