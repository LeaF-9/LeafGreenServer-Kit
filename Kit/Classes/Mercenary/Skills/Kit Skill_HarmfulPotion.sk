# Harmful Potion

function kitEffect_HarmfulPotion_Shoot(player: player, event: object):
	metadata value "gametype" of {_player} is "Kit"
	set {_proj} to {_event}.getEntity()
	"%{_proj}%" is "thrown potion"
	set metadata value "kit-harmful" of {_proj} to "true"

on damage of player with priority HIGH:
	damage was caused by potion
	metadata value "kit-harmful" of projectile is "true"
	cancel event

	set {_hp} to victim's health
	set {_addhp} to {_hp} * 0.25 * 0.1
	if getDebuffs(victim, "Blood") is true:
		set {_firsthp} to {_hp} * 0.25
	else:
		set {_firsthp} to {_hp} * 0.5
	set victim's health to {_firsthp}

	set {_sub.health} to {_firsthp} + ({_addhp} * 10)
	kitEffectDescription(victim, "§4❤%{_sub.health}%")
	kitHarmfulEffect(victim)

	loop 10 times:
		wait 2 tick
		if kitCheckAlive(victim) is true:
			add {_addhp} to victim's health

function kitHarmfulEffect(player: player):
	set {_bl} to location of {_player}
	set {_y} to -0.5
	loop 5 times:
		add 0.5 to {_y}
		set {_rad} to 0
		loop 10 times:
			set {_el} to {_bl}
			add 36 to {_rad}
			add 0.5 * sin({_rad}) to x-coordinate of {_el}
			add 0.5 * cos({_rad}) to z-coordinate of {_el}
			add {_y} to y-coordinate of {_el}
			spawnParticle_Dust(rgb(255, 0, 0), 1, {_el})
		wait a tick

on death of player:
	{Class::%{LastKiller::%victim%}%} is "Mercenary"
	getDebuffs(victim, "Blood") is true
	set {_potion} to kitSkillItem("Mercenary", 1)
	set {_potion} to kitItemTagChanger({_potion}, "Mercenary", "Skill", 1)
	if {LastKiller::%victim%}'s inventory does not contain {_potion}:
		give {_potion} to {LastKiller::%victim%}'s inventory
		send action bar "§4§lHarmful Potion§bを獲得しました!" to {LastKiller::%victim%}