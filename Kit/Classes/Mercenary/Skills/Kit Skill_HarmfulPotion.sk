# Harmful Potion

function kitEffect_HarmfulPotion_Shoot(player: player, event: object):
	metadata value "gametype" of {_player} is "Kit"
	set {_proj} to {_event}.getEntity()
	"%{_proj}%" is "thrown potion"
	set metadata value "kit-harmful" of {_proj} to {_player}

on projectile hit:
	set {_player} to metadata value "kit-harmful" of projectile
	{_player} is set
	set {_loc} to location of projectile
	spill({_player}, {_loc})

local function spill(player: player, loc: location):
	loop all entities in radius 4 around {_loc}:
		kitCheckTeam({_player}, loop-entity, false) is true
		hit({_player}, loop-entity)

local function hit(player: player, victim: entity):
	LastKiller({_victim}, {_player})

	set {_hp} to health of {_victim}
	set {_addhp} to {_hp} * 0.25 * 0.1
	if getDebuffs({_victim}, "Blood") is true:
		set {_firsthp} to {_hp} * 0.25
	else:
		set {_firsthp} to {_hp} * 0.5
	set health of {_victim} to {_firsthp}

	set {_sub.health} to {_firsthp} + ({_addhp} * 10)
	kitEffectDescription({_victim}, "§4❤%{_sub.health}%")
	kitHarmfulEffect({_victim})

	loop 10 times:
		wait 2 tick
		if kitCheckAlive({_victim}) is true:
			add {_addhp} to health of {_victim}

function kitHarmfulEffect(player: entity):
	set {_bl} to location of {_player}
	set {_y} to -0.5
	loop 5 times:
		add 0.5 to {_y}
		set {_rad} to 0
		loop 10 times:
			add 36 to {_rad}
			set {_vec} to vector(0.5, 0, 0)
			rotate {_vec} around y-axis by {_rad}
			set {_el} to {_bl} ~ {_vec}
			add {_y} to y-pos of {_el}
			spawnParticle_Dust(rgb(255, 0, 0), 1, {_el})
		wait a tick

on death of player:
	{Class::%{LastKiller::%victim%}%} is "Mercenary"
	getDebuffs(victim, "Blood") is true
	set {_potion} to kitSkillItem("Mercenary", 1)
	set {_potion} to kitItemTagChanger({_potion}, "Mercenary", "Skill", 1)
	if {LastKiller::%victim%}'s inventory does not contain {_potion}:
		give {_potion} to {LastKiller::%victim%}'s inventory
		send action bar "§4§lHarmful Potion§bを獲得しました!" to {LastKiller::%victim%}