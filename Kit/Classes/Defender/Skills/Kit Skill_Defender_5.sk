# King Shield

options:
	width: 7
	height: 3



on damage of slime with priority HIGHEST:
	metadata value "King Shield" of victim is set
	set {_name} to metadata value "King Shield" of victim parsed as player
	set {_attacker} to getAttacker(victim)
	if kitCheckTeam({_name}, {_attacker}, false) is true:
		if checkDamagable(victim) is true:
			playSound("entity.armor_stand.hit", 1, 1, victim)
			set {_dmg} to getDamageModification_ALL(victim, damage)
			setDamageModification_FIX(victim, 0)
			set {_King-Shield} to kitStats_GET(victim, "King Shield")
			subtract {_dmg} from {_King-Shield}
			kitStats_SET(victim, "King Shield", {_King-Shield})
		else:
			cancel event
	else:
		cancel event



function kitSkill_Defender_5(player: player):
	kitEffect_KingShield({_player})



local function kitEffect_KingShield(player: player):
	wait 6 ticks
	set {_King-Shield} to kitStats_GET({_player}, "King Shield")
	if {_King-Shield} is not set:
		kitStats_SET({_player}, "King Shield", 200)
	else if {_King-Shield} <= 0:
		loop all items in {_player}'s inventory:
			name of loop-item is "§1King Shield"
			delete loop-item
		stop

	playSound("item.armor.equip_netherite", 1, 1, {_player})
	set {_now} to now
	set {_baseloc} to location of head of {_player}
	set {_pitch} to pitch of {_baseloc}
	set {_loc.shield} to location 1.5 meters in front of {_baseloc}
	set {_groundloc} to location 1.5 meters above {_player}
	set {_groundloc} to location 1.5 meters in front of {_groundloc}

	# 見た目
	set {_item} to "minecraft:blue_stained_glass_pane"
	if {_King-Shield} < 50:
		set {_item} to "minecraft:white_stained_glass_pane"
	else if {_King-Shield} < 100:
		set {_item} to "minecraft:light_blue_stained_glass_pane"
	else if {_King-Shield} < 150:
		set {_item} to "minecraft:cyan_stained_glass_pane"

	set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[0f,0f,0f]", "[{@width}f,{@height}f,1f]")
	set {_nbt} to nbt compound from "{transformation:%{_transformation}%,item:{id:""%{_item}%"",Count:1b},brightness:{block:15,sky:15},teleport_duration:1}"
	spawn an item display at {_loc.shield} with nbt {_nbt}
	set {_itemdisplay} to spawned item display
	set metadata value "gametype" of {_itemdisplay} to "Kit"
	set metadata value "King Shield" of {_itemdisplay} to "%{_player}%"

	# 当たり判定 (左上ベース)
	set {_height.mul} to cos({_pitch})
	set {_width.mul} to sin({_pitch})

	set {_height} to {@height} * {_height.mul}
	set {_width} to {@width} * {_width.mul}

	set {_loc.hit} to {_groundloc}
	add {_height.mul} * 1 to y-pos of {_loc.hit}
	set pitch of {_loc.hit} to 0
	set {_loc.hit} to location {@width} / 2 - 0.25 meters to the left of {_loc.hit}
	set {_front} to 0.6 * {_width.mul}
	set {_loc.hit} to location {_front} meters in front of {_loc.hit}


	# 高さ分ループ (大きさ0.5のため2倍)
	set {_subfront} to 0.5 * {_width.mul}
	set {_h} to 0
	set {_h.count} to 0
	loop {@height} * 2 times:
		set {_loc.hit.height} to location {_h} meters below {_loc.hit}
		set {_loc.hit.height} to location {_front} meters in front of {_loc.hit.height}

		# 幅分ループ (大きさ0.5のため2倍)
		set {_w} to 0
		set {_w.count} to 0
		loop {@width} * 2 times:
			set {_loc.slime} to location {_w} meters to the right of {_loc.hit.height}
			spawn a slime at {_loc.slime} with nbt compound from "{NoGravity:1b,Size:0.5,PersistenceRequired:1,NoAI:1,Silent:1,DropItem:0,Team:""noCollision"",active_effects:[{id:invisibility,duration:12000,amplifier:0,show_particles:0b}]}"
			set {_slime} to spawned slime
			set {_slime::%{_h.count}%::%{_w.count}%} to {_slime}
			set metadata value "gametype" of {_slime} to "Kit"
			set metadata value "shield entity" of {_slime} to true
			set metadata value "King Shield" of {_slime} to "%{_player}%"
			set display name of {_slime} to "%{_player}%"
			add 0.5 to {_w}
			add 1 to {_w.count}
		subtract {_subfront} from {_front}
		add 0.5 * {_height.mul} to {_h}
		add 1 to {_h.count}
	
	while {_player} is blocking with shield:
		set {_baseloc} to location of head of {_player}
		set {_pitch} to pitch of {_baseloc}
		set {_loc.shield} to location 1.5 meter in front of {_baseloc}
		set {_groundloc} to location 1.5 meters above {_player}
		set {_groundloc} to location 1.5 meters in front of {_groundloc}

		# 見た目
		set {_item.check} to "minecraft:blue_stained_glass_pane"

		delete {_King-Shield}
		set {_King-Shield} to kitStats_GET({_player}, "King Shield")
		if {_King-Shield} <= 0:
			#壊れる処理
			loop all entities:
				metadata value "King Shield" of loop-entity is "%{_player}%"
				delete loop-entity
			loop all items in {_player}'s inventory:
				name of loop-item is "§1King Shield"
				delete loop-item
			playSound("entity.item.break", 1, 0.7, {_player})
			stop	

		else if {_King-Shield} < 50:
			set {_item.check} to "minecraft:white_stained_glass_pane"
		else if {_King-Shield} < 100:
			set {_item.check} to "minecraft:light_blue_stained_glass_pane"
		else if {_King-Shield} < 150:
			set {_item.check} to "minecraft:cyan_stained_glass_pane"
		
		if {_item.check} is not {_item}:
			set {_item} to {_item.check}
			loop all entities:
				loop-entity is {_itemdisplay}
				delete loop-entity
			
			set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[0f,0f,0f]", "[{@width}f,{@height}f,1f]")
			set {_nbt} to nbt compound from "{transformation:%{_transformation}%,item:{id:""%{_item}%"",Count:1b},brightness:{block:15,sky:15},teleport_duration:1}"
			spawn an item display at {_loc.shield} with nbt {_nbt}
			set {_itemdisplay} to spawned item display
			set metadata value "gametype" of {_itemdisplay} to "Kit"
			set metadata value "King Shield" of {_itemdisplay} to "%{_player}%"
		
		else:
			teleport {_itemdisplay} to {_loc.shield}

		# 当たり判定 (左上ベース)
		set {_height.mul} to cos({_pitch})
		set {_width.mul} to sin({_pitch})

		set {_height} to {@height} * {_height.mul}
		set {_width} to {@width} * {_width.mul}

		set {_loc.hit} to {_groundloc}
		set pitch of {_loc.hit} to 0
		add {_height.mul} * 1 to y-pos of {_loc.hit}
		set {_loc.hit} to location {@width} / 2 - 0.25 meters to the left of {_loc.hit}
		set {_front} to 0.6 * {_width.mul}
		set {_loc.hit} to location {_front} meters in front of {_loc.hit}

		# 高さ分ループ (大きさ0.5のため2倍)
		set {_subfront} to 0.5 * {_width.mul}
		set {_h} to 0
		set {_h.count} to 0
		loop {@height} * 2 times:
			set {_loc.hit.height} to location {_h} meters below {_loc.hit}
			set {_loc.hit.height} to location {_front} meters in front of {_loc.hit.height}

			# 幅分ループ (大きさ0.5のため2倍)
			set {_w} to 0
			set {_w.count} to 0
			loop {@width} * 2 times:
				set {_loc.slime} to location {_w} meters to the right of {_loc.hit.height}
				teleport {_slime::%{_h.count}%::%{_w.count}%} to {_loc.slime}
				add 0.5 to {_w}
				add 1 to {_w.count}
			subtract {_subfront} from {_front}
			add 0.5 * {_height.mul} to {_h}
			add 1 to {_h.count}

		chargeActionBar({_player}, ({_King-Shield} * 0.25), 50)
		wait a tick
	playSound("item.armor.equip_generic", 1, 1, {_player})
	loop all entities:
		metadata value "King Shield" of loop-entity is "%{_player}%"
		delete loop-entity