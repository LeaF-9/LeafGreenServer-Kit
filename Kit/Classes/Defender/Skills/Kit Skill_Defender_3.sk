# Defense Matrix

function kitSkill_Defender_3(player: player):
	if {-Stats.%{_player}%::SkillLocation} is set:
		toolCT({_player}, "Defender", 3)
		set {_now} to now
		set {-Stats.%{_player}%::DefenseMatrix-Time} to {_now}
		kitFakeDeleteData({_player}, "Defense Matrix")
		set {_loc} to {-Stats.%{_player}%::SkillLocation}
		set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.5f,0f,-0.5f]", "[1f,1f,1f]")
		kitFakeBlock({_player}, {_loc}, "minecraft:cyan_stained_glass", {_transformation}, "Defense Matrix")
		set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[-0.45f,0f,-0.45f]", "[0.9f,0.1f,0.9f]")
		kitFakeBlock({_player}, {_loc}, "minecraft:obsidian", {_transformation}, "Defense Matrix")
		set {_transformation} to kitTransformation("[0f,0f,0f,1f]", "[0f,0f,0f,1f]", "[0f,0.5f,0f]", "[0.65f,0.65f,0.65f]")
		kitFakeItem({_player}, {_loc}, "minecraft:diamond_block", {_transformation}, "Defense Matrix")
		kitMsg({_player}, "§9Defense Matrix§bを設置しました!")
		playSound("block.anvil.land", 1, 0.5, {_loc})
		set {_yaw} to 0
		while {-Stats.%{_player}%::DefenseMatrix-Time} is {_now}:
			wait a tick
			set {_exist} to kitFakeCheck({_player}, "Defense Matrix")
			if {_exist} is false:
				delete {-Stats.%{_player}%::DefenseMatrix-Time}
				continue
			add 10 to {_yaw}
			if {_yaw} >= 360:
				set {_yaw} to 0
			set yaw of {_loc} to {_yaw}
			if {_itemdisplay} is not set:
				loop all item displays:
					metadata value "kit-item" of loop-item display is "Defense Matrix"
					metadata value "Defense Matrix" of loop-item display is "%{_player}%"
					set {_itemdisplay} to loop-item display
			teleport {_itemdisplay} to {_loc}
	else:
		send action bar "§c§l発動地点が有効ではありません!" to {_player}

on damage with priority NORMAL:
	{-Stats.%victim%::NoSupport} is not set
	metadata value "gametype" of victim is "Kit"
	loop all block displays in radius 5 around victim:
		{_a} is not set
		metadata value "kit-block" of loop-block display is "Defense Matrix"
		set {_player} to metadata value "Defense Matrix" of loop-block display parsed as player
		kitCheckTeam({_player}, victim, true) is true
		if {_player} is not victim:
			if checkEntityType(victim, player) is true:
				setSupporter(victim, {_player})
		set {_a} to true
		setDamageModification_MUL(victim, 0.5)
		kitEffectDescription(victim, "§ax0.5")
		playSound("entity.zombie.attack_wooden_door", 1, 2, victim)
		loop 10 times:
			spawnParticle_Vector(enchanted_hit, randomVector(), 0.5, location 0.5 meters above loop-block display)
