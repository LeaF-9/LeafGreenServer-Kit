# Defense Matrix

function kitEffect_DefenseMatrix(player: player):
	set {_SkillLocation} to getKitStats({_player}, "SkillLocation")
	if {_SkillLocation} is not set:
		send action bar "§c§l発動地点が有効ではありません!" to {_player}
		stop

	nameCT({_player}, "Defense Matrix")
	set {_now} to now
	setKitStats({_player}, "DefenseMatrix-Time", {_now})
	kitFakeDeleteData({_player}, "Defense Matrix")
	set {_loc} to {_SkillLocation}

	set {_transformation::left_rotation} to getAxisAngle4f(0, 0, 0, 1)
	set {_transformation::right_rotation} to getAxisAngle4f(0, 0, 0, 1)
	set {_transformation::translation} to getVector3f(-0.5, 0, -0.5)
	set {_transformation::scale} to getVector3f(1, 1, 1)
	set {_block} to getBlockState(cyan stained glass)
	kitFakeBlock({_player}, {_loc}, {_block}, {_transformation::*}, "Defense Matrix")

	set {_transformation::translation} to getVector3f(-0.45, 0, -0.45)
	set {_transformation::scale} to getVector3f(0.9, 0.1, 0.9)
	set {_block} to getBlockState(obsidian)
	kitFakeBlock({_player}, {_loc}, {_block}, {_transformation::*}, "Defense Matrix")

	set {_transformation::translation} to getVector3f(0, 0.5, 0)
	set {_transformation::scale} to getVector3f(0.65, 0.65, 0.65)
	set {_item} to diamond block
	set {_rotateDisplay} to kitFakeItem({_player}, {_loc}, {_item}, {_transformation::*}, "Defense Matrix")

	kitMsg({_player}, "§9Defense Matrix§bを設置しました!")
	playSound("block.anvil.land", 1, 0.5, {_loc})
	set {_yaw} to 0
	while getKitStats({_player}, "DefenseMatrix-Time") is {_now}:
		wait a tick
		set {_exist} to kitFakeCheck({_player}, "Defense Matrix")
		if {_exist} is false:
			deleteKitStats({_player}, "DefenseMatrix-Time")
			continue
		add 10 to {_yaw}
		if {_yaw} >= 360:
			set {_yaw} to 0
		set yaw of {_loc} to {_yaw}
		teleport {_rotateDisplay} to {_loc}

on damage with priority NORMAL:
	getKitStats(victim, "NoSupport") is not set
	metadata value "gametype" of victim is "Kit"
	loop all block displays in radius 5 around victim:
		{_a} is not set
		metadata value "kit-block" of loop-block display is "Defense Matrix"
		set {_player} to metadata value "Defense Matrix" of loop-block display parsed as player
		kitCheckTeam({_player}, victim, true) is true
		if {_player} is not victim:
			if checkEntityType(victim, player) is true:
				setSupporter(victim, {_player})
		set {_a} to true
		setDamageModification_MUL(victim, 0.5)
		kitEffectDescription(victim, "§ax0.5")
		playSound("entity.zombie.attack_wooden_door", 1, 2, victim)
		loop 10 times:
			spawnParticle_Vector(enchanted_hit, randomVector(), 0.5, location 0.5 meters above loop-block display)
