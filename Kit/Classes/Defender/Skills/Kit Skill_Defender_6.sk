# Mobile Fortress

function kitSkill_Defender_6(player: player):
	toolCT({_player}, "Defender", 6)
	set {Stats.%{_player}%::MobileFortress} to true
	kitMsg({_player}, "<##1e90ff>10秒間,<##00bfff>領域外からの攻撃を無効化します!")
	sendTimeBossbar_GREEN({_player}, "Mobile Fortress", 200)
	playSound("block.beacon.power_select", 1, 1, {_player})
	set {_y} to 0
	loop 200 times:
		set {_baseloc} to location of {_player}
		set {_rad} to 0
		add 0.2 to {_dy}
		if {_dy} >= 4:
			set {_dy} to 0
		if {_dy} > 2:
			set {_y} to 2 - ({_dy} - 2)
		else:
			set {_y} to {_dy}
		loop 40 times:
			set {_el} to {_baseloc}
			add 9 to {_rad}
			add 8 * sin({_rad}) to x-coordinate of {_el}
			add 8 * cos({_rad}) to z-coordinate of {_el}
			add {_y} to y-coordinate of {_el}
			spawnParticle_Dust(rgb(0, 191, 255), 1, {_el})
		wait a tick
	delete {Stats.%{_player}%::MobileFortress}
	kitMsg({_player}, "§bMobile Fortress<##6495ed>の効果が終了しました")

on damage with priority HIGH:
	{Stats.%victim%::NoSupport} is not set
	set {_attacker} to getLastKiller(victim)
	loop all players in radius 8 around victim:
		{_mf} is not set
		kitCheckTeam(loop-player, victim, true) is true
		{Stats.%loop-player%::MobileFortress} is true
		set {_mf} to loop-player
	{_mf} is set
	if distance between {_attacker} and {_mf} > 8:
		cancel event
		kitEffectDescription(victim, "§9§l防御")
		playSound("block.beacon.deactivate", 0.7, 2, victim)