# skill-name: Berserk Time

function kitEffect_BerserkTime(player: player):
	nameCT({_player}, "Berserk Time")
	set {_now} to now
	kitStats_SET({_player}, "BerserkTime", {_now})
	kitMsg({_player}, "<##A22041>§lBerserk Time§5を開始します...")
	kitParticle_BerserkTime({_player})
	applyEffect({_player}, speed, 1, 10 seconds, true)
	sendTimeBossbar_GREEN({_player}, "Berserk Time", 200)
	wait 10 seconds
	set {_BerserkTime} to kitStats_GET({_player}, "BerserkTime")
	if {_BerserkTime} is not {_now}:
		stop
	kitStats_DELETE({_player}, "BerserkTime")
	kitMsg({_player}, "<##A22041>Berserk Time§dが終了しました")



local function kitParticle_BerserkTime(player: player):
	playSound("entity.blaze.ambient", 1, 0.9, {_player})
	set {_now} to kitStats_GET({_player}, "BerserkTime")
	set {_y} to 0
	set {_rad} to 0
	loop 200 times:
		if kitStats_GET({_player}, "BerserkTime") is not {_now}:
			stop

		add 0.1 to {_y}
		if {_y} > 2:
			set {_y} to 0
		set {_baseloc} to location of {_player}
		loop 4 times:
			set {_el} to {_baseloc}
			add 0.5 * sin({_rad}) to x-pos of {_el}
			add 0.5 * cos({_rad}) to z-pos of {_el}
			add {_y} to y-pos of {_el}
			spawnParticle(flame, {_el})
			add 90 to {_rad}

		add 20 to {_rad}
		wait a tick