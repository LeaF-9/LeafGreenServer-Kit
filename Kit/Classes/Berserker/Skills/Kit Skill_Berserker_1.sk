# skill-name: Berserk Time

function kitSkill_Berserker_1(player: player):
	toolCT({_player}, "Berserker", 1)
	kitEffect_BerserkTime({_player})

local function kitEffect_BerserkTime(player: player):
	set {_now} to now
	set {-Stats.%{_player}%::BerserkTime} to {_now}
	kitMsg({_player}, "<##A22041>§lBerserk Time§5を開始します...")
	kitParticle_BerserkTime({_player})
	applyEffect({_player}, speed, 1, 10 seconds, true)
	sendTimeBossbar_GREEN({_player}, "Berserk Time", 200)
	wait 10 seconds
	if {-Stats.%{_player}%::BerserkTime} is not {_now}:
		stop
	delete {-Stats.%{_player}%::BerserkTime}
	kitMsg({_player}, "<##A22041>Berserk Time§dが終了しました")



local function kitParticle_BerserkTime(player: player):
	playSound("entity.blaze.ambient", 1, 0.9, {_player})
	set {_now} to {-Stats.%{_player}%::BerserkTime}
	set {_y} to 0
	set {_rad} to 0
	loop 200 times:
		if {-Stats.%{_player}%::BerserkTime} is not {_now}:
			stop

		add 0.1 to {_y}
		if {_y} > 2:
			set {_y} to 0
		set {_baseloc} to location of {_player}
		loop 4 times:
			set {_el} to {_baseloc}
			add 0.5 * sin({_rad}) to x-pos of {_el}
			add 0.5 * cos({_rad}) to z-pos of {_el}
			add {_y} to y-pos of {_el}
			spawnParticle(flame, {_el})
			add 90 to {_rad}

		add 20 to {_rad}
		wait a tick