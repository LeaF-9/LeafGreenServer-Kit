# skill-name: Rampage

on damage of player with priority LOW:
	set {_attacker} to getAttacker(victim)
	if getKitStats(victim, "Rampage") is set:
		setKitStats(victim, "Rampage-Combo", 0)
		kitParticle_Rampage_Reset(victim)
	if getKitStats({_attacker}, "Rampage") is set:
		damage was caused by attack
		set {_Rampage-Combo} to getKitStats({_attacker}, "Rampage-Combo")
		set {_damage} to 2 + {_Rampage-Combo}
		setDamageModification_SET(victim, {_damage})
		kitEffectDescription(victim, "§4❤%{_damage}%")
		add 1 to {_Rampage-Combo}
		setKitStats({_attacker}, "Rampage-Combo", {_Rampage-Combo})
		kitParticle_Rampage_Combo({_attacker}, victim, {_Rampage-Combo})



function kitEffect_Rampage(player: player):
	nameCT({_player}, "Rampage")
	set {_now} to now
	setKitStats({_player}, "Rampage", {_now})
	setKitStats({_player}, "Rampage-Combo", 0)
	kitMsg({_player}, "§6§lRampage §eを発動します...")
	sendTimeBossbar_GREEN({_player}, "Rampage", 200)
	kitParticle_Rampage({_player})
	loop 200 times:
		if getKitStats({_player}, "Rampage") is not {_now}:
			continue
		set {_Rampage-Combo} to getKitStats({_player}, "Rampage-Combo")
		send action bar "§6§lRampage Combo§b: §e§l%{_Rampage-Combo}%" to {_player}
		wait a tick
	getKitStats({_player}, "Rampage") is {_now}
	deleteKitStats({_player}, "Rampage")
	deleteKitStats({_player}, "Rampage-Combo")
	kitMsg({_player}, "§6Rampage §bが終了しました")



local function kitParticle_Rampage(player: player):
	playSound("entity.ender_dragon.growl", 1, 1.55, {_player})
	set {_now} to getKitStats({_player}, "Rampage")
	set {_rad} to 0
	loop 200 times:
		if getKitStats({_player}, "Rampage") is not {_now}:
			stop
		
		set {_el} to location of {_player}
		add 18 to {_rad}
		add 0.55 * sin({_rad}) to x-pos of {_el}
		add 0.55 * cos({_rad}) to z-pos of {_el}
		add 2 to y-pos of {_el}
		spawnParticle(angry_villager, {_el})

		wait a tick

local function kitParticle_Rampage_Combo(player: player, target: player, combo: number):
	playSound("entity.blaze.hurt", 1, 1.75, {_player})
	set {_baseloc} to location 1 meter above {_target}
	loop 20 times:
		spawnParticle_Vector(wax_on, randomVector(), 10, {_baseloc})

	loop 3 * {_combo} times:
		set {_el} to aroundEffect({_player})
		spawnParticle_Vector(trial_spawner_detection, vector(0, 0.25, 0), 0.5, {_el})

local function kitParticle_Rampage_Reset(player: player):
	playSound("entity.blaze.death", 1, 1.5, {_player})
	set {_y} to 2
	loop 11 times:
		set {_baseloc} to location of {_player}
		set {_rad} to 0
		loop 8 times:
			set {_el} to {_baseloc}
			add 0.5 * sin({_rad}) to x-pos of {_el}
			add 0.5 * cos({_rad}) to z-pos of {_el}
			add {_y} to y-pos of {_el}
			spawnParticle(splash, {_el})
			add 360 / 8 to {_rad}

		subtract 0.2 from {_y}
		wait a tick