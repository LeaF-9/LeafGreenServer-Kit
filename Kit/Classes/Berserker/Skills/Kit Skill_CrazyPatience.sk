# skill-name: Crazy Patience

on damage of player with priority NORMAL:
	set {_CrazyPatience} to getKitStats(victim, "CrazyPatience")
	{_CrazyPatience} is set
	setDamageModification_MUL(victim, 0.8)
	kitParticle_CrazyPatience_Damage(victim)



function kitEffect_CrazyPatience(player: player):
	nameCT({_player}, "Crazy Patience")
	set {_now} to now
	setKitStats({_player}, "CrazyPatience", {_now})
	deleteKitStats({_player}, "NoSupport")
	deleteKitStats({_player}, "Berserk")
	kitParticle_CrazyPatience({_player})
	kitMsg({_player}, "§8§l狂気の我慢§7により§6§lノックバック§7を§e§l無効化§7します...")
	sendTimeBossbar_GREEN({_player}, "Crazy Patience", 100)
	set knockback resistance attribute of {_player} to 1
	wait 5 seconds
	set {_CrazyPatience} to getKitStats({_player}, "CrazyPatience")
	{_CrazyPatience} is {_now}
	set knockback resistance attribute of {_player} to 0
	deleteKitStats({_player}, "CrazyPatience")
	setKitStats({_player}, "NoSupport", true)
	setKitStats({_player}, "Berserk", true)
	kitMsg({_player}, "§8§lCrazy Patience§5の効果が終了しました")



local function kitParticle_CrazyPatience(player: player):
	playSound("entity.iron_golem.death", 1, 1.35, {_player})
	set {_now} to getKitStats({_player}, "CrazyPatience")
	loop 100 times:
		if getKitStats({_player}, "CrazyPatience") is not {_now}:
			stop
		
		set {_el} to aroundEffect({_player})
		spawnParticle(end_rod, {_el})

		wait a tick

local function kitParticle_CrazyPatience_Damage(player: player):
	playSound("entity.zombie.attack_iron_door", 0.9, 1.35, {_player})
	set {_el} to location 1 meter above {_player}
	spawnParticle(explosion, {_el})