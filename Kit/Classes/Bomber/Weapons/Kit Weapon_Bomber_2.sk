# Weapon-Name: Grenade Thrower

on shoot:
	metadata value "gametype" of shooter is "Kit"
	{Class::%shooter%} is "Bomber"
	set {_tool} to shooter's tool
	kitItemAllChecker({_tool}, "Bomber", "Weapon.main", 2) is true
	cancel event
	remove an arrow from shooter
	set {_t} to getBowDrawTime(shooter) * 0.083
	shoot a primed tnt from shooter at speed {_t}
	set metadata value "attacker" of shot primed tnt to "%shooter%"
	set metadata value "kit-bomber" of shot primed tnt to 3.6
	set metadata value "TrueTeam" of shot primed tnt to true
	set {_uuid} to a random uuid
	set metadata value "grenade-thrower-uuid" of shot primed tnt to {_uuid}
	setFuseTime(shot primed tnt, 20)
	playSound("entity.creeper.death", 1, 2, shooter)
	loop 30 times:
		loop all primed tnts:
			metadata value "grenade-thrower-uuid" of loop-primed tnt is {_uuid}
			if distance between shooter and loop-primed tnt < 10:
				set metadata value "kit-bomber" of loop-primed tnt to 1
			else:
				set metadata value "kit-bomber" of loop-primed tnt to 3.6
			spawnParticle(flame, location of loop-primed tnt)
		wait a tick