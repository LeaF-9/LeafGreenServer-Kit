# Weapon-Name: Grenade Thrower

options:
	explosion-power: 3



function kitEffect_GrenadeThrower_Shoot(player: player, event: object):
	metadata value "gametype" of {_player} is "Kit"
	{Class::%{_player}%} is "Bomber"
	set {_tool} to {_player}'s tool

	{_event}.setCancelled(true)
	set {_drawtime} to getBowDrawTime({_player})
	set {_t} to {_drawtime} * 0.083
	set {_expower} to {@explosion-power} * ({_drawtime} / 24)

	shoot a primed tnt from {_player} at speed {_t}
	set {_tnt} to shot primed tnt
	set metadata value "attacker" of {_tnt} to "%{_player}%"
	set metadata value "kit-bomber" of {_tnt} to {_expower}
	set metadata value "TrueTeam" of {_tnt} to true
	set {_uuid} to a random uuid
	set metadata value "grenade-thrower-uuid" of {_tnt} to {_uuid}
	setFuseTime({_tnt}, 20)
	kitParticle_GrenadeThrower_TNT({_tnt}, {_expower})



local function kitParticle_GrenadeThrower_TNT(entity: entity, power: number):
	playSound("entity.creeper.death", 1, 2, {_entity})
	loop 30 times:
		stop if {_entity} does not exist

		set {_loc} to location 0.5 meters above {_entity}
		spawnParticle(flame, {_loc})

		set {_g} to (1 - {_power} / {@explosion-power}) * 255
		set {_color} to rgb(255, {_g}, 0)
		set {_count} to a random integer between 3 and 5
		loop {_count} times:
			set {_el} to moveRandomLocation({_loc}, 0.5)
			spawnParticle_DustTransition({_color}, rgb(50, 0, 0), 0.65, {_el})

		wait a tick
