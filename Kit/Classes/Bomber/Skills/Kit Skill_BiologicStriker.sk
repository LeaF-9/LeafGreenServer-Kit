# Biologic Striker

function kitEffect_BiologicStriker(player: player):
	nameCT({_player}, "Biologic Striker")
	kitMsg({_player}, "<##adff2f>生物兵器を召喚します...")
	playSound("entity.creeper.hurt", 1, 0.5, {_player})
	loop all creepers:
		metadata value "biologic-bomb" of loop-creeper is "%{_player}%"
		delete loop-creeper
	loop all blocks in radius 5 around {_player}:
		loop-block is passable
		block below loop-block is not passable
		add location of loop-block to {_loc::*}
	loop 3 times:
		set {_rl} to a random location out of {_loc::*}
		remove {_rl} from {_loc::*}

		set {_creeper} to kitSpawnMob({_player}, creeper, {_rl})
		set metadata value "biologic-bomb" of {_creeper} to "%{_player}%"
		set metadata value "kit-bomber" of {_creeper} to 2.5
		set metadata value "attacker" of {_creeper} to "%{_player}%"

		setAttribute({_creeper}, "movement_speed", 0.4)
		{_creeper}.setSilent(true)
		{_creeper}.setMaxFuseTicks(1)

		kitParticle_BiologicStriker_Spell({_creeper})

function kitParticle_BiologicStriker_Spell(entity: entity):
	loop 60 times:
		spawnParticle_Color(entity_effect, rgb(173, 255, 47), {_entity})
		wait a tick

on damage of creeper:
	metadata value "biologic-bomb" of victim is set
	cancel event
	damage cause is not block explosion or entity explosion
	set {_player} to metadata value "biologic-bomb" of victim parsed as player
	set {_l} to location of victim
	delete victim
	kitExplosion({_player}, 2.5, {_l})