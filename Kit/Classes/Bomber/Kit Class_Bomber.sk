# Bomberのスキル Ver1.0.0

# Weapons
function kitWeaponBomber(player: player):
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	checkCT({_player}, name of {_tool}) is true
	checkItemCT({_player}, {_tool}) is true
	if {_item.num} is 3:
		kitWeapon_Bomber_3({_player})

# Skills
function kitSkillBomber(player: player) :: boolean:
	set {_tool} to {_player}'s tool
	set {_item.num} to kitItemNumberChecker({_tool})
	if checkCT({_player}, name of {_tool}) is true:
		if {_item.num} is 1:
			kitSkill_Bomber_1({_player})
		else if {_item.num} is 2:
			kitSkill_Bomber_2({_player})
		else if {_item.num} is 3:
			kitSkill_Bomber_3({_player})
		else if {_item.num} is 4:
			kitSkill_Bomber_4({_player})
		else if {_item.num} is 5:
			kitSkill_Bomber_5({_player})
		else if {_item.num} is 6:
			kitSkill_Bomber_6({_player})
		else if {_item.num} is 7:
			kitSkill_Bomber_7({_player})
		else if {_item.num} is 8:
			kitSkill_Bomber_8({_player})
	return true

# Abilities
on damage of player:
	damage was caused by projectile
	projectile is an arrow
	{Class::%attacker%} is "Bomber"
	set damage to damage * 0.5

on death of player:
	if {Class::%victim%} is "Bomber":
		loop all primed tnts:
			metadata value "attacker" of loop-primed tnt is "%victim%"
			metadata value "ult-bomber" of loop-primed tnt is not set
			delete loop-primed tnt
		set {_v} to location of victim
		add 1 to y-coordinate of {_v}
		loop 3 times:
			spawn a primed tnt at {_v}
			push spawned primed tnt east at speed random number from -0.5 to 0.5
			push spawned primed tnt north at speed random number from -0.5 to 0.5
			set metadata value "attacker" of spawned primed tnt to "%victim%"
			set metadata value "kit-bomber" of spawned primed tnt to 2
			setFuseTime(spawned primed tnt, 20)
	if {Class::%{LastKiller::%victim%}%} is "Bomber":
		give 2 tnt to {LastKiller::%victim%}
		give 4 arrow to {LastKiller::%victim%}

# Timer
function kitTimerBomber(player: player):
	while {Class::%{_player}%} is "Bomber":
		loop 10 times:
			if {Class::%{_player}%} is "Bomber":
				wait a second
		{Class::%{_player}%} is "Bomber"
		set {_tnt} to amount of tnt in {_player}'s inventory
		if {_tnt} < 9:
			give a tnt to {_player}
		set {_arrow} to amount of arrow in {_player}'s inventory
		if {_arrow} < 16:
			give an arrow to {_player}
	loop all creepers:
		metadata value "biologic-bomb" of loop-creeper is "%{_player}%"
		delete loop-creeper