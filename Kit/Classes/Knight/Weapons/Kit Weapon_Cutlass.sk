# Cutlass

on leftclick:
	set {_tool} to player's offhand tool
	name of {_tool} is "§3Cutlass"
	checkItemCT(player, {_tool}) is true
	kitEffect_Cutlass_Launch(player)



local function kitEffect_Cutlass_Launch(player: player):
	set {_tool} to offhand tool of {_player}
	setKitStats({_player}, "Cutlass", true)
	setItemCT({_player}, {_tool})
	set offhand tool of {_player} to air

	set {_loc} to location 1 meter above {_player}

	set {_item} to "minecraft:golden_sword"
	set {_transformation} to "[0.0000f,0.0000f,-1.0000f,0.0000f,-0.7071f,-0.7071f,-0.0000f,0.0000f,-0.7071f,0.7071f,0.0000f,0.0000f,0.0000f,0.0000f,0.0000f,1.0000f]"
	set {_nbt} to nbt compound from "{transformation:%{_transformation}%,item:{id:""%{_item}%"",Count:1b},teleport_duration:1}"
	spawn an item display at {_loc} with nbt {_nbt}
	set {_itemdisplay} to spawned item display
	set metadata value "cutlass" of {_itemdisplay} to "%{_player}%"
	playSound("entity.arrow.shoot", 1, 1, {_player})

	set {_f} to 0
	loop 20 times:
		add 0.5 to {_f}
		set {_l} to location {_f} meters in front of {_loc}
		teleport {_itemdisplay} to {_l}

		loop all entities in radius 2 around {_l}:
			{_hit.%id of loop-entity%} is not set
			kitCheckTeam(loop-entity, {_player}, false, true) is true
			set {_hit.%id of loop-entity%} to true
			kitDamage({_player}, loop-entity, 3, "投剣")
			playSound("entity.player.attack.sweep", 1, 1, location of loop-entity)
			loop 5 times:
				set {_el} to aroundEffect(loop-entity)
				spawnParticle_Block(redstone block, {_el})
		wait a tick

	loop all item displays:
		metadata value "cutlass" of loop-item display is "%{_player}%"
		delete loop-item display
	if getKitStats({_player}, "Cutlass") is true:
		set offhand tool of {_player} to {_tool}
	deleteKitStats({_player}, "Cutlass")

on rightclick:
	{Class::%player%} is "Knight"
	kitSelectChecker_Weapon(player, "Knight") is 2
	if name of tool of player is "§3Cutlass":
		set {_tool} to tool of player
		set tool of player to air
		set offhand tool of player to {_tool}
	else if name of offhand tool of player is "§3Cutlass":
		set {_tool} to offhand tool of player
		set offhand tool of player to air
		set {_slot} to slot 0 of inventory of player
		set slot 0 of inventory of player to {_tool}
		if {_slot} is not air:
			give {_slot} to player
		set current hotbar slot of player to 0