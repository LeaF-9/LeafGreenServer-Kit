# Calling Soldiers

function kitEffect_CallingSoldiers(player: player):
	nameCT({_player}, "Calling Soldiers")
	kitMsg({_player}, "§73体の剣士を呼び出しました!")
	sendTimeBossbar_GREEN({_player}, "Calling Soldiers", 200)
	playSound("entity.skeleton.hurt", 1, 1, {_player})
	loop all blocks in radius 3 around {_player}:
		(y-pos of loop-block) is (y-pos of (block at ({_player})))
		add (location of loop-block) to {_ls::*}
	loop 3 times:
		set {_l} to a random location out of {_ls::*}
		remove {_l} from {_ls::*}

		set {_skeleton} to kitSpawnMob({_player}, skeleton, {_l})
		set {_playerskull} to player head
		set skull owner of {_playerskull} to {_player}
		setEntityArmor({_skeleton}, "helmet", {_playerskull})
		setEntityArmor({_skeleton}, "chestplate", iron chestplate)
		setEntityArmor({_skeleton}, "leggings", iron leggings)
		setEntityArmor({_skeleton}, "boots", air)
		setEntityArmor({_skeleton}, "mainhand", stone sword)
		setEntityArmor({_skeleton}, "offhand", air)

		{_skeleton}.setCanPickupItems(false)

		setAttribute({_skeleton}, "MAX_HEALTH", 20)

		add {_skeleton} to {_soldiers::*}
		loop 15 times:
			spawnParticle_Vector(cloud, randomVector(), 0.3, location 1 meter above {_l})
	wait 10 seconds
	loop {_soldiers::*}:
		loop-value.remove()