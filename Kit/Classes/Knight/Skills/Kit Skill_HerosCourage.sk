# Hero's Courage

function kitEffect_HerosCourage(player: player):
	nameCT({_player}, "Hero's Courage")
	kitMsg({_player}, "<##ff7f50>仲間達に<##dc143c>勇気<##ff7f50>を分け与えよう!")
	add {_player} to {_players::*}
	playSound("ui.toast.challenge_complete", 1.2, 1.5, {_player})
	kitHeal({_player}, 2)
	loop {CT.%{_player}%::*}:
		if loop-index does not contain "Hero's Courage":
			subtract 10 from {CT.%{_player}%::%loop-index%}
	
	particleAura({_player})
	loop all players in radius 5 around {_player}:
		loop-player is not {_player}
		kitCheckTeam(loop-player, {_player}, true) is true
		add loop-player to {_players::*}
		message formatted "<##ffd700>CT短縮<##daa520>と<##ffd700>回復<##daa520>という勇気をもらった!" to loop-player
		kitHeal(loop-player, 2)
		loop {CT.%loop-player%::*}:
			if loop-index does not contain "Hero's Courage":
				subtract 10 from {CT.%loop-player%::%loop-index%}
		loop all blocks in radius 2 around loop-player:
			set {_loc} to location of loop-block
			add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
			add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
			add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
			kitParticle_HerosCourage({_loc})
	kitEffectDescription({_players::*}, "§a❤2 §8& §bCT短縮")
	set {_ploc} to location of block at {_player}
	set {_bn} to 1
	loop 5 times:
		loop all blocks in radius {_bn} around {_player}:
			loop-block is air
			block below loop-block is not air
			set {_bloc} to location of loop-block
			set {_bnn} to {_bn} - 1
			if distance between {_bloc} and {_ploc} >= {_bnn}:
				add -0.3 to y-coordinate of {_bloc}
				spawnParticle_Color(entity_effect, rgb(255, 100, 100), {_bloc})
		add 1 to {_bn}
		wait a tick



local function particleAura(player: player):
	add rgb(255, 255, 100) to {_colors::*}
	add rgb(240, 240, 170) to {_colors::*}
	add rgb(245, 227, 71) to {_colors::*}

	set {_baseloc} to location of {_player}

	set {_count} to 50
	loop {_count} times:
		set {_length} to a random number between 0 and 5
		set {_vector} to randomVector({_length})
		set y of {_vector} to 0
		
		set {_loc} to {_baseloc} ~ {_vector}
		set {_color} to a random element out of {_colors::*}
		spawnParticle_Color(entity_effect, {_color}, {_loc})