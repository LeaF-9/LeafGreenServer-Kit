# Justice Power

function kitSkill_Knight_5(player: player):
	toolCT({_player}, "Knight", 5)
	set {Stats.%{_player}%::JusticePower} to true
	kitMsg({_player}, "§9与えるダメージと受けるダメージを§63§9にします...")
	sendTimeBossbar_GREEN({_player}, "Justice Power", 140)
	setDamageModification_SET({_player}, 3)
	playSound("entity.illusioner.prepare_mirror", 1, 1.5, {_player})
	loop 140 times:
		set {_loc} to location of {_player}
		add 0.02 to {_dy}
		if {_dy} >= 4:
			set {_dy} to 0
		if {_dy} > 2:
			set {_y} to 2 - ({_dy} - 2)
		else:
			set {_y} to {_dy}
		add {_y} to y-coordinate of {_loc}
		add 9 to {_rad}
		add 0.5 * sin({_rad}) to x-coordinate of {_loc}
		add 0.5 * cos({_rad}) to z-coordinate of {_loc}
		spawnParticle_Color(entity_effect, 255, 165, 0, {_loc})
		wait a tick
	removeDamageModification_SET({_player})
	delete {Stats.%{_player}%::JusticePower}
	kitMsg({_player}, "§6Justice Power§cの効果が切れました")

# Damage Modification Editor
on damage with priority NORMAL:
	set {_attacker} to metadata value "Last Killer" of victim
	# Justice Power発動者がダメージを与える
	if {Stats.%{_attacker}%::JusticePower} is true:
		if checkDamagable(victim) is false:
			stop
		if {Stats.%victim%::Invincible} is set:
			stop
		
		setDamageModification_SET(victim, 3)
		kitEffectDescription(victim, "§4❤3")

		# Particles
		playSound("entity.iron_golem.hurt", 1, 2, victim)
		loop all blocks in radius 2 around victim:
			chance of 20%:
				set {_loc} to location of loop-block
				add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
				add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
				add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
				spawnParticle_Color(entity_effect, 255, 200, 0, {_loc})

	# Justice Power発動者がダメージを受ける
	else if checkEntityType(victim, player) is true:
		if {Stats.%victim%::Invincible} is true:
			stop
		if {Stats.%victim%::ReligiousShelter} is true:
			stop
		if {Stats.%victim%::JusticePower} is not true:
			stop
		if checkDamagable(victim) is false:
			stop

		setDamageModification_SET(victim, 3)
		kitEffectDescription(victim, "§4❤3")

		# Particles
		playSound("entity.iron_golem.hurt", 1, 2, victim)
		loop all blocks in radius 2 around victim:
			chance of 20%:
				set {_loc} to location of loop-block
				add a random number between -0.5 and 0.5 to x-coordinate of {_loc}
				add a random number between -0.5 and 0.5 to y-coordinate of {_loc}
				add a random number between -0.5 and 0.5 to z-coordinate of {_loc}
				spawnParticle_Color(entity_effect, 255, 200, 0, {_loc})