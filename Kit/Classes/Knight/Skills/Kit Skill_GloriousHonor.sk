# Glorious Honor

function kitEffect_GloriousHonor(player: player):
	nameCT({_player}, "Glorious Honor")
	kitMsg({_player}, "<##ffff00>名誉<##ff8c00>が光をもたらした!")
	playSound("item.totem.use", 1, 1.5, {_player})
	apply glowing 1 to {_player} for 10 seconds
	sendTimeBossbar_YELLOW({_player}, "Glorious Honor", 200)
	loop all entities in radius 10 around {_player}:
		loop-entity is not {_player}
		metadata value "gametype" of loop-entity is "Kit"
		apply glowing 1 to loop-entity for 10 seconds
		message formatted "<##ffff00>大きな光<##ffd700>で長い発光が付与された!" to loop-entity
		kitEffectDescription(loop-entity, "§e発光")
		sendTimeBossbar_YELLOW(loop-entity, "発光", 200)
	set {_num} to 0
	set {_ploc} to location of block at {_player}
	loop 10 times:
		add 1 to {_num}
		loop all blocks in radius {_num} around {_player}:
			loop-block is air
			set {_bloc} to location of loop-block
			set {_bnn} to {_num} - 1
			if distance between {_bloc} and {_ploc} >= {_bnn}:
				chance of 20%:
					add a random number between -0.2 and 0.2 to x-coordinate of {_bloc}
					add a random number between -0.2 and 0.2 to y-coordinate of {_bloc}
					add a random number between -0.2 and 0.2 to z-coordinate of {_bloc}
					spawnParticle_Color(entity_effect, rgb(255, 255, 0), {_bloc})
			chance of 1%:
				spawnParticle(end_rod, {_bloc})
		wait a tick