# Knight Changer

function kitSkill_Knight_2(player: player):
	set {_tool} to {_player}'s tool
	toolCT({_player}, "Knight", 2)
	playSound("block.beacon.activate", 1, 1, {_player})
	set {_baseloc} to location of head of {_player}
	loop 25 times:
		if {_tg} is set:
			continue
		loop 2 times:
			{_tg} is not set
			add 1 to {_front}
			set {_loc} to location {_front} meters in front of {_baseloc}
			loop all players in radius 2 around {_loc}:
				kitCheckTeam(loop-player, {_player}, false) is true
				{_tg} is not set
				set {_tg} to loop-player
			loop 3 times:
				kitParticle_KnightChanger_Beam({_loc})
				set {_el} to {_loc}
				add a random number between -0.5 and 0.5 to x-coordinate of {_el}
				add a random number between -0.5 and 0.5 to y-coordinate of {_el}
				add a random number between -0.5 and 0.5 to z-coordinate of {_el}
				spawnParticle(end_rod, {_el})
		wait a tick
	if {_tg} is set:
		if {Class::%{_tg}%} is "Thief":
			set {team::%{_tg}%} to {TrueTeam::%{_tg}%}
		execute console command "/kjc %{_tg}%"
		execute console command "/class Knight %{_tg}% false"
		kitHeal({_tg}, 20, 0)
		message "§6あなたは §b%{_player}% §6によって §cKnight §6に転職しました!" to {_tg}
		kitMsg({_player}, "§a%{_tg}% §7のクラスを §cKnight §7に変更しました...")
		kitEffectDescription({_tg}, "§eKit§6§l変更")
		playSound("block.chest.open", 1, 1, {_tg})
		detectCT({_tg}, {_tool})
		set {_el} to location 1 meter above {_tg}
		loop 30 times:
			spawnParticle_Vector(end_rod, randomVector(), 0.3, {_el})

local function kitParticle_KnightChanger_Beam(loc: location):
	set {_baseloc} to location 0.5 meters behind {_loc}
	set {_m} to 0
	loop 5 times:
		add 0.2 to {_m}
		set {_rn} to a random integer between 1 and 3
		if {_rn} is 1:
			set {_color} to rgb(255, 243, 184)
		else if {_rn} is 2:
			set {_color} to rgb(255, 244, 98)
		else:
			set {_color} to rgb(255, 255, 249)
		set {_el} to location {_m} meters in front of {_baseloc}
		spawnParticle_Dust({_color}, 0.75, {_el})