# Summon Horse

import:
	org.bukkit.inventory.ItemStack
	org.bukkit.Material

function kitEffect_SummonHorse(player: player):
	if {_player} is riding a horse:
		send action bar "§c現在騎馬中です" to {_player}
		stop

	set {_skillitem} to {_player}'s tool
	nameCT({_player}, "Summon Horse")
	set {_dis} to display name of {_skillitem}
	set {CTStop.%{_player}%::%{_dis}%} to true
	kitMsg({_player}, "§e馬を呼び出しました!")
	
	set {_entity} to kitSpawnMob({_player}, horse, location at {_player}, false)
	set {_saddle} to new ItemStack(Material.SADDLE)
	{_entity}.getInventory().setSaddle({_saddle})
	setAttribute({_entity}, "MOVEMENT_SPEED", 0.2)
	setAttribute({_entity}, "JUMP_STRENGTH", 0.75)
	{_entity}.setAdult()
	{_entity}.setOwner({_player})
	{_entity}.setTamed(true)

	make {_player} ride {_entity}
	set {_horse} to true
	while {_horse} is true:
		if vehicle of {_player} is a horse:
			set {_horse} to true
		else:
			set {_horse} to false
		wait a tick
	delete {CTStop.%{_player}%::%{_dis}%}

on damage of horse, undead horse or skeleton horse:
	metadata value "gametype" of victim is "Kit"
	set {_d} to victim's display name
	if {team::%{_d}%} is {team::%attacker%}:
		cancel event
	else:
		set damage to 100000

On vehicle Exit:
	vehicle of player is a horse
	kill vehicle of player

on death of horse, undead horse or skeleton horse:
	clear drops