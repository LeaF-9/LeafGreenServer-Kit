# skill-name: Ivy Zipline

options:
	stats-time: "IvyZipline-Time"
	metadata-vehicle: "Ivy Zipline - Vehicle"
	max-meter: 50
	ride-radius: 2
	ride-damage: 0.5

import:
	org.bukkit.event.vehicle.VehicleEnterEvent
	org.bukkit.event.vehicle.VehicleExitEvent

	org.bukkit.FluidCollisionMode
	org.bukkit.block.data.Bisected$Half

	io.papermc.paper.entity.TeleportFlag$EntityState

on VehicleEnterEvent:
	broadcast "hey"

on VehicleExitEvent:
	broadcast "hey"
	set {_vehicle} to event.getVehicle()
	stop if metadata value {@metadata-vehicle} of {_vehicle} is not set

	cancel event



function kitEffect_IvyZipline(player: player):
	set {_onGround} to {_player}.isOnGround()
	if {_onGround} is false:
		send action bar "§c§l接地した状態で発動してください" to {_player}
		stop
	
	nameCT({_player}, "Ivy Zipline")
	set {_loc.from} to location 1.5 meters above {_player}
	kitMsg({_player}, "<##1a5132>蔦の鉤縄<##2d704a>を発射します...")
	kitEffect_IvyZipline_Shoot({_player}, {_loc.from})
	kitParticle_IvyZipline_Launch({_player})

local function kitEffect_IvyZipline_Shoot(player: player, loc: location):
	set {_baseloc} to {_loc}
	set {_world} to world of {_baseloc}
	set {_fluid} to (FluidCollisionMode.NEVER)
	set {_gravity} to vector(0, -0.01, 0)
	set {_vec} to setVector({_loc}, location 1 meter in front of {_loc})

	set {_addm} to 0.5
	set {_count} to {@max-meter} / {_addm}

	set {_ticktime} to 5
	set {_ln} to 0
	loop {_count} times:
		continue if {_hit} is set

		set {_rayTrace} to {_world}.rayTraceBlocks({_baseloc}, {_vec}, {_addm}, {_fluid}, true)
		if ({_rayTrace}.getHitBlock()) is set:
			set {_hit} to {_rayTrace}.getHitBlock()

		set {_baseloc} to setDirToVec({_baseloc}, {_vec})
		set {_baseloc} to location {_addm} meters in front of {_baseloc}
		kitParticle_IvyZipline_Ivy({_baseloc})

		wait a tick if mod({_ln}, {_ticktime}) is 0 or 5
		set {_vec} to {_vec} + {_gravity}
		add 1 to {_ln}

	if {_hit} is not set:
		kitMsg({_player}, "§c§l着地点が見つかりませんでした")
		playSound("block.note_block.bass", 1, 1.2, {_player}, true)
		stop
	
	set {_loc.below} to location 1.5 meters below {_loc}
	set {_vec.direction} to setVector({_loc.below}, {_baseloc})
	set {_loc.from} to {_loc.below}
	set yaw of {_loc.from} to yaw of {_vec.direction}

	set {_loc.to} to {_baseloc}
	set {_hitloc} to location of {_hit}
	add 0.5 to y-pos of {_hitloc}
	set y-pos of {_loc.to} to y-pos of {_hitloc}
	set yaw of {_loc.to} to ((yaw of {_vec.direction}) + 180)

	set {_blocking} to false
	set {_blocking} to true if block at (location 0.1 meters above {_loc.to}) is not passable
	set {_blocking} to true if block at (location 1.1 meters above {_loc.to}) is not passable
	if {_blocking} is true:
		kitMsg({_player}, "§c§l着弾点がジップラインを設置するのに不適切です")
		playSound("block.note_block.bass", 1, 1.2, {_player}, true)
		stop

	kitEffect_IvyZipline_Zipline({_player}, {_loc.from}, {_loc.to})

local function kitEffect_IvyZipline_Zipline(player: player, from: location, to: location):
	set {_now} to now
	setKitStats({_player}, {@stats-time}, {_now})

	kitFakeDeleteData({_player}, "Ivy Zipline")

	kitEffect_IvyZipline_Display({_player}, {_from})
	kitEffect_IvyZipline_Display({_player}, {_to})

	kitMsg({_player}, "<##39c613>§l蔦のジップライン<##8be18b>を設置しました!")
	
	set {_vec.from} to setVector({_from}, {_to}, 0.5)
	set {_from} to setDirToVec({_from}, {_vec.from})
	add 0.5 to y-pos of {_from}
	set {_vec.to} to setVector({_to}, {_from}, 0.5)
	set {_to} to setDirToVec({_to}, {_vec.to})
	add 0.5 to y-pos of {_to}

	kitParticle_IvyZipline_Place({_from}, {_to})
	set {_distance} to distance between {_from} and {_to}
	set {_metercount} to round({_distance}) * 2
	set {_uuid} to a random UUID

	while getKitStats({_player}, {@stats-time}) is {_now}:
		wait a tick

		# Particle
		kitParticle_IvyZipline_Line({_from}, {_to})

		# 乗車確認
		set {_m} to 0
		set {_addm} to 0.5
		loop {_metercount} times:
			add {_addm} to {_m}
			set {_rideloc} to location {_m} meters in front of {_from}

			loop all players in radius {@ride-radius} around {_rideloc}:
				metadata value "gametype" of loop-player is "Kit"
				{_riding::%loop-player%} is not set
				if loop-player is sneaking:
					if {_sneaking::%loop-player%} is false:
						set {_sneaking::%loop-player%} to true

						set {_playerloc} to location of loop-player
						set {_playervec} to setVector({_playerloc}, location 1 meter in front of {_playerloc})
						set {_angle.from} to {_playervec}.angle({_vec.from})
						set {_angle.to} to {_playervec}.angle({_vec.to})
						if {_angle.from} <= {_angle.to}:
							set {_ridingvec} to {_vec.from}
						else:
							set {_ridingvec} to {_vec.to}

						set {_riding::%loop-player%} to {_rideloc}
						set {_riding.vec::%loop-player%} to {_ridingvec}

						set {_rideentity} to kitEffect_IvyZipline_Vehicle({_player}, {_rideloc})
						set {_riding.entity::%loop-player%} to {_rideentity}
						{_rideentity}.addPassenger(loop-player)

						kitParticle_IvyZipline_Ride({_rideloc})
						continue
				set {_sneaking::%loop-player%} to false if loop-player is not sneaking

		# 乗車中処理
		loop all players:
			continue if {_riding::%loop-player%} is not set

			set {_rideloc} to {_riding::%loop-player%} ~ {_riding.vec::%loop-player%}
			set {_riding::%loop-player%} to {_rideloc}

			set {_rideentity} to {_riding.entity::%loop-player%}

			set {_sneaking::%loop-player%} to false if loop-player is not sneaking

			if loop-player is sneaking:
				if {_sneaking::%loop-player%} is false:
					set {_sneaking::%loop-player%} to true
					{_rideentity}.remove()
					delete {_riding::%loop-player%}
					delete {_riding.vec::%loop-player%}
					delete {_riding.entity::%loop-player%}
					kitParticle_IvyZipline_Drop({_rideloc})
					continue
			
			if {_riding.vec::%loop-player%} = {_vec.from}:
				set {_disbase} to {_from}
			else:
				set {_disbase} to {_to}
			set {_disride} to distance between {_disbase} and {_rideloc}
			if {_disride} > {_distance}:
				delete {_sneaking::%loop-player%}
				delete {_riding::%loop-player%}
				delete {_riding.vec::%loop-player%}
				delete {_riding.entity::%loop-player%}
				{_rideentity}.remove()
				kitParticle_IvyZipline_Drop({_rideloc})
				continue

			{_rideentity}.addPassenger(loop-player)
			{_rideentity}.teleport({_rideloc}, EntityState.RETAIN_PASSENGERS)

			continue if kitCheckTeam(loop-player, {_player}, false) is false
			set {_damage::%loop-player%} to 0 if {_damage::%loop-player%} is not set
			if mod({_damage::%loop-player%}, 10) is 0 or 10:
				kitDamage({_player}, loop-player, {@ride-damage}, "蔦の鉤縄")
				kitParticle_IvyZipline_Damage(loop-player)
			add 1 to {_damage::%loop-player%}

		set {_exist} to kitFakeCheck({_player}, "Ivy Zipline")
		if {_exist} is false:
			deleteKitStats({_player}, {@stats-time})
	loop all block displays:
		metadata value {@metadata-vehicle} of loop-block display is {_player}
		delete loop-block display

local function kitEffect_IvyZipline_Display(player: player, loc: location):
	set pitch of {_loc} to 0
	set {_placeloc} to {_loc}

	set {_rotation.left} to getQuaternionf(0, 0, 0, 1)
	set {_rotation.right} to getQuaternionf(0, 0, 0, 1)
	set {_translation} to getVector3f(-0.5, 0, -0.5)
	set {_scale} to getVector3f(1, 0.75, 1)
	set {_transformation} to getTransformation({_rotation.left}, {_rotation.right}, {_translation}, {_scale})
	set {_block} to getBlockData(small dripleaf)
	set {_displays::1} to kitFakeBlock({_player}, {_placeloc}, {_block}, {_transformation}, "Ivy Zipline")

	set {_placeloc} to location 0.75 meters above {_loc}
	set {_transformation} to getTransformation({_rotation.left}, {_rotation.right}, {_translation}, {_scale})
	set {_displays::2} to kitFakeBlock({_player}, {_placeloc}, {_block}, {_transformation}, "Ivy Zipline")

	set {_placeloc} to location 1.5 meters above {_loc}
	set {_translation} to getVector3f(-0.25, 0, -0.25)
	set {_scale} to getVector3f(0.5, 0.5, 0.5)
	set {_transformation} to getTransformation({_rotation.left}, {_rotation.right}, {_translation}, {_scale})
	set {_block} to getBlockData(flowering azalea leaves)
	set {_displays::3} to kitFakeBlock({_player}, {_placeloc}, {_block}, {_transformation}, "Ivy Zipline")

	set {_translation} to getVector3f(-0.625, 0, -0.3)
	set {_scale} to getVector3f(1.25, 0.85, 0.6)
	set {_transformation} to getTransformation({_rotation.left}, {_rotation.right}, {_translation}, {_scale})
	set {_block} to getBlockData(lilac)
	set {_blockdata} to {_block}.setHalf(Half.TOP)
	set {_displays::4} to kitFakeBlock({_player}, {_placeloc}, {_block}, {_transformation}, "Ivy Zipline")

local function kitEffect_IvyZipline_Vehicle(player: player, loc: location) :: entity:
	spawn a block display at {_loc}
	set {_vehicle} to spawned block display

	set metadata value "gametype" of {_vehicle} to "Kit"
	set metadata value {@metadata-vehicle} of {_vehicle} to {_player}

	setBrightness({_vehicle})
	{_vehicle}.setTeleportDuration(1)
	{_vehicle}.setViewRange(1)
	set {_block} to getBlockData(barrier)
	setDisplayBlock({_vehicle}, {_block})

	return {_vehicle}



local function kitParticle_IvyZipline_Launch(player: player):
	set {_loc} to head of {_player}
	playSound("block.grass.break", 1, 0.7, {_loc})

	loop 20 times:
		set {_el} to moveRandomLocation({_loc}, 0.6)
		spawnParticle_Block(birch leaves, {_el})

local function kitParticle_IvyZipline_Ivy(loc: location):
	spawnParticle(happy_villager, {_loc})

local function kitParticle_IvyZipline_Place(from: location, to: location):
	playSound("block.wet_grass.break", 1, 0.6, {_from})
	playSound("block.wet_grass.break", 1, 0.6, {_to})

	loop 30 times:
		set {_rm} to a random number between 0 and 1.5
		set {_rv} to randomVector({_rm})
		set {_el} to {_from} ~ {_rv}
		spawnParticle(egg_crack, {_el})
		set {_el} to {_to} ~ {_rv}
		spawnParticle(egg_crack, {_el})

local function kitParticle_IvyZipline_Line(from: location, to: location):
	add rgb(114, 244, 43) to {_colors::*}
	add rgb(54, 239, 100) to {_colors::*}
	add rgb(6, 209, 98) to {_colors::*}

	set {_from.1} to {_from}
	set {_to.1} to {_to}
	set {_vector} to setVector({_from}, {_to})
	set yaw of {_from.1} to yaw of {_vector}
	set yaw of {_to.1} to yaw of {_vector}
	set pitch of {_from.1} to 0
	set pitch of {_to.1} to 0
	set {_from.1} to location 0.625 meters to the left of {_from.1}
	set {_to.1} to location 0.625 meters to the left of {_to.1}
	
	set {_color} to a random element out of {_colors::*}
	spawnParticle_Trail({_color}, {_to.1}, 3 seconds, {_from.1})

	set {_from.2} to {_to}
	set {_to.2} to {_from}
	set {_vector} to setVector({_to}, {_from})
	set yaw of {_from.2} to yaw of {_vector}
	set yaw of {_to.2} to yaw of {_vector}
	set pitch of {_from.2} to 0
	set pitch of {_to.2} to 0
	set {_from.2} to location 0.625 meters to the left of {_from.2}
	set {_to.2} to location 0.625 meters to the left of {_to.2}

	set {_color} to a random element out of {_colors::*}
	spawnParticle_Trail({_color}, {_to.2}, 3 seconds, {_from.2})

local function kitParticle_IvyZipline_Ride(loc: location):
	playSound("block.sweet_berry_bush.pick_berries", 1, 1.3, {_loc})

	loop 30 times:
		set {_el} to moveRandomLocation(1)
		spawnParticle(sneeze, {_el})

local function kitParticle_IvyZipline_Drop(loc: location):
	playSound("block.flowering_azalea.place", 1, 1.2, {_loc})

	loop 30 times:
		set {_el} to moveRandomLocation(1)
		spawnParticle_BlockData(block_crumble, oak leaves, {_el})

local function kitParticle_IvyZipline_Riding(loc: location):
	playSound("block.wet_grass.step", 0.9, 1.8, {_loc})

	set {_el} to moveRandomLocation(1)
	spawnParticle(composter)
	
	chance of 30%:
		set {_el} to moveRandomLocation(0.5)
		spawnParticle_BlockData(falling_dust, lime wool, {_el})

local function kitParticle_IvyZipline_Damage(player: player):
	playSound()

	set {_baseloc} to location 1 meter above {_player}
	loop 30 times:
		set {_rm} to a random number between 0 and 1
		set {_rv} to randomVector({_rm})
		set {_el} to {_baseloc} ~ {_rv}
		spawnParticle_Item(sweet berries, {_el})