# Sonic Revolution

import:
	org.bukkit.Material
	org.bukkit.inventory.ItemStack

function kitEffect_SonicRevolution(player: player):
	set {_tool} to slot 0 of {_player}
	if kitItemTypeChecker({_tool}) is not "Weapon":
		send action bar "§c§lWeaponがありません" to {_player}
		stop

	nameCT({_player}, "Sonic Revolution")
	setKitStats({_player}, "SonicRevolution", true)
	kitMsg({_player}, "§b回転斬りをします...")
	sendTimeBossbar_GREEN({_player}, "Sonic Revolution", 60)
	apply speed 2 to {_player} for 3 seconds
	set generic jump strength attribute of {_player} to 0.21

	set {_weapon} to slot 0 of {_player}
	set slot 0 of {_player} to a stick
	set {_baseloc} to location of {_player}
	add 1 to y-pos of {_baseloc}
	set pitch of {_baseloc} to 0

	set {_display} to kitSpawnMob({_player}, item display, {_baseloc}, false)
	setBrightness({_display})
	{_display}.setTeleportDuration(1)
	{_display}.setInterpolationDuration(1)
	{_display}.setViewRange(1000)

	set {_ItemMeta} to {_tool}.getItemMeta()
	set {_item} to {_ItemMeta}.getItemModel()
	set {_itemname} to "%{_item}%"
	replace "minecraft:" with "" in {_itemname}
	set {_itemname} to {_itemname} in upper case
	set {_modelitem} to new ItemStack(Material.valueOf({_itemname}))
	setDisplayItem({_display}, {_modelitem})

	set {_matrix} to getAdjustedMatrix4f(2.1213, 2.1213, 0, 0, 0, -0, -3, 0, -2.1213, 2.1213, -0, 2, 0, 0, 0, 1)
	{_display}.setTransformationMatrix({_matrix})

	set {_yaw} to yaw of {_baseloc}
	loop 60 times:
		set {_loc} to location of {_player}
		add 1 to y-pos of {_loc}
		playSound("entity.arrow.shoot", 0.75, 0.75, {_player})
		set pitch of {_loc} to 0
		add 54 to {_yaw}
		if {_yaw} > 360:
			set {_yaw} to {_yaw} - 360
		set yaw of {_loc} to {_yaw}
		teleport {_display} to {_loc}
		delete {_forn}
		loop 5 times:
			add 1 to {_forn}
			set {_forb} to location {_forn} meters in front of {_loc}
			spawnParticle_Dust(rgb(0, 191, 255), 1, {_forb})
		loop all entities in radius 5 around {_player}:
			{Class::%{_player}%} is "Warrior"
			kitCheckTeam(loop-entity, {_player}, false) is true
			kitDamage({_player}, loop-entity, 1.5, "回転斬り")
			kitEffectDescription(loop-entity, "§4❤1.5")
		wait a tick
	{_display}.remove()
	if getKitStats({_player}, "SonicRevolution") is true:
		set slot 0 of {_player} to {_weapon}
		apply nausea 1 to {_player} for 10 seconds
	set generic jump strength attribute of {_player} to 0.42
	deleteKitStats({_player}, "SonicRevolution")