# Sonic Revolution

function kitSkill_Warrior_4(player: player):
	set {_tool} to slot 0 of {_player}
	if kitItemTypeChecker({_tool}) is "Weapon.main":
		toolCT({_player}, "Warrior", 4)
		kitEffect_SonicRevolution({_player})
	else:
		send action bar "§c§lWeaponがありません" to {_player}



local function kitEffect_SonicRevolution(player: player):
	set {Stats.%{_player}%::SonicRevolution} to true
	kitMsg({_player}, "§b回転斬りをします...")
	sendTimeBossbar_GREEN({_player}, "Sonic Revolution", 60)
	apply speed 2 to {_player} for 3 seconds
	set generic jump strength attribute of {_player} to 0.21

	set {_weapon} to slot 0 of {_player}
	set slot 0 of {_player} to a stick
	set {_baseloc} to location of {_player}
	add 1 to y-pos of {_baseloc}
	set pitch of {_baseloc} to 0

	set {_item} to raw name of {_weapon}
	set {_transformation} to "[2.1213f,2.1213f,0.0000f,0.0000f,0.0000f,-0.0000f,-3.0000f,0.0000f,-2.1213f,2.1213f,-0.0000f,2.0000f,0.0000f,0.0000f,0.0000f,1.0000f]"
	set {_nbt} to nbt compound from "{transformation:%{_transformation}%,item:{id:""%{_item}%"",Count:1b},teleport_duration:1}"
	spawn an item display at {_baseloc} with nbt {_nbt}
	set metadata value "sonic-revolution" of spawned entity to "%{_player}%"

	set {_yaw} to yaw of {_baseloc}
	loop 60 times:
		set {_loc} to location of {_player}
		add 1 to y-pos of {_loc}
		playSound("entity.arrow.shoot", 0.75, 0.75, {_player})
		set pitch of {_loc} to 0
		add 54 to {_yaw}
		if {_yaw} > 360:
			set {_yaw} to {_yaw} - 360
		set yaw of {_loc} to {_yaw}
		loop all entities:
			metadata value "sonic-revolution" of loop-entity is "%{_player}%"
			teleport loop-entity to {_loc}
		delete {_forn}
		loop 5 times:
			add 1 to {_forn}
			set {_forb} to location {_forn} meters in front of {_loc}
			spawnParticle_Dust(rgb(0, 191, 255), 1, {_forb})
		loop all entities in radius 5 around {_player}:
			{Class::%{_player}%} is "Warrior"
			kitCheckTeam(loop-entity, {_player}, false) is true
			kitDamage({_player}, loop-entity, 1.5)
			kitEffectDescription(loop-entity, "§4❤1.5")
		wait a tick
	loop all entities:
		metadata value "sonic-revolution" of loop-entity is "%{_player}%"
		delete loop-entity
	if {Stats.%{_player}%::SonicRevolution} is true:
		set slot 0 of {_player} to {_weapon}
		apply nausea 1 to {_player} for 10 seconds
	set generic jump strength attribute of {_player} to 0.42
	delete {Stats.%{_player}%::SonicRevolution}