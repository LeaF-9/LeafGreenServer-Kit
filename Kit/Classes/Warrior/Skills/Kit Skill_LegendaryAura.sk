# Legendary Aura

import:
	org.bukkit.Material
	org.bukkit.inventory.ItemStack

function kitEffect_LegendaryAura(player: player):
	set {_tool} to slot 0 of {_player}
	if kitItemTypeChecker({_tool}) is not "Weapon":
		send action bar "§c§lWeaponがありません" to {_player}
		stop
	if block 0.1 meters below {_player} is passable:
		send action bar "§c§l地に足をつけて発動してください" to {_player}
		stop
	
	nameCT({_player}, "Legendary Aura")
	setKitStats({_player}, "LegendaryAura", true)
	kitMsg({_player}, "<##d9e367>剣のエネルギーを地に放出します!")
	sendTimeBossbar_GREEN({_player}, "Legendary Aura", 100)

	set {_weapon} to slot 0 of {_player}
	set slot 0 of {_player} to a stick
	set {_loc} to location of {_player}

	set {_loc.display} to {_loc}
	set pitch of {_loc.display} to 0

	set {_display} to kitSpawnMob({_player}, item display, {_loc.display}, false)
	setBrightness({_display})
	{_display}.setTeleportDuration(1)
	{_display}.setInterpolationDuration(1)
	{_display}.setViewRange(1000)

	set {_ItemMeta} to {_weapon}.getItemMeta()
	set {_item} to {_ItemMeta}.getItemModel()
	set {_itemname} to "%{_item}%"
	replace "minecraft:" with "" in {_itemname}
	set {_itemname} to {_itemname} in upper case
	set {_modelitem} to new ItemStack(Material.valueOf({_itemname}))
	setDisplayItem({_display}, {_modelitem})

	set {_matrix} to getAdjustedMatrix4f(-0, -1, 0, 0, 1, -0, 0, 0.25, 0, 0, 1, 0, 0, 0, 0, 1)
	{_display}.setTransformationMatrix({_matrix})

	loop 5 times:
		kitEffect_LegendaryAura_Wave({_player}, {_loc})
		wait a second

	{_display}.remove()
	if getKitStats({_player}, "LegendaryAura") is true:
		set slot 0 of {_player} to {_weapon}
	deleteKitStats({_player}, "LegendaryAura")

local function kitEffect_LegendaryAura_Wave(player: player, loc: location):
	playSound("entity.ender_dragon.hurt", 1, 0.75, {_loc})

	set {_m} to 0
	loop 10 times:
		add 1 to {_m}
		set {_m.min} to {_m} - 0.5
		set {_m.max} to {_m} + 0.5
		kitParticle_LegendaryAura_Wave({_loc}, {_m})

		loop all entities in radius 11 around {_loc}:
			{_hit.%uuid of loop-entity%} is not set
			kitCheckTeam(loop-entity, {_player}, false) is true
			block 0.1 meters below loop-entity is not passable
			set {_dis} to distance between loop-entity and {_player}
			if {_dis} >= {_m.min}:
				if {_dis} <= {_m.max}:
					set {_hit.%uuid of loop-entity%} to true
					kitDamage({_player}, loop-entity, 2.3, "大剣の衝撃")
					kitEffectDescription(loop-entity, "§4❤2.3")
					kitParticle_LegendaryAura_Hit(loop-entity)

		if mod(loop-number, 2) is 0:
			wait a tick



local function kitParticle_LegendaryAura_Wave(baseloc: location, meter: number):
	set {_locs::*} to particleLocation_Circle({_baseloc}, 180, {_meter})
	loop {_locs::*}:
		spawnParticle(electric_spark, loop-value)

local function kitParticle_LegendaryAura_Hit(entity: entity):
	playSound("entity.shulker.hurt_closed", 1, 0.65, location of {_entity})

	add rgb(96, 33, 214) to {_colors::*}
	add rgb(96, 38, 233) to {_colors::*}
	add rgb(78, 102, 236) to {_colors::*}

	set {_baseloc} to location 1 meters above {_entity}
	loop 30 times:
		spawnParticle_Vector(enchanted_hit, randomVector(), 2.5, {_baseloc})
		set {_color} to a random element out of {_colors::*}
		spawnParticle_Color(entity_effect, {_color}, moveRandomLocation({_baseloc}, 1))