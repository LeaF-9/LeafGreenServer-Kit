# Overpowered Attack

function kitUltWarrior(player: player):
	deleteKitStats({_player}, "SonicRevolution")
	deleteKitStats({_player}, "LegendaryAura")
	set {_weapon} to kitPlayerWeaponItem({_player}, "Warrior")
	load yml "plugins/skript-yaml/Kit Classes/Warrior.yml" as "ultwarrior.%{_player}%"
	set {_SpecialSword} to yml value "kititems.Special Sword" from "ultwarrior.%{_player}%"
	set slot 0 of {_player} to {_SpecialSword}
	setKitStats({_player}, "UltWarrior", true)
	wait 10 seconds
	unload yml "ultwarrior.%{_player}%"
	if getKitStats({_player}, "UltWarrior") is true:
		set slot 0 of {_player} to {_weapon}
	deleteKitStats({_player}, "UltWarrior")
	deleteKitStats({_player}, "UltWarrior-OK")

function kitEffect_UltWarrior(player: player):
	while getKitStats({_player}, "UltWarrior-OK") is true:
		if getKitStats({_player}, "UltWarrior") is not set:
			deleteKitStats({_player}, "UltWarrior-OK")
		set {_loc} to location of {_player}
		add 0.02 to {_dy}
		if {_dy} >= 4:
			set {_dy} to 0
		if {_dy} > 2:
			set {_y} to 2 - ({_dy} - 2)
		else:
			set {_y} to {_dy}
		add 9 to {_rad}
		set {_vec} to vector(0.5, {_y}, 0)
		rotate {_vec} around y-axis by {_rad}
		set {_loc} to {_loc} ~ {_vec}
		spawnParticle(angry_villager, {_loc})
		wait a tick

on sneak toggle:
	{Class::%player%} is "Warrior"
	player is not sneaking
	if getKitStats(player, "UltWarrior") is true:
		set {_SpecialSword} to yml value "kititems.Special Sword" from "ultwarrior.%player%"
		player's tool is {_SpecialSword}
		if getKitStats(player, "UltWarrior-OK") is not set:
			set {_num} to 0
			wait a tick
			set {_true} to false
			while {_true} is false:
				if player is not sneaking:
					delete {_true}
				else if player's tool is not {_SpecialSword}:
					delete {_true}
				else if {_num} >= 30:
					set {_true} to true
				else:
					add 1 to {_num}
					chargeActionBar(player, {_num}, 30)
					# 音の演出
					set {_sound} to {_num} / 15
					playSound("entity.experience_orb.pickup", 0.75, {_sound}, player)
					# パーティクルの演出
					spawnParticle(explosion, location of player)
					wait a tick
			if {_true} is true:
				playSound("entity.player.levelup", 1.2, 0.5, player)
				kitMsg(player, "§4§lパワーが貯まりました!")
				setKitStats(player, "UltWarrior-OK", true)
				kitEffect_UltWarrior(player)

on damage with priority LOW:
	set {_attacker} to getAttacker(victim)
	if getKitStats({_attacker}, "UltWarrior-OK") is true:
		setDamageModification_FIX(victim, 100)
		deleteKitStats({_attacker}, "UltWarrior-OK")
		strike lightning effect at victim
		loop 10 times:
			set {_loc} to aroundEffect(victim)
			spawnParticle(soul, {_loc})