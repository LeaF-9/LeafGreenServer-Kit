# Mutant Mummy

function kitEffect_MutantMummy(player: player):
	set {_SkillTarget} to getKitStats({_player}, "SkillTarget")
	if {_SkillTarget} is not set:
		send action bar "§c§l対象が存在しません!" to {_player}
		stop

	nameCT({_player}, "Mutant Mummy")
	set {_tg} to {_SkillTarget}
	execute console command "/skclass %{_tg}% Kit-Mummy"
	setKitStats({_tg}, "MutantMummy", true)
	kitEffectDescription({_tg}, "§8§lミイラ化")
	sendTimeBossbar_RED({_tg}, "§8ミイラ化", 200)
	playSound("entity.wither.spawn", 1, 1, {_tg})
	set {_memsg} to kitGhostMessage("§5%{_tg}% §8をミイラに変形させた...")
	set {_youmsg} to kitGhostMessage("§5Noooooooooo!!!!!", true)
	kitMsg({_player}, {_memsg})
	message formatted {_youmsg} to {_tg}
	apply nausea 1 to {_tg} for 5 seconds
	apply blindness 1 to {_tg} for 5 seconds
	loop 10 times:
		set {_el} to aroundEffect({_tg})
		spawnParticle_BlockData(falling_dust, white concrete powder, {_el})
	wait 10 seconds
	if getKitStats({_tg}, "MutantMummy") is true:
		execute console command "/class %{Class::%{_tg}%}% %{_tg}% false"