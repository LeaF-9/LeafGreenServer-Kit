# Phren Possession

on damage with priority HIGHEST:
	set {_attacker} to getAttacker(victim)
	metadata value "Phren Possession" of {_attacker} is true
	cancel event

function kitEffect_PhrenPossession(player: player):
	set {_SkillTarget} to getKitStats({_player}, "SkillTarget")
	if {_SkillTarget} is not set:
		send action bar "§c§l対象が存在しません!" to {_player}
		stop

	if kitCheckSpirit({_player}, "Phren Possession") is false:
		stop

	nameCT({_player}, "Phren Possession")
	set {_tg} to {_SkillTarget}
	set metadata value "Phren Possession" of {_player} to true
	set metadata value "Phren Possession" of {_tg} to true

	playSound("block.end_portal.spawn", 1, 2, {_tg})
	set {_memsg} to kitGhostMessage("§5%{_tg}% §4の身体を乗っ取った...")
	set {_youmsg} to kitGhostMessage("§5%{_player}% §4に身体を盗られた!", true)
	kitMsg({_player}, {_memsg})
	message formatted {_youmsg} to {_tg}
	kitEffectDescription({_tg}, "§0§l身体憑依")
	sendTimeBossbar_GREEN({_player}, "Phren Possession", 60)
	sendTimeBossbar_RED({_tg}, "身体憑依", 60)
	set {_targetbaseloc} to location of {_tg}
	set {_playerbaseloc} to location of {_player}
	kitPalsy({_player}, 60, false)
	kitPalsy({_tg}, 60, false)
	setKitStats({_player}, "Invincible", true)
	setKitStats({_tg}, "Invincible", true)
	teleport {_player} to {_targetbaseloc}
	set {_backlocation} to {_targetbaseloc}
	loop 60 times:
		hide {_player} from all players
		hide {_tg} from {_player}

		set {_tploc} to location of {_player}
		teleport {_tg} to {_tploc}
		kitParticle_PhrenPossession_Control({_player})

		# check target is viable
		loop 10 times:
			continue if {_ok} is true

			set {_cl} to location loop-number-2 meters below {_tploc}
			set {_ok} to true if block at {_cl} is not passable

		loop all blocks in radius 3 around {_tploc}:
			delete {_ok} if "%loop-block%" contains "lava"

			if {Kit-GameStage::name} is "Acid Rain":
				delete {_ok} if "%loop-block%" contains "water"

		if {_ok} is true:
			set {_backlocation} to {_tploc}

		wait a tick
	extinguish {_tg}
	delete metadata value "Phren Possession" of {_player}
	delete metadata value "Phren Possession" of {_tg}
	deleteKitStats({_player}, "Invincible")
	deleteKitStats({_tg}, "Invincible")
	teleport {_player} to {_playerbaseloc}
	teleport {_tg} to {_backlocation} if {_ok} is not set
	reveal {_player} from all players
	reveal {_tg} from {_player}
	set {_memsg} to kitGhostMessage("§1身体の乗っ取りを解除しました")
	set {_youmsg} to kitGhostMessage("§6自分の身体が返ってきた!", true)
	kitMsg({_player}, {_memsg})
	message formatted {_youmsg} to {_tg}



local function kitParticle_PhrenPossession_Control(player: player):
	set {_effbaseloc} to location of {_player}
	set {_rad} to 0
	loop 6 times:
		set {_el} to {_effbaseloc}
		add 60 to {_rad}
		add 0.5 * sin({_rad}) to x-coordinate of {_el}
		add 0.5 * cos({_rad}) to z-coordinate of {_el}
		add 2 to y-coordinate of {_el}
		spawnParticle_Color(entity_effect, rgb(50, 0, 0), {_el})