options:
	prefix: §2[%{CLS::%player%}%]
	attackprefix: §2[%{CLS::%attacker%}%]
	victimprefix: §2[%{CLS::%victim%}%]
	arg2prefix: §2[%{CLS::%arg 2%}%]
	lore: 一番近い敵のいる方向と距離を示します

# Officerのスキル ver1.0.0

on damage:
	{CLS::%victim%} is "AMB"
	attacker has blindness
	set damage to damage / 2

on damage:
	damage was caused by fall
	if {CLS::%victim%} is "AMB":
		cancel event

on damage:
	name of attacker's tool is "§5Dizzy Knife"
	{Stats.%attacker%::LatentEscape} is not set
	apply blindness 1 to victim for 1 seconds

on damage of player:
	if {Stats.%attacker%::LatentEscape} is true:
		cancel event
	if {Stats.%victim%::LatentEscape} is true:
		cancel event

on rightclick holding a firework star:
	name of player's tool is "§8Smoke Bomb"
	shoot a snowball at speed 3
	set metadata value "smoke" of shot snowball to "true"
	play raw sound "random.bow" at the player with pitch 0.5 volume 1
	set {_lore} to lore of player's tool
	remove a firework star named "§8Smoke Bomb" with lore "%{_lore}%" from the player

on projectile hit:
	projectile is snowball
	metadata value "smoke" of projectile is "true"
	play raw sound "mob.ghast.fireball" at projectile with pitch 0.5 volume 1
	create a fake explosion at the projectile
	loop all players in radius 5 around projectile:
		if {team::%loop-player%} is not {team::%shooter%}:
			message "§8煙幕に掛かった！" to loop-player
			drawDot count 1, particle "cloud", RGB 0, 0, 0, center loop-player, visibleRange 30, pulseDelay 0, keepFor 20 ticks
			remove blindness from loop-player
			apply blindness 1 to loop-player for 3 seconds
		else if {team::%loop-player%} is {team::%shooter%}:
			add loop-player to {_inv::*}
			set {Invisible::%loop-player%} to true
			loop all players:
				{CLS::%loop-player-2%} is not "THI"
				hide loop-player-1 from loop-player-2
			message "§8透明化が付与された!" to loop-player
	wait 3 seconds
	loop {_inv::*}:
		{Invisible::%loop-value%} is true
		delete {Invisible::%loop-value%}
		reveal loop-value from all players
		set action bar of loop-value to "§b§l透明化が解除されました!"

on sneak toggle:
	{CLS::%player%} is "AMB"
	if player is not sneaking:
		message "{@prefix} §75秒お待ち下さい..."
		set {Stats.%player%::SpyPrepare} to true
		set {_Spy} to 20
		while {Stats.%player%::SpyPrepare} is set:
			subtract 1 from {_Spy}
			if {_Spy} is less than 0:
				delete {Stats.%player%::SpyPrepare}
			wait 5 ticks
		if {_Spy} is less than 0:
			set {Invisible::%player%} to true
			message "{@prefix} §8透明化が完了しました！"
			play raw sound "mob.wither.shoot" at the player with pitch 10 volume 0.5
			loop all players:
				{CLS::%loop-player%} is not "THI"
				hide player from loop-player
			set {_loc} to location of player
			while {Invisible::%player%} is true:
				if distance between player and {_loc} is more than 3:
					delete {Invisible::%player%}
					set action bar of player to "§b§l透明化が解除されました!"
					reveal player from all players
				else:
					loop all players:
						{CLS::%loop-player%} is not "THI"
						hide player from loop-player
					wait a second
		else:
			set action bar of player to "§c§l透明化に失敗しました"
	else:
		if {Invisible::%player%} is true:
			delete {Invisible::%player%}
			reveal player from all players
			set action bar of player to "§b§l透明化が解除されました!"
		else if {Stats.%player%::SpyPrepare} is true:
			delete {Stats.%player%::SpyPrepare}

on rightclick holding a 409:
	name of player's tool is "§bSwift Slay"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {_pl} to location of player
		set {_n} to 0
		loop 10 times:
			{_ng} is not set
			add 1 to {_n}
			set {_fb} to block {_n} blocks in front of {_pl}
			if {_fb} is not air:
				subtract 1 from {_n}
				set {_ng} to true
			else:
				drawDot count 5, particle "redstone", RGB 100, 255, 255, center location of {_fb}, visibleRange 30, keepFor 20 ticks
				loop all players in radius 3 around location of {_fb}:
					{team::%loop-player%} is not {team::%player%}
					make player damage loop-player by 6
		teleport the player {_n} meters in front of player
		message "{@prefix} §b目にも留まらぬ速さで斬る！"
		set {CT.%player%::%name of player's tool%} to 15
		play raw sound "random.bow" at the player with pitch 1.5 volume 1
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a gray dye:
	name of player's tool is "§8Latent Escape"
	if {CT.%player%::%name of player's tool%} is not set:
		message "{@prefix} §7透明になりました..."
		create a fake explosion at the player
		apply speed 2 to player for 5 seconds
		loop all players:
			{CLS::%loop-player%} is not "THI"
			hide player from loop-player
		set {Stats.%player%::LatentEscape} to true
		set {CT.%player%::%name of player's tool%} to 30
		wait 5 seconds
		delete {Stats.%player%::LatentEscape}
		reveal the player from all players
		set action bar of player to "§b§l透明化が解除されました!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a ender pearl:
	name of player's tool is "§5Dark Travel"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		if the target block exists:
			if distance between player and target block <= 15:
				if the block above target block is air:
					set {CT.%player%::§5Dark Travel} to 20
					set {_bl} to location of block above target block
					drawDot count 5, particle "portal", center player, visibleRange 30, keepFor 10 ticks
					teleport the player to {_bl}
					play raw sound "mob.zombie.unfect" at the player with pitch 2 volume 1
					message "{@prefix} §5目先地点にTPして透明化します..."
					set {Invisible::%player%} to true
					loop all players:
						{CLS::%loop-player%} is not "THI"
						hide player from loop-player
					wait 5 seconds
					if {Invisible::%player%} is true:
						reveal player from all players
						delete {Invisible::%player%}
						set action bar of player to "§b§l透明化が解除されました!"
				else:
					set action bar of player to "§c§lその場所にはテレポート出来ません!"
			else:
				set action bar of player to "§c§l遠すぎます!"
		else:
			set action bar of player to "§c§l目先にブロックがありません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding an ender pearl:
	name of player's tool is "§5Dark Travel"
	set {_n} to name of player's tool
	set {_l} to lore of player's tool
	wait a tick
	give an ender pearl named "%{_n}%" with lore "%{_l}%" to player
	wait a tick
	remove an ender pearl named "%{_n}%" with lore "%{_l}%" from player

on rightclick holding a eye of ender:
	name of player's tool is "§eFlash Light"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 25
		message "{@prefix} §5周辺の敵の視界を奪います..."
		loop all players in radius 5 around player:
			{team::%loop-player%} is not {team::%player%}
			message "§5視界が奪われた!" to loop-player
			add player to {damager.%loop-player%::*}
			apply blindness 1 to loop-player for 5 seconds
			play raw sound "mob.zombie.infect" at the loop-player with pitch 2 volume 1
			drawDot count 5, particle "witchspell", center loop-player, visibleRange 30, keepFor 60 ticks
		play raw sound "mob.wither.hurt" at the player with pitch 2 volume 1
		drawSphere style 1, particle "redstone", RGB 255, 255, 150, center location of block at player, id "%player%", radius 5, density 300, visibleRange 30
		set {Invisible::%player%} to true
		loop all players:
			{CLS::%loop-player%} is not "THI"
			hide player from loop-player
		wait a second
		stopEffect "%player%"
		wait 2 seconds
		if {Invisible::%player%} is true:
			reveal player from all players
			delete {Invisible::%player%}
			set action bar of player to "§b§l透明化が解除されました!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

every a second:
	loop all players:
		loop-player-1's inventory contains compass named "§cSearch Compass" with lore "{@lore}"
		loop all players:
			{team::%loop-player-2%} is not {team::%loop-player-1%}
			set {_loc.%loop-player-1%::%loop-player-2%} to location of loop-player-2
			set {_dis.%loop-player-1%::%loop-player-2%} to distance between loop-player-1 and loop-player-2
		set {_high} to 10000
		loop {_dis.%loop-player-1%::*}:
			loop-value-2 < {_high}
			set {_high} to loop-value-2
			set {_highplayer} to loop-index
		set loop-player-1's compass target to {_loc.%loop-player-1%::%{_highplayer}%}
		if name of loop-player-1's tool is "§cSearch Compass":
			set action bar of loop-player-1 to "§5Target§7: §a%{_highplayer}% §6Distance§7:§b%{_high}%m"