# skill-name: Latent Escape

options:
	max-effect-tick: 100

on damage:
	set {_attacker} to getAttacker(victim)
	cancel event if getKitStats(victim, "Latent Escape") is set
	cancel event if getKitStats({_attacker}, "Latent Escape") is set

function kitEffect_LatentEscape(player: player):
	if getKitStats({_player}, "Latent Escape") is set:
		setKitStats({_player}, "Latent Escape", false)
		stop
	
	set {_skillitem} to tool of {_player}
	setKitStats({_player}, "Latent Escape", true)
	kitSpecter({_player}, {@max-effect-tick})
	kitMsg({_player}, "<##695d5d>潜伏して戦線離脱します...")
	kitParticle_LatentEscape_Launch({_player})

	loop {@max-effect-tick} times:
		continue if getKitStats({_player}, "Latent Escape") is not true
	
		applyEffect({_player}, speed, 2, 3, true)
		send action bar "§7§l§o潜伏中です..." to {_player}
		wait a tick
	
	detectCT({_player}, {_skillitem})
	removeSpecter({_player})
	stop if getKitStats({_player}, "Latent Escape") is not set

	kitMsg({_player}, "<##828b8b>§l伏走状態<##3571ca>が終了しました")
	kitParticle_LatentEscape_End({_player})
	deleteKitStats({_player}, "Latent Escape")
	set {_pop} to 0
	loop all players in radius 10 around {_player}:
		kitCheckTeam({_player}, loop-player, false, true) is true
		add 1 to {_pop}
	
	stop if {_pop} > 0
	delete metadata value "Stealth Behavior - Damage" of {_player}
	kitMsg({_player}, "§b§lStealth Behavior§7に移行します...")



local function kitParticle_LatentEscape_Launch(player: player):
	playSound("entity.generic.explode", 1, 1.9, {_player})

	set {_baseloc} to location 1 meter above {_player}
	spawnParticle(gust_emitter_large, {_baseloc})

local function kitParticle_LatentEscape_End(player: player):
	playSound("entity.ender_dragon.flap", 1, 2, {_player}, true)

	set {_baseloc} to location 1 meter above {_player}
	loop 10 times:
		set {_el} to moveRandomLocation({_baseloc}, 1)
		spawnParticle(small_gust, {_el}, {_player})