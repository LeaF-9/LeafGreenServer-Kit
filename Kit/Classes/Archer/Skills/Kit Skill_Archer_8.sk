# Leap Gale

function kitSkill_Archer_8(player: player):
	toolCT({_player}, "Archer", 8)
	kitEffect_LeapGale({_player})



local function kitEffect_LeapGale(player: player):
	kitMsg({_player}, "§b§l突風射撃!")
	kitEffect_LeapGale_Leap({_player})
	wait 10 ticks
	kitEffect_LeapGale_Gale({_player})

local function kitEffect_LeapGale_Leap(player: player):
	kitParticle_LeapGale_Leap({_player})
	set {_vector} to vector(0, 0.8, 0)
	set velocity of {_player} to {_vector}

local function kitEffect_LeapGale_Gale(player: player):
	set {_baseloc} to location of head of {_player}
	# velocity
	kitParticle_LeapGale_Gale(location 1 meter above {_player})
	set {_vector} to setVector(location of {_player}, location 10 meters behind {_player}, 0.9)
	set velocity of {_player} to {_vector}
	# arrow
	loop 30 times:
		kitEffect_LeapGale_Arrow({_player}, {_baseloc})

local function kitEffect_LeapGale_Arrow(player: player, loc: location):
	set {_el} to {_loc}
	set pitch of {_el} to 0
	set {_leftm} to a random number between -3 and 3
	set {_el} to location {_leftm} meters to the left of {_el}
	set {_pitch.random} to a random number between -45 and 45
	set pitch of {_el} to {_pitch.random} + pitch of {_loc}
	set {_el} to location 10 meters in front of {_el}
	set {_vector} to setVector({_loc}, {_el}, 2)
	shoot an arrow from {_loc} at speed 1
	set {_arrow} to shot arrow
	set velocity of {_arrow} to {_vector}
	set shooter of {_arrow} to {_player}
	set metadata value "arrowdamage" of {_arrow} to 3
	kitParticle_LeapGale_Arrow({_arrow})



local function kitParticle_LeapGale_Leap(player: player):
	playSound("entity.wither.shoot", 1, 1.7, {_player})
	# 波紋
	set {_baseloc} to location of {_player}
	set pitch of {_baseloc} to 0
	set {_addyaw} to 0
	loop 36 times:
		set {_el} to {_baseloc}
		add {_addyaw} to yaw of {_el}
		set {_to} to location 10 meters in front of {_el}
		set {_vector} to setVector({_baseloc}, {_to})
		spawnParticle_Vector(cloud, {_vector}, 0.5, {_baseloc})
		add 10 to {_addyaw}
	# 残像
	loop 5 times:
		loop 2 times:
			set {_ae} to aroundEffect({_player})
			kitParticle_LeapGale_Up({_ae})
		wait a tick

local function kitParticle_LeapGale_Up(loc: location):
	loop 5 times:
		set {_el} to location loop-number - 0.5 meters above {_loc}
		spawnParticle_Dust(rgb(235, 246, 247), 0.5, {_el})
		wait a tick

local function kitParticle_LeapGale_Gale(loc: location):
	playSound("entity.zombie.break_wooden_door", 1, 0.9, {_loc})
	loop 5 times:
		set {_els::*} to particleLocation_CircleV({_loc}, 10 + 5 * (loop-number - 1), loop-number * 0.2)
		loop {_els::*}:
			spawnParticle_DustTransition(rgb(160, 216, 239), rgb(187, 226, 241), 0.65, loop-value-2)
		wait a tick

local function kitParticle_LeapGale_Arrow(proj: projectile):
	loop 20 times:
		spawnParticle(glow, location of {_proj})
		wait a tick