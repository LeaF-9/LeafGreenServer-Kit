# Gyration Shot

function kitEffect_GyrationShot(player: player):
	nameCT({_player}, "Gyration Shot")
	kitMsg({_player}, "§58方向§bに§9§l矢をばら撒きます!")
	kitParticle_GyrationShot_Prepare({_player})
	wait a second
	if metadata value "gametype" of {_player} is not "Kit":
		stop
	kitEffect_GyrationShot_Shoot({_player})

local function kitEffect_GyrationShot_Shoot(player: player):
	set {_yaw} to -45
	loop 40 times:
		set {_baseloc} to location 1 meter above {_player}
		set pitch of {_baseloc} to 0
		add 45 to {_yaw}
		if {_yaw} >= 360:
			set {_yaw} to 0
		set yaw of {_baseloc} to {_yaw}
		shoot an arrow from {_baseloc} at speed 5
		set metadata value "arrowdamage" of shot arrow to 2.5
		setShooter(shot arrow, {_player})
		kitParticle_GyrationShot_Arrow(shot arrow)
		wait a tick
		




local function kitParticle_GyrationShot_Prepare(player: player):
	playSound("entity.guardian.attack", 1, 2, {_player})
	set {_rad} to 0
	set {_y} to 0
	loop 20 times:
		set {_baseloc} to location of {_player}
		loop 4 times:
			set {_el} to {_baseloc}
			set {_tr} to {_rad} + 90 * loop-number-2
			add 0.5 * sin({_tr}) to x-coordinate of {_el}
			add 0.5 * cos({_tr}) to z-coordinate of {_el}
			add {_y} to y-coordinate of {_el}
			spawnParticle(end_rod, {_el})
		add 18 to {_rad}
		add 0.1 to {_y}
		wait a tick

local function kitParticle_GyrationShot_Arrow(entity: entity):
	playSound("entity.arrow.shoot", 1, 1.5, {_entity})
	loop 20 times:
		spawnParticle(witch, {_entity})
		wait a tick