# Shoot Booster

function kitSkill_Archer_1(player: player):
	if {Stats.%{_player}%::ST} is true:
		playSound("entity.arrow.shoot", 1, 1.5, {_player})
		shoot an arrow from {_player} at speed 2.0
		set metadata value "arrowdamage" of shot arrow to 1
		set metadata value "shootboost" of shot arrow to "true"
		loop 20 times:
			spawnParticle(happy_villager, shot entity)
			wait a tick
	else:
		playSound("block.wooden_door.open", 1, 0.5, {_player})
		kitMsg({_player}, "§4§lShoot Booster§cを起動します...")
		kitMsg({_player}, "§63秒間の間右クリックをした分だけ矢が発射されます!")
		set {Stats.%{_player}%::ST} to true
		set {_time} to 60
		while {_time} > 0:
			if {_time} is 20, 40 or 60:
				set {_num} to {_time} / 20
				kitMsg({_player}, "§a残り時間 §b%{_num}% §a秒...")
			subtract 1 from {_time}
			wait a tick
		kitMsg({_player}, "§4§lShoot Booster§cの効果が終了しました")
		toolCT({_player}, "Archer", 1)
		delete {Stats.%{_player}%::ST}