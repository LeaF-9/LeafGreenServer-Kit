# Arrow Rain

function kitEffect_ArrowRain(player: player):
	nameCT({_player}, "Arrow Rain")
	kitMsg({_player}, "§1矢の雨を降らせる卵を発射します...!")
	setKitStats({_player}, "ArrowRain", location of {_player})
	shoot an egg from {_player} at speed 1.5
	set metadata value "arrowegg" of shot entity to "true"
	set {_shotegg} to shot entity
	playSound("entity.arrow.shoot", 1, 0.5, {_player})
	loop 20 times:
		spawnParticle_Dust(rgb(0, 0, 255), 2, {_shotegg})
		wait a tick

on projectile hit:
	projectile is an egg
	metadata value "arrowegg" of projectile is set
	set {_ArrowRain} to getKitStats(shooter, "ArrowRain")
	{_ArrowRain} is set
	playSound("entity.generic.burn", 1, 0.5, location of projectile)
	set {_face} to getKitStats(shooter, "ArrowRain")
	set {_baseloc} to location of block at projectile
	set {_y} to y-coordinate of {_baseloc}
	set y-coordinate of {_face} to {_y}
	set {_dis} to distance between location of {_face} and location of {_baseloc}
	loop 10 times:
		set {_n} to loop-number
		set {_cloud} to location of block {_n} meters upwards {_baseloc}
		spawnParticle(cloud, {_cloud})
		wait a tick
	playSound("entity.lightning_bolt.thunder", 1, 0.5, {_cloud})
	loop 10 times:
		set {_n} to loop-number
		set {_plus} to {_dis} + {_n}
		set {_sky} to location of block {_plus} meters in front of {_face}
		set y-coordinate of {_sky} to {_y}
		add 10 to y-coordinate of {_sky}
		loop all blocks in radius 5 around {_sky}:
			y-coordinate of loop-block is y-coordinate of {_sky}
			loop-block is air
			add location of loop-block to {_rain::*}
		loop 5 times:
			set {_cloud} to {_sky}
			set {_x} to a random number between -1 and 1
			set {_z} to a random number between -1 and 1
			add {_x} to x-coordinate of {_cloud}
			add {_z} to z-coordinate of {_cloud}
			spawnParticle(cloud, {_cloud})
			loop {_rain::*}:
				chance of 5%:
					shoot an arrow from block at loop-value-3 at speed 3 downward
					set metadata value "arrowdamage" of shot arrow to 2
					setShooter(shot arrow, shooter)
					set metadata value "arrowrain" of shot arrow to "%shooter%"
					kitParticle_ArrowRain_Dust(loop-value-3)
					kitParticle_ArrowRain_Spell(shot entity)
			loop all projectiles in radius 5 around shooter:
				loop-projectile is an arrow
				metadata value "arrowrain" of loop-projectile is "%shooter%"
				delete loop-projectile
			wait a tick
		delete {_rain::*}

function kitParticle_ArrowRain_Dust(loc: location):
	loop 4 times:
		spawnParticle_Dust(rgb(255, 255, 255), 2, {_loc})
		wait a tick

function kitParticle_ArrowRain_Spell(entity: entity):
	loop 10 times:
		spawnParticle_Color(entity_effect, rgb(0, 0, 255), {_entity})
		wait a tick