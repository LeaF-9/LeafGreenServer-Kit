# Holy Bow

on shoot:
	metadata value "gametype" of shooter is "Kit"
	{Class::%shooter%} is "Archer"
	set {_tool} to shooter's tool
	kitItemAllChecker({_tool}, "Archer", "Weapon", 2) is true
	shooter is sneaking
	set {_pow} to getBowDrawTime(shooter)
	{_pow} >= 24
	set {_arrow} to amount of arrow in shooter's inventory
	{_arrow} >= 3
	loop 2 times:
		remove an arrow from shooter
	set glowing of projectile to true
	set metadata value "arrowdamage" of shot arrow to 3
	set metadata value "holy bow" of projectile to true
	# --- 音の演出 ---
	playSound("block.amethyst_cluster.break", 1, 0.5, shooter)
	# --------------
	# --- パーティクルの演出 ---
	kitParticle_HolyBow_Arrow(projectile)
	# -----------------------
	set {_enable} to true
	loop 200 times:
		if metadata value "holy bow" of projectile is not true:
			set {_enable} to false
		if {_enable} is not true:
			stop

		set {_loc} to location of projectile
		loop all entities in radius 3 around {_loc}:
			kitCheckTeam(loop-entity, shooter, false, true) is true
			{_hit.%id of loop-entity%} is not set
			set {_hit.%id of loop-entity%} to true
			kitDamage(shooter, loop-entity, 2, "神聖の矢撃", true)
			kitPalsy(loop-entity, 60)
			kitParticle_HolyBow_Hit(shooter, loop-entity)
			playSound("block.amethyst_block.resonate", 1, 1.5, shooter, true)
		wait a tick

local function kitParticle_HolyBow_Arrow(proj: projectile):
	set {_enable} to true
	wait a tick
	while {_enable} is true:
		if metadata value "holy bow" of {_proj} is false:
			delete {_enable}
			continue
		
		set {_loc} to location of {_proj}
		if {_proloc} is set:
			if {_proloc} is {_loc}:
				delete {_enable}
				continue
			
			set {_fromloc} to setDirToVec(location 0.5 meters above {_proloc}, setVector({_proloc}, {_loc}))
			set {_distance} to distance between {_proloc} and {_loc}
			set {_loopnum} to ceil({_distance} * 1)
			set {_m} to 0
			loop {_loopnum} times:
				add 1 to {_m}
				set {_baseel} to location {_m} meters in front of {_fromloc}
				kitParticle_HolyBow_Circle({_baseel})
				loop a random integer between 0 and 3 times:
					set {_el} to {_baseel}
					add a random number between -3 and 3 to x-pos of {_el}
					add a random number between -3 and 3 to y-pos of {_el}
					add a random number between -3 and 3 to z-pos of {_el}
					spawnParticle(wax_off, {_el})

		set {_proloc} to location of {_proj}
		wait a tick

local function kitParticle_HolyBow_Circle(loc: location):
	set {_radius} to 0
	loop 3 times:
		add 1 to {_radius}
		spawnParticle_DustTransition(colorRandomizer(255, 247, 173, 0, 0, 20), rgb(255, 255, 255), 0.75, particleLocation_CircleV({_loc}, 30, {_radius}))
		wait a tick

local function kitParticle_HolyBow_Hit(player: player, entity: entity):
	playSound("entity.allay.item_thrown", 1, 1.35, {_entity})
	set {_baseloc} to location 1 meter above {_entity}
	set yaw of {_baseloc} to yaw of {_player}
	set pitch of {_baseloc} to pitch of {_player}
	set {_radius} to 0
	loop 4 times:
		add 0.5 to {_radius}
		spawnParticle_DustTransition(colorRandomizer(255, 243, 103, 0, 0, 50), rgb(255, 243, 203), 1, particleLocation_CircleV({_baseloc}, 60, {_radius}))
		wait a tick

on projectile hit:
	metadata value "holy bow" of projectile is true
	set metadata value "holy bow" of projectile to false
	set {_baseloc} to location of projectile
	strike lightning effect at projectile
	# --- 着弾点がはじけ飛ぶ演出 ---
	loop 30 times:
		set {_v} to randomVector(5)
		add 5 to y of {_v}
		spawnParticle_Vector(wax_on, {_v}, 10, {_baseloc})
	# --------------------------

on damage:
	damage was caused by projectile
	metadata value "holy bow" of projectile is set
	kitPalsy(victim, 100)
	applyEffect(victim, glowing, 1, 5 seconds)