# Curse Bow

on damage with priority LOW:
	damage was caused by projectile
	metadata value "cursebow" of projectile is "true"
	setDamageModification_ADD(victim, 2)
	apply wither 2 to victim for 5 seconds
	# --- 音の演出 ---
	playSound("entity.warden.ambient", 1, 1.5, victim)
	# ---------------
	# --- パーティクルの演出 ---
	set {_l} to location of head of victim
	loop 10 times:
		set {_v} to randomVector({_l})
		spawnParticle_Vector(squid_ink, {_v}, 1, {_l})
	# -----------------------



function kitEffect_CurseBow(player: player):
	if {_player} does not have potion bad luck:
		kitLuck({_player}, -1)
	if {_player} has arrow:
		load yml "plugins/skript-yaml/Kit Classes/Archer.yml" as "cursearrow.%{_player}%"
		set {_CurseArrow} to yml value "kititems.Curse Arrow" from "cursearrow.%{_player}%"
		unload yml "cursearrow.%{_player}%"
		set {_amount} to amount of arrow in {_player}'s inventory
		set {_amoweak} to amount of {_CurseArrow} in {_player}'s inventory
		add {_amoweak} to {_amount}
		set {_s} to 35
		set {_a} to 0
		remove all arrow from {_player}
		loop {_amount} times:
			add 1 to {_a}
			set slot {_s} of {_player} to {_a} of {_CurseArrow}
			if {_a} is 64:
				subtract 1 from {_s}
				set {_a} to 0

function kitEffect_CurseBow_Shoot(player: player, event: object):
	metadata value "gametype" of {_player} is "Kit"
	{Class::%{_player}%} is "Archer"
	{_player} is sneaking

	set {_projectile} to {_event}.getEntity()

	damage {_player} by 1 heart
	set metadata value "cursebow" of {_projectile} to "true"
	# --- 音の演出 ---
	playSound("entity.warden.tendril_clicks", 1, 1.25, {_player})
	# ---------------
	# --- パーティクルの演出 ---
	kitParticle_CurseBow_Arrow({_projectile})
	# -----------------------



local function kitParticle_CurseBow_Arrow(projectile: projectile):
	loop 20 times:
		set {_loc} to location of {_projectile}
		set {_ts} to a random integer between 10 and 50
		spawnParticle_DustTransition(rgb(0, 0, 0), rgb({_ts}, 0, {_ts}), 5, {_loc})
		wait a tick