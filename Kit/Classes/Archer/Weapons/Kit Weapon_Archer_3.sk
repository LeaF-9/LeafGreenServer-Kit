# Mechanical Bow

on shoot:
	metadata value "gametype" of shooter is "Kit"
	{Class::%shooter%} is "Archer"
	set {_tool} to shooter's tool
	if kitItemAllChecker({_tool}, "Archer", "Weapon.main", 3) is true:
		{_tool} is enchanted with flame
		if {Stats.%shooter%::MechanicalBow} < 10:
			set metadata value "mechanicalbow" of projectile to "true"
			drawDot count 1, particle "flame", center projectile, visibleRange 32, keepFor 1 seconds
			set {_to} to vector of location 100 meters in front of shooter
			set {_from} to vector of location of shooter
			set {_vector} to {_to} -- {_from}
			set {_vector} to normalized {_vector}
			set x of {_vector} to x of {_vector} * 10
			set y of {_vector} to y of {_vector} * 10
			set z of {_vector} to z of {_vector} * 10
			set velocity of projectile to {_vector}
			add 1 to {Stats.%shooter%::MechanicalBow}
			if {Stats.%shooter%::MechanicalBow} >= 10:
				ignite shooter for 10 seconds
				kitMsg(shooter, "<##d70035>§lオーバーヒートした!")
				loop all items in shooter's inventory:
					name of loop-item is "§8Mechanical Bow"
					if loop-item is enchanted with flame:
						disenchant loop-item
						enchant loop-item with quick charge 2
		else:
			cancel event

on leftclick holding a crossbow:
	{Class::%player%} is "Archer"
	set {_tool} to player's tool
	kitItemAllChecker({_tool}, "Archer", "Weapon.main", 3) is true
	cancel event
	if checkCT(player, name of player's tool) is true:
		setCT(player, name of player's tool, 30, false)
		playSound("entity.iron_golem.hurt", 1, 2, player)
		kitMsg(player, "<##8da0b6>ブースターを起動しました!")
		set {Stats.%player%::MechanicalBow} to 0
		enchant player's tool with flame
		enchant player's tool with quick charge 5
		loop 100 times:
			delete {_ok}
			loop all items in player's inventory:
				name of loop-item is "§8Mechanical Bow"
				if loop-item is enchanted with flame:
					set {_ok} to true
			if {_ok} is true:
				drawDot count 1, particle "lava", center player, visibleRange 30
			wait a tick
		loop all items in player's inventory:
			name of loop-item is "§8Mechanical Bow"
			if loop-item is enchanted with flame:
				disenchant loop-item
				enchant loop-item with quick charge 2
				kitMsg(player, "<##c9caca>ブースターが終了しました")
		delete {Stats.%player%::MechanicalBow}

on damage:
	type of attacker is player
	damage was caused by projectile
	if metadata value "mechanicalbow" of projectile is "true":
		set damage to 3