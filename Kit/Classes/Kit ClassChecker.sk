import:
	org.bukkit.NamespacedKey
	org.bukkit.persistence.PersistentDataType

# Item tag Checker
function kitItemKitChecker(skill: item) :: text:
	set {_checkkey} to "loadout-kit"
	set {_kit} to getNamespacedKey({_skill}, {_checkkey}, "string")
	return {_kit}

function kitItemTypeChecker(skill: item) :: text:
	set {_checkkey} to "loadout-type"
	set {_type} to getNamespacedKey({_skill}, {_checkkey}, "string")
	return {_type}

function kitItemNumberChecker(skill: item) :: number:
	set {_checkkey} to "loadout-number"
	set {_num} to getNamespacedKey({_skill}, {_checkkey}, "integer")
	return {_num}

function kitItemAllChecker(skill: item, kit: text, type: text, num: number) :: boolean:
	if kitItemKitChecker({_skill}) is {_kit}:
		if kitItemTypeChecker({_skill}) is {_type}:
			if kitItemNumberChecker({_skill}) is {_num}:
				return true
	return false

function kitItemTagChanger(skill: item, kit: text, type: text, num: number) :: item:
	set {_ItemMeta} to {_skill}.getItemMeta()

	set {_key} to "loadout-kit"
	set {_Namespacedkey} to new NamespacedKey("leafgreen", {_key})
	{_ItemMeta}.getPersistentDataContainer().set({_Namespacedkey}, PersistentDataType.STRING, {_kit})

	set {_key} to "loadout-type"
	set {_Namespacedkey} to new NamespacedKey("leafgreen", {_key})
	{_ItemMeta}.getPersistentDataContainer().set({_Namespacedkey}, PersistentDataType.STRING, {_type})

	set {_key} to "loadout-number"
	set {_Namespacedkey} to new NamespacedKey("leafgreen", {_key})
	set {_num} to {_num}.intValue()
	{_ItemMeta}.getPersistentDataContainer().set({_Namespacedkey}, PersistentDataType.INTEGER, {_num})

	{_skill}.setItemMeta({_ItemMeta})
	return {_skill}

# Player Choosing Checker
function kitSelectChecker_Weapon(player: player, kit: text) :: number:
	set {_uuid} to UUID of {_player}
	load yml "plugins/skript-yaml/Kit Players/%{_uuid}%.yml" as "player.%{_player}%.selectchecker"
	set {_num} to yml value "custom.%{_kit}%.weapon" from "player.%{_player}%.selectchecker"
	unload yml "player.%{_player}%.selectchecker"
	return {_num}

function kitSelectChecker_Skill(player: player, kit: text) :: numbers:
	set {_uuid} to UUID of {_player}
	load yml "plugins/skript-yaml/Kit Players/%{_uuid}%.yml" as "player.%{_player}%.selectchecker"
	loop 3 times:
		set {_num} to yml value "custom.%{_kit}%.skill.%loop-number%" from "player.%{_player}%.selectchecker"
		add {_num} to {_numlist::*}
	return {_numlist::*}