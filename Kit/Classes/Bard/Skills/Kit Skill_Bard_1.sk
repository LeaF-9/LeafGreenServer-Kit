# skill-name: Sweet Serenade

function kitSkill_Bard_1(player: player):
	if {Stats.%{_player}%::SkillTarget} is not set:
		send action bar "§c§l対象を指定してください" to {_player}
		stop
	set {_target} to {Stats.%{_player}%::SkillTarget}
	if {_target} is {Stats.%{_player}%::SweetSerenade}:
		kitMsg({_player}, "§c§l現在演奏中の味方に対して付与することはできません")
		stop
	toolCT({_player}, "Bard", 1)
	kitEffect_SweetSerenade({_player}, {_target})



local function kitEffect_SweetSerenade(player: player, target: target):
	kitMsg({_player}, "§d%{_target}% §cに§4ハートの小夜曲§cを捧げます...")
	message formatted "§b%{_player}% §dがハートの小夜曲を演奏してくれた!" to {_target}
	set {Stats.%{_player}%::SweetSerenade} to {_target}
	setSupporter({_target}, {_player})
	set {_timer} to 0
	while {Stats.%{_player}%::SweetSerenade} is {_target}:
		set {_distance} to distance between {_target} and {_player}
		if {_distance} > 10:
			delete {Stats.%{_player}%::SweetSerenade}
			continue
		add 1 to {_timer}
		if {_target} does not have absorption:
			set {_tier} to 1
			if {Stats.%{_player}%::Fortissimo} is true:
				set {_tier} to 2
			applyEffect({_target}, absorption, {_tier}, 3 seconds)
		if {_timer} >= 20:
			set {_timer} to 0
			kitHeal({_target}, 0.5, 1)
		wait a tick
	kitMsg({_player}, "§9%{_target}% §5への小夜曲は途切れた...")
	message formatted "§7小夜曲が聴こえなくなりました" to {_target}