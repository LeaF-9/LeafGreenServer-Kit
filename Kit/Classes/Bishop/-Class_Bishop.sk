options:
	prefix: §2[%{CLS::%player%}%]
	attackprefix: §2[%{CLS::%attacker%}%]
	victimprefix: §2[%{CLS::%victim%}%]
	arg2prefix: §2[%{CLS::%arg 2%}%]
	loopprefix: §2[%{CLS::%loop-player%}%]

# Bishopのスキル Ver1.0.0
#AroundResurrection の効果はGhostに記載

on rightclick holding a ghast tear:
	name of player's tool is "§6God Mercy"
	if {CT.%player%::%name of player's tool%} is not set:
		set {_tg} to target player
		if {_tg} is set:
			if {team::%{_tg}%} is {team::%player%}:
				if distance between {_tg} and player <= 10:
					if {CLS::%{_tg}%} is not "BSK":
						if {Resurrection::%{_tg}%} is not set:
							if {Stats.%player%::GodMercy} is set:
								delete {Resurrection::%{Stats.%player%::GodMercy}%}
								message "§c蘇生能力が消え去りました..." to {Stats.%player%::GodMercy}
								delete {Stats.%player%::GodMercy}
							set {Stats.%player%::GodMercy} to {_tg}
							add player to {supporter.%{_tg}%::*}
							set {CT.%player%::%name of player's tool%} to 40
							set {Resurrection::%{_tg}%} to true
							drawDot count 5, particle "happyvillager", center location of block above {_tg}, visibleRange 30
							message "{@prefix} §b%{_tg}% §6に蘇生能力を付与しました!"
							message "§d%player% §bに蘇生能力を付与してもらった!" to {_tg}
							play raw sound "mob.ghast.moan" at {_tg} with pitch 5 volume 1
						else:
							set action bar of player to "§b§l%{_tg}% §c§lは既に復活能力を所持しています..."
					else:
						set action bar of player to "§b§l%{_tg}% §c§lは恩恵を受け付けないプレイヤーの様です..."
				else:
					set action bar of player to "§c§l距離が遠すぎます!"
			else:
				set action bar of player to "§c§l味方プレイヤーにのみ発動可能です"
		else:
			set action bar of player to "§c§l対象のプレイヤーがいません!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on death of player:
	{Stats.%victim%::GodMercy} is set
	{Resurrection::%{Stats.%victim%::GodMercy}%} is set
	delete {Resurrection::%{Stats.%victim%::GodMercy}%}
	message "§c蘇生能力が消え去りました..." to {Stats.%victim%::GodMercy}
	delete {Stats.%victim%::GodMercy}

on rightclick holding a fermented spider eye:
	name of player's tool is "§dGrant Blessing"
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 40
		message "{@prefix} §d1秒毎に目先のプレイヤーを回復します..."
		play raw sound "fireworks.blast" at player with pitch 2 volume 1
		loop 10 times:
			set {_tg} to target player
			if {_tg} is set:
				if {team::%{_tg}%} is {team::%player%}:
					if {CLS::%{_tg}%} is not "BSK":
						if {_tg} is alive:
							add player to {supporter.%{_tg}%::*}
							heal {_tg} by 1 heart
							message "{@prefix} §b%{_tg}% §dを癒やしました!"
							message "§e%player% §dに回復してもらった!" to {_tg}
							play raw sound "fireworks.twinkle" at {_tg} with pitch 30 volume 1
							drawDot count 1, particle "heart", center location of block 2 above {_tg}, visibleRange 30, pulseDelay 0
						else:
							set action bar of player to "§e回復不可"
					else:
						set action bar of player to "§e回復不可"
				else:
					set action bar of player to "§e回復不可"
			else:
				set action bar of player to "§c回復失敗"
			delete {_tg}
			wait a second
		message "{@prefix} §6効果が切れました"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a nether star:
	name of player's tool is "§9Refine Evil"
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 50
		message "{@prefix} §d全ての味方にダメージを受けた際に回復する能力を付与します!"
		play raw sound "portal.travel" at player with pitch 30 volume 1
		loop all players:
			{team::%loop-player%} is {team::%player%}
			loop-player is not player
			{CLS::%loop-player%} is not "BSK"
			set {Stats.%loop-player%::RefineEvil} to true
			message "§dダメージを受けた際に回復能力が付与されます!" to loop-player
			drawDot count 5, particle "mobspell", RGB 255, 105, 180, center loop-player, visibleRange 30, keepFor 10 seconds
			add loop-player to {_TEN::*}
		wait 7 seconds
		loop {_TEN::*}:
			delete {Stats.%loop-value%::RefineEvil}
			message "§5回復能力が削除されました" to loop-value
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on damage of player:
	{Stats.%victim%::RefineEvil} is true
	apply regeneration 2 to victim for 3 seconds

on rightclick holding a eye of ender:
	name of player's tool is "§2Emergency Salvation"
	cancel event
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 40
		message "{@prefix} §c体力3以下のプレイヤーを保護します!"
		play raw sound "random.splash" at player with pitch 30 volume 1
		loop all players:
			{team::%loop-player%} is {team::%player%}
			loop-player is not player
			{CLS::%loop-player%} is not "BSK" or "VAM"
			loop-player's health <= 3
			add player to {supporter.%loop-player%::*}
			teleport loop-player to player
			apply absorption 5 to loop-player for 10 seconds
			apply regeneration 1 to loop-player for 10 seconds
			drawDot count 1, particle "heart", center loop-player, visibleRange 30, pulseDelay 0, keepFor 200 ticks
			message "§a%player% §6が応急処置を施してくれた!" to loop-player
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on rightclick holding a feather:
	name of player's tool is "§fAngel Vow"
	if {CT.%player%::%name of player's tool%} is not set:
		set {CT.%player%::%name of player's tool%} to 60
		message "{@prefix} §f空を飛んで周辺の味方をサポートします!"
		play raw sound "mob.ghast.scream" at player with pitch 2 volume 1
		allow fly for player
		drawWings style 6, particle1 "redstone", RGB 255, 255, 255, particle2 "redstone", RGB2 179, 255, 255, particle3 "mobspell", RGB3 255, 244, 148, center player, id "%player%", angle 110, height 0, space 0.2, visibleRange 30, tps 0, second 3
		set max health of player to 3
		apply mining fatigue 3 to player for 10 seconds
		set {Stats.%player%::AngelVow} to true
		loop 200 times:
			if {Stats.%player%::AngelVow}:
				loop all players in radius 7 around player:
					{team::%loop-player%} is {team::%player%}
					loop-player is not player
					{CLS::%loop-player%} is not "BSK"
					drawDot count 1, particle "mobspell", RGB 255, 255, 148, center loop-player, visibleRange 30
					apply regeneration 1 to loop-player for 1 tick
					apply resistance 1 to loop-player for 1 tick
				wait a tick
		if player is alive:
			set max health of player to 10
			heal player by 10 hearts
		disallow fly for player
		delete {Stats.%player%::AngelVow}
		stopEffect "%player%"
		message "{@prefix} §8天使の加護は消滅した!"
	else:
		set action bar of player to "§c§lクールタイムが終わっていません!"

on damage:
	if {Stats.%victim%::AngelVow} is true:
		cancel event
	if {Stats.%attacker%::AngelVow} is true:
		cancel event
	type of victim is a player
	loop all players in radius 5 around victim:
		{team::%loop-player%} is {team::%victim%}
		victim is not loop-player
		{Stats.%loop-player%::AngelVow} is true
		subtract 1 from loop-player's health