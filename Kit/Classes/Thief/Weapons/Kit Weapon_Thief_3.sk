# Weapon-Name: Mystic Card

function kitWeapon_Thief_3(player: player):
	subtract 1 from item amount of tool of {_player}
	kitEffect_MysticCard_Throw({_player})

function kitEffect_MysticCard_Throw(player: player):
	set {_baseloc} to location of {_player}
	set pitch of {_baseloc} to 0
	spawn an armor stand at {_baseloc} with nbt compound from "{Invisible:1b,Invulnerable:1b,NoBasePlate:1b,NoGravity:1b,ShowArms:1b,ArmorItems:[{},{},{},{}],HandItems:[{id:""dune_armor_trim_smithing_template"",Count:1b},{}],Pose:{RightArm:[0f,0f,270f]}}"
	set {_entity} to spawned armor stand
	set metadata value "mystic-card" of {_entity} to false
	# 発射音
	playSound("entity.arrow.shoot", 1, 1.6, {_player})
	set {_loc} to {_baseloc}
	loop 60 times:
		if metadata value "mystic-card" of {_entity} is true:
			continue
		add 36 to {_spin}
		if {_target} is not set:
			# プレイヤー感知
			loop all players in radius 5 around {_loc}:
				kitCheckTeam({_player}, loop-player, false) is true
				{_target} is not set
				set {_target} to loop-player
				kitParticle_MysticCard_Target({_loc})
		if {_target} is set:
			set {_vec} to setVector({_loc}, location of head of {_target})
			set {_newloc} to setDirToVec({_loc}, {_vec})
			set {_loc} to forceTurnAngle({_loc}, {_newloc}, 10)
		set {_loc} to location 1 meter in front of {_loc}
		teleport {_entity} to {_loc}
		kitParticle_MysticCard_Trail({_loc})
		set {_nbt} to nbt compound of {_entity}
		add nbt from "{RightArm:[0f,%{_spin}%f,270f]}" to {_nbt}
		loop all entities in radius 2 around {_loc}:
			metadata value "mystic-card" of {_entity} is false
			kitCheckTeam(loop-entity, {_player}, false) is true
			set metadata value "mystic-card" of {_entity} to true
			kitDamage({_player}, loop-entity, 2)
			if "%type of loop-entity%" is "player":
				kitTrail(loop-entity, 5 seconds, location of {_player})
		wait a tick
	kitParticle_MysticCard_Hit({_loc})
	loop all entities:
		loop-entity is {_entity}
		delete loop-entity

function kitEffect_MysticCard_Restore(player: player, timer: number):
	if mod({_timer}, 5) is not 0:
		stop
	loop all items in {_player}'s inventory:
		name of loop-item contains "Mystic Card"
		set {_hi} to loop-item
	if {_hi} is set:
		if item amount of {_hi} >= 3:
			stop
	set {_tool} to kitWeaponItem("Thief", 3, "main")
	set {_item} to kitItemTagChanger({_tool}, "Thief", "Weapon.main", 3)
	set item amount of {_item} to 1
	give {_item} to {_player}
	playSound("entity.item.pickup", 1, 1, {_player}, true)

function kitParticle_MysticCard_Target(loc: location):
	playSound("entity.phantom.flap", 1.2, 1.15, {_loc})
	loop 5 times:
		set {_el} to {_loc}
		add a random number between -0.5 and 0.5 to x-pos of {_el}
		add a random number between -0.5 and 0.5 to y-pos of {_el}
		add a random number between -0.5 and 0.5 to z-pos of {_el}
		spawnParticle_Color(entity_effect, 112, 108, 170, {_el})

function kitParticle_MysticCard_Trail(loc: location):
	spawnParticle_Vector(end_rod, randomVector(), 0.2, {_loc})

function kitParticle_MysticCard_Hit(loc: location):
	loop 10 times:
		spawnParticle_Vector(end_rod, randomVector(), 0.5, {_loc})