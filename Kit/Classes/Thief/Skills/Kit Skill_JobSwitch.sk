# Job Switch

function kitEffect_JobSwitch(player: player):
	set {_tool} to {_player}'s tool
	nameCT({_player}, "Job Switch")
	kitMsg({_player}, "§2光線を発射します...")
	set {_baseloc} to head of {_player}
	set {_axisVector} to getVectorFromRollAxis({_baseloc})
	set {_rotateVector} to getVectorFromYawAxis({_baseloc}, 0.5)

	loop 30 times:
		{_tg} is not set
		add 1 to {_front}
		add 40 to {_rad}
		set {_loc} to location {_front} meters in front of {_baseloc}
		loop all players in radius 2 around {_loc}:
			kitCheckTeam(loop-player, {_player}, false, true) is true
			{_tg} is not set
			set {_tg} to loop-player
		playSound("entity.experience_orb.pickup", 1, 0.05 * {_front}, {_loc})
		spawnParticle(happy_villager, {_loc})
	# 円を描くパーティクル ##########
		set {_vec} to {_rotateVector}
		rotate {_vec} around {_axisVector} by {_rad}
		set {_eb} to {_loc} ~ {_vec}
	
		spawnParticle(happy_villager, {_eb})
	# #############################
		wait a tick
	if {_tg} is not set:
		stop
	playSound("entity.player.levelup", 1, 1, {_tg})
	playSound("entity.player.levelup", 1, 1, {_player})
	kitEffectDescription({_tg}, "§7§l窃盗")
	sendTimeBossbar_GREEN({_player}, "Job Switch", 200)
	sendTimeBossbar_RED({_tg}, "§7§l窃盗", 60)
	kitMsg({_player}, "§6%{_tg}% §aの職を奪った!")
	message formatted "§3職を奪われてしまった!" to {_tg}
	set {_class} to {Class::%{_tg}%}
	setKitStats({_tg}, "Stolen.Job Switch", true)
	# Needyにする処理 ##############
	set {_th} to health of {_tg}
	execute console command "/skclass %{_tg}% Kit-Needy"
	set health of {_tg} to {_th}
	# Kitを変更する処理 ########
	execute console command "/kjc %{_player}%"
	execute console command "/class %{_class}% %{_player}% false"
	setKitStats({_player}, "Stealing.Job Switch", true)
	# #############################
	wait 3 seconds
	if getKitStats({_tg}, "Stolen.Job Switch") is true:
		execute console command "/class %{_class}% %{_tg}% false"
		deleteKitStats({_tg}, "Stolen.Job Switch")
	wait 7 seconds
	if getKitStats({_player}, "Stealing.Job Switch") is true:
		execute console command "/kjc %{_player}%"
		execute console command "/class Thief %{_player}% false"
		deleteKitStats({_player}, "Stealing.Job Switch")