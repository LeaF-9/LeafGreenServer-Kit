# Job Switch

function kitEffect_JobSwitch(player: player):
	set {_tool} to {_player}'s tool
	nameCT({_player}, "Job Switch")
	kitMsg({_player}, "§2光線を発射します...")
	set {_baseloc} to location of head of {_player}
	loop 30 times:
		{_tg} is not set
		add 1 to {_front}
		add 40 to {_rad}
		set {_loc} to location {_front} meters in front of {_baseloc}
		loop all players in radius 2 around {_loc}:
			kitCheckTeam(loop-player, {_player}, false, true) is true
			{_tg} is not set
			set {_tg} to loop-player
		playSound("entity.experience_orb.pickup", 1, 0.05 * {_front}, {_loc})
		spawnParticle(happy_villager, {_loc})
	# 円を描くパーティクル ##########
		set {_eb} to {_loc}
		set {_m1} to {_eb}
		set {_p} to pitch of {_m1}
		if {_p} < 0:
			set {_p} to -1 * 90 - {_p}
			set {_y} to yaw of {_m1}
			add 180 to {_y}
			if {_y} >= 360:
				set {_y} to {_y} - 360
			set yaw of {_m1} to {_y}
		else:
			set {_p} to {_p} - 90
		set pitch of {_m1} to {_p}
		set {_test1} to 0.5 * cos({_rad})
		set {_t1} to location {_test1} meters in front of {_m1}
		set {_m2} to {_eb}
		set pitch of {_m2} to 0
		set {_test2} to 0.5 * sin({_rad})
		set {_t2} to location {_test2} meters to the right of {_m2}
		set x-coordinate of {_eb} to x-coordinate of {_t1} + x-coordinate of {_t2} - x-coordinate of {_eb}
		set y-coordinate of {_eb} to y-coordinate of {_t1} + y-coordinate of {_t2} - y-coordinate of {_eb}
		set z-coordinate of {_eb} to z-coordinate of {_t1} + z-coordinate of {_t2} - z-coordinate of {_eb}
		spawnParticle(happy_villager, {_eb})
	# #############################
		wait a tick
	if {_tg} is not set:
		stop
	playSound("entity.player.levelup", 1, 1, {_tg})
	playSound("entity.player.levelup", 1, 1, {_player})
	kitEffectDescription({_tg}, "§7§l窃盗")
	sendTimeBossbar_GREEN({_player}, "Job Switch", 200)
	sendTimeBossbar_RED({_tg}, "§7§l窃盗", 60)
	kitMsg({_player}, "§6%{_tg}% §aの職を奪った!")
	message formatted "§3職を奪われてしまった!" to {_tg}
	set {_class} to {Class::%{_tg}%}
	kitStats_SET({_tg}, "Stolen.Job Switch", true)
	# Needyにする処理 ##############
	set {_th} to health of {_tg}
	execute console command "/skclass %{_tg}% Kit-Needy"
	set health of {_tg} to {_th}
	# Kitを変更する処理 ########
	execute console command "/kjc %{_player}%"
	execute console command "/class %{_class}% %{_player}% false"
	kitStats_SET({_player}, "Stealing.Job Switch", true)
	# #############################
	wait 3 seconds
	if kitStats_GET({_tg}, "Stolen.Job Switch") is true:
		execute console command "/class %{_class}% %{_tg}% false"
		kitStats_DELETE({_tg}, "Stolen.Job Switch")
	wait 7 seconds
	if kitStats_GET({_player}, "Stealing.Job Switch") is true:
		execute console command "/kjc %{_player}%"
		execute console command "/class Thief %{_player}% false"
		kitStats_DELETE({_player}, "Stealing.Job Switch")