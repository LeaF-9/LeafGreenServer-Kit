# Skill-Name: Perfect Robbery

function kitEffect_PerfectRobbery(player: player):
	set {_SkillTarget} to getKitStats({_player}, "SkillTarget")
	if {_SkillTarget} is not set:
		send action bar "§c§l対象が存在しません!" to {_player}
		stop
	set {_tool} to {_player}'s tool
	set {_tg} to {_SkillTarget}
	if {TrueTeam::%{_tg}%} is {TrueTeam::%{_player}%}:
		set action bar of {_player} to "§c§l同じチームのプレイヤーからは盗めません!"
		stop
	nameCT({_player}, "Perfect Robbery")
	if {Class::%{_tg}%} is "Werewolf":
		if getKitStats({_tg}, "Instinct") is true:
			set {_type} to "Werewolf_Instinct"
		else:
			set {_type} to {Class::%{_tg}%}
	else if {Class::%{_tg}%} is "Vampire":
		if getKitStats({_tg}, "BatMode") is true:
			set {_type} to "Vampire_Bat"
		else:
			set {_type} to {Class::%{_tg}%}
	else:
		set {_type} to {Class::%{_tg}%}
	loop 3 times:
		set {_skill} to kitPlayerSkillItem({_tg}, {_type}, loop-number)
		add {_skill} to {_skills::*}
	loop {_skills::*}:
		inventory of {_tg} contains loop-value
		add loop-value to {_own::*}
	if size of {_own::*} is 0:
		kitMsg({_player}, "§8スキルを上手く盗めませんでした...")
		stop
	playSound("entity.item.pickup", 1, 2, {_player})
	kitEffectDescription({_tg}, "§7§l窃盗")
	sendTimeBossbar_GREEN({_player}, "Perfect Robbery", 140)
	sendTimeBossbar_RED({_tg}, "§7§l窃盗", 140)
	kitMsg({_player}, "§6%{_tg}% §bのスキルを全部しまいこんだ!")
	message formatted "§dスキルを全部盗まれてしまった!" to {_tg}
	setKitStats({_tg}, "Stolen.Perfect Robbery", true)
	setKitStats({_player}, "Stealing.Perfect Robbery", true)
	loop {_own::*}:
		remove loop-value from {_tg}

		set {_stolenskill} to setNamespacedKey(loop-value, "stolen", "string", "Perfect Robbery")
		add {_stolenskill} to {_giveitems::*}
	set {StealItem-Perfect Robbery.%{_player}%::*} to {_giveitems::*}
	remove {_tool} from {_player}
	wait a tick
	give {_giveitems::1} to {_player}
	wait 7 seconds
	if getKitStats({_tg}, "Stolen.Perfect Robbery") is true:
		loop 3 times:
			set {_skill} to kitPlayerSkillItem({_tg}, {_type}, loop-number)
			give {_skill} to {_tg}
		deleteKitStats({_tg}, "Stolen.Perfect Robbery")
	if getKitStats({_player}, "Stealing.Perfect Robbery") is true:
		loop {_giveitems::*}:
			remove loop-value from {_player}
		give {_tool} to {_player}
		delete {StealItem-Perfect Robbery.%{_player}%::*}
		deleteKitStats({_player}, "Stealing.Perfect Robbery")