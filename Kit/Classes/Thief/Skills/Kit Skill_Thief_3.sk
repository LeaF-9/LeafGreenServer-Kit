# Skill-Name: Perfect Steal

function kitSkill_Thief_3(player: player):
	set {_SkillTarget} to kitStats_GET({_player}, "SkillTarget")
	if {_SkillTarget} is not set:
		send action bar "§c§l対象が存在しません!" to {_player}
		stop
	set {_tool} to {_player}'s tool
	set {_tg} to {_SkillTarget}
	if {TrueTeam::%{_tg}%} is {TrueTeam::%{_player}%}:
		set action bar of {_player} to "§c§l同じチームのプレイヤーからは盗めません!"
		stop
	toolCT({_player}, "Thief", 3)
	if {Class::%{_tg}%} is "Werewolf":
		if kitStats_GET({_tg}, "Instinct") is true:
			set {_type} to "Werewolf_Instinct"
		else:
			set {_type} to {Class::%{_tg}%}
	else if {Class::%{_tg}%} is "Vampire":
		if kitStats_GET({_tg}, "BatMode") is true:
			set {_type} to "Vampire_Bat"
		else:
			set {_type} to {Class::%{_tg}%}
	else:
		set {_type} to {Class::%{_tg}%}
	loop 3 times:
		set {_skill} to kitPlayerSkillItem({_tg}, {_type}, loop-number)
		add {_skill} to {_skills::*}
	loop {_skills::*}:
		inventory of {_tg} contains loop-value
		add loop-value to {_own::*}
	if size of {_own::*} is 0:
		kitMsg({_player}, "§8スキルを上手く盗めませんでした...")
		stop
	playSound("entity.item.pickup", 1, 2, {_player})
	kitEffectDescription({_tg}, "§7§l窃盗")
	sendTimeBossbar_GREEN({_player}, "Perfect Robbery", 140)
	sendTimeBossbar_RED({_tg}, "§7§l窃盗", 140)
	kitMsg({_player}, "§6%{_tg}% §bのスキルを全部しまいこんだ!")
	message formatted "§dスキルを全部盗まれてしまった!" to {_tg}
	kitStats_SET({_tg}, "Stolen.Perfect Robbery", true)
	kitStats_SET({_player}, "Stealing.Perfect Robbery", true)
	loop {_own::*}:
		remove loop-value from {_tg}
		set {_nbt} to nbt compound of loop-value
		set string tag "stolen" of {_nbt} to "Perfect Robbery"
		set {_stolenskill} to loop-value with custom nbt {_nbt}
		add {_stolenskill} to {_giveitems::*}
	set {StealItem-Perfect Robbery.%{_player}%::*} to {_giveitems::*}
	remove {_tool} from {_player}
	wait a tick
	give {_giveitems::1} to {_player}
	wait 7 seconds
	if kitStats_GET({_tg}, "Stolen.Perfect Robbery") is true:
		loop 3 times:
			set {_skill} to kitPlayerSkillItem({_tg}, {_type}, loop-number)
			give {_skill} to {_tg}
		kitStats_DELETE({_tg}, "Stolen.Perfect Robbery")
	if kitStats_GET({_player}, "Stealing.Perfect Robbery") is true:
		loop {_giveitems::*}:
			remove loop-value from {_player}
		give {_tool} to {_player}
		delete {StealItem-Perfect Robbery.%{_player}%::*}
		kitStats_DELETE({_player}, "Stealing.Perfect Robbery")