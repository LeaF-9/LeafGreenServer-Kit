# Skin Scanner

function kitSkill_Thief_6(player: player):
	kitOpenSkinScanner({_player})

function kitOpenSkinScanner(player: player):
	wait a tick
	open chest with 3 row named "§6§lSkin Scanner" to {_player}
	wait a tick
	set {_slot} to 0
	loop all players in radius 30 around {_player}:
		metadata value "gametype" of loop-player is "Kit"
		loop-player is not {_player}
		set slot {_slot} of {_player}'s current inventory to loop-player's head named "§f§l%name of loop-player%"
		add 1 to {_slot}

function kitSetSkinScanner(player: player, target: player):
	toolCT({_player}, "Thief", 6)
	set {_Class} to {Class::%{_target}%}
	set {_CLS} to {CLS::%{_target}%}
	kitStats_SET({_player}, "FakePlayer", {_target})
	kitStats_SET({_player}, "FakeClass", {_Class})
	kitStats_SET({_player}, "FakeCLS", {_CLS})
	execute console command "/disguiseplayer %{_player}% Player %{_target}%"
	kitMsg({_player}, "§6§l%{_target}% §bに変装しました...")
	sendTimeBossbar_GREEN({_player}, "Skin Scanner", 600)
	playSound("block.enchantment_table.use", 1, 0.5, {_player})
	execute console command "/skclass %{_player}% Kit-%{_Class}%"
	set {_weapon.main} to kitPlayerWeaponItem({_target}, {_Class})
	give {_weapon.main} to {_player}
	loop 3 times:
		set {_skill} to kitPlayerSkillItem({_target}, {_Class}, loop-number)
		give {_skill} to {_player}
		kitItemTargetTimer({_player}, {_skill})
	set {_el} to location 1 meter above {_player}
	loop 30 times:
		spawnParticle_Vector(scrape, randomVector(), 0.3, {_el})
	wait 30 seconds
	if kitStats_GET({_player}, "FakePlayer") is set:
		execute console command "/undisplayer %{_player}%"
		kitStats_DELETE({_player}, "FakePlayer")
		kitStats_DELETE({_player}, "FakeClass")
		kitStats_DELETE({_player}, "FakeCLS")
		kitMsg({_player}, "§c§l変装が解除された!")
		execute console command "/skclass %{_player}% Kit-Thief"
		set {_weapon.main} to kitPlayerWeaponItem({_player}, "Thief")
		give {_weapon.main} to {_player}
		loop 3 times:
			set {_skill} to kitPlayerSkillItem({_player}, "Thief", loop-number)
			give {_skill} to {_player}

on damage of player:
	set {_attacker} to getAttacker(victim)
	{_attacker} is set
	kitStats_GET(victim, "FakePlayer") is set
	kitCheckTeam(victim, {_attacker}, false) is true
	apply glowing 1 to victim for 1 second replacing existing effect