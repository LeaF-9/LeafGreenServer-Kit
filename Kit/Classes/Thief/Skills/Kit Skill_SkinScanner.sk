# Skin Scanner

function kitEffect_SkinScanner(player: player):
	kitOpenSkinScanner({_player})

function kitOpenSkinScanner(player: player):
	wait a tick
	open chest with 3 row named "§6§lSkin Scanner" to {_player}
	wait a tick
	set {_close} to false
	while {_close} is false:
		if {_player}.getOpenInventory().getTitle() is not "§6§lSkin Scanner":
			set {_close} to true
		stop if {_close} is true

		set {_slot} to 0
		loop all players in radius 30 around {_player}:
			metadata value "gametype" of loop-player is "Kit"
			loop-player is not {_player}

			set {_title} to "§f§l%name of loop-player%"

			delete {_lores::*}
			add "§eKit§7: §6§l%{Class::%loop-player%}%" to {_lores::*}

			set slot {_slot} of {_player}'s current inventory to loop-player's head named "§f§l%name of loop-player%" with lore {_lores::*}
			add 1 to {_slot}
		
		loop (3 * 9 - {_slot}) times:
			set slot {_slot} of {_player}'s current inventory to air
			add 1 to {_slot}

		wait a tick

function kitSetSkinScanner(player: player, target: player):
	nameCT({_player}, "Skin Scanner")
	set {_Class} to {Class::%{_target}%}
	set {_CLS} to {CLS::%{_target}%}
	setKitStats({_player}, "FakePlayer", {_target})
	setKitStats({_player}, "FakeClass", {_Class})
	setKitStats({_player}, "FakeCLS", {_CLS})
	execute console command "/disguiseplayer %{_player}% Player %{_target}%"
	kitMsg({_player}, "§6§l%{_target}% §bに変装しました...")
	sendTimeBossbar_GREEN({_player}, "Skin Scanner", 600)
	playSound("block.enchantment_table.use", 1, 0.5, {_player})
	execute console command "/skclass %{_player}% Kit-%{_Class}%"
	set {_weapon.main} to kitPlayerWeaponItem({_target}, {_Class})
	give {_weapon.main} to {_player}
	loop 3 times:
		set {_skill} to kitPlayerSkillItem({_target}, {_Class}, loop-number)
		give {_skill} to {_player}
	set {_el} to location 1 meter above {_player}
	loop 30 times:
		spawnParticle_Vector(scrape, randomVector(), 0.3, {_el})
	wait 30 seconds
	if getKitStats({_player}, "FakePlayer") is set:
		execute console command "/undisplayer %{_player}%"
		deleteKitStats({_player}, "FakePlayer")
		deleteKitStats({_player}, "FakeClass")
		deleteKitStats({_player}, "FakeCLS")
		kitMsg({_player}, "§c§l変装が解除された!")
		execute console command "/skclass %{_player}% Kit-Thief"
		set {_weapon.main} to kitPlayerWeaponItem({_player}, "Thief")
		give {_weapon.main} to {_player}
		loop 3 times:
			set {_skill} to kitPlayerSkillItem({_player}, "Thief", loop-number)
			give {_skill} to {_player}

on damage of player:
	set {_attacker} to getAttacker(victim)
	{_attacker} is set
	getKitStats(victim, "FakePlayer") is set
	kitCheckTeam(victim, {_attacker}, false) is true
	apply glowing 1 to victim for 1 second replacing existing effect