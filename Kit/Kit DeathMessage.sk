options:
	prefix: <##00bfff>[<##00FF7F>PvP<##00bfff>]

on load:
	delete {DamageCause::*}
	load yml "plugins/skript-yaml/General Options/Damage Causes.yml" as "damagecauses"
	loop yaml node keys "causes" from "damagecauses":
		set {_cause} to yml value "%loop-node%" from "damagecauses"
		set {-DamageCause::%loop-key%} to {_cause}
	unload yml "damagecauses"
	log "§c§lDamageCause§aの読込が完了しました"

on damage of player:
	wait a tick
	deleteLastDamage(victim)

on death of player:
	if {Join::%victim%} is "Kit":
		set death message to ""

		set {_cause} to getLastDamage(victim)
		if {_cause} is not set:
			set {_damagecause} to "%damage cause%"
			set {_cause} to {-DamageCause::%{_damagecause}%}
		kitDeathMessage(victim, {_cause})
		if {_cause} is not set:
			loop all players:
				loop-player has permission "sk.admin"
				message "§e[§6デバッグ§e] §c未知のDamage cause: §d%{_damagecause}% §cです。" to loop-player

function kitDeathMessage(vtm: player, cause: text):
# victim fake checher
	set {_fakeVictim} to getKitStats({_vtm}, "FakePlayer")
	if {_fakeVictim} is set:
		set {_victim} to {_fakeVictim}
		set {_cls.victim} to getKitStats({_vtm}, "FakeCLS")
	else:
		set {_victim} to {_vtm}
		set {_cls.victim} to {CLS::%{_vtm}%}
		set {_cls.victim} to {LastCLS::%{_vtm}%} if {_cls.victim} is not set
# victim Display設定
	set {_team.victim} to {TrueTeam::%{_victim}%}
	if {_team.victim} is "red":
		set {_display.victim} to "§c%display name of {_victim}%"
	else if {_team.victim} is "blue":
		set {_display.victim} to "§9%display name of {_victim}%"
	else if {_team.victim} is "yellow":
		set {_display.victim} to "§e%display name of {_victim}%"
	else if {_team.victim} is "green":
		set {_display.victim} to "§a%display name of {_victim}%"
	else if {_team.victim} is "purple":
		set {_display.victim} to "§5%display name of {_victim}%"
# victim Prefix設定
	set {_prefix.victim} to "<##00FF00>[%{_cls.victim}%<##00FF00>]"
# attacker fake checker
	if {LastKiller::%{_vtm}%} is set:
		set {_atk} to {LastKiller::%{_vtm}%}
		set {_fakeAttacker} to getKitStats({_atk}, "FakePlayer")
		if {_fakeAttacker} is set:
			set {_attacker} to {_fakeAttacker}
			set {_cls.attack} to getKitStats({_atk}, "FakeCLS")
		else:
			set {_attacker} to {_atk}
			set {_cls.attack} to {CLS::%{_atk}%}
			set {_cls.attack} to {LastCLS::%{_atk}%} if {_cls.attack} is not set
#	attacker Display設定
		set {_team.attack} to {TrueTeam::%{_attacker}%}
		if {_team.attack} is "red":
			set {_display.attack} to "§c%display name of {_attacker}%"
		else if {_team.attack} is "blue":
			set {_display.attack} to "§9%display name of {_attacker}%"
		else if {_team.attack} is "yellow":
			set {_display.attack} to "§e%display name of {_attacker}%"
		else if {_team.attack} is "green":
			set {_display.attack} to "§a%display name of {_attacker}%"
		else if {_team.attack} is "purple":
			set {_display.attack} to "§5%display name of {_attacker}%"
#	attacker Prefix設定
		set {_prefix.attack} to "<##00FF00>[%{_cls.attack}%<##00FF00>]"
# damage cause設定
	set {_damagecause} to "<##7cfc00>><##ffff00>><##7cfc00>> <##ff69b4>[<##dc143c>%{_cause}%<##ff69b4>] <##7cfc00>><##ffff00>><##7cfc00>>"
# メッセージ送信
	if {_atk} is set:
		loop all players:
			if {Join::%loop-player%} is "Kit":
				message formatted "{@prefix} %{_prefix.attack}%%{_display.attack}% %{_damagecause}% %{_prefix.victim}%%{_display.victim}%" to loop-player
	else:
		loop all players:
			if {Join::%loop-player%} is "Kit":
				message formatted "{@prefix} §1Suicide %{_damagecause}% %{_prefix.victim}%%{_display.victim}%" to loop-player

function setLastDamage(victim: player, cause: text):
	set metadata value "Last Damage" of {_victim} to {_cause}

function getLastDamage(victim: player) :: text:
	set {_cause} to metadata value "Last Damage" of {_victim}
	return {_cause}

function deleteLastDamage(victim: player):
	delete metadata value "Last Damage" of {_victim}