command /join:
	executable by: player
	trigger:
		if {Join::%player%} is set:  # 既に参加済
			message "§cあなたは既にゲームに参加しています" to player
			stop
		if {Kit-Testing::%player%} is true:  # スキルテスト中の排除
			message "§cスキルテスト中はJoinすることが出来ません!" to player
			stop
		set {Join::%player%} to "Kit"
		add 1 to {Kit-Join}
		broadcast "§2[§aKit PvP§2] §6%player% §eが参加しました §7(§b%{Kit-Join}%§9人参加中です§7)"
		delete {Class::%player%}
		delete {CLS::%player%}
		if {Kit-tochu} is true:  # 試合開始済
			if {TrueTeam::%player%} is not set:  # チーム未分類
				add 1 to {Kit-lnum}
				if {Kit-GameStage::team} is 4:
					if {Kit-lnum} is 1:
						set {_setteam} to "red"
					else if {Kit-lnum} is 2:
						set {_setteam} to "blue"
					else if {Kit-lnum} is 3:
						set {_setteam} to "yellow"
					else if {Kit-lnum} is 4:
						set {_setteam} to "green"
						remove 4 from {Kit-lnum}
				else if {Kit-GameStage::team} is 2:
					if {Kit-lnum} is 1:
						set {_setteam} to "red"
					else if {Kit-lnum} is 2:
						set {_setteam} to "blue"
						remove 2 from {Kit-lnum}
				add player to {Kit-team%{_setteam}%::*}
				set {TrueTeam::%player%} to {_setteam}
				set {team::%player%} to {_setteam}
				if {Kit-GameStage::rule} is "DTC":
					if {Kit-GameStage::corenumber-%{_setteam}%} is less than 1:
						execute console command "/team join bandit %player%"
						set {TrueTeam::%player%} to "purple"
						set {team::%player%} to "purple"
					else:
						execute console command "/team join kit-%{_setteam}% %player%"
				else:
					execute console command "/team join kit-%{_setteam}% %player%"
			else:
				set {_setteam} to {TrueTeam::%player%}
				if {Kit-GameStage::rule} is "DTC":
					if {Kit-GameStage::corenumber-%{_setteam}%} is less than 1:
						execute console command "/team join bandit %player%"
						set {TrueTeam::%player%} to "purple"
						set {team::%player%} to "purple"
					else:
						execute console command "/team join kit-%{_setteam}% %player%"
				else:
					execute console command "/team join kit-%{_setteam}% %player%"
			set {_tp} to a random location out of {Kit-GameStageRespawns.%{TrueTeam::%player%}%::*}
			teleport player to {_tp}
			set gamemode of player to survival
			execute console command "/skclass %player% GameInv"
			kitScoreJoining(player)
		else:  # 未試合
			if {Kit-lnum} is set:  # チーム分け済
				if {TrueTeam::%player%} is not set:  # チーム未分類時
					add 1 to {Kit-lnum}
					if {Kit-GameStage::team} is 4:
						if {Kit-lnum} is 1:
							set {_setteam} to "red"
						else if {Kit-lnum} is 2:
							set {_setteam} to "blue"
						else if {Kit-lnum} is 3:
							set {_setteam} to "yellow"
						else if {Kit-lnum} is 4:
							set {_setteam} to "green"
							remove 4 from {Kit-lnum}
					else if {Kit-GameStage::team} is 2:
						if {Kit-lnum} is 1:
							set {_setteam} to "red"
						else if {Kit-lnum} is 2:
							set {_setteam} to "blue"
							remove 2 from {Kit-lnum}
					add player to {Kit-team%{_setteam}%::*}
					set {TrueTeam::%player%} to {_setteam}
					set {team::%player%} to {_setteam}
					execute console command "/team join kit-%{_setteam}% %player%"
				else:  # チーム分類済
					execute console command "/team join kit-%{TrueTeam::%player%}% %player%"
#			試合未開始 (vote機能)
			else:
				if {Kit-Join} >= 4:
					loop all players:
						{Join::%loop-player%} is "Kit"
						message "§d人数が集まりました! §cゲームを開始します..." to loop-player
					execute console command "/gamestart"
				# 投票機能
				if {Kit-StageVote} is true:  # 投票の場合
					if {Kit-VoteStage::1} is not set:  # 投票ステージが未決定
						kitVoteSetting()
					kitOpenVoting(player)

command /joinset <number>:
	permission: sk.admin
	trigger:
		set {Kit-Join} to arg 1
		message "§bKit§7の現在の参加人数を§a%{Kit-Join}%§7に設定しました"

command /leave:
	executable by: player
	trigger:
		if {Join::%player%} is "Kit":
			if {Kit-tochu} is true:
				set {_pl} to player's location
				message "§a10秒間§c動かずに§aお待ち下さい..." to player
				loop 10 times:
					if player's location is not {_pl}:
						message "§cテレポートがキャンセルされました" to player
						stop
					wait a second
			message "§7ゲームから退出しました。" to player
			delete {Join::%player%}
			delete {Class::%player%}
			delete {CLS::%player%}
			subtract 1 from {Kit-Join}
			broadcast "§2[§aKit PvP§2] §6%player% §dが退出しました §7(§b%{Kit-Join}%§9人参加中です§7)"
			execute console command "/team leave %player%"
			teleport the player to {LobbyLocation::Kit}
			wait a tick
			set gamemode of player to survival
			execute console command "/cclass %player% Kit-LobbyInv"
			execute console command "/kjc %player%"
			if player has permission "sk.admin":
				give a diamond named "§9Admin Tools" to player
		else:
			message "§cあなたはゲームに参加していません。" to player