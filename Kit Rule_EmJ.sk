options:
	prefix: §e[§aEmJ§e]§r
#	nbt: {VillagerData:{profession:%{Kit-GameStage::villager-profession}%,level:%{Kit-GameStage::villager-level}%,type:%{Kit-GameStage::villager-type}%},Invulnerable:1,PersistenceRequired:1,Silent:1,CustomName:"\"§aJewel Exchanger\"",ActiveEffects:[{Id:2,Amplifier:10,Duration:999999},{Id:8,Amplifier:-10,Duration:999999}],Offers:{Recipes:[{buy:{id:emerald,Count:3,tag:{display:{Name:"\"§aJewel\""}}},sell:{id:diamond,Count:1,tag:{display:{Name:"\"§bGem\""}}},rewardExp:0b,maxUses:9999999},{buy:{id:diamond,Count:1,tag:{display:{Name:"\"§bGem\""}}},sell:{id:emerald,Count:2,tag:{display:{Name:"\"§aJewel\""}}},rewardExp:0b,maxUses:9999999},{buy:{id:emerald,Count:5,tag:{display:{Name:"\"§aJewel\""}}},sell:{id:blaze_rod,Count:1,tag:{display:{Name:"\"§e§lBox Key\"",Lore:["\"Gem Boxをクリックすることで発動します\"","\"Gem Boxに入っているGemを全て取り出します\""]}}},rewardExp:0b,maxUses:9999999}]}}

command /kit-EmJSetting:
	permission: sk.admin
	trigger:
		kitEmJHolograph({Kit-GameStage::name})
		set {Kit-TeamJewel::red} to 0
		set {Kit-TeamJewel::blue} to 0
		set {Kit-TeamJewel::yellow} to 0
		set {Kit-TeamJewel::green} to 0
		set {Kit-Gems::red} to 0
		set {Kit-Gems::blue} to 0
		set {Kit-Gems::yellow} to 0
		set {Kit-Gems::green} to 0
#		loop {Kit-GameStageVillager::*}:
#			spawn a villager at loop-value with nbt {@nbt}
		loop 20 times:
			loop 30 times:
				if {Kit-tochu} is true:
					wait a second
			if {Kit-tochu} is true:
				kitTopJewely()

function kitEmJHolograph(stage: text):
	loop {Kit-GameStage::*}:
		loop-index contains "gembox"
		set {_color} to kitTeamColor(loop-index)
		replace "gembox-" with "" in {_color}
		set {_team} to loop-index in strict proper case
		set {_gemloc} to loop-value
		set {_teamholoc} to location 1.5 meters above {_gemloc}
		set {_contholoc} to location 1.3 meters above {_gemloc}
		spawn an armor stand at {_teamholoc} with nbt "{Invulnerable:1b,PersistenceRequired:1b,NoBasePlate:1b,NoGravity:1b,Invisible:1b,Marker:1b}"
		set spawned armor stand's display name to "%{_color}%%{_team}% §bGembox"
		set metadata value "gametype" of spawned armor stand to "Kit"
		spawn an armor stand at {_contholoc} with nbt "{Invulnerable:1b,PersistenceRequired:1b,NoBasePlate:1b,NoGravity:1b,Invisible:1b,Marker:1b}"
		set spawned armor stand's display name to "§b0 §7Gems"
		set metadata value "gametype" of spawned armor stand to "Kit"
		set metadata value "gembox" of spawned armor stand to "%{_team}%"

function kitEmJChangeHolo(team: text, gem: number):
	set {_teamname} to {_team} in strict proper case
	loop all entities:
		type of loop-entity is an armor stand
		metadata value "gembox" of loop-entity is "%{_teamname}%"
		set loop-entity's display name to "§b%{_gem}% §7Gems"

on damage of dropped item:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	metadata value "undelete" of victim is "true"
	damage was caused by lava or void
	teleport victim to {Kit-GameStage::voidtp}

on damage of player:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	{Join::%victim%} is "Kit"
	if damage cause is not void:
		set {_amount} to amount of emerald named "§aJewel" in victim's inventory
		if {_amount} is not 0:
			set {_buff} to {_amount} / 10
			set damage to damage + {_buff}
	else:
		cancel event
		if {LastKiller::%victim%} is set:
			teleport victim to location of {LastKiller::%victim%}
		else:
			teleport victim to {Kit-GameStage::voidtp}
		kill victim

on death of player:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	{Join::%victim%} is "Kit"
	if damage is caused by lava or fire:
		if {LastKiller::%victim%} is set:
			teleport victim to location of {LastKiller::%victim%}
		else:
			teleport victim to {Kit-GameStage::voidtp}
	delete {Kit-PlayerBuff::%victim%}
	set {_amount} to amount of emerald named "§aJewel" in victim's inventory
	set {_amogem} to amount of diamond named "§bGem" in victim's inventory
	set {_leaf} to round({_amount} / 2)
	set {_v} to location of victim
	loop {_amount} times:
		drop a emerald named "§aJewel" at {_v}
		set metadata value "undelete" of last dropped item to "true"
		set glowing of last dropped item to true
	loop {_amogem} times:
		drop a diamond named "§bGem" at {_v}
		set metadata value "undelete" of last dropped item to "true"
		set glowing of last dropped item to true
	add 1 to {Kit-EmJDeath::%victim%}
	kitScorePersonal(victim, 1, "§4%{Kit-EmJKill::%victim%}% §7/ §1%{Kit-EmJDeath::%victim%}%")
	add 1 to {Kit-TeamDeaths::%{TrueTeam::%victim%}%}
	set {Kit-EmJKD::%victim%} to {Kit-EmJKill::%victim%} / {Kit-EmJDeath::%victim%}
	if {LastKiller::%victim%} is set:
		add 1 to {Kit-EmJKill::%{LastKiller::%victim%}%}
		kitScorePersonal({LastKiller::%victim%}, 1, "§4%{Kit-EmJKill::%{LastKiller::%victim%}%}% §7/ §1%{Kit-EmJDeath::%{LastKiller::%victim%}%}%")
		set {Kit-EmJKD::%{LastKiller::%victim%}%} to {Kit-EmJKill::%{LastKiller::%victim%}%} / {Kit-EmJDeath::%{LastKiller::%victim%}%}
		add 1 to {Kit-TeamKills::%{TrueTeam::%{LastKiller::%victim%}%}%}
		give a emerald named "§aJewel" to {LastKiller::%victim%}
		if {_leaf} is not 0:
			execute console command "/givecoin %{LastKiller::%victim%}% %{_leaf}% Jewel強奪"

aliases:
	shulk = minecraft:red_shulker_box, minecraft:blue_shulker_box, minecraft:yellow_shulker_box, minecraft:lime_shulker_box

on rightclick on shulk:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	if name of player's tool is not "§e§lBox Key":
		if {Kit-GameStage::gembox-%{TrueTeam::%player%}%} is location of event-block:
			if player has diamond named "§bGem":
				set {_gem} to amount of diamond named "§bGem" in player's inventory
				message "%{_gem}%" to player
				remove all diamonds named "§bGem" from player
				add {_gem} to {Kit-Gems::%{TrueTeam::%player%}%}
				message "{@prefix} §bGem§aをBoxに仕舞いました!" to player
				set {_world} to worldChecker("Kit", {Kit-GameStage::name})
				replace every "Kit-" with "" in {_world}
				kitEmJChangeHolo({TrueTeam::%player%}, {Kit-Gems::%{TrueTeam::%player%}%})
				loop all players:
					{Game::%loop-player%} is "Kit"
					{TrueTeam::%player%} is {TrueTeam::%loop-player%}
					send action bar "§bGembox収納数§7: §9%{Kit-Gems::%{TrueTeam::%player%}%}%" to loop-player
			else:
				message "§4!!!WARNING!!! §bGem§cを持っていません §4!!!" to player
	else:
		loop 4 times:
			if loop-number is 1:
				set {_t} to "red"
			if loop-number is 2:
				set {_t} to "blue"
			if loop-number is 3:
				set {_t} to "yellow"
			if loop-number is 4:
				set {_t} to "green"
			if {Kit-GameStage::gembox-%{_t}%} is location of event-block:
				set {_team} to {_t}
		{_team} is set
		if name of player's tool is "§e§lBox Key":
			if {Kit-Gems::%{_team}%} is not 0:
				set {_lore::*} to lore of player's tool
				remove a blaze rod named "§e§lBox Key" with lore {_lore::*} from player's inventory
				set {_gem} to {Kit-Gems::%{_team}%}
				subtract {_gem} from {Kit-Gems::%{_team}%}
				loop {_gem} times:
					drop a diamond named "§bGem" at location of event-block
					set metadata value "undelete" of last dropped item to "true"
					set glowing of last dropped item to true
				kitEmJChangeHolo({_team}, {Kit-Gems::%{_team}%})
				play sound "block.shulker_box.open" with volume 1.5 with pitch 1 at location of event-block
				message "{@prefix} §bGem§cを §3§l%{_gem}% §3個§c取り出しました!" to player
				loop all players:
					{Game::%loop-player%} is "Kit"
					{TrueTeam::%loop-player%} is {_team}
					send title "§bGembox §dの鍵が開きました!" with subtitle "§5解錠者: <%{TrueTeam::%player%}%>%player%" to loop-player for 5 seconds
					play sound "entity.blaze.death" with volume 1 with pitch 2 to loop-player
			else:
				message "§4!!!WARNING!!! §cGemが一つも入っていません §4!!!" to player
		else:
			message "§4!!!WARNING!!! §cこれは自チームの§bGembox§cではありません §4!!!" to player

on place of ender chest:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	name of player's tool is "§a§lJewelry Case"
	cancel event
	wait a tick
	if block at event-location is not air:
		send action bar "§c既にブロックのある位置には設置できません" to player
	else:
		if player has emerald named "§aJewel":
			set {_lore::*} to lore of player's tool
			remove an ender chest named "§a§lJewelry Case" with lore {_lore::*} from player's inventory
			if {Kit-JewelryCase::%player%} is set:
				set block at {Kit-JewelryCaseLoc::%player%} to air
				loop all entities:
					type of loop-entity is armor stand
					metadata value "JewelryCase" of loop-entity is "%player%"
					delete loop-entity
				set {_addjewel} to {Kit-JewelryCase::%player%}
			set block at event-location to ender chest
			set {Kit-JewelryCaseLoc::%player%} to event-location
			spawn armor stand at event-location with nbt "{Invisible:1b,Invulnerable:1b,PersistenceRequired:1b,NoBasePlate:1b,NoGravity:1b,Small:1b,Marker:1b,CustomNameVisible:1b}"
			set spawned armor stand's display name to "§6%player%§e's §a§lJC"
			set metadata value "JewelryCase" of spawned armor stand to "%player%"
			set {_jewel} to amount of emerald named "§aJewel" in player's inventory
			remove all emeralds named "§aJewel" from player's inventory
			set {Kit-JewelryCase::%player%} to {_jewel}
			message "{@prefix} §a現在のJewelを全て§lJewelry Case§aに保存しました!" to player
			if {_addjewel} is set:
				add {_addjewel} to {Kit-JewelryCase::%player%}
				message "{@prefix} §b既に設置してある§a§lJewelry Case§bを統合しました!" to player
			message "{@prefix} §6%player%§e's §a§lJewelry Case§7: §b%{Kit-JewelryCase::%player%}%" to player
		else:
			message "§4!!!WARNING!!! §cJewelを持っていません §4!!!" to player

on rightclick on a ender chest:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	if location of targeted block is {Kit-JewelryCaseLoc::%player%}:
		if player has emerald named "§aJewel":
			play sound "block.ender_chest.close" with volume 1 with pitch 1 to player
			remove an emerald named "§aJewel" from player's inventory
			add 1 to {Kit-JewelryCase::%player%}
			message "{@prefix} §aJewelを§lJewelry Case§aに保存しました!" to player
			message "{@prefix} §6%player%§e's §a§lJewelry Case§7: §b%{Kit-JewelryCase::%player%}%" to player
		else:
			message "§4!!!WARNING!!! §cJewelを持っていません §4!!!" to player
	else:
		message "§4!!!WARNING!!! §c自分の§a§lJewelry Case§cではありません!" to player

on leftclick on a ender chest:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	set {_loc} to location of targeted block
	loop all players:
		{Game::%loop-player%} is "Kit"
		{TrueTeam::%loop-player%} is not {TrueTeam::%player%}
		{Kit-JewelryCaseLoc::%loop-player%} is {_loc}
		set {_player} to loop-player
	if {_player} is set:
		set block at {Kit-JewelryCaseLoc::%{_player}%} to air
		loop {Kit-JewelryCase::%{_player}%} times:
			drop an emerald named "§aJewel" at {Kit-JewelryCaseLoc::%{_player}%}
			set metadata value "undelete" of last dropped item to "true"
			set glowing of last dropped item to true
		loop all entities:
			type of loop-entity is armor stand
			metadata value "JewelryCase" of loop-entity is "%{_player}%"
			delete loop-entity
		delete {Kit-JewelryCase::%{_player}%}
		delete {Kit-JewelryCaseLoc::%{_player}%}
		play sound "block.ender_chest.open" with volume 1.5 with pitch 1 at {_loc}
		message "{@prefix} §6%{_player}% §eの§a§lJewelry Case§eを破壊しました!" to player
		message "{@prefix} §a§lJewelry Case§c§lが破壊された!" to {_player}
		loop 3 times:
			play sound "block.note_block.pling" with volume 1 with pitch 1.5 to {_player}
			wait 5 ticks

on rightclick holding an eye of ender:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	name of player's tool is "§c§lDrop Bomb"
	cancel event
	set {_lore::*} to lore of player's tool
	remove an eye of ender named "§c§lDrop Bomb" with lore {_lore::*} from player
	shoot a snowball from player at speed 1
	set metadata value "DropBomb" of shot projectile to "true"
	drawDot count 10, particle "witchspell", center shot projectile, visibleRange 32, keepFor 3 seconds
	play sound "entity.ender_eye.launch" with volume 1.5 with pitch 1 at player

on projectile hit:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	projectile is a snowball
	metadata value "DropBomb" of projectile is "true"
	play sound "entity.ender_eye.death" with volume 1.5 with pitch 1 at projectile
	set {_loc} to location of projectile
	set {_now} to now
	loop all players in radius 3 around projectile:
		{Join::%loop-player%} is "Kit"
		add loop-player to {_db::*}
		set {Kit-CannotJewel::%loop-player%} to {_now}
		set {_jewel} to amount of emerald named "§aJewel" in loop-player's inventory
		set {_l} to location of loop-player
		loop {_jewel} times:
			drop a emerald named "§aJewel" at {_l}
			set metadata value "undelete" of last dropped item to "true"
			set glowing of last dropped item to true
	set {_y} to -0.5
	loop 5 times:
		set {_rad} to 0
		add 0.5 to {_y}
		loop 10 times:
			set {_el} to {_loc}
			add 36 to {_rad}
			add sin(0.5 * {_rad}) to x-coordinate of {_el}
			add cos(0.5 * {_rad}) to z-coordinate of {_el}
			add {_y} to y-coordinate of {_el}
			drawDot count 1, particle "witchspell", center {_el}, visibleRange 32
		wait a tick
	loop 19 times:
		loop {_db::*}:
			{Kit-CannotJewel::%loop-value-2%} is {_now}
			loop 3 times:
				set {_loc} to location of loop-value-2
				add a random number between -1 and 1 to x-coordinate of {_loc}
				add a random number between 0 and 2 to y-coordinate of {_loc}
				add a random number between -1 and 1 to z-coordinate of {_loc}
				drawDot count 1, particle "witchspell", center {_loc}, visibleRange 30
		wait 5 ticks
	loop {_db::*}:
		{Kit-CannotJewel::%loop-value%} is {_now}
		delete {Kit-CannotJewel::%loop-value%}

on pick up emerald:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	name of item is "§aJewel"
	if {Kit-CannotJewel::%player%} is true:
		cancel event
	else:
		execute console command "/givecoin %player% 1 Jewel拾得"

every a second:

# Bufflose
	{Kit-tochu} is true
	loop all players:
		{Join::%loop-player%} is "Kit"
		{TrueTeam::%loop-player%} is set
#	Teambuffの導入
		if {Kit-PlayerBuff::%loop-player%} is not set:
			if {Kit-TeamBuff::%{TrueTeam::%loop-player%}%} is set:
				set {Kit-PlayerBuff::%loop-player%} to {Kit-TeamBuff::%{TrueTeam::%loop-player%}%}
		set {_amount} to amount of emerald named "§aJewel" in loop-player's inventory
		set {_amocase} to {Kit-JewelryCase::%loop-player%}
		add {_amocase} to {_amount}
#	Teambuffの換算
		if {Kit-PlayerBuff::%loop-player%} is set:
			set {_amount} to round({_amount} * {Kit-PlayerBuff::%loop-player%})
		loop all items in loop-player's inventory:
			name of loop-item is "§d§lFortune Booster"
			set {_fb.%loop-player%} to true
		if {_fb.%loop-player%} is true:
			set {_amount} to {_amount} * 2
			set {_FortuneBooster::%{TrueTeam::%loop-player%}%} to true
		if {Kit-EarnJewel::%loop-player%} is not set:
			set {Kit-EarnJewel::%loop-player%} to 0
		if {Kit-EarnJewel::%loop-player%} is not {_amount}:
			set {Kit-EarnJewel::%loop-player%} to {_amount}
		add {_amount} to {_teamamount::%{TrueTeam::%loop-player%}%}
	add ({Kit-Gems::red} * 1) to {_teamamount::red}
	add ({Kit-Gems::blue} * 1) to {_teamamount::blue}
	add ({Kit-Gems::yellow} * 1) to {_teamamount::yellow}
	add ({Kit-Gems::green} * 1) to {_teamamount::green}
	size of {Kit-WinTeam::*} is 0
	if {_teamamount::red} is not {Kit-TeamJewel::red}:
		set {Kit-TeamJewel::red} to {_teamamount::red}
		kitScoreGlobal(7, "§c赤§7: §6%{Kit-TeamJewel::red}%")
		{_FortuneBooster::red} is not set
		if {Kit-TeamJewel::red} >= {Kit-GameStage::goal}:
			add "red" to {Kit-WinTeam::*}
			gameMsg("Kit", "§c§l試合終了!")
			execute console command "/timer end Kit-EmJ"
	if {_teamamount::blue} is not {Kit-TeamJewel::blue}:
		set {Kit-TeamJewel::blue} to {_teamamount::blue}
		kitScoreGlobal(6, "§9青§7: §6%{Kit-TeamJewel::blue}%")
		{_FortuneBooster::blue} is not set
		if {Kit-TeamJewel::blue} >= {Kit-GameStage::goal}:
			add "blue" to {Kit-WinTeam::*}
			gameMsg("Kit", "§c§l試合終了!")
			execute console command "/timer end Kit-EmJ"
	if {_teamamount::yellow} is not {Kit-TeamJewel::yellow}:
		set {Kit-TeamJewel::yellow} to {_teamamount::yellow}
		kitScoreGlobal(5, "§e黄§7: §6%{Kit-TeamJewel::yellow}%")
		{_FortuneBooster::yellow} is not set
		if {Kit-TeamJewel::yellow} >= {Kit-GameStage::goal}:
			add "yellow" to {Kit-WinTeam::*}
			gameMsg("Kit", "§c§l試合終了!")
			execute console command "/timer end Kit-EmJ"
	if {_teamamount::green} is not {Kit-TeamJewel::green}:
		set {Kit-TeamJewel::green} to {_teamamount::green}
		kitScoreGlobal(4, "§a緑§7: §6%{Kit-TeamJewel::green}%")
		{_FortuneBooster::green} is not set
		if {Kit-TeamJewel::green} >= {Kit-GameStage::goal}:
			add "green" to {Kit-WinTeam::*}
			gameMsg("Kit", "§c§l試合終了!")
			execute console command "/timer end Kit-EmJ"

on damage of player:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	{Join::%victim%} is "Kit"
	loop all items in victim's inventory:
		name of loop-item is "§d§lFortune Booster"
		set {_lore::*} to lore of loop-item
		set {_fb} to true
	{_fb} is true
	chance of 5%:
		remove a nether star named "§d§lFortune Booster" with lore {_lore::*} from victim's inventory
		playSound("entity.item.break", 1.5, 1, victim)
		message "{@prefix} §d§lFortune Booster§cが壊れてしまいました!" to victim

on place of floor soul torch:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	name of player's tool is "§b§lRich Searcher"
	cancel event
	wait a tick
	if block at event-location is not air:
		send action bar "§c既にブロックのある位置には設置できません" to player
	else:
		set {_lore::*} to lore of player's tool
		remove an ender chest named "§b§lRich Searcher" with lore {_lore::*} from player's inventory
		set block at event-location to floor soul torch
		add event-location to {Kit-RichSearcher.%player%::*}
		message "{@prefix} §b§lRich Searcher§eを設置しました!" to player

every 10 seconds:
	set {_high} to 0
	loop all players:
		{Join::%loop-player%} is "Kit"
		delete {_jwl}
		set {_jwl} to amount of emerald named "§aJewel" in loop-player's inventory
		{_jwl} > {_high}
		set {_high} to {_jwl}
		set {_top} to loop-player
	if {_high} is not 0:
		set {_toploc} to location of block above {_top}
		loop all players:
			{Game::%loop-player%} is "Kit"
			loop {Kit-RichSearcher.%loop-player%::*}:
				set {_from} to vector of loop-value-2
				set {_to} to vector of {_toploc}
				set {_vector} to {_to} -- {_from}
				set {_vector} to normalized {_vector}
				set {_lineloc} to loop-value-2
				set yaw of {_lineloc} to yaw of {_vector}
				set pitch of {_lineloc} to pitch of {_vector}
				set {_dis} to round(distance between loop-value-2 and {_toploc})
				loop {_dis} times:
					set {_el} to location loop-number meters in front of {_lineloc}
					drawDot count 1, particle "soulfireflame", center {_el}, visibleRange 30, keepFor a second

on leftclick on a floor soul torch:
	{Kit-tochu} is true
	{Kit-GameStage::rule} is "EmJ"
	set {_loc} to location of targeted block
	loop all players:
		{Game::%loop-player%} is "Kit"
		{TrueTeam::%loop-player%} is not {TrueTeam::%player%}
		{Kit-RichSearcher.%loop-player%::*} contains {_loc}
		set {_player} to loop-player
	if {_player} is set:
		set block at {_loc} to air
		remove {_loc} from {Kit-RichSearcher.%{_player}%::*}
		playSound("entity.blaze.hurt", 1.5, 1, {_loc})

function kitTopJewely():
	set {_count} to 3
	loop 3 times:
		loop all players:
			{Game::%loop-player%} is "Kit"
			send action bar "§3最多所持者公開まで.... §4§l§n%{_count}%" to loop-player
		subtract 1 from {_count}
		wait a second
	set {_high} to 0
	loop all players:
		{Join::%loop-player%} is "Kit"
		delete {_jwl}
		set {_jwl} to amount of emerald named "§aJewel" in loop-player's inventory
		{_jwl} > {_high}
		set {_high} to {_jwl}
		set {_top} to loop-player
	if {_high} is not 0:
		gameMsg("Kit", "§9現在§aJewel§2最多所持者§9は §6§l%{_top}% §9です!")
		strike lightning effect at {_top}
		apply glowing 1 to {_top} for 10 seconds

command /emjend:
	permission: sk.admin
	trigger:
		loop all players:
			{Join::%loop-player%} is "Kit"
			set gamemode of loop-player to spectator
		wait 5 seconds
		gameMsg("Kit", "§2----------=====§5Game Result§2=====----------")
		gameMsg("Kit", " ")
		gameMsg("Kit", "§cRed§7: §6Jewel: §a%{Kit-TeamJewel::red}% §6Kill: §a%{Kit-TeamKills::red}%")
		gameMsg("Kit", "§9Blue§7: §6Jewel: §a%{Kit-TeamJewel::blue}% §6Kill: §a%{Kit-TeamKills::blue}%")
		gameMsg("Kit", "§eYellow§7: §6Jewel: §a%{Kit-TeamJewel::yellow}% §6Kill: §a%{Kit-TeamKills::yellow}%")
		gameMsg("Kit", "§aGreen§7: §6Jewel: §a%{Kit-TeamJewel::green}% §6Kill: §a%{Kit-TeamKills::green}%")
		gameMsg("Kit", " ")
		if size of {Kit-WinTeam::*} is 0:
			set {_maxJewel} to max({Kit-TeamJewel::*})
			loop {Kit-TeamJewel::*}:
				loop-value = {_maxJewel}
				add loop-index to {Kit-WinTeam::*}
		loop {Kit-WinTeam::*}:
			set {_color} to kitTeamColor(loop-value)
			set {_text} to "%{_color}%§l%loop-value%§r"
			add {_text} to {_winteams::*}
		gameMsg("Kit", "§6§lWINNER: %{_winteams::*}%")
		loop all players:
			{Kit-WinTeam::*} contains {TrueTeam::%loop-player%}
			execute console command "/givecoin %loop-player% 100 勝利"
		gameMsg("Kit", " ")
		set {_maxKill} to max({Kit-EmJKill::*})
		loop {Kit-EmJKill::*}:
			loop-value = {_maxKill}
			add loop-index to {_maxKiller::*}
		gameMsg("Kit", "§4§lTopKiller§7: <##ffffff>§l%{_maxKiller::*}% §c%{_maxKill}% Kills")
		set {_maxJewel} to max({Kit-EarnJewel::*})
		loop {Kit-EarnJewel::*}:
			loop-value = {_maxJewel}
			add loop-index to {_maxJeweler::*}
		gameMsg("Kit", "§6§lTopEarner§7: <##ffffff>§l%{_maxJeweler::*}% §e%{_maxJewel}% Jewels")
		gameMsg("Kit", " ")
		gameMsg("Kit", "§bチームの戦績§7:")
		loop all players:
			loop all players:
				{TrueTeam::%loop-player-2%} is {TrueTeam::%loop-player-1%}
				message "§e%loop-player-2%§7: §6Kill:§9%{Kit-EmJKill::%loop-player-2%}% §eJewel:§9%{Kit-EarnJewel::%loop-player-2%}%" to loop-player-1
		gameMsg("Kit", " ")
		loop all players:
			message "§aあなたの戦績§7: §4Kill: %{Kit-EmJKill::%loop-player%}% §1Death: %{Kit-EmJDeath::%loop-player%}% §6K/D: %{Kit-EmJKD::%loop-player%}% §aJewel: %{Kit-EarnJewel::%loop-player%}%" to loop-player
		set {_stage} to {Kit-GameStage::name}
		replace every " " with "" in {_stage}
		loop all players:
			send title "§6ゲーム終了!" with subtitle "%{_winteams::*}% §5の勝利です!" to loop-player for 10 seconds
			delete {damager.%loop-player%::*}
			loop {Kit-RichSearcher.%loop-player%::*}:
				set block at loop-value-2 to air
			delete {Kit-RichSearcher.%loop-player%::*}
			set block at {Kit-JewelryCaseLoc::%loop-player%} to air
		kitGameInsert()
		wait 10 seconds
		delete {LastKiller::*}
		delete {Kit-TeamKills::*}
		delete {Kit-TeamDeaths::*}
		delete {Kit-EmJKill::*}
		delete {Kit-EmJDeath::*}
		delete {Kit-EmJKD::*}
		delete {Kit-EarnJewel::*}
		delete {Kit-TeamJewel::*}
		delete {Kit-Gems::*}
		delete {Kit-WinTeam::*}
		delete {Kit-JewelryCase::*}
		delete {Kit-JewelryCaseLoc::*}
		delete {Kit-CannotJewel::*}
		delete {Kit-TeamBuff::*}
		delete {Kit-PlayerBuff::*}
		execute console command "/kjclear"
		execute console command "/sk disable Kit EmJ"

on quit:
	delete {damager.%player%::*}